# group tab { include:'onload' }

var klienti_mesic_rok: text
var klienti_uids: text
var href: text
var info: text
menu m {type:'left'
  proc onclick(x) {
    klienti_mesic_rok.set(x.par.mesic_rok);
    klienti_uids.set(x.par.uids);
    u.tit.set(tit(conc('Přehled ',x.par.celkem,' hostů v období ',x.par.mesic_rok)));
    [ u.seznam.browse_load(conc("FIND_IN_SET(id_order,'",klienti_uids.get,"')"),
        "id_order,rodina,narozeni DESC") ];
    // zpráva o objednávkách daného období
    info.set(ask('ds_compare_list',klienti_uids.get));
    u.msg.set(info.get('msg'));
    u.htm.set(info.get('html'))
  }
}
proc onstart() {
  var mn: text
  mn.set(ask('ds_kli_menu'));
  m.attach_code(mn.cd.get);
  m.part(mn.th.get).click(1)
}

proc tit (x) { return(conc("<div class='CSection CMenu'><h3 class='CTitle'>",x,"</h3></div>")) }

use u: form f [0,0,,]
form f [,,720,500] {
  label tit [10,0,680,] { title:'Přehled hostů' }
  button [620,18,60,20] {title:'Export'
    proc onclick() {
      href.set(ask('ds_xls_hoste',klienti_uids.get,klienti_mesic_rok.get));
      msg.set(conc(info.get('msg'),href.get));
  } }
  label msg [10,50,700,] { title:'msg' }
  label htm [400,78,300,] { title:'html' }
  view osoba: table tab.ds_osoba
  view objednavka: table tab.tx_gnalberice_order {join_type:'LEFT', join:'ON uid=id_order' }
  browse seznam [10,76,,] { rows:30, qry_rows:1
    show id_osoba { data:osoba.id_osoba } // řídící tabulka
    show akc [,,30,] { title:'akce', data:objednavka.akce, format:'q=rs+' }
    show rodina [,,25,] { title:'rod', data:osoba.rodina, format:'q*s' }
    show prijmeni [,,80,] { title:'příjmení', data:osoba.prijmeni, format:'q*s' }
    show jmeno    [,,80,]  { title:'jméno', data:osoba.jmeno, format:'q*s' }
    show obec [,,80,] { title:'obec', data:osoba.obec, format:'q*s' }
    show vek  [,,20,]  { title:'věk', expr:"if(osoba.narozeni,year(now())-year(osoba.narozeni),-1)", format:'rs' }
#     show [,,30,]  { title:'od', data:osoba.fromday, format:'s:d.m' }
#     show [,,30,]  { title:'do', data:osoba.untilday, format:'s:d.m' }
#     show [,,20,]  { title:'?', data:osoba.pozn, format:'q*s' }
    show obj [,,30,] { title:'obj.', data:osoba.id_order, format:'q=s+' }
    // nezobrazené
    show ulice { data:osoba.ulice }
    show psc { data:osoba.psc }
  }
}

