# Systém Ans(w)er - modul Ekonomika
# (c) 2008-2010 Martin Šmídek <martin@smidek.eu>

const KAPITOLY = "Kapitoly: a=akce, da=DS akce, dh=DS hosté, dp= DS režie, ml=MS léto, mo=MS obnovy, s=YS.<br />Údaje jsou graficky zobrazeny OBSAHEM obdélníku, ne výškou."

var D_info: text
var D_cond: text
var D_suma: text
var D_cond2: text

proc onstart () {
  D_cond2.set('');
}

# ================================================================================================== Deník
panel den [,,,543] {type:'right', title:'Účetní deník',_sys:'den'
  menu d {type:'left', active:d.m.a
    menu m {title:'Účetní deník',type:'group',_sys:'den', active:m.a
      item {title:'Rok 2008'                   ,par:°{s:'u-show',c:'2008'}}
      item a {title:'Rok 2009'                 ,par:°{s:'u-show',c:'2009'}}
      item {title:'Import deníku, akcí, osnovy',par:°{s:'u-load',c:''}}
    }
    menu {title:'Přehledy',type:'group',_sys:'pre'
      item {title:'celková bilance'            ,par:°{s:'u-surv',c:'1'}},
#         item {title:'náklady a výnosy (2)'       ,par:°{s:'u-surv',c:'2'}},
#         item {title:'náklady a výnosy (3)'       ,par:°{s:'u-surv',c:'3'}},
#         item {title:'náklady a výnosy (4)'       ,par:°{s:'u-surv',c:'4'}},
#         item {title:'náklady a výnosy (5)'       ,par:°{s:'u-surv',c:'5'}},
      item {title:'náklady a výnosy s osnovou' ,par:°{s:'u-surv',c:'6'}},
      item {title:'... loni a letos'           ,par:°{s:'u-srov',c:'6'}}
    }
    menu {title:'Měsíční náklady',type:'group',_sys:'mna'
      item {title:'celkové náklady'            ,par:°{s:'u-mesice',c:'%'}},
      item {title:'náklady MS'                 ,par:°{s:'u-mesice',c:'MS'}},
      item {title:'náklady DS'                 ,par:°{s:'u-mesice',c:'DS'}},
      item {title:'náklady DH'                 ,par:°{s:'u-mesice',c:'DH'}},
      item {title:'náklady nezařazené'         ,par:°{s:'u-mesice',c:'x'}},
      item {title:'celkové náklady vers. loni' ,par:°{s:'u-mesice',c:'+%'}},
      item {title:'náklady MS vers. loni'      ,par:°{s:'u-mesice',c:'+MS'}},
      item {title:'náklady DS vers. loni'      ,par:°{s:'u-mesice',c:'+DS'}},
      item {title:'náklady DH vers. loni'      ,par:°{s:'u-mesice',c:'+DH'}},
      item {title:'nezařazené vers. loni'      ,par:°{s:'u-mesice',c:'+x'}}
    }
    menu {title:'Měsíční výnosy',type:'group',_sys:'mpr'
      item {title:'celkové výnosy'             ,par:°{s:'u+mesice',c:'%'}},
      item {title:'výnosy MS'                  ,par:°{s:'u+mesice',c:'MS'}},
      item {title:'výnosy DS'                  ,par:°{s:'u+mesice',c:'DS'}},
      item {title:'výnosy DH'                  ,par:°{s:'u+mesice',c:'DH'}},
      item {title:'výnosy nezařazené'          ,par:°{s:'u+mesice',c:'x'}},
      item {title:'celkové výnosy vers. loni'  ,par:°{s:'u+mesice',c:'+%'}},
      item {title:'výnosy MS vers. loni'       ,par:°{s:'u+mesice',c:'+MS'}},
      item {title:'výnosy DS vers. loni'       ,par:°{s:'u+mesice',c:'+DS'}},
      item {title:'výnosy DH vers. loni'       ,par:°{s:'u+mesice',c:'+DH'}},
      item {title:'nezařazené vers. loni'      ,par:°{s:'u+mesice',c:'+x'}}
    }
    proc onclick (i) { D_menu(i.par.s,i.par.c,i.par) }
  }
  use rp: form right [12,4,,]
  proc D_menu (s,c,par) {
    D_show.hide; D_show_load.hide;
    D_info.set(ask('ucet_todo','',s,c,par));
    D_cond.set(D_info.get('cond'));
    rp.info.set(conc("<div id='info' class='info'>",D_info.get('html'),"</div>"));
    { eq(s,'u-show'); D_show.popup(12,50)
    | eq(s,'u-load'); D_show_load.popup(12,50)
    };
    panel.property(°{height:'*',min_height:500,width:'*'});
  }
  # ------------------------------------------------------------------------------------------------ D_show
  # deník
  panel D_show [0,0,645,620] {
    var obrat: text,
    use d: form _denik [0,0,,],
    proc onfocus () { D_zmena_check }
    proc D_zmena_check () {
      D_cond2.set("0");
      D_cond2.set(conc(D_cond2.get,cconc(d.u5.get," OR left(md,1)=5")));
      D_cond2.set(conc(D_cond2.get,cconc(d.u6.get," OR left(dal,1)=6")));
      D_cond2.set(conc(D_cond2.get,cconc(d.u2.get," OR (left(md,1)=2 AND dal!=961000) OR left(dal,1)=2")));
      d.denik.browse_load(conc(D_cond.get,cconc(or(d.u2.get,d.u5.get,d.u6.get),conc(" AND (",D_cond2.get,")"))));
      D_prepocet }
    proc d.denik.onrowclick () {  }
    proc d.denik.onchange() {
      D_prepocet
     }
    proc D_prepocet() {
      obrat.set(ask('ucet_suma',D_info.get('year'),D_cond.get,d.denik.get_query));
      d.naklad.set(obrat.get('naklad')); d.vynos.set(obrat.get('vynos'));
      d.penize.set(obrat.get('penize')); d.obrat.set(obrat.get('obrat'))
    }
  }
  # ------------------------------------------------------------------------------------------------ _denik
  form _denik [,,700,500] {
    check u5 [0,0,100,14] { title:'náklady', format:'t', proc onchange () { D_show.D_zmena_check } }
    field naklad [73,0,70,17] { format:'r' }
    check u6 [165,0,100,14] { title:'výnosy', format:'t', proc onchange () { D_show.D_zmena_check } }
    field vynos  [247,0,70,17] { format:'r' }
    label [336,0,40,17] { title:'výsledek' }
    field obrat  [400,0,70,17] { format:'r' }
    check u2 [490,0,100,14] { title:'peníze', format:'t', proc onchange () { D_show.D_zmena_check } }
    field penize [561,0,70,17] { format:'r' }
    view d: table udenik,
    view a: table uakce {join_type:'left', join:'ON a.rok=left(id_udenik,4) AND akce=d.Cinnost'},
    browse denik [0,25,700,500] { rows:24, qry_rows:2
      show id     [,,  0,] {                 data:d.id_udenik },
      show datum  [,, 70,] { title:'datum',  data:d.Datum,           format:'rs-q' },
      show kap    [,, 20,] { title:'kap',    data:a.kapitola,        format:'sq' },
      show popis  [,,180,] { title:'popis',  data:d.Text,            format:'sq' },
      show md     [,, 50,] { title:'MD',     data:d.MD,              format:'rsq' },
      show dal    [,, 50,] { title:'DAL',    data:d.DAL,             format:'rsq' },
      show castka [,, 70,] { title:'částka', data:d.Castka,          format:'rsq' },
      show ref    [,, 30,] { title:'ref',    data:d.RefCin,          format:'rsq' },
      show akce   [,, 30,] { title:'akce',   data:d.Cinnost,         format:'rsq' },
      show dotace [,, 60,] { title:'dotace', data:d.Stredisko,       format:'sq' },
      show co     [,, 20,] { title:'co',     data:d.Zakazka,         format:'sq' },
      show zdroj  [,, 40,] { title:'zdroj',  data:d.Zdroj,           format:'sq' },
    }
  }
  # ================================================================================================ D_show_load
  panel D_show_load [0,0,645,520] {
    use dl: form _cmd [0,0,,],
    var dl_info: text
    proc dl.import_denik.onclick () {
      dl_info.set(ask('ucet_load_denik',D_info.get('year')));
      dl.result.set(dl_info.get('html'))
    }
    proc dl.import_odhad.onclick () {
      dl_info.set(ask('ucet_load_odhad',D_info.get('year')));
      dl.result.set(dl_info.get('html'))
    }
    proc dl.import_osnova.onclick () {
      dl_info.set(ask('ucet_load_osnova',D_info.get('year')));
      dl.result.set(dl_info.get('html'))
    }
    proc dl.import_akce.onclick () {
      dl_info.set(ask('ucet_load_akce',D_info.get('year')));
      dl.result.set(dl_info.get('html'))
    }
  }
  # ------------------------------------------------------------------------------------------------ _CMD
  form _cmd [,,500,200] {
    button import_denik [0,0,100,20] { title:'import deníku' }
    button import_odhad [150,0,100,20] { title:'import odhadu' }
    button import_osnova [300,0,100,20] { title:'import osnovy' }
    button import_akce [450,0,100,20] { title:'import akcí' }
    label result [0,30,700,600] { title:'...' },
  }
  # ------------------------------------------------------------------------------------------------ D_show_surv
#   panel D_show_surv [0,0,645,520] {
#     use ds: form _cmds [0,0,,],
#     var ds_info: text
#     proc ds.surv1.onclick () {
#       ds_info.set(ask('ucet_surv',D_info.get('year')));
#       ds.result.set(ds_info.get('html'))
#     }
#   }
#   # ------------------------------------------------------------------------------------------------ _CMDS
#   form _cmds [,,500,200] {
#     button surv1 [0,0,100,20] { title:'účty' }
#     label result [0,30,700,600] { title:'...' },
#   }
}

# ================================================================================================== Projekty
panel proj [,,,543] {type:'right', title:'Projekty',_sys:'*'
  menu {type:'left', active:*
    menu {title:'Projekty',type:'group',_sys:'pro'
      item {title:'Rok 2008'                   ,par:°{s:'u-proj',c:'2008',od:'1',do:'12'}}
      item {title:'Rok 2008 - 1.pololetí'      ,par:°{s:'u-proj',c:'2008',od:'1',do:'6'}}
      item {title:'Rok 2008 - 2.pololetí'      ,par:°{s:'u-proj',c:'2008',od:'7',do:'12'}}
      item {title:'Rok 2009'                   ,par:°{s:'u-proj',c:'2009',od:'1',do:'12'}}
      item {title:'Rok 2009 - 1.pololetí'      ,par:°{s:'u-proj',c:'2009',od:'1',do:'6'}}
      item {title:'Rok 2009 - 1.pololetí+odhad',par:°{s:'u-proj',c:'2009',od:'1',do:'6',odhad:'12'}}
      item {title:'Rok 2009 - jen odhad'       ,par:°{s:'u-proj',c:'2009',od:'1',do:'0',odhad:'12'}}
    }
    menu {title:'Nákladové skupiny',type:'group',_sys:'odh'
      item {title:'agregované náklady 2008'    ,par:°{s:'naklad',c:'2008',od:'1',do:'12'}}
      item {title:'agregované náklady 2008/10-12',par:°{s:'naklad',c:'2008',od:'10',do:'12'}}
      item {title:'agregované náklady 2009'    ,par:°{s:'naklad',c:'2009',od:'1',do:'12'}}
      item {title:'agregované náklady 2009/1-9',par:°{s:'naklad',c:'2009',od:'1',do:'9'}}
      proc onclick (i) {
        D_info.set(ask('ucet_todo','',i.par.s,i.par.c,i.par));
        rp.info.set(conc("<div id='info' class='info'>",D_info.get('html'),"</div>"));
        D_cond.set(D_info.get('cond'));
#         { eq(i.par.s,'e-show'); E_show.popup(12,50)
#         | E_show.hide
#         }
      }
    }
#     menu {title:'Účetní odhady',type:'group',_sys:'odh'
#       item {title:'Rok 2008'                   ,par:°{s:'e-show',c:'2008'}}
#       item {title:'Rok 2009'                   ,par:°{s:'e-show',c:'2009'}}
#     }
#     }
    proc onclick (i) {
      D_info.set(ask('ucet_todo','',i.par.s,i.par.c,i.par));
      rp.info.set(conc("<div id='info' class='info'>",D_info.get('html'),"</div>"));
      D_cond.set(D_info.get('cond'))
    }
  }
  use rp: form right [12,4,,]
}
# ================================================================================================== Rozbory
panel roz [,,,543] {type:'right', title:'Kapitoly',_sys:'*'
  var d_rok: text
  var d_prijmy: number
  menu {type:'left', active:*
    menu {title:'Struktura nákladů',type:'group',_sys:'odh'
      item {title:'Rok 2008'                   ,par:°{rok:'2008'}}
      item {title:'Rok 2009'                   ,par:°{rok:'2009'}}
      proc onclick (i) {
        E_show.hide; D_show2.hide;
        rp.fill(conc("Struktura nákladů v roce ",i.par.rok),conc(KAPITOLY.get,
          "<br />Číselné údaje jsou v tisících Kč (v závorce jsou mzdové náklady z celku)",ask("rok_struktura",i.par)));
      }
    }
    menu {title:'Struktura výnosů',type:'group',_sys:'odh'
      item {title:'Rok 2008'                   ,par:°{rok:'2008',prijmy:'1'}}
      item {title:'Rok 2009'                   ,par:°{rok:'2009',prijmy:'1'}}
      proc onclick (i) {
        E_show.hide; D_show2.hide;
        rp.fill(conc("Struktura nákladů v roce ",i.par.rok),conc(KAPITOLY.get,
          "<br />Číselné údaje jsou v tisících Kč. Nejsou zobrazeny státní dotace (ale jsou uvedeny v závorce jako nezobrazený výnos)",
          ask("rok_struktura",i.par)));
      }
    }
    menu {title:'Náklady podle kapitol',type:'group',_sys:'den'
      item {title:'Rok 2008'                   ,par:°{rok:'2008'}}
      item {title:'Rok 2009'                   ,par:°{rok:'2009'}}
      proc onclick (i) {
        D_show2.hide; d_prijmy.set(0);
        rp.fill(conc("Účetní deník - náklady podle kapitol v roce ",i.par.rok),'');
        d_rok.set(i.par.rok); D_show2.popup(12,50)
      }
    }
    menu {title:'Výnosy podle kapitol',type:'group',_sys:'den'
      item {title:'Rok 2008'                   ,par:°{rok:'2008'}}
      item {title:'Rok 2009'                   ,par:°{rok:'2009'}}
      proc onclick (i) {
        D_show2.hide; d_prijmy.set(1);
        rp.fill(conc("Účetní deník - výnosy podle kapitol v roce ",i.par.rok),'');
        d_rok.set(i.par.rok); D_show2.popup(12,50)
      }
    }
    menu {title:'Nákladové skupiny',type:'group',_sys:'odh'
      item {title:'agregované náklady 2008'    ,par:°{s:'naklad',c:'2008',od:'1',do:'12'}}
      item {title:'agregované náklady 2008/10-12',par:°{s:'naklad',c:'2008',od:'10',do:'12'}}
      item {title:'agregované náklady 2009'    ,par:°{s:'naklad',c:'2009',od:'1',do:'12'}}
      item {title:'agregované náklady 2009/1-9',par:°{s:'naklad',c:'2009',od:'1',do:'9'}}
    }
#     menu {title:'Účetní odhady',type:'group',_sys:'odh'
#       item {title:'Rok 2008'                   ,par:°{s:'e-show',c:'2008'}}
#       item {title:'Rok 2009'                   ,par:°{s:'e-show',c:'2009'}}
#     }
    proc onclick (i) {
      D_show2.hide; D_info.set(ask('ucet_todo','',i.par.s,i.par.c,i.par));
      rp.info.set(conc("<div id='info' class='info'>",D_info.get('html'),"</div>"));
      D_cond.set(D_info.get('cond'));
      { eq(i.par.s,'e-show'); E_show.popup(12,50)
      | E_show.hide
      }
    }
  }
  use rp: form right [12,4,,]
  # ------------------------------------------------------------------------------------------------ E_show
  # odhad
  panel E_show [0,0,645,620] {
    use e: form _odhad [0,0,,],
    proc onfocus () { E_zmena_check }
    proc E_zmena_check () {
      e.odhad.browse_load(D_cond.get)
    }
  }
  # ------------------------------------------------------------------------------------------------ _odhad
  form _odhad [,,700,500] {
    browse odhad [0,0,700,500] { rows:24, qry_rows:2
      show id     [,,  0,] {                 data:uodhad.id_uodhad },
      show datum  [,, 70,] { title:'datum',  data:uodhad.datum,           format:'s-q' },
      show md     [,, 50,] { title:'MD',     data:uodhad.md,              format:'rsq' },
      show castka [,, 70,] { title:'částka', data:uodhad.castka,          format:'rsq' },
      show dotace [,, 60,] { title:'dotace', data:uodhad.Stredisko,       format:'sq' },
      show co     [,,100,] { title:'poznámka',data:uodhad.poznamka,       format:'sq' },
    }
  }
  # ------------------------------------------------------------------------------------------------ D_show2
  # deník podla kapitol
  panel D_show2 [0,0,645,900] {
    var rok: text
    var cond: text
    use k: form _kap [0,0,,],
    use d2: form _denik2 [0,240,,],
    proc onfocus () {
      { d_prijmy.get;
        cond.set(conc("id_udenik BETWEEN ",d_rok.get,"00000 AND ",
          d_rok.get,"99999 AND left(DAL,1)='6' "));
        k.kapitoly.ucty.set_attrib('expr',"group_concat(distinct DAL)");
        k.kapitoly.ucel.set_attrib('expr',"(CASE WHEN left(dal,2)='69' THEN 'dotace' WHEN LEFT(dal,2)='68' THEN 'dary' ELSE 'ostatní' END)")
      | cond.set(conc("id_udenik BETWEEN ",d_rok.get,"00000 AND ",
          d_rok.get,"99999 AND left(MD,1)='5' AND md!='551200' "));
        k.kapitoly.ucty.set_attrib('expr',"group_concat(distinct MD)");
        k.kapitoly.ucel.set_attrib('expr',"(CASE WHEN left(md,2)='52' THEN 'mzdy' WHEN (LEFT(md,4)='5122' OR LEFT(md,5)='50125') THEN 'cesty' WHEN LEFT(md,4)='5182' THEN 'služby' WHEN LEFT(md,5)='50122' THEN 'potraviny' WHEN LEFT(md,5)='50221' THEN 'energie' WHEN LEFT(md,4)='5112' THEN 'opravy' ELSE 'ostatní' END)")
      };
#       not(eq(d_rok.get,rok.get)); rok.set(d_rok.get);
      k.kapitoly.browse_load(cond.get);
      k.kapitoly.raise('onrowclick');
      panel.property(°{height:'*',min_height:500,width:'*'});
    }
    # ---------------------------------------------------------------------------------------------- _kap
    form _kap [,,700,500] {
      label nazvys [240,0,440,300]
      view d: table udenik,
      view a: table uakce {join_type:'LEFT', join:'ON a.rok=left(id_udenik,4) AND akce=d.Cinnost' },
      view o: table uosnova {join_type:'LEFT', join:'ON ucet=md AND o.rok=left(id_udenik,4)' },
      browse kapitoly [0,5,700,500] { rows:10, qry_rows:1, group_by:'kapitola,ucel'
        show id     [,,  0,] {                 data:d.id_udenik },
        show        [,,  0,] {                 data:o.rok },
        show kap    [,, 20,] { title:'kap',    data:a.kapitola, format:'sq' },
        show ucel   [,, 50,] { title:'účel',   expr:'' },
        show pocet  [,, 50,] { title:'dokladů',   expr:'count(*)', format:'r' },
        show castka [,, 70,] { title:'tisíce Kč', expr:'round(sum(Castka)/1000)', format:'r' },
        show ucty   [,, 0,]  { expr:'' },
        show nazvy  [,, 0,]  { expr:"group_concat(distinct o.nazev separator ', ')" },
        proc onrowclick () {
          { d_prijmy.get;
            d2.denik.browse_load(conc("id_udenik BETWEEN ",d_rok.get,"00000 AND ",d_rok.get,"99999 ",
            " AND FIND_IN_SET(dal,'",ucty.get,"') AND a.kapitola='",kap.get,"'"));
          | d2.denik.browse_load(conc("id_udenik BETWEEN ",d_rok.get,"00000 AND ",d_rok.get,"99999 ",
            " AND FIND_IN_SET(md,'",ucty.get,"') AND a.kapitola='",kap.get,"'"));
          };
          nazvys.set(conc(KAPITOLY.get,"<br />",nazvy.get))
        }
      }
    }
    # ---------------------------------------------------------------------------------------------- _denik2
    form _denik2 [,,700,500] {
      view d: table udenik,
      view a: table uakce {join_type:'left', join:'ON a.rok=left(id_udenik,4) AND akce=d.Cinnost'},
      browse denik [0,0,700,500] { rows:24, qry_rows:1
        show id     [,,  0,] {                 data:d.id_udenik },
        show datum  [,, 70,] { title:'datum',  data:d.Datum,           format:'rs-q' },
        show kap    [,, 20,] { title:'kap',    data:a.kapitola,        format:'sq' },
        show popis  [,,180,] { title:'popis',  data:d.Text,            format:'sq' },
        show md     [,, 50,] { title:'MD',     data:d.MD,              format:'rsq' },
        show dal    [,, 50,] { title:'DAL',    data:d.DAL,             format:'rsq' },
        show castka [,, 70,] { title:'částka', data:d.Castka,          format:'rsq' },
        show ref    [,, 30,] { title:'ref',    data:d.RefCin,          format:'rsq' },
        show akce   [,, 30,] { title:'akce',   data:d.Cinnost,         format:'rsq' },
        show dotace [,, 60,] { title:'dotace', data:d.Stredisko,       format:'sq' },
        show co     [,, 20,] { title:'co',     data:d.Zakazka,         format:'sq' },
        show zdroj  [,, 40,] { title:'zdroj',  data:d.Zdroj,           format:'sq' },
      }
    }
  }
}
# ================================================================================================== Akce
panel akc [,,,543] {type:'right', title:'Akce',_sys:'*'
  var akce_rok: number
  menu {type:'left', active:*
    menu {title:'Přehledy',type:'group',_sys:'*'
      item {title:'náklady a výnosy akcí 2008'      ,par:°{rok:'2008'}}
      item {title:'náklady a výnosy akcí 2009'      ,par:°{rok:'2009'}}
      proc onclick (i) {
        Akce.hide; rp.fill(i.title,ask('ucet_akce',i.par.rok));
        panel.property(°{height:'*',min_height:500,width:'*'});
      }
    }
    menu {title:'Číselník',type:'group',_sys:'*'
      item {title:'číselník akcí 2008'      ,par:°{rok:'2008'}}
      item {title:'číselník akcí 2009'      ,par:°{rok:'2009'}}
      proc onclick (i) {
        akce_rok.set(i.par.rok); rp.fill(i.title,''); Akce.popup(12,50)
      }
    }
  }
  use rp: form right [12,4,,]
  # ------------------------------------------------------------------------------------------------ Akce
  # číselník akcí
  panel Akce [0,0,645,620] {
    use a: form _akce [0,0,,],
    proc onfocus () { a.seznam.browse_load(conc('rok=',akce_rok.get)) }
    # ---------------------------------------------------------------------------------------------- _akce
    form _akce [,,700,500] {
      browse seznam [0,0,700,500] { rows:24, qry_rows:2
        show id     [,,  0,] {                data:uakce.id_uakce },
        show rok    [,,  0,] {                data:uakce.rok },
        show typ    [,, 30,] { title:'typ',   data:uakce.typ,        format:'rsq' },
        show akce   [,, 30,] { title:'akce',  data:uakce.akce,       format:'rs+q' },
        show nazev  [,,320,] { title:'název', data:uakce.nazev_akce, format:'sq' },
        show datum  [,, 70,] { title:'datum', data:uakce.datum,      format:'rsq' },
        show dnu    [,, 30,] { title:'dnů',   data:uakce.dnu,        format:'rsq' },
        show osob   [,, 30,] { title:'osob',  data:uakce.osob,       format:'rsq' },
        show kap    [,, 30,] { title:'kap.',  data:uakce.kapitola,   format:'sq' },
        proc onsubmit () { a_oprava.modal(300,100) }
      }
    }
    # ---------------------------------------------------------------------------------------------- a_oprava
    panel a_oprava [0,0,350,190] { title:'oprava akce', type:'popup', css:'dialog'
      use ao: form _a_oprava [1,9,,],
      proc onfocus() { ao.load(a.seznam.id.get); }
    }
    # ---------------------------------------------------------------------------------------------- _a_oprava
    form _a_oprava [,,350,140] {
      label [10,10,50,17] { title:'typ', format:'r' },
      field [70,10,50,17] { data:uakce.typ },
      label [10,30,50,17] { title:'akce', format:'r' },
      field [70,30,50,17] { data:uakce.akce, format:'r' },
      label [10,50,50,17] { title:'název', format:'r' },
      field [70,50,250,17] { data:uakce.nazev_akce },
      label [10,70,50,17] { title:'datum', format:'r' },
      field [70,70,80,17] { type:'date', data:uakce.datum }
      label [10,90,50,17] { title:'dnů', format:'r' },
      field [70,90,50,17] { data:uakce.dnu, format:'r' },
      label [10,110,50,17] { title:'osob', format:'r' },
      field [70,110,50,17] { data:uakce.osob, format:'r' },
      label [10,130,50,17] { title:'kapitola', format:'r' },
      field [70,130,50,17] { data:uakce.kapitola },
      button save [240,160,45,20] { type:'submit', title:'OK', help:'uložit změny',
        proc onclick () { the_formsave(form,a.seznam); panel.hide(form.key) }
      }
      button cancel [143,160,45,20] { title:'Storno', help:'zadané údaje neukládat, vrátit se zpět'
        proc onclick () { panel.hide(0) }
      }
    }
  }
}
# ================================================================================================== tabulky
table udenik { key_id:'id_udenik'
  number id_udenik { key:'primary' }
  date Datum  { sql_pipe:'sql_date1' }
  text Zdroj
  text Text
  text MD
  text DAL
  number Castka
  text Stredisko
  text RefCin
  text Cinnost
  text Zakazka
}

table uodhad { key_id:'id_uodhad'
  number id_uodhad { key:'primary' }
  date datum  { sql_pipe:'sql_date1' }
  number castka
  text md
  text Zakazka
  text Stredisko
  text poznamka
}

table uosnova {
  number rok
  text ucet
  text nazev
}
