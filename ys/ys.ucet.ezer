#pragma names,group,attrs,syntax,prefix,get,box
# (c) 2009, Martin Šmídek

module UCETNICTVI [0,0,30,12] { active:Denik, units:'px'
  var D_info: text,
  var D_cond: text,
  var D_suma: text, var D_cond2: text,
  #                                                                                                 DENÍK
  panel Denik [,,,543] { title:'Deník a rozbory', type:'panel'
    menu {type:'left',
      menu {title:'Účetní deník',type:'group'
        item {title:'Rok 2008'                   ,par:°{s:'u-show',c:'2008'}}
        item {title:'Rok 2009'                   ,par:°{s:'u-show',c:'2009'}}
        item {title:'Import deníku, akcí, osnovy',par:°{s:'u-load',c:''}}
      },
      menu {title:'Účetní odhady',type:'group'
        item {title:'Rok 2008'                   ,par:°{s:'e-show',c:'2008'}}
        item {title:'Rok 2009'                   ,par:°{s:'e-show',c:'2009'}}
      },
      menu {title:'Projekty',type:'group'
        item {title:'Rok 2008'                   ,par:°{s:'u-proj',c:'2008',od:'1',do:'12'}}
        item {title:'Rok 2008 - 1.pololetí'      ,par:°{s:'u-proj',c:'2008',od:'1',do:'6'}}
        item {title:'Rok 2008 - 2.pololetí'      ,par:°{s:'u-proj',c:'2008',od:'7',do:'12'}}
        item {title:'Rok 2009'                   ,par:°{s:'u-proj',c:'2009',od:'1',do:'12'}}
        item {title:'Rok 2009 - 1.pololetí'      ,par:°{s:'u-proj',c:'2009',od:'1',do:'6'}}
        item {title:'Rok 2009 - 1.pololetí+odhad',par:°{s:'u-proj',c:'2009',od:'1',do:'6',odhad:'12'}}
        item {title:'Rok 2009 - jen odhad'       ,par:°{s:'u-proj',c:'2009',od:'1',do:'0',odhad:'12'}}
      },
      menu {title:'Přehledy',type:'group'
        item {title:'náklady a výnosy akcí'      ,par:°{s:'u-akce',c:'1'}},
        item {title:'celková bilance'            ,par:°{s:'u-surv',c:'1'}},
#         item {title:'náklady a výnosy (2)'       ,par:°{s:'u-surv',c:'2'}},
#         item {title:'náklady a výnosy (3)'       ,par:°{s:'u-surv',c:'3'}},
#         item {title:'náklady a výnosy (4)'       ,par:°{s:'u-surv',c:'4'}},
#         item {title:'náklady a výnosy (5)'       ,par:°{s:'u-surv',c:'5'}},
        item {title:'náklady a výnosy s osnovou' ,par:°{s:'u-surv',c:'6'}},
        item {title:'... loni a letos'           ,par:°{s:'u-srov',c:'6'}}
      },
      menu {title:'Měsíční náklady',type:'group'
        item {title:'celkové náklady'            ,par:°{s:'u-mesice',c:'%'}},
        item {title:'náklady MS'                 ,par:°{s:'u-mesice',c:'MS'}},
        item {title:'náklady DS'                 ,par:°{s:'u-mesice',c:'DS'}},
        item {title:'náklady DH'                 ,par:°{s:'u-mesice',c:'DH'}},
        item {title:'náklady nezařazené'         ,par:°{s:'u-mesice',c:'x'}},
        item {title:'celkové náklady vers. loni' ,par:°{s:'u-mesice',c:'+%'}},
        item {title:'náklady MS vers. loni'      ,par:°{s:'u-mesice',c:'+MS'}},
        item {title:'náklady DS vers. loni'      ,par:°{s:'u-mesice',c:'+DS'}},
        item {title:'náklady DH vers. loni'      ,par:°{s:'u-mesice',c:'+DH'}},
        item {title:'nezařazené vers. loni'      ,par:°{s:'u-mesice',c:'+x'}}
      },
      menu {title:'Měsíční příjmy',type:'group'
        item {title:'celkové příjmy'             ,par:°{s:'u+mesice',c:'%'}},
        item {title:'příjmy MS'                  ,par:°{s:'u+mesice',c:'MS'}},
        item {title:'příjmy DS'                  ,par:°{s:'u+mesice',c:'DS'}},
        item {title:'příjmy DH'                  ,par:°{s:'u+mesice',c:'DH'}},
        item {title:'příjmy nezařazené'          ,par:°{s:'u+mesice',c:'x'}},
        item {title:'celkové příjmy vers. loni'  ,par:°{s:'u+mesice',c:'+%'}},
        item {title:'příjmy MS vers. loni'       ,par:°{s:'u+mesice',c:'+MS'}},
        item {title:'příjmy DS vers. loni'       ,par:°{s:'u+mesice',c:'+DS'}},
        item {title:'příjmy DH vers. loni'       ,par:°{s:'u+mesice',c:'+DH'}},
        item {title:'nezařazené vers. loni'      ,par:°{s:'u+mesice',c:'+x'}}
      }
      proc onclick (i) { D_menu(i.par.s,i.par.c,i.par.get) }
    }
    use D_touch: group _touch [12,4,,],
    proc onstart () { D_touch.group_add; D_cond2.set(''); stop}
    proc onclick () { D_touch.volby.menu_fill2('ucet_menu','','',''); stop}
    proc D_menu (s,c,par) { E_show.panel_hide; D_show.panel_hide; D_show_load.panel_hide; D_show_surv.panel_hide;
      eq(s,'u-show');
      D_info.set(ask('ucet_todo','',s,c));
      D_touch.result.set(conc("<div id='info' class='info'>",D_info.get('html'),"</div>"));
      D_cond.set(D_info.get('cond'));
      D_show.popup(12,20)
    | eq(s,'e-show');
      D_info.set(ask('ucet_todo','',s,c));
      D_touch.result.set(conc("<div id='info' class='info'>",D_info.get('html'),"</div>"));
      D_cond.set(D_info.get('cond'));
      E_show.popup(12,20)
    | eq(s,'u-load');
      D_info.set(ask('ucet_todo','',s,c));
      D_touch.result.set(conc("<div id='info' class='info'>",D_info.get('html'),"</div>"));
      D_cond.set(D_info.get('cond'));
      D_show_load.popup(12,20)
    | eq(s,'u-surv','u-akce','msds','u-mesice','u+mesice','u-proj','u-srov');
      D_info.set(ask('ucet_todo','',s,c,par));
      D_touch.result.set(conc("<div id='info' class='info'>",D_info.get('html'),"</div>"));
      D_cond.set(D_info.get('cond'));
#       D_show_surv.popup(236,80)
      stop
    }
  # deník
  panel D_show [0,0,645,620] { type:'right'
    var obrat: text,
    use d: form _denik [0,0,,],
    proc onstart () { stop }
    proc onfocus () { D_zmena_check; stop }
    proc D_zmena_check () {
      D_cond2.set("0");
      D_cond2.set(conc(D_cond2.get,cconc(d.u5.get," OR left(md,1)=5")));
      D_cond2.set(conc(D_cond2.get,cconc(d.u6.get," OR left(dal,1)=6")));
      D_cond2.set(conc(D_cond2.get,cconc(d.u2.get," OR (left(md,1)=2 AND dal!=961000) OR left(dal,1)=2")));
      d.denik.browse_load(conc(D_cond.get,cconc(or(d.u2.get,d.u5.get,d.u6.get),conc(" AND (",D_cond2.get,")"))));
      D_prepocet }
    proc d.denik.onrowclick () { stop }
    proc d.denik.onchange() { D_prepocet }
    proc D_prepocet() {
      echo('rok=',D_info.get('year'));
      obrat.set(ask('ucet_suma',D_info.get('year'),D_cond.get,d.denik.get_query));
      d.naklad.set(obrat.get('naklad')); d.vynos.set(obrat.get('vynos'));
      d.penize.set(obrat.get('penize')); d.obrat.set(obrat.get('obrat')) }
  }
  # odhad
  panel E_show [0,0,645,620] { type:'right'
    use e: form _odhad [0,0,,],
    proc onfocus () { E_zmena_check; stop }
    proc E_zmena_check () {
      e.odhad.browse_load(D_cond.get)
    }
  }
  panel D_show_load [0,0,645,520] { type:'right'
    use dl: form _cmd [0,0,,],
    var dl_info: text
    proc dl.import_denik.onclick () {
      dl_info.set(ask('ucet_load_denik',D_info.get('year')));
      dl.result.set(dl_info.get('html'));
      stop
    }
    proc dl.import_odhad.onclick () {
      dl_info.set(ask('ucet_load_odhad',D_info.get('year')));
      dl.result.set(dl_info.get('html'));
      stop
    }
    proc dl.import_osnova.onclick () {
      dl_info.set(ask('ucet_load_osnova',D_info.get('year')));
      dl.result.set(dl_info.get('html'));
      stop
    }
    proc dl.import_akce.onclick () {
      dl_info.set(ask('ucet_load_akce',D_info.get('year')));
      dl.result.set(dl_info.get('html'));
      stop
    }
  }
  panel D_show_surv [0,0,645,520] { type:'right'
    use ds: form _cmds [0,0,,],
    var ds_info: text
    proc ds.surv1.onclick () {
      ds_info.set(ask('ucet_surv',D_info.get('year')));
      ds.result.set(ds_info.get('html'));
      stop
    }
  }
  }
#   #                                                                                                 ROZBORY
#   panel Kalendar [,,,543] { title:'Rozbory', type:'panel'
#   }
  proc onstart () {
    set_cookie(conc(sys('system'),'_modul'),conc(sys('system'),'_',sys('module'))); stop }
}
#                                                                                             ---   _CMD
form _cmd [,,500,200] {
  button import_denik [0,0,100,20] { title:'import deníku' }
  button import_odhad [150,0,100,20] { title:'import odhadu' }
  button import_osnova [300,0,100,20] { title:'import osnovy' }
  button import_akce [450,0,100,20] { title:'import akcí' }
  label result [0,30,700,600] { title:'...' },
}
form _cmds [,,500,200] {
  button surv1 [0,0,100,20] { title:'účty' }
  label result [0,30,700,600] { title:'...' },
}
#                                                                                             ---   _DENIK
form _denik [,,700,500] {
  check u5 [0,0,100,14] { title:'náklady', format:'t', proc onchange () { D_zmena_check } }
  field naklad [73,0,70,17] { format:'r' }
  check u6 [165,0,100,14] { title:'výnosy', format:'t', proc onchange () { D_zmena_check } }
  field vynos  [247,0,70,17] { format:'r' }
  label [336,0,40,17] { title:'výsledek' }
  field obrat  [400,0,70,17] { format:'r' }
  check u2 [490,0,100,14] { title:'peníze', format:'t', proc onchange () { D_zmena_check } }
  field penize [561,0,70,17] { format:'r' }
  browse denik [0,25,700,500] { rows:24, qry_rows:2
    show id     [,,  0,] {                 data:udenik.id_udenik },
    show datum  [,, 70,] { title:'datum',  data:udenik.Datum,           format:'s-q' },
    show zdroj  [,, 40,] { title:'zdroj',  data:udenik.Zdroj,           format:'sq' },
    show popis  [,,180,] { title:'popis',  data:udenik.Text,            format:'sq' },
    show md     [,, 50,] { title:'MD',     data:udenik.MD,              format:'rsq' },
    show dal    [,, 50,] { title:'DAL',    data:udenik.DAL,             format:'rsq' },
    show castka [,, 70,] { title:'částka', data:udenik.Castka,          format:'rsq' },
    show ref    [,, 30,] { title:'ref',    data:udenik.RefCin,          format:'rsq' },
    show akce   [,, 30,] { title:'akce',   data:udenik.Cinnost,         format:'rsq' },
    show dotace [,, 60,] { title:'dotace', data:udenik.Stredisko,       format:'sq' },
    show co     [,, 20,] { title:'co',     data:udenik.Zakazka,         format:'sq' },
  }
}
#                                                                                             ---   _ODHAD
form _odhad [,,700,500] {
  browse odhad [0,0,700,500] { rows:24, qry_rows:2
    show id     [,,  0,] {                 data:uodhad.id_uodhad },
    show datum  [,, 70,] { title:'datum',  data:uodhad.datum,           format:'s-q' },
    show md     [,, 50,] { title:'MD',     data:uodhad.md,              format:'rsq' },
    show castka [,, 70,] { title:'částka', data:uodhad.castka,          format:'rsq' },
    show dotace [,, 60,] { title:'dotace', data:uodhad.Stredisko,       format:'sq' },
    show co     [,,100,] { title:'poznámka',data:uodhad.poznamka,       format:'sq' },
  }
}
#                                                                                             ---   _TOUCH
form _touch [,,700,500] {
  label volby [0,0,200,570] { title:'', style:'backgroundColor:#fff;'
#     proc onmenuclick (s,c) {
#       eq(s,'u-show','u-load','u-surv','u-akce','msds','u-mesice','u+mesice','u-proj'); D_menu(s,c)
#     | stop
#     }
  },
  label result [0,0,700,600] { title:'' },
}
table udenik {
  number id_udenik { key:'primary' }
  date Datum
  text Zdroj
  text Text
  text MD
  text DAL
  number Castka
  text Stredisko
  text RefCin
  text Cinnost
  text Zakazka
}

table uodhad {
  number id_uodhad { key:'primary' }
  date datum
  number castka
  text md
  text Zakazka
  text Stredisko
  text poznamka
}
