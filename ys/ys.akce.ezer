# Systém Ans(w)er - modul Akce
# (c) 2008-2010 Martin Šmídek <martin@smidek.eu>

var the_akce: number
var the_rok: number
var the_title: text
var the_source: text // google|sey

proc onstart() {
  the_akce.set(get_cookie("ys_akce_id",0));
  the_title.set(get_cookie("ys_akce_title",'Vyber akci ...'));
  the_rok.set(get_cookie('ys_akce_rok',0));
  Ucastnici.u.fill(the_title.get);
  Vyber.info.fill(the_title.get,'');
  Vyber.info.rok.selects(javascript("var x='';for(i=2011;i>=1990;i--){x+=i+',';};x+='0'"));
  not(the_akce.get);
  // v cookie není akce
  #Vyber.focus; až onfirstfocus
/*
| // v cookie je akce
  #Vyber.m.rok.part('item',0,'title',the_rok.get).click; -- to nelze
  the_akce.set(get_cookie("ys_akce_id",0));
  Ucastnici.focus;
  Ucastnici.reload(0);
  Vyber.info.seznam.browse_seek(conc("id_akce=",the_akce.get));
  Vyber.info.seznam.browse_focus;
*/
}
# ================================================================================================== VÝBĚR
panel Vyber {type:'right', title:'Výběr akce'
  proc onfirstfocus() {
    echo('výběr');
    the_akce.get;
    info.rok.set(the_rok.get)
  | info.rok.set(2011);
  }
  use info: form _akce [12,4,,],
  menu {type:'left'
    menu {title:'Výběr akce ',type:'group'
      item {title:'z číselníku akcí na intranetu'
        proc onclick (i) {
          info.google.display(1); info.sey.display(0); the_source.set('google'); akce_roku
        }
      }
      item {title:'ze Seyova programu'
        proc onclick (i) {
          info.sey.display(1); info.google.display(0); the_source.set('sey'); akce_roku
        }
      }
    }
  }
  proc akce_roku() {
    the_akce.set(0); the_rok.set(info.rok.get);
    [info.google.browse_load(conc('g_rok=',the_rok.get))];
    [info.sey.browse_load(conc('YEAR(datum_od)=',the_rok.get))];
    info.fill(' &nbsp; Vyber akci ...','');
    set_cookie('ys_akce_rok',the_rok.get);
  }
  # ------------------------------------------------------------------------------------------------ _akce
  # formulář pro výběr akce pro další práci
  form _akce [,,640,50] {
    select rok [4,19,50,] { format:'t'
      proc onchange() { akce_roku }
    }
    button volba [564,17,100,16] { title:'Zapamatovat akci', style:'display:block;zIndex:2',
      proc onclick() {
        the_akce.set(if(eq(the_source.get,'google'),google.id_akce.get,sey.id_akce.get));
        set_cookie("ys_akce_id",the_akce.get);
#         set_cookie("ys_akce_title",'');
#         google.selected('clear');
        { and(the_akce.get,not(eq(the_akce.get,'0')));
          [ /*google.selected('set',the_akce.get);*/
            the_title.set(if(eq(the_source.get,'google'),
              conc(' &nbsp; Akce ',google.kod.get,' roku ',google.rok.get,' - ',google.nazev.get),
              conc(' &nbsp; ',sey.nazev.get)
            ));
            Vyber.info.fill(the_title.get,'');
            set_cookie("ys_akce_title",the_title.get);
            Ucastnici.u.fill(the_title.get);
            Ucastnici.reload(0)
          ]
#         | DefIdAkce.modal(500,200);
#           ask('akce_roku_id',google.kod.get,the_rok.get,DefIdAkce.f.lm.get,DefIdAkce.f.id.get);
#           google.browse_row
        }
    }}
    view ga: table g_akce
    view ja: table join_akce {join_type:'LEFT',join:'USING(g_rok,g_kod)'}
    view ma: table ms_akce   {join_type:'LEFT',join:'USING(source,akce)'}
    browse google [0,50,200,500] { type:'smart', rows:30, qry_rows:1, buf_rows:120,
      show id_gakce {data:ga.id_gakce}
      show rok {data:ga.g_rok}
      show kod   [,,30,] {title:'kód', data:ga.g_kod, format:'rs+q*'}
      show nazev [,,300,] {title:'název', data:ga.g_nazev, format:'sq*'}
      show       [,,80,] {title:'od', data:ga.g_od, format:'rsq*'}
      show       [,,80,] {title:'do', data:ga.g_do, format:'rsq*'}
      show source [,,15,] {title:'source', data:ja.source, format:'rsqu', css_cell:'source,0:yellow'}
      show akce  [,,30,] {title:'akce', data:ja.akce, format:'rsqu', css_cell:'akce,0:yellow'}
      show id_akce [,,30,] {title:'klíč', data:ma.id_akce, format:'rsq'}
      show       [,,30,] {title:'typ', data:ga.g_typ, format:'sq*'}
      show       [,,30,] {title:'kap', data:ga.g_kap, format:'sq*'}
      proc onsubmit() { volba.onclick() }
      proc onrowclick() { echo('id_akce=',id_akce.get);}
    }
    view ma2: table ms_akce
    view ja2: table join_akce {join_type:'LEFT',join:'USING(source,akce)'}
    view ga2: table g_akce    {join_type:'LEFT',join:'USING(g_rok,g_kod)'}
    browse sey [0,50,200,500] { type:'smart', rows:30, qry_rows:1, buf_rows:120,
      show id_akce {data:ma2.id_akce}
      show  {data:ja2.akce}
      show  {data:ga2.id_gakce}
      show  {data:ga2.g_rok}
      show kod   [,,30,] {title:'kód', data:ga2.g_kod, format:'rs+q*u', css_cell:'kod,0:yellow'}
      show nazev [,,300,] {title:'název', expr:"CONCAT(ma2.nazev,' ',ma2.misto)", format:'sq*'}
      show       [,,80,] {title:'od', data:ma2.datum_od, format:'rsq*'}
      show       [,,80,] {title:'do', data:ma2.datum_do, format:'rsq*'}
      show       [,,15,] {title:'source', data:ma2.source, format:'rsq'}
      show       [,,30,] {title:'akce', data:ma2.akce, format:'rsq'}
      show       [,,30,] {title:'klíč', data:ma2.id_akce, format:'rsq'}
      show       [,,30,] {title:'typ', data:ga2.g_typ, format:'sq*'}
      show       [,,30,] {title:'kap', data:ga2.g_kap, format:'sq*'}
      proc onsubmit() { volba.onclick() }
      proc onrowclick() { echo('id_akce=',id_akce.get);}
    }
    label head [0,0,680,50] { title:'' }
    label note [550,50,480,200] { title:'' }
    button [414,17,100,] { title:'Obnovit rok z intranetu', help:"obnoví akce roku z http://intranet.setkani.org"
      # získání seznamu akcí z docs.google.com pomocí API spredsheet
      proc onclick() {
        alert('bylo obnoveno ',ask('akce_roku_update',the_rok.get),' záznamů o akcích');
        google.browse_seek;
      }
    }
    proc fill(x,y) {
      [ x; head.set(conc("<div class='CSection CMenu'><h3 class='CTitle'>",x,"</h3></div>")) ];
      [ y; note.set(y) ]
    }
  }
  # ================================================================================================ definice akce
  panel DefIdAkce [,,220,60] {type:'popup', title:'Zapsání id_akce'
    use f: form _id_akce [12,10,,]
    form _id_akce [,,100,50] {
      label [10,10,50,] { title:'L|M akce' }
      field lm [60,10,10,] { help:'L|M na rozlišení verze MS.EXE'}
      field id [80,10,40,] { help:'číslo akce z programu MS.EXE'}
      button [140,8,,] { type:'submit', title:'Zapiš', proc onclick() {
        panel.hide(id.get)
      } }
    }
  }
}
# ================================================================================================== VÝBĚR
panel Ucastnici [,,,543] {type:'plain', title:'Účastníci akce'
  proc onfirstfocus() {
    echo('účastníci');
  }
  use u: form _ucast [4,0,,],
#   proc onfocus() {
#     u.head.set(Vyber.info.head.get); reload(0)
#   }
  proc reload(id_paru) {
  [ u.b.browse_load(conc("mk.id_akce=",the_akce.get));
    u.b.browse_focus;
    { id_paru; u.b.raise('onrowclick',id_paru)
    | u.b.raise('onrowclick') } ]
  }
  var pall: object
  proc pary_add(p,i) {  // používají panely PridatJmenem a PridatAkce
    p.chck.get;
    pall.set(p.all.get);
    u.init;
    u.key_pary.set(pall.get('id_pary'));     u.key_pary.change;
    u.key_akce.set(the_akce.get);            u.key_akce.change;
    u.mk.insert;
  }
  # ------------------------------------------------------------------------------------------------ _ucast
  form _ucast [,,200,50] { css:'ds_form'
    proc fill(x) {
      head.set(conc("<div class='CSection CMenu'><h3 class='CTitle'>",x,"</h3></div>"))
    }
    label head [0,0,890,50]  { title:'' }
    label [0,40,820,465] { css:'ae_work'}
    label [51,48,,] {title:"<img src='img/ms1.png'>" }
    view mk:  table ms_kurs
    view mp:  table ms_pary {join:'ON mp.id_pary=mk.id_pary'}
    view md:  table ms_deti {join_type:'LEFT',join:'ON md.id_pary=mp.id_pary'}
    view mkd: table ms_kursdeti {join_type:'LEFT',join:'ON mkd.id_pary=mk.id_pary AND mkd.id_akce=mk.id_akce'}
    view mks: table ms_kurs {join_type:'LEFT',join:'ON mks.id_akce=mk.id_akce AND mks.skupina=mk.skupina AND mks.skupina!=0'}
    view mps: table ms_pary {join_type:'LEFT',join:'ON mps.id_pary=mks.id_pary'}
    view ma:  table ms_akce {join:'ON ma.id_akce=mk.id_akce'}
    browse d [610,244,200,200] {type:'smart', rows:4, css_rows:'id_kursdeti,0:nic,:nakursu'
      show id_deti
      show id_kursdeti
      show jmeno    [,,70,] { title:'Děti' }
      show vek      [,,20,] { title:'věk', js_pipe:'rc2roky', format:'r' }
      show rodcislo
      show poznamka [,,80,] { title:'poznámka' }
      proc onsubmit() {
        echo('id_deti=',id_deti.get,', id_kursdeti=',id_kursdeti.get);
        copy_by_name(d,Dite.dite);
        Dite.key_deti.set(id_deti.get); Dite.key_kursdeti.set(id_kursdeti.get);
        Dite.modal(260,240)
      }
    }
    browse s [610,355,200,200] {type:'smart', rows:3
      show id_pary
      show jmeno [,,170,] {title:'Skupinka'}
      proc onsubmit () {
      }
    }
    browse b [8,50,200,490] {type:'smart', buf_rows:120, rows:19, qry_rows:1, group_by:'mp.id_pary',
      optimize:°{qry:'noseek'}
      proc onclick() {
        xfunkce.width; xfunkce.width(0);  skupina.width(33);
      | skupina.width; xfunkce.width(33); skupina.width(0);
      }
      proc onrowclick() {
        #echo('tab_act=',browse_active,', key=',browse_key,', jmeno=',jmeno.get,', funkce=',funkce.get);
        copy_by_name(b,form); mk.key(key_kurs.get); mp.key(key_pary.get);
#         d.browse_fill(deti.get);
        s.browse_fill(skup.get);
        // obarvení tlačítka [Str.pol]
        { or(cstrava_cel.get,cstrava_pol.get); stravy.set_css('cervena','cerna')
        | stravy.set_css('cerna','cervena') };
      }
      // klíče
      show key_kurs {data:mk.id_kurs}
      show key_pary {data:mp.id_pary}
      show key_skup {data:mks.id_pary}
      show key_akce {data:ma.id_akce}
      // akce
      show dnu      {expr:"DATEDIFF(ma.datum_do,ma.datum_od)+1"}
      show datum_od {data:ma.datum_od}
      // zobrazení
      show [,,160,] {title:'jméno',
        expr:"CASE mk.pouze WHEN 0 THEN CONCAT(mp.jmeno,' ',mp.jmeno_m,' a ',mp.jmeno_z) WHEN 1 THEN CONCAT(mp.prijmeni_m,' ',mp.jmeno_m) WHEN 2 THEN CONCAT(mp.prijmeni_z,' ',mp.jmeno_z) END"
        format:'s+q*'}
      show xfunkce [,,30,]  {title:'fce', data:mk.funkce, format:'rqs', map_pipe:ms_akce_funkce.zkratka}
      show skupina [,, 0,] {title:'sk.', data:mk.skupina, format:'rqs'}
      // pary - načtené a nezobrazené položky
      show jmeno      {data:mp.jmeno}                   show jmeno_m    {data:mp.jmeno_m}
      show jmeno_z    {data:mp.jmeno_z}                 show svatba     {data:mp.svatba}
      show adresa     {data:mp.adresa}                  show psc        {data:mp.psc}
      show mesto      {data:mp.mesto}                   show telefon    {data:mp.telefon}
      show email      {data:mp.email}                   show poznamka   {data:mp.poznamka}
      show datsvatba  {data:mp.datsvatba}               show spz        {data:mp.spz}
      show prijmeni_m {data:mp.prijmeni_m}              show prijmeni_z {data:mp.prijmeni_z}
      show rodcislo_m {data:mp.rodcislo_m}              show rodcislo_z {data:mp.rodcislo_z}
      show cislo_m    {data:mp.cislo_m}                 show cislo_z    {data:mp.cislo_z}
      show clen_m     {data:mp.clen_m}                  show clen_z     {data:mp.clen_z}
      show cirkev_m   {data:mp.cirkev_m}                show cirkev_z   {data:mp.cirkev_z}
      show vzdelani_m {data:mp.vzdelani_m}              show vzdelani_z {data:mp.vzdelani_z}
      show zamest_m   {data:mp.zamest_m}                show zamest_z   {data:mp.zamest_z}
      show zajmy_m    {data:mp.zajmy_m}                 show zajmy_z    {data:mp.zajmy_z}
      show jazyk_m    {data:mp.jazyk_m}                 show jazyk_z    {data:mp.jazyk_z}
      show aktivita_m {data:mp.aktivita_m}              show aktivita_z {data:mp.aktivita_z}
      show funkce     {data:mk.funkce}
      // kurs - načtené a nezobrazené položky
      show skup       {expr:"GROUP_CONCAT(DISTINCT mps.id_pary,'|',mps.jmeno SEPARATOR '|')"}
      show luzka      {data:mk.luzka}                   show pristylky  {data:mk.pristylky}
      show pocetdnu   {data:mk.pocetdnu}                show kocarek    {data:mk.kocarek}
      show strava_cel {data:mk.strava_cel}              show strava_pol {data:mk.strava_pol}
      show cstrava_cel{data:mk.cstrava_cel}             show cstrava_pol{data:mk.cstrava_pol}
      show svp        {data:mk.svp}                     show platba     {data:mk.platba}
      show platba1    {data:mk.platba1}                 show platba2    {data:mk.platba2}
      show platba3    {data:mk.platba3}                 show platba4    {data:mk.platba4}
      show zpusobplat {data:mk.zpusobplat}              show datplatby  {data:mk.datplatby}
      show budova     {data:mk.budova}                  show pokoj      {data:mk.pokoj}
      show k_poznamka {data:mk.poznamka}                show pouze      {data:mk.pouze}
#       // děti - načtené a nezobrazené položky
#       show deti       {expr:
#         "GROUP_CONCAT(DISTINCT md.id_deti,'|',IF(ISNULL(mkd.id_kursdeti),0,mkd.id_kursdeti),'|',md.jmeno,'|',md.rodcislo,'|',md.rodcislo,'|',md.poznamka ORDER BY mkd.id_deti DESC SEPARATOR '|')"}
#       show deti       {expr:
#         "(SELECT GROUP_CONCAT(DISTINCT md.id_deti,'|',IF(ISNULL(mkd.id_kursdeti),0,mkd.id_kursdeti),'|',md.jmeno,'|',md.rodcislo,'|',md.rodcislo,'|',md.poznamka ORDER BY mkd.id_deti DESC SEPARATOR '|') as deti FROM ms_deti AS md LEFT JOIN ms_kursdeti AS mkd ON mkd.id_deti=md.id_deti AND mkd.id_akce=190 WHERE md.id_pary=mp.id_pary)"}
    }
    label             [240,50,100,] {title:'Příjmení páru'}
    field jmeno       [240,65,100,] {data:mp.jmeno, help:'příjmení'}
    label             [352,50,100,] {title:'Jméno muže'}
    field jmeno_m     [352,65,100,] {data:mp.jmeno_m}
    label             [465,50,100,] {title:'Jméno ženy'}
    field jmeno_z     [464,65,100,] {data:mp.jmeno_z}
    label             [576,50,26,] {title:'Muž'}
    field _vek_m      [582,65,19,] {format:'o', value:'?'}
    label             [604,50,11,] {title:'Žena'}
    field _vek_z      [608,65,20,] {format:'o', value:'?'}
    label             [634,50,28,] {title:'Spolu'}
    field _spolu      [641,65,26,] {format:'o', value:'?'}
    label             [668,50,38,] {title:'Svatba'}
    field svatba      [668,65,36,] {data:mp.svatba}
    label             [715,50,38,] {title:'Funkce'}
    select funkce     [715,65,50,] {type:'map', data:mk.funkce, options:ms_akce_funkce.zkratka }
    label             [771,50,38,] {title:'Skup.'}
    field skupina     [770,65,35,] {data:mk.skupina, format:'r'}
    label             [240,83,100,] {title:'Ulice'}
    field adresa      [240,98,126,] {data:mp.adresa}
    label             [376,83,36,] {title:'Psč'}
    field psc         [376,98,36,] {data:mp.psc}
    label             [423,83,100,] {title:'Město'}
    field mesto       [422,98,114,] {data:mp.mesto}
    label             [240,124,37,] {title:'Telefon'}
    field telefon     [281,123,525,] {data:mp.telefon}
    label             [240,148,33,] {title:'e-mail'}
    field email       [281,148,525,] {data:mp.email}
    label             [240,166,33,] {title:'Poznámka'}
    edit poznamka     [241,181,491,52] {data:mp.poznamka}
    label             [746,169,66,] {title:'Datum svatby'}
    field datsvatba   [745,184,63,] {data:mp.datsvatba}
    label             [745,204,38,] {title:'Spz'}
    field spz         [746,219,62,] {data:mp.spz}
    label             [287,245,44,] {title:'Muž'}
    field prijmeni_m  [329,244,108,] {data:mp.prijmeni_m}
    label             [449,245,31,] {title:'Žena'}
    field prijmeni_z  [492,244,108,] {data:mp.prijmeni_z}
    label             [240,267,50,] {title:'Rodné č.'}
    field rodcislo_m  [286,266,73,] {data:mp.rodcislo_m}
    label             [363,267,37,] {title:'YMCA'}
    field cislo_m     [394,265,42,] {data:mp.cislo_m, format:'r'}
    field rodcislo_z  [448,266,73,] {data:mp.rodcislo_z}
    field cislo_z     [557,265,42,] {data:mp.cislo_z, format:'r'}
    label             [240,288,44,] {title:'Církev'}
    select cirkev_m   [286,287,152,] {type:'map', data:mp.cirkev_m, options:ms_akce_cirkev.zkratka}
    select cirkev_z   [448,287,152,] {type:'map', data:mp.cirkev_z, options:ms_akce_cirkev.zkratka}
    label             [240,309,44,] {title:'Vzdělání'}
    select vzdelani_m [286,308,152,] {type:'map', data:mp.vzdelani_m, options:ms_akce_vzdelani.zkratka}
    select vzdelani_z [448,308,152,] {type:'map', data:mp.vzdelani_z, options:ms_akce_vzdelani.zkratka}
    label             [240,330,44,] {title:'Zaměst.'}
    field zamest_m    [286,329,152,] {data:mp.zamest_m}
    field zamest_z    [448,329,152,] {data:mp.zamest_z}
    label             [240,351,44,] {title:'Zájmy'}
    field zajmy_m     [286,350,152,] {data:mp.zajmy_m}
    field zajmy_z     [448,350,152,] {data:mp.zajmy_z}
    label             [240,372,44,] {title:'Jazyk'}
    field jazyk_m     [286,371,152,] {data:mp.jazyk_m}
    field jazyk_z     [448,371,152,] {data:mp.jazyk_z}
    label             [240,393,44,] {title:'Aktivita'}
    field aktivita_m  [286,392,152,] {data:mp.aktivita_m}
    field aktivita_z  [448,392,152,] {data:mp.aktivita_z}
    select clen_m     [314,414,58,] {type:'map', data:mp.clen_m, options:ms_akce_clen.zkratka}
    select clen_z     [512,414,57,] {type:'map', data:mp.clen_z, options:ms_akce_clen.zkratka}
    // provoz kurzu
    select pouze      [400,414,96,] {type:'map', data:mk.pouze, options:ms_akce_pouze.zkratka}
    label [9,432,100,] {title:'Lůž. Při. Dnů Koč'}
    field luzka       [15,447,11,] {data:mk.luzka}
    field pristylky   [35,447,11,] {data:mk.pristylky}
    field pocetdnu    [56,447,11,] {data:mk.pocetdnu}
    field kocarek     [76,447,11,] {data:mk.kocarek}
    field strava_cel  [99,447,11,] {data:mk.strava_cel}
    field strava_pol  [117,447,11,] {data:mk.strava_pol}
    label [141,432,100,] {title:'Svp  Platba'}
    field svp         [141,447,11,] {data:mk.svp}
    field platba      [159,447,50,] {data:mk.platba}
    label [216,432,100,] {title:'Způsob platby'}
    select zpusobplat [216,447,86,] {type:'map', data:mk.zpusobplat, options:ms_akce_platba.zkratka, format:'u'}
    label [307,432,100,] {title:'Dne'}
    field datplatby   [307,447,84,] {type:'date', data:mk.datplatby}
    label [398,432,100,] {title:'Budova Pokoj'}
    field budova      [398,447,32,] {data:mk.budova}
    field pokoj       [438,447,40,] {data:mk.pokoj}
    label [492,432,100,] {title:'Poznámka k akci'}
    field k_poznamka  [492,447,314,] {data:mk.poznamka}
    field platba1     [15,477,47,] {data:mk.platba1}
    field platba2     [65,477,47,] {data:mk.platba2}
    field platba3     [115,477,47,] {data:mk.platba3}
    field platba4     [165,477,47,] {data:mk.platba4}
    // příkazy
    button stravy     [93,428,37,] {title:'Str Po', style:'font-size:10px;height:19px;width:40px'
      proc onclick() {
        Strava.dnu.set(u.b.dnu.get); // pokud jsou definovány výjimky z pravidelné stravy
        lt(Strava.dnu.get,15);  // 15 je maximálně možný počet řádků vyjímek ze stravy
        Strava.modal(240,50)
    } }
    button novy       [225,476,,] {title:'Nový',    help:'Vytvoření nového účastníka nebo páru'
      proc onclick() {   // založení páru a jeho zařazení na akci
        form.init(1); b.blur(1); d.browse_init; s.browse_init;
        jmeno.change; jmeno_m.change; jmeno_z.change; adresa.change; psc.change; mesto.change;
        mk.key(0); mp.key(0);
#         mp.insert; echo('mp=',mp.key);
#         key_pary.set(mp.key); key_pary.change; key_akce.set(the_akce_id.get); key_akce.change;
#         mk.insert; echo('mk=',mk.key);
#         reload(mk.key);
    } }
    button pridat_a   [276,476,,] {title:'Přidat z akce', help:'Přidání účastníky jiných akcí'
      proc onclick() {
        PridaniAkce.modal(240,50)
    } }
    button pridat_n   [370,476,,] {title:'Přidat jménem', help:'Přidání účastníka podle jména'
      proc onclick() {
        PridaniJmenem.modal(240,50)
    } }
    button vyradit    [487,476,,] {title:'Vyřadit', help:'Vyřazení účastníka nebo páru z účasti na akci'
      proc onclick() {
        confirm("Opravdu vyřadit ",jmeno.get,' (',jmeno_m.get,' a ',jmeno_z.get,") z této akce?");
        ms_kurs.delete_record(conc('id_kurs=',key_kurs.get));
        reload(0);
    } }
    button zrusit     [548,476,,] {title:'Zrušit', help:'Vymazání informací o páru z evidence'
      proc onclick() {
        confirm("Opravdu zrušit všechny informace o ",jmeno.get,' (',jmeno_m.get,' a ',jmeno_z.get,")?");
        ms_kurs.delete_record(conc('id_kurs=',key_kurs.get));
        ms_pary.delete_record(conc('id_pary=',key_pary.get));
        reload(0);
    } }
    button dite       [613,476,,] {title:'Přidat dítě', help:'Přidání dalšího dítěte do rodiny'
      proc onclick() {
        Dite.dite.init(2);
        Dite.key_deti.set(0); Dite.key_kursdeti.set(0);
        Dite.modal(260,240)
    } }
    button zpet       [703,476,,] {title:'Zpět',  help:'Neměnit změněná data', proc onclick() {
        reload(b.browse_key);
    } }
    button ulozit     [753,476,,] {title:'Uložit',  help:'Uložit změněná data',
      proc onclick() {
        var zmena_browse: number
        // oprava
        mk.key;
        [ zmena_browse.set(0);
          or(jmeno.changed,jmeno_m.changed,prijmeni_m.changed,jmeno_z.changed,prijmeni_z.changed,
            funkce.changed,skupina.changed,pouze.changed);
          zmena_browse.set(1) ];
        mk.key(b.key_kurs.get); [ mk.save; mk.load ];
        mp.key(b.key_pary.get); [ mp.save; mp.load ];
        { zmena_browse.get; b.browse_seek | copy_by_name(u,b) };
        b.browse_focus; b.focus
      | // přidání
        mp.insert; echo('mp=',mp.key);
        key_pary.set(mp.key); key_pary.change; key_akce.set(the_akce.get); key_akce.change;
        mk.insert; echo('mk=',mk.key);
        reload(mk.key);
    } }
    // stavový řádek
    label             [622,21,31,] {title:'pár:'}
    field key_pary    [648,18,50,] {data:mk.id_pary}
    label             [728,21,31,] {title:'kurs:'}
    field key_kurs    [758,18,50,] {data:mk.id_kurs}
    field key_akce                 {data:mk.id_akce}
    // počítané položky
    proc onload() {
      _vek_m.set(rc2roky(rodcislo_m.get));
      _vek_z.set(rc2roky(rodcislo_z.get));
      _spolu.set(cconc(svatba.get,roku(datsvatba.get,svatba.get),' '));
    }
  }
  # ================================================================================================ Dítě
  panel Dite [,,300,115] {type:'popup', title:'Úprava dětí'
    use dite: form _dite [12,10,,]
    var key_deti: number
    var key_kursdeti: number
    proc onfocus() {
      dite.sebou.set(cconc(key_kursdeti.get,1,0))
    }
    form _dite [,,200,50] { css:'ds_form'
      view d: table ms_deti
      field id_deti                  { data:d.id_deti }
      field id_pary                  { data:d.id_pary }
      check sebou    [0,10,90,17]
      field jmeno    [61,12,90,17]   { data:d.jmeno }
      field rodcislo [161,12,103,17] { data:d.rodcislo }
      field poznamka [61,52,203,17]  { data:d.poznamka }
      // ukončení
      button [100,87,,] { title:'Zrušit', help:'Vyřadit toto dítě z evidence', proc onclick() {
        confirm("Opravdu zrušit všechny informace o ",jmeno.get,"?");
        // případně vyjmout z kurzu
        [ key_kursdeti.get; ms_kursdeti.delete_record(conc('id_kursdeti=',key_kursdeti.get)) ];
        [ key_deti.get; ms_deti.delete_record(conc('id_deti=',key_deti.get)) ];
        reload(u.b.browse_key);
        panel.hide(1)
      }}
      button [166,87,,] { title:'Zpět', proc onclick() {
        panel.hide(0)
      }}
      button [225,87,,] { type:'submit', title:'Uložit', proc onclick() {
        { key_deti.get; echo('oprava děti');
          d.key(key_deti.get); [ d.save ];
          [ and(key_kursdeti.get,not(sebou.get)); // vyjmout z kurzu
            ms_kursdeti.delete_record(conc('id_kursdeti=',key_kursdeti.get))
          | and(not(key_kursdeti.get),sebou.get); // přidat na kurz
            dite_na_kurs
          ];
        | echo('vytvoření');
          id_pary.set(u.b.key_pary.get); id_pary.change; d.insert; key_deti.set(d.key);
          dite_na_kurs
        };
        reload(u.b.browse_key);
        panel.hide(1)
      }}
      proc dite_na_kurs() {
        ask('query',conc('INSERT INTO ms_kursdeti (id_akce,id_deti) VALUES (',
          the_akce.get,',',key_deti.get,')'));
      }
    }
  }
  # ================================================================================================ Přidání jménem
  panel PridaniJmenem [,,320,280] {type:'popup', title:'Přidání známých párů jménem'
    use fl: form _fl [12,10,,]
    form _fl [,,300,200] {
      var last: number
      var rod: object
      var popis: text
      button [0,250,50,16] { title:'přidat do účastníků', proc onclick() {
        each(L,pary_add); reload(0);
      } }
      button [130,250,50,16] { title:'jiná jména', proc onclick() {
        jmena.init; vse.init; L.init } }
      button [220,250,50,16] { title:'konec', proc onclick() {
        jmena.init; vse.init; L.init; panel.hide(0) } }
      // výběr z databáze MS
      select jmena [100,0,200,17] { type:'auto', par:°{fce:'akce_auto_jmena'}, format:'t'
        proc onchanged() {
          rod.set(ask('akce_auto_jmenovci',this.key));
          each(rod.get,jmenovci); // nesmí volat asynchronní operace
        }
      }
      proc jmenovci(p,i) {
        last.set(L.add);
        popis.set(p.nazev);
        L.part(last.get).part('info').set(popis.get);
        L.part(last.get).part('all').set(p);
      }
      // nastavení výběru
      check vse [4,0,80,16] { title:'všichni', format:'t'
        proc onchange() { each(L,nastav) }
        proc nastav (x,i) { x.chck.set(form.vse.get) }
      }
      list L [0,23,300,200] {rows:22      // anonymní group
        check chck [4,0,50,16] { title:' ', format:'t'  /*, proc onchange() { echo('jo/ne') }*/ }
        label info [23,4,300,16] {style:'white-space:nowrap'}
        var all: object
      }
    }
  }
  // =============================================================================================== Přidání akce
  panel PridaniAkce [,,320,280] {type:'popup', title:'Přidání účastníků známých z jiných akcí'
    use fl: form _fl [12,10,,]
    form _fl [,,300,200] {
      var last: number
      var pary: object
      var popis: text
      button [0,250,50,16] { title:'přidat do účastníků', proc onclick() {
        each(L,pary_add); reload(0);
      } }
      button [130,250,50,16] { title:'jiná akce', proc onclick() {
        akce.init; vse.init; L.init } }
      button [220,250,50,16] { title:'konec', proc onclick() {
        akce.init; vse.init; L.init; panel.hide(0) } }
      // výběr z akcí
      select akce [100,0,200,17] { type:'auto', par:°{fce:'akce_auto_akce'}, format:'t'
        proc onchanged() {
          pary.set(ask('akce_auto_ucast',this.key));
          each(pary.get,ucastnici); // nesmí volat asynchronní operace
        }
      }
      proc ucastnici(p,i) {
        last.set(L.add);
        popis.set(p.nazev);
        L.part(last.get).part('info').set(popis.get);
        L.part(last.get).part('all').set(p);
      }
      // nastavení výběru
      check vse [4,0,80,16] { title:'všichni', format:'t'
        proc onchange() { each(L,nastav) }
        proc nastav (x,i) { x.chck.set(form.vse.get) }
      }
      list L [0,23,300,200] {rows:22      // anonymní group
        check chck [4,0,50,16] { title:' ', format:'t'  /*, proc onchange() { echo('jo/ne') }*/ }
        label info [23,4,300,16] {style:'white-space:nowrap'}
        var all: object
      }
    }
  }
  // =============================================================================================== Strava
  panel Strava [,,320,280] {type:'popup', title:'Nastavení nestandardního počtu strav'
    var dnu: number
    var arr: object
    use n: form nastaveni [5,5,,]
    proc onfocus() {
      var cela: text  // řetězec strav: sov.... po dnech
      var polo: text  // řetězec strav: sov.... po dnech
      [ cela.set(u.b.cstrava_cel.get); cela.get | cela.set(strava_def(u.b.strava_cel.get)) ];
      [ polo.set(u.b.cstrava_pol.get); polo.get | polo.set(strava_def(u.b.strava_pol.get)) ];
      n.L.init;
      n.cele.set(u.b.strava_cel.get); n.polo.set(u.b.strava_pol.get);
      arr.set(ask('akce_strava_denne',u.b.datum_od.get,dnu.get,cela.get,polo.get));
      each(arr.get,den);
    }
    proc strava_def(strav) {
      return(conc('0','0',strav,
        repeat(strav,multiply(3,sum(dnu.get,minus(2)))),
        strav,strav,'0'))
    }
    proc den (p,i) {
      [ n.L.add ];
      n.L.part(i).part('den').set(p.den);
      strava_set(i,'sc',p.sc,n.cele.get);
      strava_set(i,'sp',p.sp,n.polo.get);
      strava_set(i,'oc',p.oc,n.cele.get);
      strava_set(i,'op',p.op,n.polo.get);
      strava_set(i,'vc',p.vc,n.cele.get);
      strava_set(i,'vp',p.vp,n.polo.get);
    }
    proc strava_set(i,s,val,norm) {
      var s1:text
      s1.set(substr(s,0,1));
      { or(and(eq(i,0),eq(s1,'s','o')),and(eq(i,sum(dnu.get,minus(1))),eq(s1,'v')));
        n.L.part(i).part(s).enable(0);
      | n.L.part(i).part(s).set(val);
        not(eq(val,norm));
        n.L.part(i).part(s).set_css('red')
      }
    }
    proc reset(co,jak) {
      each(arr.get,reset_den,co,jak);
    }
    proc reset_den(p,i,co,jak) {
      strava_set(i,co,jak,jak)
    }
    form nastaveni [0,0,320,200] {
      // sada návratu na default
      button [0,19,40,] { title:'V'
        proc onclick() {
          reset('sc',n.cele.get); reset('oc',n.cele.get); reset('vc',n.cele.get);
          reset('sp',n.polo.get); reset('op',n.polo.get); reset('vp',n.polo.get);
      }}
      label  [ 43, 0,,] { title:'Celé'}
      field cele [39,20,13,] { proc onchange { reset_cele.onclick }}
      button reset_cele [ 55,19,,] { title:'V', style:'width:17px;padding:0'
        proc onclick() {
          reset('sc',n.cele.get); reset('oc',n.cele.get); reset('vc',n.cele.get);
      }}
      label  [ 83, 0,,] { title:'Dětské'}
      field polo [83,20,13,] { proc onchange { reset_polo.onclick }}
      button reset_polo [ 99,19,,] { title:'V', style:'width:17px;padding:0'
        proc onclick() {
          reset('sp',n.polo.get); reset('op',n.polo.get); reset('vp',n.polo.get);
      }}
      label  [153, 0,,] { title:'Snídaně'}
      button [155,19,,] { title:'V', style:'width:17px;padding:0'
        proc onclick() { reset('sc',n.cele.get); }}
      button [175,19,,] { title:'V', style:'width:17px;padding:0'
        proc onclick() { reset('sp',n.polo.get); }}
      label  [215, 0,,] { title:'Oběd' }
      button [213,19,,] { title:'V', style:'width:17px;padding:0'
        proc onclick() { reset('oc',n.cele.get); }}
      button [233,19,,] { title:'V', style:'width:17px;padding:0'
        proc onclick() { reset('op',n.polo.get); }}
      label  [267, 0,,] { title:'Večeře'}
      button [269,19,,] { title:'V', style:'width:17px;padding:0'
        proc onclick() { reset('vc',n.cele.get); }}
      button [289,19,,] { title:'V', style:'width:17px;padding:0'
        proc onclick() { reset('vp',n.polo.get); }}
      // dny
      list L [0,50,320,200] {rows:22
        field den [4,0,140,16] {format:'o' }
        field sc  [155,0,15,] {format:':e' }
        field sp  [175,0,15,] {format:':e' }
        field oc  [213,0,15,] {format:':e' }
        field op  [233,0,15,] {format:':e' }
        field vc  [269,0,15,] {format:':e' }
        field vp  [289,0,15,] {format:':e' }
      }
      // ukončení
      button [205,255,,] { title:'Zpět', proc onclick() {
        panel.hide(0)
      }}
      var scele: text
      var spolo: text
      button [264,255,,] { type:'submit', title:'OK', proc onclick() {
        scele.set(''); spolo.set('');
        each(L,save_den);
        ask('akce_strava_denne_save',u.b.key_kurs.get,dnu.get,
          scele.get,u.b.strava_cel.get,u.b.cstrava_cel.get,
          spolo.get,u.b.strava_pol.get,u.b.cstrava_pol.get);
        panel.hide(0);
        u.b.browse_seek;
        u.b.browse_focus; u.b.focus
      }}
      proc save_den (p,i) {
        scele.set(conc(scele.get,pocet_strav(i,'sc'),pocet_strav(i,'oc'),pocet_strav(i,'vc')));
        spolo.set(conc(spolo.get,pocet_strav(i,'sp'),pocet_strav(i,'op'),pocet_strav(i,'vp')));
      }
      proc pocet_strav(i,f) {
        var x: text
        x.set(L.part(i).part(f).get);
        return(cconc(x,x,'0'));
      }
    }
  }
}
# ================================================================================================== TISK
panel tisk [,,,543] {type:'right', title:'Výpisy akce', include:'onclick'}
# ================================================================================================== VSICHNI
panel vse [,,,543] {type:'plain', title:'Všichni', include:'onclick'}
# ================================================================================================== MAIL
panel mail [,,,543] {type:'right', title:'Maily', skill:'yam', include:'onclick'}
/*
# ================================================================================================== MAPS
map ms_akce_clen:     table _cis {where:"druh='ms_akce_clen'",     order:'poradi', key_id:'data'}
map ms_akce_pouze:    table _cis {where:"druh='ms_akce_pouze'",    order:'poradi', key_id:'data'}
map ms_akce_funkce:   table _cis {where:"druh='ms_akce_funkce'",   order:'poradi', key_id:'data'}
map ms_akce_cirkev:   table _cis {where:"druh='ms_akce_cirkev'",   order:'poradi', key_id:'data'}
map ms_akce_vzdelani: table _cis {where:"druh='ms_akce_vzdelani'", order:'poradi', key_id:'data'}
map ms_akce_platba:   table _cis {where:"druh='ms_akce_platba'",   order:'poradi', key_id:'data'}
*/
