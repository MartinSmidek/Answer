# Systém Ans(w)er - modul Akce
# (c) 2008-2010 Martin Šmídek <martin@smidek.eu>

var the_akce: number
var the_akce_ms: number
var the_akce_ma: number
proc onstart() {
  the_akce.set(0);
  // pro ladění
  Vyber.focus; Vyber.akce_roku(2009); Vyber.info.seznam.browse_seek("akce=413");
  Vyber.info.volba.onclick; Ucastnici.focus;
}
# ================================================================================================== VÝBĚR
panel Vyber [,,,543] {type:'right', title:'Výběr akce'
  use info: form _akce [12,4,,],
  menu m {type:'left', // active:m.rok.loni
    menu rok {title:'Volba roku',type:'group'
      item letos {title:'2010',par:°{rok:'2010'} }
      item loni {title:'2009',par:°{rok:'2009'} }
      item {title:'2008',par:°{rok:'2008'} }
      item {title:'2007',par:°{rok:'2007'} }
      proc onclick (i) {
        info.seznam.selected('clear'); the_akce.set(0); the_akce_ms.set(''); the_akce_ma.set(0);
        akce_roku(i.par.rok)
      }
    }
  }
  proc akce_roku(rok){
    info.seznam.browse_load(conc('rok=',rok,' AND akce BETWEEN 301 AND 598'));
    info.fill('Vyber akci ...','')
  }
  # ------------------------------------------------------------------------------------------------ _akce
  # formulář pro výběr akce pro další práci
  form _akce [,,640,50] {
    button volba [550,10,100,16] { title:'Zapamatovat akci', style:'display:block;zIndex:2',
      proc onclick() {
        seznam.browse_key; the_akce.set(seznam.browse_key);
        set_cookie("ys_akce_id",the_akce.get);
        the_akce_ma.set(seznam.ms_akce.get); the_akce_ms.set(seznam.ms_source.get);
        seznam.selected('clear'); seznam.selected('set',the_akce.get);
        Vyber.info.fill(conc('Akce ',seznam.akce.get,' roku ',seznam.rok.get,' - ',seznam.nazev.get),'');
    }}
    browse seznam [0,50,200,500] { rows:30, qry_rows:1,
      show id [,,30,] {data:uakce.id_uakce}
      show rok {data:uakce.rok}
      show ms_source [,,10,] {data:uakce.ms_source}
      show ms_akce [,,30,] {data:uakce.ms_akce}
      show akce [,,30,] {title:'akce', data:uakce.akce, format:'rs+q*'}
      show nazev [,,170,] {title:'název', data:uakce.nazev_akce, format:'sq*'}
      proc onsubmit() { volba.onclick() }
    }
    label head [0,0,680,50] { title:'' }
    label note [200,50,480,550] { title:'' }
    proc fill(x,y) {
      [ x; head.set(conc("<div class='CSection CMenu'><h3 class='CTitle'>",x,"</h3></div>")) ];
      [ y; note.set(y) ]
    }
  }
}
# ================================================================================================== VÝBĚR
panel Ucastnici [,,,543] {type:'plain', title:'Účastníci'
  use u: form _ucast [4,0,,],
#   use p: form _par [240,50,,],
  proc onfocus(){
    u.head.set(Vyber.info.head.get);
    u.b.browse_load(conc("mk.source='",the_akce_ms.get,"' AND mk.akce=",the_akce_ma.get));
    u.b.browse_focus; u.b.raise('onrowclick')
  }
  proc u.b.onrowclick() {
#     echo('tab_act=',u.b.browse_active,', key=',u.b.browse_key,', jmeno=',u.b.jmeno.get,', funkce=',u.b.funkce.get);
    copy_by_name(u.b,u); u.d.browse_fill(u.b.deti.get); u.s.browse_fill(u.b.skup.get);
    // obarvení tlačítka [Str.pol]
    { or(u.b.cstrava_cel.get,u.b.cstrava_pol.get); u.stravy.set_css('cervena','cerna')
    | u.stravy.set_css('cerna','cervena') };
  }
  proc u.ulozit.onclick() {
    var zmena_browse: number
    [ zmena_browse.set(0);
      or(u.jmeno.changed,u.jmeno_m.changed,u.jmeno_z.changed,u.funkce.changed,u.skupina.changed);
      zmena_browse.set(1) ];
    u.mk.key(u.b.key_kurs.get); [ u.mk.save; u.mk.load ];
    u.mp.key(u.b.key_pary.get); [ u.mp.save; u.mp.load ];
    { zmena_browse.get; u.b.browse_seek | copy_by_name(u,u.b) };
    u.b.browse_focus; u.b.focus
  }
  # ------------------------------------------------------------------------------------------------ _ucast
  form _ucast [,,200,50] { css:'ds_form'
    label head [0,0,890,50]  { title:'' }
    label [0,40,820,465] { css:'ae_work'}
    label [51,48,,] {title:"<img src='img/ms1.png'>" }
    view mk:  table ms_kurs
    view ma:  table ms_druhakce {join:'ON ma.akce=mk.akce'}
    view mp:  table ms_pary {join:'ON mp.source=mk.source AND mp.cislo=mk.cislo'}
    view md:  table ms_deti {join:'ON md.source=mk.source AND md.cislo=mp.cislo'}
    view mkd: table ms_kursdeti {join_type:'LEFT', join:'ON mkd.source=mk.source AND mkd.cislo=mp.cislo AND mkd.akce=mk.akce'}
    view mks: table ms_kurs {join_type:'LEFT',
      join:'ON mks.source=mk.source AND mks.akce=mk.akce AND mks.skupina=mk.skupina AND mks.cislo!=mk.cislo'}
    view mps: table ms_pary {join:'ON mps.source=mks.source AND mps.cislo=mks.cislo'}
    browse d [610,244,200,200] {type:'smart', rows:4
      show id_deti
      show jmeno  [,,90,] {title:'Děti'}
      show rodcis [,,80,]
    }
    browse s [610,355,200,200] {type:'smart', rows:3
      show cislo
      show jmeno [,,170,] {title:'Skupinka'}
      proc onsubmit () {
#         b.browse_seek(conc('mp.cislo=',cislo.get))
      }
    }
    browse b [4,50,200,500] {type:'smart', buf_rows:120, rows:19, qry_rows:1, group_by:'mp.cislo'
      proc onclick() {
        xfunkce.width; xfunkce.width(0);  skupina.width(33);
      | skupina.width; xfunkce.width(33); skupina.width(0);
      }
      // klíče
      show key_kurs {data:mk.id_kurs}
      show key_akce {data:ma.akce}
      show key_pary {data:mp.cislo}
      show key_skup {data:mks.cislo}
      // akce
      show dnu      {expr:"DATEDIFF(ma.datum_do,ma.datum_od)+1"}
      show datum_od {data:ma.datum_od}
      // zobrazení
      show [,,167,] {title:'jméno', expr:"CONCAT(mp.jmeno,' ',mp.jmeno_m,' a ',mp.jmeno_z)", format:'s+q*'}
      show xfunkce [,,33,]  {title:'vps', data:mk.funkce, format:'rqs', map_pipe:ms_akce_funkce.zkratka}
      show skupina [,, 0,] {title:'sk.', data:mk.skupina, format:'rqs'}
      // pary - načtené a nezobrazené položky
      show jmeno      {data:mp.jmeno}                   show jmeno_m    {data:mp.jmeno_m}
      show jmeno_z    {data:mp.jmeno_z}                 show svatba     {data:mp.svatba}
      show adresa     {data:mp.adresa}                  show psc        {data:mp.psc}
      show mesto      {data:mp.mesto}                   show telefon    {data:mp.telefon}
      show email      {data:mp.email}                   show poznamka   {data:mp.poznamka}
      show datsvatba  {data:mp.datsvatba}               show spz        {data:mp.spz}
      show prijmeni_m {data:mp.prijmeni_m}              show prijmeni_z {data:mp.prijmeni_z}
      show rodcislo_m {data:mp.rodcislo_m}              show rodcislo_z {data:mp.rodcislo_z}
      show cislo_m    {data:mp.cislo_m}                 show cislo_z    {data:mp.cislo_z}
      show clen_m     {data:mp.clen_m}                  show clen_z     {data:mp.clen_z}
      show cirkev_m   {data:mp.cirkev_m}                show cirkev_z   {data:mp.cirkev_z}
      show vzdelani_m {data:mp.vzdelani_m}              show vzdelani_z {data:mp.vzdelani_z}
      show zamest_m   {data:mp.zamest_m}                show zamest_z   {data:mp.zamest_z}
      show zajmy_m    {data:mp.zajmy_m}                 show zajmy_z    {data:mp.zajmy_z}
      show jazyk_m    {data:mp.jazyk_m}                 show jazyk_z    {data:mp.jazyk_z}
      show aktivita_m {data:mp.aktivita_m}              show aktivita_z {data:mp.aktivita_z}
      show funkce     {data:mk.funkce}
      // kurs - načtené a nezobrazené položky
      show skup       {expr:"GROUP_CONCAT(DISTINCT mps.cislo,'|',mps.jmeno SEPARATOR '|')"}
      show luzka      {data:mk.luzka}                   show pristylky  {data:mk.pristylky}
      show pocetdnu   {data:mk.pocetdnu}                show kocarek    {data:mk.kocarek}
      show strava_cel {data:mk.strava_cel}              show strava_pol {data:mk.strava_pol}
      show cstrava_cel{data:mk.cstrava_cel}             show cstrava_pol{data:mk.cstrava_pol}
      show svp        {data:mk.svp}                     show platba     {data:mk.platba}
      show platba1    {data:mk.platba1}                 show platba2    {data:mk.platba2}
      show platba3    {data:mk.platba3}                 show platba4    {data:mk.platba4}
      show zpusobplat {data:mk.zpusobplat}              show datplatby  {data:mk.datplatby}
      show budova     {data:mk.budova}                  show pokoj      {data:mk.pokoj}
      show k_poznamka {data:mk.poznamka}                show pouze      {data:mk.pouze}
      // děti - načtené a nezobrazené položky
      show deti       {expr:"GROUP_CONCAT(DISTINCT md.id_deti,'|',md.jmeno,'|',md.rodcislo SEPARATOR '|')"}
      show kursdeti   {expr:"GROUP_CONCAT(DISTINCT mkd.id_kursdeti,'|',mkd.jmeno SEPARATOR '|')"}
    }
    label             [240,50,100,] {title:'Příjmení páru'}
    field jmeno       [240,65,100,] {data:mp.jmeno}
    label             [353,50,100,] {title:'Jméno muže'}
    field jmeno_m     [353,65,100,] {data:mp.jmeno_m}
    label             [465,50,100,] {title:'Jméno ženy'}
    field jmeno_z     [464,65,100,] {data:mp.jmeno_z}
    label             [576,50,26,] {title:'Muž'}
    field _vek_m      [582,65,19,] {format:'o'}
    label             [604,50,11,] {title:'Žena'}
    field _vek_z      [608,65,20,] {format:'o'}
    label             [634,50,28,] {title:'Spolu'}
    field _spolu      [641,65,26,] {format:'o'}
    label             [668,50,38,] {title:'Svatba'}
    field svatba      [668,65,36,] {data:mp.svatba}
    label             [715,50,38,] {title:'Funkce'}
    select funkce     [715,65,50,] {type:'map', data:mk.funkce, options:ms_akce_funkce.zkratka }
    label             [771,50,38,] {title:'Skup.'}
    field skupina     [770,65,35,] {data:mk.skupina, format:'r'}
    label             [240,83,100,] {title:'Ulice'}
    field adresa      [240,98,126,] {data:mp.adresa}
    label             [373,83,36,] {title:'Psč'}
    field psc         [373,98,36,] {data:mp.psc}
    label             [416,83,100,] {title:'Město'}
    field mesto       [415,98,114,] {data:mp.mesto}
    label             [240,122,37,] {title:'Telefon'}
    field telefon     [281,121,525,] {data:mp.telefon}
    label             [240,146,33,] {title:'e-mail'}
    field email       [281,146,525,] {data:mp.email}
    label             [240,166,33,] {title:'Poznámka'}
    edit poznamka     [241,181,491,52] {data:mp.poznamka}
    label             [746,169,66,] {title:'Datum svatby'}
    field datsvatba   [745,184,63,] {data:mp.datsvatba}
    label             [745,204,38,] {title:'Spz'}
    field spz         [746,219,62,] {data:mp.spz}
    label             [287,245,44,] {title:'Muž'}
    field prijmeni_m  [329,244,108,] {data:mp.prijmeni_m}
    label             [449,245,31,] {title:'Žena'}
    field prijmeni_z  [493,244,108,] {data:mp.prijmeni_z}
    label             [240,267,50,] {title:'Rodné č.'}
    field rodcislo_m  [286,266,73,] {data:mp.rodcislo_m}
    label             [363,267,37,] {title:'YMCA'}
    field cislo_m     [394,265,42,] {data:mp.cislo_m, format:'r'}
    field rodcislo_z  [451,266,73,] {data:mp.rodcislo_z}
    field cislo_z     [558,265,42,] {data:mp.cislo_z, format:'r'}
    label             [240,288,44,] {title:'Církev'}
    select cirkev_m   [286,287,152,] {type:'map', data:mp.cirkev_m, options:ms_akce_cirkev.zkratka}
    select cirkev_z   [451,287,152,] {type:'map', data:mp.cirkev_z, options:ms_akce_cirkev.zkratka}
    label             [240,309,44,] {title:'Vzdělání'}
    select vzdelani_m [286,308,152,] {type:'map', data:mp.vzdelani_m, options:ms_akce_vzdelani.zkratka}
    select vzdelani_z [451,308,152,] {type:'map', data:mp.vzdelani_z, options:ms_akce_vzdelani.zkratka}
    label             [240,330,44,] {title:'Zaměst.'}
    field zamest_m    [286,329,152,] {data:mp.zamest_m}
    field zamest_z    [451,329,152,] {data:mp.zamest_z}
    label             [240,351,44,] {title:'Zájmy'}
    field zajmy_m     [286,350,152,] {data:mp.zajmy_m}
    field zajmy_z     [451,350,152,] {data:mp.zajmy_z}
    label             [240,372,44,] {title:'Jazyk'}
    field jazyk_m     [286,371,152,] {data:mp.jazyk_m}
    field jazyk_z     [451,371,152,] {data:mp.jazyk_z}
    label             [240,393,44,] {title:'Aktivita'}
    field aktivita_m  [286,392,152,] {data:mp.aktivita_m}
    field aktivita_z  [451,392,152,] {data:mp.aktivita_z}
    select clen_m     [314,414,58,] {type:'map', data:mp.clen_m, options:ms_akce_clen.zkratka}
    select clen_z     [512,414,57,] {type:'map', data:mp.clen_z, options:ms_akce_clen.zkratka}
    // provoz kurzu
    select pouze      [400,414,96,] {type:'map', data:mk.pouze, options:ms_akce_pouze.zkratka}
    label [47,432,100,] {title:'Lůž. Při. Dnů Koč'}
    field luzka       [53,447,11,] {data:mk.luzka}
    field pristylky   [73,447,11,] {data:mk.pristylky}
    field pocetdnu    [94,447,11,] {data:mk.pocetdnu}
    field kocarek     [114,447,11,] {data:mk.kocarek}
    field strava_cel  [137,447,11,] {data:mk.strava_cel}
    field strava_pol  [155,447,11,] {data:mk.strava_pol}
    label [179,432,100,] {title:'Svp  Platba'}
    field svp         [179,447,11,] {data:mk.svp}
    field platba      [197,447,50,] {data:mk.platba}
    label [254,432,100,] {title:'Způsob platby'}
    select zpusobplat [254,447,86,] {type:'map', data:mk.zpusobplat, options:ms_akce_platba.zkratka, format:'u'}
    label [345,432,100,] {title:'Dne'}
    field datplatby   [345,447,84,] {type:'date', data:mk.datplatby}
    label [434,432,100,] {title:'Budova Pokoj'}
    field budova      [434,447,32,] {data:mk.budova}
    field pokoj       [474,447,40,] {data:mk.pokoj}
    label [521,432,100,] {title:'Poznámka k akci'}
    field k_poznamka  [521,447,288,] {data:mk.poznamka}
    field platba1     [177,477,47,] {data:mk.platba1}
    field platba2     [227,477,47,] {data:mk.platba2}
    field platba3     [277,477,47,] {data:mk.platba3}
    field platba4     [327,477,47,] {data:mk.platba4}
    // příkazy
    button stravy     [133,428,37,] {title:'Str Po', style:'font-size:10px;height:19px;width:40px'
      proc onclick() {
        Strava.dnu.set(u.b.dnu.get); // pokud jsou definovány výjimky z pravidelné stravy
        lt(Strava.dnu.get,15);  // 15 je maximálně možný počet řádků vyjímek ze stravy
        Strava.modal(240,50)
    } }
    button ulozit     [756,476,,] {title:'Uložit'}
    // stavový řádek
    label             [600,12,50,] {title:'pár:'}
    field key_pary    [640,12,50,] {data:mp.cislo}
    label             [710,12,50,] {title:'kurs:'}
    field key_kurs    [750,12,50,] {data:mk.id_kurs}
    // počítané položky
    proc onload() {
      _vek_m.set(rc2roky(rodcislo_m.get));
      _vek_z.set(rc2roky(rodcislo_z.get));
      _spolu.set(roku(datsvatba.get,svatba.get));
    }
  }
  // =============================================================================================== Strava
  panel Strava [,,320,280] {type:'popup', title:'Nastavení nestandardního počtu strav'
    var dnu: number
    var arr: object
    use n: form nastaveni [5,5,,]
    proc onfocus() {
      var cela: text  // řetězec strav: sov.... po dnech
      var polo: text  // řetězec strav: sov.... po dnech
      [ cela.set(u.b.cstrava_cel.get); cela.get | cela.set(strava_def(u.b.strava_cel.get)) ];
      [ polo.set(u.b.cstrava_pol.get); polo.get | polo.set(strava_def(u.b.strava_pol.get)) ];
      n.L.init;
      n.cele.set(u.b.strava_cel.get); n.polo.set(u.b.strava_pol.get);
      arr.set(ask('akce_strava_denne',u.b.datum_od.get,dnu.get,cela.get,polo.get));
      each(arr.get,den);
    }
    proc strava_def(strav) {
      return(conc('0','0',strav,
        repeat(strav,multiply(3,sum(dnu.get,minus(2)))),
        strav,strav,'0'))
    }
    proc den (p,i) {
      [ n.L.add ];
      n.L.part(i).part('den').set(p.den);
      strava_set(i,'sc',p.sc,n.cele.get);
      strava_set(i,'sp',p.sp,n.polo.get);
      strava_set(i,'oc',p.oc,n.cele.get);
      strava_set(i,'op',p.op,n.polo.get);
      strava_set(i,'vc',p.vc,n.cele.get);
      strava_set(i,'vp',p.vp,n.polo.get);
    }
    proc strava_set(i,s,val,norm) {
      var s1:text
      s1.set(substr(s,0,1));
      { or(and(eq(i,0),eq(s1,'s','o')),and(eq(i,sum(dnu.get,minus(1))),eq(s1,'v')));
        n.L.part(i).part(s).enable(0);
      | n.L.part(i).part(s).set(val);
        not(eq(val,norm));
        n.L.part(i).part(s).set_css('red')
      }
    }
    proc reset(co,jak) {
      each(arr.get,reset_den,co,jak);
    }
    proc reset_den(p,i,co,jak) {
      strava_set(i,co,jak,jak)
    }
    form nastaveni [0,0,320,200] {
      // sada návratu na default
      button [0,19,40,] { title:'V'
        proc onclick() {
          reset('sc',n.cele.get); reset('oc',n.cele.get); reset('vc',n.cele.get);
          reset('sp',n.polo.get); reset('op',n.polo.get); reset('vp',n.polo.get);
      }}
      label  [ 43, 0,,] { title:'Celé'}
      field cele [39,20,13,] { proc onchange { reset_cele.onclick }}
      button reset_cele [ 55,19,,] { title:'V', style:'width:17px;padding:0'
        proc onclick() {
          reset('sc',n.cele.get); reset('oc',n.cele.get); reset('vc',n.cele.get);
      }}
      label  [ 83, 0,,] { title:'Dětské'}
      field polo [83,20,13,] { proc onchange { reset_polo.onclick }}
      button reset_polo [ 99,19,,] { title:'V', style:'width:17px;padding:0'
        proc onclick() {
          reset('sp',n.polo.get); reset('op',n.polo.get); reset('vp',n.polo.get);
      }}
      label  [153, 0,,] { title:'Snídaně'}
      button [155,19,,] { title:'V', style:'width:17px;padding:0'
        proc onclick() { reset('sc',n.cele.get); }}
      button [175,19,,] { title:'V', style:'width:17px;padding:0'
        proc onclick() { reset('sp',n.polo.get); }}
      label  [215, 0,,] { title:'Oběd' }
      button [213,19,,] { title:'V', style:'width:17px;padding:0'
        proc onclick() { reset('oc',n.cele.get); }}
      button [233,19,,] { title:'V', style:'width:17px;padding:0'
        proc onclick() { reset('op',n.polo.get); }}
      label  [267, 0,,] { title:'Večeře'}
      button [269,19,,] { title:'V', style:'width:17px;padding:0'
        proc onclick() { reset('vc',n.cele.get); }}
      button [289,19,,] { title:'V', style:'width:17px;padding:0'
        proc onclick() { reset('vp',n.polo.get); }}
      // dny
      list L [0,50,320,200] {rows:22
        field den [4,0,140,16] {format:'o' }
        field sc  [155,0,15,] {format:':e' }
        field sp  [175,0,15,] {format:':e' }
        field oc  [213,0,15,] {format:':e' }
        field op  [233,0,15,] {format:':e' }
        field vc  [269,0,15,] {format:':e' }
        field vp  [289,0,15,] {format:':e' }
      }
      // ukončení
      button [205,255,,] { title:'Zpět', proc onclick() {
        panel.hide(0)
      }}
      var scele: text
      var spolo: text
      button [264,255,,] { type:'submit', title:'OK', proc onclick() {
        scele.set(''); spolo.set('');
        each(L,save_den);
        ask('akce_strava_denne_save',u.b.key_kurs.get,dnu.get,
          scele.get,u.b.strava_cel.get,u.b.cstrava_cel.get,
          spolo.get,u.b.strava_pol.get,u.b.cstrava_pol.get);
        panel.hide(0);
        u.b.browse_seek;
        u.b.browse_focus; u.b.focus
      }}
      proc save_den (p,i) {
        scele.set(conc(scele.get,pocet_strav(i,'sc'),pocet_strav(i,'oc'),pocet_strav(i,'vc')));
        spolo.set(conc(spolo.get,pocet_strav(i,'sp'),pocet_strav(i,'op'),pocet_strav(i,'vp')));
      }
      proc pocet_strav(i,f) {
        var x: text
        x.set(L.part(i).part(f).get);
        return(cconc(x,x,'0'));
      }
    }
  }
}
# ================================================================================================== MAPS
map ms_akce_clen:     table _cis {where:"druh='ms_akce_clen'",     order:'poradi', key_id:'data'}
map ms_akce_pouze:    table _cis {where:"druh='ms_akce_pouze'",    order:'poradi', key_id:'data'}
map ms_akce_funkce:   table _cis {where:"druh='ms_akce_funkce'",   order:'poradi', key_id:'data'}
map ms_akce_cirkev:   table _cis {where:"druh='ms_akce_cirkev'",   order:'poradi', key_id:'data'}
map ms_akce_vzdelani: table _cis {where:"druh='ms_akce_vzdelani'", order:'poradi', key_id:'data'}
map ms_akce_platba:   table _cis {where:"druh='ms_akce_platba'",   order:'poradi', key_id:'data'}
