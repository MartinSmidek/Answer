# Systém Ans(w)er - modul Pokladna.Deník
# (c) 2008-2010 Martin Šmídek <martin@smidek.eu>

# ================================================================================================== den - Deník

var printed: number
var pocet: number
var dokl: text // json

use dd: form _d_denik [11,0,,]
use dv: form _d_vyber [695,10,,]
use dc: form _d_cmd [20,500,,]

proc onstart () {
  dd.seznam.browse_focus;
  get_cookie('p_state','1,2.1.2009,1.1.2009,31.12.2009,2,Y,Y,N,1,1,1,poštovné',
    dv,'r_dat,rd_dne,rd_od,rd_do,r_org,ro_1,ro_n,ro_pokl,r_typ,r_sta,r_goa,rf_goal');
  dv.cond
}
proc dd.seznam.onsubmit (id) {
  has_skill('yp+'); d_oprava.d_load(dd.seznam,id); d_oprava.modal(100,100); dd.seznam.browse_focus;
}
proc dc.oprav.onclick () {
  d_oprava.d_load(dd.seznam,dd.seznam.id.get); d_oprava.modal(100,100)
}
proc dc.kopie.onclick () {
  d_novy.d_copy(dd.seznam.id.get); d_novy.modal(100,100)
}
proc dc.smazat.onclick () {
  confirm(conc('Opravdu smazat doklad ',dd.seznam.org.get,dd.seznam.typ.get,dd.seznam.cislo.get,'?'));
  pdenik.delete_record(conc('id_pokl=',dd.seznam.id.get));
  dd.seznam.raise('onrowclick',dd.seznam.browse_seek(1))
}
proc dc.novy.onclick () {
  d_novy.d_init(dd.seznam,dv.r_org.get,dv.ro_1.key,dv.r_typ.get); d_novy.modal(100,100)
}
# proc dc.tisk1.onclick () {
#   tiskni(0); confirm('Proběhl tisk v pořádku, mohu označit doklad za vytištěný?');
#   ask('p_pdenik_vytisten',dd.seznam.id.get); dd.seznam.vytisten.let(1)
# }
# proc dc.tiskn.onclick () {
#   dd.seznam.browse_select(conc(d_cond.get,' AND vytisten=0 '));
#   doklad.report_batch('init'); pocet.set(0);       // inicializace dávky
#   tiskni_next(pocet.get,30);
#   pocet.get;
#   doklad.print(0);
#   confirm('Bylo správně vytištěno ',pocet.get,' dokladů?');
#   zmena(dd.seznam.selected('get',pocet.get));
#   dd.seznam.selected('clear',pocet.get);
#   reload(d_cond.get)
# }
# proc zmena (klice)  {
#   ask('update_table','pdenik','id_pokl',klice,'vytisten','1')
# }
# proc tiskni_next (n,max) {
#   lt(n,max); printed.set(dd.seznam.selected('key',n,'D'));
#   printed.get; tiskni_1(printed.get);
#   doklad.report_batch('add');  // další dopis do dávky
#   pocet.set(sum(n,1));
#   tiskni_next(pocet.get,max)                              // cyklus
# }
# proc tiskni_1 (id) {
#   doklad.report_init;
#   dokl.set(ask('p_pdenik_data',id));
#   doklad.typ.set(cconc(eq(dokl.get('typ'),1),'VÝDAJOVÝ POKLADNÍ DOKLAD','PŘÍJMOVÝ POKLADNÍ DOKLAD'));
#   doklad.cislo.set(conc(cconc(eq(dokl.get('typ'),1),'V',eq(dokl.get('typ'),2),'P'),dokl.get('cislo')));
# //      doklad.org.set(razitko(dokl.get('org')));
#   doklad.datum.set(sql2date(dokl.get('datum')));
#   doklad.priloh.set(dokl.get('priloh'));
#   doklad.suma.op1.set(cconc(eq(dokl.get('typ'),1),'Vydáno Kč','Přijato Kč'));
#   doklad.suma.celkem.set(dokl.get('castka'));
#   doklad.suma.slovy.set(castka_slovy(dokl.get('castka')));
#   doklad.suma.op2.set(cconc(eq(dokl.get('typ'),1),'Vydáno komu (jméno a adresa)','Přijato od (jméno a adresa)'));
#   doklad.suma.komu.set(dokl.get('komu'));
#   doklad.radek.dalsi.ucel.graft(dokl.get('ucel'));
#   doklad.radek.dalsi.castka.graft(dokl.get('castka'));
#   doklad.dole1.schvalil.set(cconc(eq(dokl.get('typ'),1),'Schválil',eq(dokl.get('typ'),2),''));
#   doklad.dole2.prijem.set(cconc(eq(dokl.get('typ'),1),'Podpis příjemce',eq(dokl.get('typ'),2),''));
#   doklad.report_check
# }
# //    proc razitko (org) {
# //      return(conc(pokl.get(org,'nazev'),'<br>',pokl.get(org,'ulice'),', ',pokl.get(org,'psc'),' ',pokl.get(org,'obec'),
# //      cconc(pokl.get(org,'dic'),conc('<br>DIČ ',pokl.get(org,'dic')))))
# //    }
# proc tiskni (preview) {
#   doklad.report_init;
#   doklad.typ.set(cconc(eq(dd.seznam.typ.get,'V'),'VÝDAJOVÝ POKLADNÍ DOKLAD','PŘÍJMOVÝ POKLADNÍ DOKLAD'));
#   doklad.cislo.set(conc(dd.seznam.typ.get,dd.seznam.cislo.get));
# //      doklad.org.set(razitko(dd.seznam.org_id.get));
#   doklad.datum.set(dd.seznam.datum.get);
#   doklad.priloh.set(dd.seznam.priloh.get);
#   doklad.suma.op1.set(cconc(eq(dd.seznam.typ.get,'V'),'Vydáno Kč','Přijato Kč'));
#   doklad.suma.celkem.set(dd.seznam.castka.get);
#   doklad.suma.slovy.set(castka_slovy(dd.seznam.castka.get));
#   doklad.suma.op2.set(cconc(eq(dd.seznam.typ.get,'V'),'Vydáno komu (jméno a adresa)','Přijato od (jméno a adresa)'));
#   doklad.suma.komu.set(dd.seznam.komu.get);
#   doklad.radek.dalsi.ucel.graft(dd.seznam.ucel.get);
#   doklad.radek.dalsi.castka.graft(dd.seznam.castka.get);
#   doklad.dole1.schvalil.set(cconc(eq(dd.seznam.typ.get,'V'),'Schválil',eq(dd.seznam.typ.get,'P'),''));
#   doklad.dole2.prijem.set(cconc(eq(dd.seznam.typ.get,'V'),'Podpis příjemce',eq(dd.seznam.typ.get,'P'),''));
#   doklad.report_check; doklad.print(preview)
# }
proc dd.seznam.onrowclick () {
  d_day.set(dd.seznam.datum.get)
}
proc reload(cond) {
  d_cond.set(cond); dd.seznam.browse_load(cond,"datum DESC, ident DESC"); dd.seznam.raise('onrowclick')
}
proc d_browse_seek (k) {
  dd.seznam.raise('onrowclick',dd.seznam.browse_seek(conc("id_pokl=",k)))
}
# -------------------------------------------------------------------------------------------------- _d_denik
form _d_denik [,,539,510] {
  view v: table pdenik,
  view p: table pokladna {join:'ON id_pokladna=pdenik.org'},
  browse seznam [3,10,200,200] { rows:24, qry_rows:2, css_rows:'vytisten,0:kas_1'
    show ident [,,80,] { title:'ident', data:pdenik.ident, format:'sq*' },
    show cislo [,,30,] { title:'číslo', data:pdenik.cislo, format:'rsq/' },
    show datum [,,70,] { title:'datum', data:pdenik.datum, format:'rsq/'  },
    show org_id [,,0,] { title:'Org', data:pdenik.org },
    show org [,,10,] { data:p.abbr, format:'r' },
    show typ [,,10,] { data:pdenik.typ, format:'r', sql_pipe:'pipe_pdenik_typ' },
    show priloh [,,0,] { data:pdenik.priloh, format:'rsq/' },
    show castka [,,65,] { title:'částka', data:pdenik.castka, format:'rsq/'  },
    show komu [,,165,] { title:'komu', data:pdenik.komu, format:'sq*'  },
    show ucel [,,140,] { title:'účel', data:pdenik.ucel, format:'sq*'  },
    show kat [,,40,] { title:'kateg.', data:pdenik.kat, format:'sq*'  },
    show vytisten [,,0,] { data:pdenik.vytisten },
    show id [,,0,] { data:pdenik.id_pokl },
  },
#   label [8,8,116,17] { title:'<b>Pokladní deník</b>' },
}
# -------------------------------------------------------------------------------------------------- _d_cmd
form _d_cmd [,,300,20] {
  button oprav [18,-1,100,16] { type:'submit', title:'Opravit', skill:'yp+|yp+' },
  button kopie [123,0,50,17] { title:'Zkopírovat', skill:'yp+|yp+' },
  button novy [214,0,50,17] { title:'Nový', skill:'yp+|yp+' },
  button smazat [272,0,50,17] { title:'Smazat', skill:'yp+|yp+' },
  button tisk1 [381,0,50,17] { title:'Vytisknout', skill:'yp+|yp+' },
  button tiskn [481,0,50,17] { title:'Vytisknout nevytištěné', skill:'yp+|yp+' },
}
# -------------------------------------------------------------------------------------------------- _d_vyber
form _d_vyber [,,294,270] { style:'border:1px solid #f5f5f5;backgroundColor:#ffeed6;MozBorderRadius:5px;zIndex:0'
  var ro_pokl: text
  var rc_dat: text
  var rc_org: text
  var rc_typ: text
  var rc_sta: text
  var rc_goa: text
  radio r_dat [14,13,266,62] { style:'border:1px solid #ddd'
    case [0,3,100,16] { title:'Vše', expr:'1' },
    case [0,23,100,16] { title:'Dne', expr:'2' },
    case [0,43,100,16] { title:'Od', expr:'3' },
    proc onchange () { zmena }
  },
  field rd_dne [74,33,87,20] { help:'dne|výběr jednoho dne', type:'date', format:'t'
    proc onchange () { r_dat.set(2); zmena }
  },
  field rd_od [74,56,87,17] { help:'dne_od|výběr ode dne', type:'date', format:'t'
    proc onchange () { r_dat.set(3); zmena }
  },
  label [169,59,17,17] { title:'Do' },
  field rd_do [188,56,87,17] { help:'dne_do|výběr do dne', type:'date', format:'t'
    proc onchange () { r_dat.set(3); zmena }
  },
  radio r_org [14,78,266,64] { style:'border:1px solid #ddd'
    case [0,3,200,16] { title:'Všechny pokladny', expr:'1' },
    case [0,23,200,16] { title:'Jediná', expr:'2' },
    case [0,43,200,16] { title:'Více', expr:'3' },
    proc onchange () { zmena }
  },
  select ro_1 [140,100,134,17] { type:'map', map_pipe:pokl.abbr, options:pokl.abbr, format:'t',
    help:'pokladna|výběr jedné pokladny',
    proc onchange () { r_org.set(2); ro_pokl.set(this.get); zmena }
  },
  field ro_n [140,121,131,17] { format:'t', help:'pokladny|počáteční písmena pokladen'
    proc onchange () { r_org.set(3); ro_pokl.set(this.get); zmena }
  }
  radio r_typ [14,145,116,65] { style:'border:1px solid #ddd'
    case [0,3,200,16] { title:'Výdaje i příjmy', expr:'1' },
    case [0,23,200,16] { title:'Výdaje', expr:'2' },
    case [0,43,200,16] { title:'Příjmy', expr:'3' },
    proc onchange () { zmena }
  },
  radio r_sta [131,145,149,65] { style:'border:1px solid #ddd'
    case [0,3,200,16] { title:'Všechny', expr:'1' },
    case [0,23,200,16] { title:'Nevytištěné', expr:'2' },
    case [0,43,200,16] { title:'Vytištěné', expr:'3' },
    proc onchange () { zmena }
  },
  radio r_goa [14,213,266,41] { style:'border:1px solid #ddd'
    case [0,3,200,16] { title:'Pro všechny účely', expr:'1' },
    case [0,23,200,16] { title:'Jediný', expr:'2' },
    proc onchange () { zmena }
  },
  select rf_goal [140,234,136,17] { type:'map', map_pipe:p_ucel.hodnota, options:p_ucel.zkratka,
    format:'t', help:'účel|výběr účelu'
    proc onchange () { r_goa.set(2); zmena }
  }
#   button st [207,12,100,13] { title:'standard' },
  proc cond () {
    rc_dat.set(cconc(eq(r_dat.get,1),'1'
      ,eq(r_dat.get,2),conc(" datum='",date2sql(rd_dne.get),"'")
      ,eq(r_dat.get,3),conc(" datum BETWEEN '",date2sql(rd_od.get),"' AND '",date2sql(rd_do.get),"'")));
    rc_org.set(cconc(eq(r_org.get,1),''
      ,eq(r_org.get,2),conc(" AND org='",ro_1.key,"'")
      ,eq(r_org.get,3),conc(" AND LOCATE(left(ident,1),'",ro_n.get,"')>0")));
    rc_typ.set(cconc(eq(r_typ.get,1),'',eq(r_typ.get,2),' AND typ=1',eq(r_typ.get,3),' AND typ=2'));
    rc_sta.set(cconc(eq(r_sta.get,1),'',eq(r_sta.get,2),' AND vytisten=0',eq(r_sta.get,3),' AND vytisten=1'));
    rc_goa.set(cconc(eq(r_goa.get,1),'',eq(r_goa.get,2),conc(" AND LOCATE('",rf_goal.get,"',ucel)")));
    reload(conc(rc_dat.get,rc_org.get,rc_typ.get,rc_sta.get,rc_goa.get))
  }
  proc zmena () {
    set_cookie('p_state','',form,'r_dat,rd_dne,rd_od,rd_do,r_org,ro_1,ro_n,ro_pokl,r_typ,r_sta,r_goa,rf_goal');
    cond
  }
}
# ================================================================================================== d_oprava
panel d_oprava [0,0,482,370] { title:' oprava dokladu', type:'popup', css:'dialog'
  use do: form _d_doklad [2,8,,]
  proc d_load (brw,id) {
    do.brw.set(brw); do.load(id); do.sel_typ.enable(0); do.org.enable(0)
  }
}
# ================================================================================================== d_pridani
panel d_pridani [0,0,482,370] { title:' přidání řádku k dokladu', type:'popup', css:'dialog'
  use d: form _d_doklad [1,1,,]
  proc d_add (id) {
    d.load(id); d.castka.set(0); d.ucel.set(''); d.copy;
    d.datum.enable(0); d.sel_typ.enable(0); d.org.enable(0); d.komu.enable(0);
    d.poznamka.enable(0); d._d_clen.enable(0); d._d_zam.enable(0);
  }
}
# ================================================================================================== d_novy
panel d_novy [0,0,482,370] { title:' nový doklad', type:'popup', css:'dialog'
  use d: form _d_doklad [1,1,,]
  proc d_init (brw,org1,org,typ1) { d.init;
    d.datum.set(now); d.datum.change;
    // pokud je vybrán typ, použij jej
    [ eq(typ1,2,3); d.sel_typ.key(cconc(eq(typ1,2),1,2)); d.sel_typ.change ];
    // pokud je nastavena jediná pokladna použij ji
    [ eq(org1,2); d.org.key(org); d.org.change ];
  }
  proc d_copy (id) {
    d.load(id); d.cislo.set(0); d.vytisten.set(0); d.copy
  }
}
# ================================================================================================== d_zam
panel d_zam [0,0,294,370] { title:' výběr zaměstnance', type:'popup', css:'dialog'
  use dc: form _d_doklad_zam [1,1,,]
  use dcc: form _d_vyber_cmd [1,340,,]
  proc onstart() { dc.zamestnanci.browse_load }
  proc onfocus() { dc.zamestnanci.init_queries }
  proc dcc.vyber.onclick () { panel.hide(dc.zamestnanci.popis.get) }
  proc dc.zamestnanci.onsubmit () { panel.hide(dc.zamestnanci.popis.get) }
  proc dc.zamestnanci.oncancel () { panel.hide(0) }
# -------------------------------------------------------------------------------------------------- _d_doklad_zam
  form _d_doklad_zam [,,260,320] {
    browse zamestnanci [5,20,260,300] { rows:15, qry_rows:1
      show [,,125,] { title:'Příjmení a jméno', expr:"concat(trim(person.prijmeni),' ',trim(person.jmeno))", format:'sq'  },
      show [,,125,] { title:'Obec', data:person.obec, format:'sq' },
      show popis [,,0,] { expr:"concat(trim(person.prijmeni),' ',trim(person.jmeno))" },
      show id [,,0,] { data:person.id_person },
    }
  }
}
# ================================================================================================== d_ucely
panel d_ucely [0,0,294,370] { title:' výběr účelu', type:'popup', css:'dialog'
  use dc: form _d_doklad_ucel [1,1,,]
  use dcc: form _d_vyber_cmd [1,340,,]
  proc onstart() { dc.ucely.browse_load("druh='p_ucel'","poradi") }
  proc onfocus() { dc.ucely.init_queries }
  proc dcc.vyber.onclick () { panel.hide(dc.ucely.popis.get) }
  proc dc.ucely.onsubmit () { panel.hide(dc.ucely.popis.get) }
  proc dc.ucely.oncancel () { panel.hide(0) }
# -------------------------------------------------------------------------------------------------- _d_doklad_ucel
  form _d_doklad_ucel [,,260,320] {
    browse ucely [5,20,260,300] { rows:15, qry_rows:1
      show popis [,,250,] { title:'účel', data:_cis.hodnota, format:'sq'  },
      show id [,,0,] { data:_cis.id_cis },
    }
  }
}
# ================================================================================================== společné
# -------------------------------------------------------------------------------------------------- _d_vyber_cmd
form _d_vyber_cmd [,,260,30] {
  button vyber [227,0,50,17] { type:'submit', title:'Vybrat' },
  button [152,0,50,17] { title:'storno',
    proc onclick () { panel.hide(0); stop}
  }
}
# -------------------------------------------------------------------------------------------------- _d_doklad
form _d_doklad [,,419,354] {
  var brw: number,
  var result: text,
  label [6,20,100,20] { title:'Datum' },
  label [6,41,50,17] { title:'Číslo' },
  label [325,23,50,17] { title:'Typ' },
  label [181,41,50,17] { title:'Částka' },
  label [181,21,50,17] { title:'Organizace' },
  label [327,43,50,17] { title:'Kategorie' },
  label [7,62,50,17] { title:'Komu' },
  label [9,112,50,17] { title:'Účel' },
  label [6,161,50,17] { title:'Poznámka' },
  label [277,315,50,17] { title:'Příloh' },
  field datum [74,19,95,17] { type:'date', data:pdenik.datum },
  field typ [74,38,13,17] { format:'d' },
  field cislo [90,38,79,17] { data:pdenik.cislo, format:'d' },
  select org [253,20,61,17] { type:'map', data:pdenik.org, options:pokl.abbr, map_pipe:pokl.abbr }
  select sel_typ [392,20,73,17] { type:'map', data:pdenik.typ, options:p_typ.hodnota
    proc onchange () { typ.set(p_typ.get(this.key,'zkratka')) }
  },
  field castka[252,39,62,17] { data:pdenik.castka },
  field kat [392,40,73,17] { data:pdenik.kat },
  edit komu [73,62,334,46] { data:pdenik.komu },
  edit ucel [73,111,335,47] { data:pdenik.ucel },
  edit poznamka [73,161,391,143] { data:pdenik.poznamka },
  field priloh [320,311,24,17] { data:pdenik.priloh },
  check vytisten [391,313,73,17] { data:pdenik.vytisten },
  label [415,315,73,17] { title:'vytištěno' },
  button save [422,338,45,20] { type:'submit', title:'OK', help:'uložit změny',
    proc onclick () { _d_save }
  },
  proc _d_save () { _d_save1; panel.hide(form.key); stop }
  proc _d_save1 () {
    form.same; stop
  | form.key; form.save; brw.get.browse_seek
  | form.make('p_pdenik_insert','save:datum,castka,kat,komu,ucel,poznamka,priloh,vytisten;load:cislo'
    ,sel_typ.key,org.key,pokl.get(org.key,'abbr'),datum.get);
    form.load; den.d_browse_seek(form.key)
  }
  button cancel [316,337,45,20] { title:'Storno', help:'zadané údaje neukládat, vrátit se zpět'
    proc onclick () { panel.hide(0); stop}
  },
#   button _d_clen [411,63,50,17] { title:'člen >'
#     proc onclick () { result.set(d_clen.modal(584,100)); result.get; komu.set(result.get); komu.change }
#   },
  button _d_zam [411,87,50,17] { title:'zam >'
    proc onclick () { result.set(d_zam.modal(584,100)); result.get; komu.set(result.get); komu.change }
  },
  button [411,138,50,17] { title:'účel >'
    proc onclick () { result.set(d_ucely.modal(584,100)); result.get; ucel.set(result.get); ucel.change }
  },
  # události
  proc onsubmit () { _d_save }
  proc onload () { typ.set(p_typ.get(sel_typ.key,'zkratka')) }
}
# ================================================================================================== REPORTY
# -------------------------------------------------------------------------------------------------  DOKLAD
report doklad [10,10,180,267] { format:'A4'
  const P= 3.4, const Q= 56, const W= 132, const W3= 44,
  const A= 15, const B= 13,
  const H= 5,

#   box        [ 0,  0, 190, 277] { css:'O', title:'layout A4' },
  box        [ 0,  0, 20, H] { css:'text', title:'Organizace' },
  box typ    [ Q,  0, 70, H] { css:'bold', title:'PŘÍJMOVÝ POKLADNÍ DOKLAD' },
  box org    [ 0,  4, 40,  H+H] { css:'org', title:'firma' },
  box        [ Q,  H,  6, H] { css:'text', title:'č.:' },
  box cislo  [ ^,  ~, 10, H] { css:'bold', title:'Pnnnn' },
  box        [ Q,  ^, 12, H] { css:'text', title:'Ze dne' },
  box datum  [ ^,  ~, 23, H] { css:'bold', title:'dd.mm.rrrr' },
  box        [ ^,  ~, 19, H] { css:'text', title:'Přílohy:' },
  box priloh [ ^,  ~,  7, H] { css:'bold', title:'nn' },
  box suma   [ 0, A, W, $] { css:'O', xactive,
    box op1    [ P, 2, 19, H] { css:'text', title:'Přijato Kč' },
    box celkem [ ^, ~, 23, H] { css:'bold', title:'kkkkk.hh' },
    box        [ Q, ~, 13, H] { css:'text', title:'Slovy:' },
    box slovy  [ ^, ~, 60, H] { css:'bold', title:'k.........cettisíck......setk.....' },
    box op2    [ P, ^+2, 55, H] { css:'text', title:'Přijato od (jméno a adresa)' },
    box komu   [ Q, ~, 73, *] { css:'bold', title:'Jméno Příjmení, Obec, Ulice č., \n(číslo)' },
  }
  box radek  [ 0, ^, W, $+4] { css:'U', xactive
    box        [ P, 2+^,100, H] { css:'text', title:'Účel' },
    box        [ ^, ~, 25, H] { css:'numb', title:'Částka' },
    box dalsi  [ 0, ^, W, $] {
      box ucel   [ P, ^,100, H] { css:'bold', title:'Účel' },
      box castka [ ^, ~, 25, H] { css:'br', title:'Částka' },
    }
  }
  box dole1  [ 0, ^, W3,  B] { css:'U', xactive
    box schvalil [ P, 2, 40, H] { css:'text', title:'Schválil' },
  }
  box dole2  [ ^, ~, W3,  B] { css:'_', xactive
    box prijem [ P, 2, 20, H] { css:'text', title:'Podpis příjemce' },
  }
  box        [ ^, ~, W3,  B] { css:'U', xactive
    box        [ P, 2, 20, H] { css:'text', title:'Podpis pokladníka' },
  }
}
