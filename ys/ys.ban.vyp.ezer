# Systém FiS - modul Banka.Výpisy
# (c) 2008-2010 Martin Šmídek <martin@smidek.eu>

# ================================================================================================== vyp - Výpisy

var v_id: number
var d_kat: text

use vy: form _vypisy [8,4,,]
use pr: form _prevody [8,190,,]
use s: form _b_sumy [470,151,,]
use v: form _b_vyber [620,7,,]
use d: form _b_detail [624,185,,]
use cp: form _b_cmd_prevod [846,414,,]
use cv: form _b_cmd_vypis [606,530,,]

// zobrazení daného převodu v kontextu výpisu
// procedura volaná z HLEDÁNÍ a z ÚČTY
# proc show_vypis (v_ident,p_ident) {
#   vyp.focus;
#   vy.vypisy.browse_focus;
#   v_id.set(vy.vypisy.browse_seek(conc("ident='",v_ident,"'"))); v_id.get;
#   pr.prevody.browse_load(conc("ident LIKE '",v_ident,"%'"));
#   pr.prevody.browse_seek(conc("ident='",p_ident,"'"));
#       echo('převod ',p_ident,' ... ale ',pr.prevody.id.get);
#   pr.prevody.focus; pr.prevody.browse_focus;
# #   s.make('bank_sumy','load:obrat,dary',v_ident);
# #   dzmena
# | alert('Hledaný převod je mimo omezení ve Výpisy')
# }

proc onstart () { b_vypis_filter.set(get_cookie("b_vypis_filter",'M'));
  b_prevod_filter.set(get_cookie("b_prevod_filter",'123456789'));
  v.obdobi.key(get_cookie("b_vypis_obdobi",1304)); v.b_init;
  vy.vypisy.browse_focus;
#     hledani.h_init; ucty.u_init;
}

# proc pzmena (p_ident) {
#   pr.prevody.raise('onrowclick',pr.prevody.browse_seek(conc("IDENT='",p_ident,"'"),
#     conc("ident LIKE '",vy.vypisy.ident.get,"%' AND LOCATE(TYP,' ",b_prevod_filter.get,"')")));
#   s.make('bank_sumy','load:obrat,dary',vy.vypisy.ident.get)
# }
proc pzmena0 () {
  vy.vypisy.browse_count;
  pr.prevody.browse_load(conc("ident LIKE '",vy.vypisy.ident.get,"%' AND LOCATE(TYP,' ",b_prevod_filter.get,"')"));
  pr.prevody.browse_focus;
  s.make('bank_sumy','load:obrat,dary',vy.vypisy.ident.get); pr.dzmena
| pr.prevody.browse_init; d.init; s.obrat.set(''); s.dary.set('')
}
# proc d.popkat.onclick () {
#   d_kat.set(PopKat.modal(160,100));
#   d_kat.get; d.kat.set(d_kat.get); d.kat.change
# }
proc the_formsave (f,b) {
   f.same
 | f.key; f.save; f.load; b.browse_seek
 | f.insert; f.load;
   b.raise('onrowclick',b.browse_seek(conc(f.id_key,'=',f.key)))
 }

# -------------------------------------------------------------------------------------------------- _vypisy
form _vypisy [,,578,400] { tabindex:1000
  label [2,5,137,17] { title:'<b>Bankovní výpisy</b>' },
  browse vypisy [1,24,300,200] {type:'smart', buf_rows:100, rows:6, qry_rows:1, css_rows:'color,vyp_n,vyp_r,vyp_p'
    show color [,,0,] { data:vypis.ident, sql_pipe:'bank_vypis_icss' },
    show ident [,,60,] { title:'ident', data:vypis.ident, format:'qsr' },
    show soubor [,,65,] { title:'soubor', data:vypis.soubor, format:'qs' },
    show datum [,,70,] { title:'datum', data:vypis.datum, format:'qs-r' },
    show stav_poc [,,70,] { title:'počáteční', data:vypis.stav_poc, format:'sr', skill:'fbc' },
    show stav [,,70,] { title:'stav', data:vypis.stav, format:'sr' },
    show obrat [,,70,] { title:'obrat', expr:'stav-stav_poc', format:'r', skill:'fbc' },
    show id [,,0,] { data:vypis.id_vypis },
    proc onrowclick () { }
  }
  proc onready () { pzmena0 }
}
# -------------------------------------------------------------------------------------------------- _prevody
form _prevody [,,578,400] { tabindex:1000
  label [3,0,255,17] { title:'<b>Převody nastaveného výpisu</b>' },
  browse prevody [2,20,300,200] {type:'smart', buf_rows:100, rows:17, qry_rows:1
    show ident [,,80,] { title:'ident', data:prevod.ident, format:'qs+r' },
    show typ [,,20,] { title:'typ', data:prevod.typ, format:'qsr' },
    show ss [,,70,] { title:'ssym', data:prevod.ssym, format:'qsr' },
    show kat [,,30,] { title:'kat', data:prevod.kat, format:'qsr' },
    show popis [,,130,] { title:'popis', data:prevod.popis, format:'qs' },
    show poznamka [,,150,] { title:'poznamka', data:prevod.poznamka, format:'qs' },
    show castka [,,80,] { title:'částka', data:prevod.castka, format:'qsr' },
    show id [,,0,] { data:prevod.id_prevod },
    proc onrowclick () { }
  },
  proc dzmena () {
    pr.prevody.browse_active; d.load(pr.prevody.id.get)
  }
  proc onready () { dzmena }
}
# -------------------------------------------------------------------------------------------------- _b_sumy
form _b_sumy [,,139,59] { tabindex:0,
  label [3,3,130,56] { title:'', css:'ae_info' }
  field obrat [42,37,79,17] { help:'obrat', format:'ro' },
  field dary [42,17,79,17] { help:'dary', format:'ro' },
  label [26,17,50,17] { title:'dary' },
  label [23,36,50,18] { title:'obrat' },
}
# -------------------------------------------------------------------------------------------------- _b_vyber
form _b_vyber [,,322,115] { tabindex:5000,
  label [1,2,348,108] { title:'', css:'ae_parm' }
  select obdobi [234,26,102,17] { type:'map', map_pipe:omezeni.hodnota, options:omezeni.zkratka,
      format:'t', help:'omezení|výběr zobrazeného období'
    proc onchange () { vzmena }
  }
  check chY [10,47,71,17] { title:'YS', format:'t'
    proc onchange () { vzmena }
  }
  check chD [10,66,71,17] { title:'DS', format:'t'
    proc onchange () { vzmena }
  }
  check chIn [262,64,63,17] { title:'příjmy', format:'t'
    proc onchange () {
      b_prevod_filter.set(cconc(v.chOut.get,'1234',v.chIn.get,'56789'));
      set_cookie("b_prevod_filter",b_prevod_filter.get); pzmena0
    }
  }
  check chOut [262,83,69,17] { title:'výdaje', format:'t'
    proc onchange () {
      b_prevod_filter.set(cconc(v.chOut.get,'1234',v.chIn.get,'56789'));
      set_cookie("b_prevod_filter",b_prevod_filter.get); pzmena0
    }
  }
	proc b_init () {
	  cset(b_vypis_filter.get,v.chY,'Y',v.chD,'D');
	  cset(b_prevod_filter.get,v.chOut,'1234',v.chIn,'56789');
	  vzmena
	}
	proc vzmena () {
	  b_vypis_filter.set(cconc(v.chY.get,'M',v.chD.get,'D'));
	  set_cookie("b_vypis_filter",b_vypis_filter.get);
	  set_cookie("b_vypis_obdobi",v.obdobi.key);
	  vy.vypisy.browse_load(conc("LOCATE(LEFT(IDENT,1),' ",b_vypis_filter.get,"') AND ",v.obdobi.get));
	  vy.vypisy.raise('onrowclick')
	}
  label [12,27,50,17] { title:'Reiffensenbank', style:'fontWeight:bold' },
  label [11,7,320,17] { title:'<b>Omezení zobrazených výpisů a převodů</b>' },
}
# -------------------------------------------------------------------------------------------------- _b_detail
form _b_detail [,,282,197] { tabindex:2000,
  view dp: table prevod,
  view dpu: table ucet {join_type:'LEFT', join:'ON dpu.protiucet=dp.protiucet AND dpu.banka=dp.banka'},
  label [0,24,337,69] { title:'', css:'ae_info' } //style:'border:1px solid #f5f5f5;backgroundColor:#f5f5f5;MozBorderRadius:5px;zIndex:0' },
  field clen [111,30,120,17] { data:dp.clen, format:'ro' },
  field splatnost [234,30,94,17] { data:dp.splatnost, format:'o' },
  field vsym [111,50,78,17] { data:dp.vsym, format:'ro' },
  field ksym [192,50,52,17] { data:dp.ksym, format:'ro' },
  field ssym [247,50,81,17] { data:dp.ssym, format:'ro' },
  field castka [111,70,60,17] { data:dp.castka, format:'ro' },
  field protiucet [174,70,120,17] { data:dp.protiucet, format:'ro' },
  field banka [295,70,33,17] { data:dp.banka, format:'ro' },
  label [0,103,335,112] { title:'', css:'ae_info' } //, style:'border:1px solid #f5f5f5;backgroundColor:#f5f5f5;MozBorderRadius:5px;zIndex:0' },
  field nazev [109,112,216,17] { data:dpu.nazev, format:'o' },
  field popis [109,132,216,17] { data:dp.popis, format:'o'  },
  field poznamka [109,152,216,17] { data:dp.poznamka, format:'o'  },
  field kat [230,172,77,17] { data:dp.kat },
  select [230,192,98,17] { data:dp.typ, options:b_typ.hodnota, type:'map' },
  label [29,31,77,17] { title:'dárce, datum' },
  label [36,53,72,17] { title:'VS, KS, SS' },
  label [35,74,77,17] { title:'částka, účet' },
  label [18,113,92,17] { title:'náš název účtu' },
  label [41,133,92,17] { title:'název účtu' },
  label [44,153,62,17] { title:'poznámka' },
  label [37,173,190,17] { title:'zařazení převodu podle kategorií' },
  label [2,4,222,17] { title:'<b>Detail nastaveného převodu</b>' },
  label [47,192,208,17] { title:'klasifikace převodu podle typu' },
#   button popkat [310,172,20,17] { title:'?', css:'button_small' },
}
# -------------------------------------------------------------------------------------------------- _b_cmd_vypis
form _b_cmd_vypis [,,300,20] { tabindex:4000,
  button import [18,0,100,16] { title:'Přidej výpisy', help:'provést import nových bankovních informací'
    proc onclick () {
      alert('bylo importováno ',ask('bank_import','*',0,1,''),' nových výpisů'); v.vzmena
  } }
  button oprav_vypis [128,0,100,16] { skill:'fbc|fbc', title:'Opravit výpis', help:'odstranit nastavený výpis a provést znovu import'
    proc onclick () {
      alert('bylo opraveno ',ask('bank_import',vy.vypisy.soubor.get,1,0,'vypisy'),' výpisů (převody zůstaly)');
      pzmena0
  } }
  button import0 [240,0,50,16] { title:'Zjistit výpisy', help:'zjistit jaké výpisy budou importovány'
    proc onclick () {
      alert('výpisy pro import: ',ask('bank_import0','*',0,1)); v.vzmena
  } }
  button oprav_prevod [0,24,100,16] { skill:'fbc|fbc', title:'Opravit převody', help:'odstranit převody nastaveného výpisu a provést znovu import'
    proc onclick () {
      alert('bylo opraveno ',ask('bank_import',vy.vypisy.soubor.get,1,0,'prevody'),' výpisů a jejich převodů');
      pzmena0
  } }
  button dopln_prevod [120,24,100,16] { skill:'fbc|fbc', title:'Doplnit převody', help:'doplnit převody nastaveného výpisu'
    proc onclick () {
      alert('bylo doplneno ',ask('bank_import',vy.vypisy.soubor.get,1,0,'missing'),' převodů');
      pzmena0
  } }
  button smaz_soubor [240,24,100,16] { skill:'fbc|fbc', title:'Smazat soubor', help:'vymazat výpisy a převody vložené z nastaveného souboru'
    proc onclick () {
      confirm(conc('Odstranit výpisy a převody souboru ',vy.vypisy.soubor.get,' ze dne ',vy.vypisy.datum.get,'?'));
      alert(ask('bank_import_remove',vy.vypisy.soubor.get,vy.vypisy.datum.get)); v.vzmena
  } }
#   button tiskni [223,0,50,16] { title:'Vytisknout výpis', help:'vytisknout nastavený výpis' },
}
# -------------------------------------------------------------------------------------------------- _b_cmd_prevod
form _b_cmd_prevod [,,115,20] { tabindex:3000,
  button oprav [20,2,100,16] { title:'Uložit změny', type:'submit', help:'změnit poznámku, název účtu, typ nebo kategorii'
    proc onclick () {
      the_formsave(d,pr.prevody)
  } }
}
# ================================================================================================== PopKat
# panel PopKat [0,0,502,369] { title:' výběr kategorie', type:'popup', css:'dialog'
#   use k: form _kat [1,17,,],
# }

map omezeni: table _cis {where:"druh='b_omezeni'", order:'poradi'}

map b_typ: table _cis {where:"druh='b_typ'", order:'poradi', key_id:'zkratka'}
