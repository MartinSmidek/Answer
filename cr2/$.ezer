# Systém Ans(w)er pro CPR Ostrava
# (c) 2008-2021 Martin Šmídek <martin@smidek.eu>

menu cr2 { type:'main', title:'cr2', active:*, _sys:'*'
  func onstart() { php.table_lock('none'); } // uvolni všechny zámky řádků tabulek přihlášeného 
  var global: text,
  tabs akce2 {title:"[fa-database] Databáze", _sys:'*', include:'onload,db2.akce2', active:*, skill:'ca'
    proc reaccess() {}
    panel track { type:'popup'
      proc back_show(kde,klic) {}
    }
  }
  tabs syst {title:"[fa-cog] Systém", _sys:'sys',  include:'onload,ezer2.syst', active:*
    proc reaccess() {}
    panel oso {type:'right', title:'Osobní nastavení', _sys:'*', include:'onclick,ezer2.pers' }
    panel dat {type:'right', title:'[fa-database] Databáze', _sys:'*', include:'onload,ezer3.db', skill:'a'
      par:{
        infos: [
          {title:'[fa-object-group] Schema databáze',
           html: "Schema databáze<br><br><img src='db2/img/schema_db_2015.png'>"}
        ],
        tables: {
          akce:  "id_duakce>*,nazev,datum_od",
          pobyt: "id_pobyt>*,id_akce>akce,i0_rodina>rodina,hnizdo,funkce",
          spolu: "id_spolu>-,id_pobyt>pobyt,id_osoba>osoba,s_hnizdo,s_role",
          osoba: "id_osoba>*,jmeno,prijmeni",
          tvori: "id_tvori>-,id_osoba>osoba,id_rodina>rodina,role",
          rodina:"id_rodina>*,nazev",
          dopis: "id_dopis>*,hnizdo,nazev",
          mail:  "id_mail>*,id_dopis>dopis,id_clen>osoba"
        },
        css: 'stat' // styl tabulek
    }}
    panel nas {type:'right', title:'Data', _sys:'nas', skill:'a'
      menu {type:'left', active:*
        menu {title:'Přehled',type:'group', _sys:'*'
          item {title:'Záznamy', par:°{db:'cr2',org:'4'}
            proc onclick (i) {
              clear;
              info.fill(conc(i.owner.title,' - ',i.title),'... probíhá výpočet ...');
              info.fill('',ask('db2_info',i.par)) }}
        }
        menu {title:'Hromadné opravy dat',type:'group', _sys:'*'
          item {title:'rodinné pobyty (i0_rodina)'      ,par:°{opravit:0,cmd:'i0_rodina'}, skill:'m'}
          item {title:'... provést UPDATE'              ,par:°{opravit:1,cmd:'i0_rodina'}, skill:'m'}
          item {title:'definice UMI podle FUNKCE'       ,par:°{opravit:0,cmd:'umi'},       skill:'m'}
          item {title:'... provést UPDATE'              ,par:°{opravit:1,cmd:'umi'},       skill:'m'}
          item {title:'schema 2014 - osobni kontakty'   ,par:°{opravit:0,cmd:'kontakty'},  skill:'m'}
          item {title:'... provést UPDATE'              ,par:°{opravit:1,cmd:'kontakty+'}, skill:'m'}
          item {title:'schema 2014 - osobni adresy'     ,par:°{opravit:0,cmd:'adresy'},    skill:'m'}
          item {title:'... provést UPDATE'              ,par:°{opravit:1,cmd:'adresy+'},   skill:'m'}
          proc onclick (i) {
            clear;
            info.fill(conc(i.owner.title,' - ',i.title),' ... probíhá oprava dat ...');
            info.fill('',ask('db2_oprava_dat',i.par));
          }
        }
        menu {title:'Kontroly konzistence dat',type:'group', _sys:'*'
          item {title:'kontroly databáze - test'        ,par:°{opravit:0,test:'test'}, skill:'m'}
          item {title:'kontroly databáze - test+oprava' ,par:°{opravit:1,test:'test'}, skill:'m'}
          item {title:'kontroly databáze'               ,par:°{opravit:0}}
          item {title:'kontroly databáze s opravou'     ,par:°{opravit:1}, skill:'m'}
          proc onclick (i) {
            clear;
            info.fill(conc(i.owner.title,' - ',i.title),' ... probíhá kontrola dat ...');
            info.fill('',ask('db2_kontrola_dat',i.par));
          }
        }
        menu {title:'Testovací databáze',type:'group', _sys:'*'
          item {title:'obnovit testovací db z ostré', par:°{db:'cr2'}
            proc onclick (i) {
              clear;
              confirm("vytvořit znovu testovací databázi z aktuálního stavu ostré databáze?");
              info.fill(conc(i.owner.title,' - ',i.title),' ... probíhá update ...');
              info.fill('',ask('db2_copy_test_db',i.par.db)); }}
        }
      }
      use info: form right [12,4,,]
    }
  }
  tabs doc  {title:"[fa-question] Nápověda", _sys:'*',  include:'onload,ezer2.help' }
  tabs off  {title:"[fa-power-off] Odhlásit", type:'logoff' }
  // přepnutí access - refresh formulářů
  proc spinner(on) {
    on;
    function("$('wait_mask').setStyle('display','block'); return 1;");
  | function("$('wait_mask').setStyle('display', 'none'); return 1;");
  }
  proc reaccess(msg) {
//    echo(msg); 
//    set_trace('E',1,'spinner,reaccess');
    spinner(1);
    akce2.reaccess;
    syst.reaccess;
    spinner(0); 
//    set_trace('E',0,'spinner,reaccess');
  }
  // univerzální procedury
  proc the_formsave (f,b) {
    f.same
  | f.key; f.save; f.load; b.browse_seek
  | f.insert; f.load;
    b.raise('onrowclick',b.browse_seek(conc(f.key_id,'=',f.key)))
  }

# ===========================================================================================> TRACK
# přehled provedených změn
# panel track [,,540,276] { title:'Historie oprav', type:'popup', css:'dialog'
#   var reverted_table= '', reverted_key= 0
#   use back: form _back [0,16,,] { tabindex:20 },
#   proc back_show(kde,klic) {
#     panel.popup(0,1);
#     panel.set_attrib('title',conc('Historie oprav pro ',kde,' id=',klic));
#     reverted_table.set(''); reverted_key.set(0);
#     back.lst.browse_load(conc("(kde='",kde,"' AND klic=",klic,")"),"kdy DESC");
#     back.lst.raise('onrowclick')
#   }
#   # ------------------------------------------------------------- _back
#   # id_track,kdy,kdo,kde,klic,zmena:fld,op,val,old
#   form _back [,,255,250] {
#     browse lst [0,0,150,100] { rows:12,
#       show kdy [,,90,] { title:'kdy', data:_track.kdy, sql_pipe:'sql_time1' }
#       show kdn [,,30,] { title:'kdn', data:_track.kdo }
#       show op [,,12,] { title:'?', data:_track.op }
#       show kde [,,40,] { title:'tabulka', data:_track.kde }
#       show fld [,,60,] { title:'položka', data:_track.fld }
#       show old [,,160,] { title:'původní hodnota', data:_track.old, format:'t' }
#       show val [,,120,] { title:'změněná hodnota', data:_track.val, format:'t' }
#       show id_track [,,0,] { data:_track.id_track }
#       menu { type:'context'
#         item { title:'vrátit nastavenou opravu'
#           proc onclick() {
#             lst.selected('clear'); lst.selected('toggle',1); revert }}
#         item { title:'vrátit i související opravy'
#           proc onclick() { var ret:object
#             lst.selected('clear');
#             ret.set(ask('track_like',lst.browse_key));
#             { ret.ok; lst.selected('set',ret.ids); [ revert ]
#             | alert(ret.msg) }
#           }}
#       }
#       proc revert() { var ret:object
#         confirm('mám zkusit vrátit opravy označených řádků?');
#         ret.set(ask('track_revert',lst.selected('get',0)));
#         { ret.ok; warning("změny byly vráceny");
#           reverted_table.set(ret.tab); reverted_key.set(ret.klic);
#           lst.browse_refresh;
#         | alert(ret.msg) }
#       }
#     },
#     label [0,250,545,] { title:"ÚDAJE: i=vložení (insert), u=úprava, U= hromadná úprava<div style='float:right'>V=vrácení spojených záznamů</div><br>ZÁZNAM: x=smazano, o=obnoveno, d=vzniklo spojením duplicitních záznamů, r=různé od uvedeného záznamu" }
#   }
# }



}
// univerzální formulář pro levostranné menu
form right [,,700,50] {
  label info [0,0,680,600] { title:'' }
  label head [0,0,680,50]  { title:'' }
  label note [0,50,680,550] { title:'' }
  proc fill(x,y) {
    [ x; head.set(conc("<div class='karta'>",x,"</div>")) ];
    [ y; note.set(y) ]
  }
  proc append(x) { note.get; note.set(conc(note.get,'<br>',x)) | note.set(x) }
}

table akce { key_id:'id_duakce'
  number id_duakce
  number typ
}

table join_akce { // spojení číselníku akcí s MS
  number g_rok     { help:"rok akce podle číselníku akcí" }
  number g_kod     { help:"kód akce podle číselníku akcí" }
  text   source    { help:"L:Lídino M:Milošovo MS.EXE" }
  number akce      { help:"staré číslo akce podle MS.EXE" }
}

# kopie číselníků akcí z intranetu YS
table uakce { key_id:'id_uakce'
  number id_uakce
  number id_akce
  text ms_source
  number ms_akce
  number rok
  text akce
  text nazev_akce
  date datum { sql_pipe:'sql_date1' }
  number dnu
  number osob
  number typ
  text kapitola
}

table ms_akce { key_id:'id_akce'  // Sey
  number id_akce
  text source
  text nazev
  number akce
  number druh
  number poradatel
  date datum_od        // SQL tvar se používá v ys.akce
  date datum_do
  text misto
}

table g_akce { key_id:'id_gakce' // kopie číselníku akcí z intranetu
  number id_gakce
  number g_rok     { help:"rok akce podle číselníku akcí" }
  number g_kod     { help:"kód akce podle číselníku akcí" }
  text   g_nazev   { help:"název akce z číselníku akcí" }
  date   g_od      { help:"začátek akce z číselníku akcí", sql_pipe:'sql_date1' }
  date   g_do      { help:"ukončení akce z číselníku akcí", sql_pipe:'sql_date1' }
  number g_ucast   { help:"počet účastníků akce z číselníku akcí" }
  text   g_typ     { help:"typ akce z číselníku akcí" }
  text   g_kap     { help:"účetní kapitola akce z číselníku akcí" }
}

table mail { key_id:'id_mail'
  number id_mail { key:'primary' },
  number id_dopis { help:'mail|index mailu' }, // nepoužívá se pro dopisy s druh!='@'
  number id_davka
  number id_znacka                             // nepoužívá se pro dopisy s druh=='@'
  number id_clen
  text znacka
  text email
  number stav
  text msg
  text body
}

table dopis { key_id:'id_dopis'
  number id_dopis { key:'primary' },
  number id_davka { help:'dávka|číslo rozesílání dávky dopisů' },
  text nazev
  text prilohy
  number potvrzeni { help:"potvrzení|vyžádat zaslání potvrzení o přečtení" }
  number kopie	{ help:"poslat slepou kopii 'odesílateli'" }
  text druh { help:'druh|D-dopis, S-samolepka,legitimace, N-nesamostatná složka' },
  text typ { help:'typ|značka dopisu' },
  number nw
  number nh
  number komu     // pouze pro maily
  number pocet    // pouze pro maily
  text report { help:'vzor|identifikátor reportu' },
  number aktualni { help:'aktualni|text dopisu je připraven k tisku' },
  date datum { help:'datum|vročení dopisu', sql_pipe:'sql_date1' },
  text obsah { help:'obsah|text dopisu' },
  text var_list { help:'seznam proměnných'}
  text post_vars { help:'seznam proměnných počítaných po generování'}
  text nest_list { help:'seznam složek (druh=N)'}
  text add_list
}

table _user { key_id:'id_user', db:'ezer_system'
  number id_user
  text deleted,
  text abbr,
  text skills,
  text options { help:'privátní nastavení|ve formátu JSON' }
  text username,
  text password,
  number state,
  text forename,
  text surname,
  text login,
  text history,
  text sess_id,
  text sess_time,
  text sess_data,
  number zmena_kdo,
  date zmena_kdy {  },
}

table _file_ {
  text fid
  date fdate
  text fname
  text ftype
  text finfo
}

table _cis { key_id:'id_cis'
  number id_cis text druh, text data, text zkratka, number poradi,
  text hodnota, text popis, text barva, text ikona
}

map ms_akce_clen:     table _cis {where:"druh='ms_akce_clen'",     order:'poradi', key_id:'data'}
map ms_akce_pouze:    table _cis {where:"druh='ms_akce_pouze'",    order:'poradi', key_id:'data'}
map ms_akce_funkce:   table _cis {where:"druh='ms_akce_funkce'",   order:'poradi', key_id:'data'}
map ms_akce_prednasi: table _cis {where:"druh='ms_akce_prednasi'", order:'poradi', key_id:'data'}
map ms_akce_cirkev:   table _cis {where:"druh='ms_akce_cirkev'",   order:'poradi', key_id:'data'}
map ms_akce_vzdelani: table _cis {where:"druh='ms_akce_vzdelani'", order:'poradi', key_id:'data'}
map ms_akce_platba:   table _cis {where:"druh='ms_akce_platba'",   order:'poradi', key_id:'data'}

