var the_order: number
var authorized: number
var ds_web: number

proc order_load (ord) {
  echo("ord=",ord); the_order.set(ord); pobyt.precti; ucastnici.precti
}

proc onstart () {
  ds_web.set(sys('ezer','options','ds_web'));
  { ds_web.get; authorized.set(0);  ucastnici.to_show.set(0); cast;
    the_order.set(javascript("Ezer.parm[1]")); pobyt.precti
  | ucastnici.to_show.set(1); ucastnici.formular
  }
}

proc autorizace {
  authorized.set(p.modal(200,20)); echo('authorized=',authorized.get);
}

panel p [,,200,70] { type:'popup', title:'Dialog'
  use v: form f [0,0,,]
  form f [,,200,70] {
    label [0,10,200,40] { title:"Napište prosím PIN, který Vám byl pro tuto akci zaslaný správcem Domu setkání při potvrzení objednávky:" }
    field pin [0,55,50,] { value:'6' }
    button [100,55,80,17] { title:'Pokračovat',
      proc onclick() { panel.hide(ask('pin_test',the_order.get,pin.get)) }
    }
  }
}

proc cast { ds_web.get; javascript("window.resizeTo(720,330);",1) }

proc cely { ds_web.get; javascript("window.resizeTo(720,680);",1); pobyt.popis.ukaz.display(0); }

// ================================================================================================= pobyt
// ukáže a obslouží horní polovinu formuláře
group pobyt {

  use popis: form zahlavi [0,0,,]
  use obj: form objednavka [10,30,,]
  use fak: form fakturace [380,30,,]
  // zobrazení objednávky
  proc precti  {
    popis.tit.set(conc('<h3>Objednávka číslo ',the_order.get,'</h3>')); obj.load(the_order.get);
    fak.load(the_order.get); fak.faktura.display(0); ucastnici.precti(obj.rooms.get)
  }
  proc obj.save.onclick() {
    obj.save; order_load(the_order.get)
  }
  // účast objednatele na pobytu
  proc objednavatel_save(ucast,changed) {
    fak.save; fak.load; echo('objednavatel_save:',ucast,':',changed);
    [ changed; ucast; echo(ask('ds_ucast_objednatele',the_order.get,ucast)) ];
    ucastnici.obnova
  }
  // vystavení zálohy na objednávku
  proc make_zaloha () {
    fak.faktura.set(ask('ds_zaloha',the_order.get)); fak.faktura.display(1)
  }
  // vystavení faktur pro rodiny na objednávce
  proc make_faktury () {
    fak.faktura.set(ask('ds_faktury',the_order.get)); fak.faktura.display(1)
  }
  // porovnání objednávky se seznamem účastníků
  var comp: text
  var fields: text
  proc compare () {
    comp.set(ask('ds_compare',the_order.get)); debug(comp.get,'compare');
    form_set(obj.get,comp.get('form')); obj.info.set(comp.get('html'))
  }
  proc ukaz_spodek {
    ucastnici.to_show.get
  | ucastnici.to_show.set(1); ucastnici.formular; ucastnici.precti(obj.rooms.get)
  }
  // ------------------------------------------------------------------ záhlaví
  form zahlavi [,,600,30] { css:'ds_form'
    label tit [ 10,  0,300,] { title:'<h3>Objednávka pobytu</h3>' }
    button pin [180,2,50,18] { title:'pin',  skill:'yd|yd', help:'zobrazí PIN umožňující objednateli zapsat a opravovat účastníky',
      proc onclick() {
        alert(ask('pin_make',the_order.get))
      }
    }
    button ukaz [245,2,50,18] { title:'zobraz účastníky', help:'umožní zapsat a opravit seznam účastníků'
      proc onclick() {
        autorizace; authorized.get; cely; ukaz_spodek
      | alert("Bohužel, toto není správný PIN")
      }
    }
    label [380,  0,300,] { title:'<h3>Fakturační adresa objednavatele</h3>' }
  }
  // ------------------------------------------------------------------ OBJEDNÁVKA
  form objednavka [,,300,100] { css:'ds_form'
    const PL=0; PT=90,
    label       [  0,  0,100,] { title:'stav objednávky' }
    select      [  0, 15,150,] { type:'map', data:tab.tx_gnalberice_order.state, options:tab.ds_stav.hodnota }
    label       [180,  0,200,] { title:'poznámka k objednávce' }
    edit        [180,15,164,57] { data:tab.tx_gnalberice_order.note  }
    label       [  0, 40, 70,] { title:'příjezd' }
    field       [  0, 55, 80,] { type:'date', data:tab.tx_gnalberice_order.fromday }
    label       [ 90, 40, 70,] { title:'odjezd' }
    field       [ 90, 55, 80,] { type:'date', data:tab.tx_gnalberice_order.untilday }
    label       [  0, 80,150,] { title:'seznam pokojů:' }
    check   p1  [PL+  0, PT,14,] { title:'1',  format:'c', css:'CheckT', help:"1 - 2 lůžka jednoduchá (V podkrovní pokojíček se střešním oknem)" }
    check   p2  [PL+ 20, PT,14,] { title:'2',  format:'c', css:'CheckT', help:"2 - 2 lůžka jako patrová postel (V pokojíček vedle sprchy, má svoje umyvadlo)" }
    check   p3  [PL+ 40, PT,14,] { title:'3',  format:'c', css:'CheckT', help:"3 - 2 lůžka jednoduchá (JZ pokojíček v rohu)" }
    check   p4  [PL+ 60, PT,14,] { title:'4',  format:'c', css:'CheckT', help:"4 - 2 lůžka jednoduchá a 2 lůžka jako patrová postel (Z pokoj se dvěma okny)" }
    check   p5  [PL+ 80, PT,14,] { title:'5',  format:'c', css:'CheckT', help:"5 - 1 lůžko jednoduché a 2 lůžka jako patrová postel (Z pokoj s jedním oknem)" }
    check   p6  [PL+100, PT,14,] { title:'6',  format:'c', css:'CheckT', help:"6 - 2 lůžka jednoduchá a 4 lůžka jako patrová postel (S pokoj velký)" }
    check   p7  [PL+120, PT,14,] { title:'7',  format:'c', css:'CheckT', help:"7 - palanda a lůžko, nový pokoj vedle koupelny" }
    check   p8  [PL+140, PT,14,] { title:'8',  format:'c', css:'CheckT', help:"8 - komfortní pokoj se sprchou a WC, dvoulůžko" }
    check   p9  [PL+160, PT,14,] { title:'9',  format:'c', css:'CheckT', help:"9 - komfortní pokoj se sprchou a WC, dvoulůžko" }
    check   p10 [PL+180, PT,14,] { title:'10', format:'c', css:'CheckT', help:"10 - 2 postele jednoduché (Z pokoj mezi kaplí a sprchami)" }
    check   p11 [PL+200, PT,14,] { title:'11', format:'c', css:'CheckT', help:"11 - 2 jednoduché postele a 2 patrové postele a 1 rozkládací gauč (S pokoj velký)" }
    check   p12 [PL+220, PT,14,] { title:'12', format:'c', css:'CheckT', help:"12 - 4 lůžka jednoduchá (SZ pokoj s vlastním umyvadlem a el.průtokovým ohřívačem)" }
    check   p13 [PL+240, PT,14,] { title:'13', format:'c', css:'CheckT', help:"13 - komfortní pokoj se sprchou a WC, 2 samostatné postele" }
    check   p14 [PL+260, PT,14,] { title:'14', format:'c', css:'CheckT', help:"14 - komfortní pokoj se sprchou a WC, 2 samostatné postele" }
    check   p15 [PL+280, PT,14,] { title:'15', format:'c', css:'CheckT', help:"15 - apartmán; 2 místnosti a sociálka bezbariérová: 2 lůžka jednoduchá" }
    check   p16 [PL+300, PT,14,] { title:'16', format:'c', css:'CheckT', help:"16 - apartmán; 2 místnosti a sociálka bezbariérová: 1 lůžko jednoduché a 2 lůžka jako patrová postel" }
    field noroom [PL+330,PT+5,17,] { format:'dr:e' }
    field rooms { data:tab.tx_gnalberice_order.rooms }
    label       [  0,120, 50,] { title:'dospělí' }
    field       [0,135,17,] { data:tab.tx_gnalberice_order.adults, format:'r:e' }
    field adults [24,135,17,] { format:'dr:e' }
    label       [54,120,50,] { title:'děti 10-15' }
    field       [54,135,17,] { data:tab.tx_gnalberice_order.kids_10_15, format:'r:e' }
    field kids_10_15 [78,135,17,] { format:'dr:e' }
    label       [110,120,40,] { title:'děti 3-9' }
    field       [111,135,17,] { data:tab.tx_gnalberice_order.kids_3_9, format:'r:e' }
    field kids_3_9 [135,135,17,] { format:'dr:e' }
    label       [165,120,50,] { title:'děti do 3' }
    field       [165,135,17,] { data:tab.tx_gnalberice_order.kids_3, format:'r:e' }
    field kids_3 [190,135,17,] { format:'dr:e' }
    label       [220,120,17,] { title:'??' }
    field nevek [220,135,17,] { format:'dr:e' }
    label       [252,120,100,] { title:'typ stravy' }
    select      [250,135,100,] { type:'map', data:tab.tx_gnalberice_order.board, options:tab.ds_strava.hodnota }
    button      [250,160,100,18] { title:'lze fakturovat?'
      proc onclick() { compare } }
    button save [249,186,100,18] { title:'zapsat změny', type:'submit' }
    label info  [1,159,238,53] { title:'' }
    proc onload () { rooms_check(rooms,form,'p') }
    proc onsave () { check_rooms(rooms,form,'p') }
  }
  // ------------------------------------------------------------------ FAKTURACE
  form fakturace [,,300,100] { css:'ds_form'
    label       [  0,  0,210,] { title:'název organizace (plátce faktury)' }
    field       [  0, 15,210,] { data:tab.tx_gnalberice_order.org }
    label       [220,  0, 80,] { title:'IČ' }
    field       [220, 15, 80,] { data:tab.tx_gnalberice_order.ic }
    label       [  0, 40,100,] { title:'příjmení' }
    field       [  0, 55,120,] { data:tab.tx_gnalberice_order.name }
    label       [130, 40,100,] { title:'jméno' }
    field       [130, 55, 80,] { data:tab.tx_gnalberice_order.firstname }
    label       [220, 40, 80,] { title:'účastní se' }
    check ucast [218, 54, 80,] { title:'pobytu', data:tab.tx_gnalberice_order.ucast }
    label       [  0, 80,200,] { title:'e-mail' }
    field       [  0, 95,210,] { data:tab.tx_gnalberice_order.email  }
    label       [220, 80,100,] { title:'telefon' }
    field       [220, 95, 80,] { data:tab.tx_gnalberice_order.telephone}
    label       [  0,120, 70,] { title:'ulice' }
    field       [0,135,210,] { data:tab.tx_gnalberice_order.address }
    label       [  0,160, 70,] { title:'psč' }
    field       [  0,175, 50,] { data:tab.tx_gnalberice_order.zip}
    label       [ 60,160, 70,] { title:'obec' }
    field       [ 60,175,150,] { data:tab.tx_gnalberice_order.city}
    label faktura [223,119,120,] { title:'...' }
    button      [220,132,100,18] { title:'vystavit zálohu'
      proc onclick() { make_zaloha } }
    button      [220,159,100,18] { title:'vystavit faktury'
      proc onclick() { make_faktury } }
    button      [220,185,100,18] { title:'zapsat změny'
      proc onclick() { objednavatel_save(ucast.get,ucast.changed) } }
  }
}

// ================================================================================================= účastníci
// ukáže a obslouží spodní polovinu formuláře tj. zobrazení účastníků akce
// vnější proměnná the_order obsahuje číslo objednávky

group ucastnici {

  var to_show: number                           // 1 => zobrazovat
  var akc: form
  var per: form
#   use per: form osoba [380,280,,]

  // zobrazení formuláře účastníků
  proc formular {
#     to_show.get; akc.set(new_form(akce,10,280)); //per.set(new_form(osoba,380,280));
    to_show.get; akc.set(new_form(akce,10,280)); per.set(new_form(osoba,380,280));
    new_form(zahlavi,0,240)
  }

  proc precti (pokoje) {
    to_show.get; per.pokoj.selects(conc('0,',pokoje));
    akc.seznam.browse_load(conc('id_order=',the_order.get));
    akc.seznam.raise('onrowclick')
  }

  // obnoví seznam po vnější změně
  proc obnova {
    akc.seznam.browse_seek
  }

  // zobrazení účastníka
  proc osoba_load (id_osoba) {
    per.load(id_osoba)
  }
  // vložení nového účastníka
  proc osoba_new {
    per.init; per.id_order.set(the_order.get); per.insert;
    akc.seznam.browse_seek
  }
  // vložení nového dítěte účastníka
  proc osoba_new_child (id_osoba) {
    per.init; per.id_order.set(the_order.get);
    per.rodina.set(akc.seznam.rodina.get);
    per.prijmeni.set(akc.seznam.prijmeni.get);
    per.ulice.set(akc.seznam.ulice.get);
    per.psc.set(akc.seznam.psc.get);
    per.obec.set(akc.seznam.obec.get);
    per.insert;
    akc.seznam.browse_seek
  }
  // zápis opravy účastníka
  proc osoba_save {
    per.save; per.load; akc.seznam.browse_seek
  }
  // zrušení účastníka
  proc osoba_del (id_osoba) {
    confirm(conc('Opravdu smazat ',akc.seznam.prijmeni.get,' ',akc.seznam.jmeno.get,' ze seznamu?'));
    tab.ds_osoba.delete_record(conc('id_osoba=',id_osoba));
    akc.seznam.browse_seek
  }
  // ------------------------------------------------------------------ záhlaví
  form zahlavi [,,600,30] { css:'ds_form'
    label [ 10,0,694,] { title:'<hr>' }
    label [ 10,10,300,] { title:'<h3>Seznam účastníků pobytu</h3>' }
    label [380,10,300,] { title:'<h3>Údaje vybraného účastníka</h3>' }
  }
  // ------------------------------------------------------------------ AKCE
  form akce [,,300,100] { css:'ds_form'
    button [  0,0,100,18] { title:'přidat účastníka'
      proc onclick () { osoba_new} }
    button [122,0,100,18] { title:'přidat dítě'
      proc onclick () { osoba_new_child} }
    button [210,0,100,18] { title:'odebrat účastníka'
      proc onclick () { osoba_del(seznam.id_osoba.get)} }
    browse seznam [0,30,,] { rows:14
      show rodina [,,20,] { title:'rod', data:tab.ds_osoba.rodina, format:'s+' }
      show prijmeni [,,70,] { title:'příjmení', data:tab.ds_osoba.prijmeni, format:'s' }
      show jmeno    [,,50,]  { title:'jméno', data:tab.ds_osoba.jmeno, format:'s' }
      show vek  [,,20,]  { title:'věk', expr:"if(ds_osoba.narozeni,year(now())-year(ds_osoba.narozeni),'??')", format:'r' }
      show [,,20,]  { title:'pok.', data:tab.ds_osoba.pokoj, format:'rs:e' }  // :e zobrazí 0 jako nic
      show [,,20,]  { title:'lůž.', data:tab.ds_osoba.luzko, map_pipe:tab.ds_luzko.zkratka, format:'rs' }
      show [,,20,]  { title:'str.', data:tab.ds_osoba.strava, map_pipe:tab.ds_strava.zkratka, format:'rs' }
      show [,,30,]  { title:'od', data:tab.ds_osoba.fromday, format:'s:d.m' }
      show [,,30,]  { title:'do', data:tab.ds_osoba.untilday, format:'s:d.m' }
      show [,,20,]  { title:'?', data:tab.ds_osoba.pozn, format:'s' }
      // nezobrazené
      show id_osoba { data:tab.ds_osoba.id_osoba }
      show ulice { data:tab.ds_osoba.ulice }
      show psc { data:tab.ds_osoba.psc }
      show obec { data:tab.ds_osoba.obec }
      proc onrowclick() { osoba_load(id_osoba.get) }
    }
  }
  // ------------------------------------------------------------------ OSOBA
  form osoba [,,300,100] { css:'ds_form'
    field id_osoba { data:tab.ds_osoba.id_osoba }
    field id_order { data:tab.ds_osoba.id_order }
    // zobrazení
    label       [  0,  0, 40,] { title:'rodina' }
    field rodina   [  0, 15, 40,] { data:tab.ds_osoba.rodina }
    label       [ 50,  0, 70,] { title:'příjmení' }
    field prijmeni [ 50, 15, 70,] { data:tab.ds_osoba.prijmeni }
    label       [130,  0,100,] { title:'jméno' }
    field       [130, 15, 80,] { data:tab.ds_osoba.jmeno }
    label       [220,  0,100,] { title:'narozen/a' }
    field       [220,15,86,] { type:'date', data:tab.ds_osoba.narozeni }
    label       [  0, 40,200,] { title:'e-mail' }
    field       [  0, 55,210,] { data:tab.ds_osoba.email  }
    label       [220, 40,100,] { title:'telefon' }
    field       [220, 55, 80,] { data:tab.ds_osoba.telefon}
    label       [  0, 80, 70,] { title:'ulice' }
    field ulice [  0, 95,210,] { data:tab.ds_osoba.ulice}
    label       [  0,120, 70,] { title:'psč' }
    field psc [  0,135, 50,] { data:tab.ds_osoba.psc}
    label       [ 60,120, 70,] { title:'obec' }
    field obec [ 60,135,150,] { data:tab.ds_osoba.obec}
    label       [  0,160,300,] { title:'--- <i>upřesnění údajů o pobytu</i>' }
    label       [  0,175, 70,] { title:'pokoj' }
    select pokoj [  0,190, 50,] { data:tab.ds_osoba.pokoj, format:':e' }
    label       [ 60,175, 70,] { title:'typ lůžka' }
    select luzka [ 60,190,150,] { type:'map', data:tab.ds_osoba.luzko, options:tab.ds_luzko.hodnota }
    label       [  0,215, 70,] { title:'poznámka' }
    field       [  0,230,210,] { data:tab.ds_osoba.pozn }
    button      [220,229,100,18] { title:'zapsat změny',
      proc onclick() { osoba_save } }
    // specifické údaje
    label       [0,255,327,] { title:'--- <i>údaje o pobytu (jen pokud se liší od údajů v objednávce)</i> ---' }
    label       [0,272,70,] { title:'příjezd' }
    field   od  [0,287,83,] { type:'date', data:tab.ds_osoba.fromday }
    label       [90,272,70,] { title:'odjezd' }
    field   do  [90,287,83,] { type:'date', data:tab.ds_osoba.untilday }
    label       [181,272,97,] { title:'typ stravy' }
    select      [181,287,133,] { type:'map', data:tab.ds_osoba.strava, options:tab.ds_strava.hodnota }
  }
}
