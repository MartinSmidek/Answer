# Systém Ans(w)er pro YMCA Familia a YMCA Setkání
# (c) 2008-2010 Martin Šmídek <martin@smidek.eu>

menu db2 { type:'main', title:'db2', active:*, _sys:'*'
  var global: text,
  tabs pok {title:"[fa-eur] Pokladna", _sys:'*',  include:'onclick,db2.pok', active:*, skill:'yp' }
  tabs eko {title:"[fa-stack-overflow] Ekonomika", _sys:'*',  include:'onclick,ys2.eko', active:*, skill:'ye' }
  tabs ds {title:"[fa-home] Dům", _sys:'*',  include:'onclick', active:*, skill:'yd' }
  tabs akce2 {title:"[fa-database] Databáze", _sys:'*', include:'onload,db2.akce2', active:*, skill:'fa;ya;mrop'
    proc reaccess() {}
    panel track { type:'popup'
      proc back_show(kde,klic) {}
    }
  }
//  tabs syst {title:"[fa-cog] Systém", _sys:'sys'
//    proc reaccess() {}
//    panel oso {type:'right', title:'Osobní nastavení', _sys:'*', include:'onload' }
//  }
  tabs syst {title:"[fa-cog] Systém", _sys:'sys',  include:'onload,ezer2.syst', active:*
    proc reaccess() {}
    panel oso {type:'right', title:'Osobní nastavení', _sys:'*', include:'onclick,ezer2.pers' }
    panel nas {type:'right', title:'Data', _sys:'nas', skill:'a'
      proc track_osoba(ido)  { akce2.track.back_show('osoba',ido) }
      proc track_rodina(idr) { akce2.track.back_show('rodina',idr) }
      proc smaz_tvori(idt)   {
        confirm("smazat záznam ",idt," v tabulce tvori?");
        ask('query',conc("DELETE FROM tvori WHERE id_tvori=",idt));
      }
      menu m {type:'left', format:'f+'
        menu {title:'Přehled',type:'group', _sys:'*'
          item {title:'Záznamy', par:°{db:'ms2',org:'3'}
            proc onclick (i) {
              clear;
              info.fill(conc(i.owner.title,' - ',i.title),'... probíhá výpočet ...');
              info.fill('',ask('db2_info',i.par)) }}
        }
        menu s  {title:'Struktura dat', type:'group', skill:'yamg;famg;camg;aa;a'
          item {title:'[fa-object-group] Přehled struktury dat'   par:°{img:'2015'} }
          item {title:'[fa-object-group] Podrobná struktura dat'  par:°{img:'2019'} }
          item {title:'[fa-database] tabulka AKCE',               par:°{tab:'akce'} }
          item {title:'[fa-database] tabulka POBYT',              par:°{tab:'pobyt'} }
          item {title:'[fa-database] tabulka SPOLU',              par:°{tab:'spolu'} }
          item {title:'[fa-database] tabulka OSOBA',              par:°{tab:'osoba'} }
          item {title:'[fa-database] tabulka TVORI',              par:°{tab:'tvori'} }
          item {title:'[fa-database] tabulka RODINA',             par:°{tab:'rodina'} }
          item {title:'[fa-database] tabulka MAILIST',            par:°{tab:'mailist'} }
          item {title:'[fa-database] tabulka DOPIS',              par:°{tab:'dopis'} }
          item {title:'[fa-database] tabulka MAIL',               par:°{tab:'mail'}  }
          item {title:'[fa-database] tabulka CENIK',              par:°{tab:'cenik'} }
          item {title:'[fa-database] tabulka DAR',                par:°{tab:'dar'} }
          item {title:'[fa-database] tabulka DOTAZ'               par:°{tab:'dotaz'}  }
          item {title:'[fa-database] tabulka PSC_AXY'             par:°{tab:'psc_axy'}  }
          item {title:'[fa-database fa-red] tabulka EZER_DOC2'    par:°{tab:'ezer_doc2'}  skill:'m' }
          item {title:'[fa-database fa-red] tabulka _CIS'         par:°{tab:'_cis'}       skill:'m' }
          item {title:'[fa-database fa-red] tabulka _GEOCODE'     par:°{tab:'_geocode'}   skill:'m' }
          item {title:'[fa-database fa-red] tabulka _HELP'        par:°{tab:'_help'}      skill:'m' }
          item {title:'[fa-database fa-red] tabulka _SKILL'       par:°{tab:'_skill'}     skill:'m' }
          item {title:'[fa-database fa-red] tabulka _TOUCH'       par:°{tab:'_touch'}     skill:'m' }
          item {title:'[fa-database fa-red] tabulka _TRACK'       par:°{tab:'_track'}     skill:'m' }
          item {title:'[fa-database fa-red] tabulka _USER'        par:°{tab:'_user'}      skill:'m' }
          item {title:'[fa-database fa-darkviolet] tabulka #stat'  par:°{tab:'`#stat`'}   skill:'faa:m' }
          item {title:'[fa-database fa-darkviolet] tabulka #psc'   par:°{tab:'`#psc`'}    skill:'faa:m' }
          item {title:'[fa-database fa-darkviolet] tabulka #obec'  par:°{tab:'`#obec`'}   skill:'faa:m' }
          item {title:'[fa-database fa-darkviolet] tabulka #okres' par:°{tab:'`#okres`'}  skill:'faa:m' }
          item {title:'[fa-database fa-darkviolet] tabulka #kraj'  par:°{tab:'`#kraj`'}   skill:'faa:m' }
          proc onclick (i) {
            clear;
            panel.display(2,'i');
            info.fill(replace_fa(conc(i.owner.title,' - ',i.title)),' ');
            { i.par.tab;  
              info.append(ask('i_doc_table_struct',i.par.tab,0));
            | info.append(conc("<br>Schema databáze pro <b>Answer</b>
                s uvedením podmínek pro spojení pomocí JOIN<br><br>
                <img src='db2/img/schema_db_",i.par.img,".png' />"));
            }
          }
        }
        menu { title:'Import a záloha databáze',type:'group',skill:'m'
          item { title:"[fa-question] přehled záloh"          par:°{typ:'listing',direct:'1'}}
          item { title:"[fa-upload] zálohuj teď"              par:°{typ:'special',direct:'0'}}
          proc onclick(i) {
            info.fill(replace_fa(conc(i.owner.title,' - ',i.title)),' ');
            info.append(ask('sys_backup_make',i.par)) 
          }
          item { title:"obnova ze zálohy (jen local)" par:°{typ:'restore',direct:'1'}
            proc onclick(i) { var heslo:text
              info.fill(conc(i.owner.title,' - ',i.title),' ');
              eq(sys('ezer','server'),0,1); // test na lokální server na NTB nebo Synology DOMA
              confirm('Opravdu mám lokální databázi přepsat?'); 
              info.append(ask('sys_backup_make',i.par))
            | alert('obnovu ze zálohy lze provádět pouze na lokální kopii systému');
            }
          }
        }
        menu { title:'Export a import polí',type:'group',skill:'m'
          item { title:"[fa-download] export adres pro geo / osoba"  par:°{tab:'osoba'}}
          item { title:"[fa-download] export adres pro geo / rodina"  par:°{tab:'rodina'}}
          func onclick(i:ezer) { var own:ezer
            own= i.owner();
            info.fill(replace_fa(`${own.title} - ${i.title}`),' ');
            info.append(php.mapa2_geo_export(i.par)) 
          }
        }
        menu o {title:'Ostrá databáze',type:'group', _sys:'*'
          item {title:'Stav sjednocování kopií', par:°{db:'db2'}
            proc onclick (i) {
              panel.display(2,'i');
              info.fill(replace_fa(conc(i.owner.title,' - ',i.title)),'... probíhá výpočet ...');
              info.fill('',ask('db2_stav',i.par.db)) }}
          item {title:'obnova pobyt.web_changes' 
            proc onclick() { 
              panel.display(2,'i');
              confirm('opravit pobyt.web_changes ?'); ask('update_web_changes') 
          }}
//          item {title:'smazat platební údaje LK2019' skill:'m'
//            proc onclick() { confirm('smazat platební údaje LK2019 ?'); ask('clear_platby_lk2019') }
//          }
        }
        menu k {title:'Kontroly konzistence dat',type:'group', _sys:'*'
          item {title:'kontroly databáze - test'        ,par:°{opravit:0,test:'test'}, skill:'m'}
          item {title:'kontroly databáze - test+oprava' ,par:°{opravit:1,test:'test'}, skill:'m'}
          item {title:'kontroly databáze'               ,par:°{opravit:0}}
          item {title:'kontroly databáze s opravou'     ,par:°{opravit:1}, skill:'m'}
          proc onclick (i) {
            panel.display(2,'i');
            info.fill(replace_fa(conc(i.owner.title,' - ',i.title)),' ... probíhá kontrola dat ...');
            info.fill('',ask('db2_kontrola_dat',i.par));
          }
        }
        menu t {title:'Testovací databáze',type:'group', _sys:'*'
          item {title:'stav sjednocování kopií', par:°{db:'db2_test'}
            proc onclick (i) {
              panel.display(2,'i');
              info.fill(replace_fa(conc(i.owner.title,' - ',i.title)),'... probíhá výpočet ...');
              info.fill('',ask('db2_stav',i.par.db)) }}
          item {title:'obnovit testovací db z ostré', par:°{db:'db2'}
            proc onclick (i) {
              panel.display(2,'i');
              confirm("vytvořit znovu testovací databázi z aktuálního stavu ostré databáze?");
              info.fill(replace_fa(conc(i.owner.title,' - ',i.title)),' ... probíhá update ...');
              info.fill('',ask('db2_copy_test_db',i.par.db)); }}
        }
      }
      use info: form right [12,4,,]     { tag:'i' }
    }
    panel str {type:'right', title:'Aplikace', _sys:'str', skill:'a;aa'
      menu m {type:'left', format:'f+'
        menu { title:'GitHub',type:'group',skill:'m'
          item { title:'[fa-question] Answer: git status'  par:°{folder:'.',op:'cmd',cmd:'status'}}
          item { title:'[fa-gear] Answer: git log'         par:°{folder:'.',op:'cmd',cmd:'log'}}
          item { title:'[fa-gear] Answer: git pull'        par:°{folder:'.',op:'cmd',cmd:'pull'}}
          item { title:'[fa-gear] Answer: git fetch ...'   help:"po fetch>git reset --hard origin/master"
                                                           par:°{folder:'.',op:'cmd',cmd:'fetch'}}
          item { title:'[fa-question] Ezer: git status' par:°{folder:'ezer',op:'cmd',cmd:'status'}}
          item { title:'[fa-gears] Ezer: git log'       par:°{folder:'ezer',op:'cmd',cmd:'log'}}
          item { title:'[fa-gears] Ezer: git pull'      par:°{folder:'ezer',op:'cmd',cmd:'pull'}}
          item { title:'[fa-gears] Ezer: git fetch ...' help:"po fetch>git reset --hard origin/master"
                                                        par:°{folder:'ezer',op:'cmd',cmd:'fetch'}}
          proc onclick (i) {  
            panel.display(2,'i'); 
            info.fill(replace_fa(conc(i.owner.title,' - ',i.title)),ask('git_make',i.par));
          }
        }
        menu m {title:'Struktura modulů a funkcí',type:'group'
          item {title:'Ezer moduly'         proc onclick () { clear; info.Set(ask('doc_ezer')) } }
          item {title:'PHP moduly'          proc onclick () { clear; info.Set(ask('doc_php')) } }
          item {title:'Seznam PHP funkcí'   proc onclick () { clear; info.Set(ask('doc_called')) } }
        }
      }
      use info: form right [12,4,,]     { tag:'i' }
    }
  }
  tabs doc  {title:"[fa-question] Nápověda", _sys:'*',  include:'onload,ezer2.help' }
  tabs off  {title:"[fa-power-off] Odhlásit", type:'logoff' }
  // přepnutí access - refresh formulářů
  proc spinner(on) {
    [ function('on',"waiting(on);",on) ];
  }
  proc reaccess(msg) {
    echo("jsou přístupná data ",msg); //set_trace('E',1,'spinner,reaccess');
    spinner(1);
    akce2.reaccess;
    syst.reaccess;
    spinner(0); //set_trace('E',0,'spinner,reaccess');
  }
  // univerzální procedury
  proc the_formsave (f,b) {
    f.same
  | f.key; f.save; f.load; b.browse_seek
  | f.insert; f.load;
    b.raise('onrowclick',b.browse_seek(conc(f.key_id,'=',f.key)))
  }

}
// univerzální formulář pro levostranné menu
form right [,,*,*] {
  label info [0,0,*,600] { title:'' style:'padding-right:10px'}
  label head [0,0,*,50]  { title:'' style:'padding-right:10px'}
  label note [0,50,,*] { title:'' style:'padding-right:10px;width:calc(100% - 15px)'}
  proc Set(x) {
    head.set(''); note.set(''); info.set(x)
  }
  proc fill(x,y) {
    info.set('');
    [ x; head.set(conc("<div class='karta'>",x,"</div>")) ];
    [ y; note.set(y) ]
  }
  proc append(x) { note.get; note.set(conc(note.get,'<br>',x)) | note.set(x) }
}

table akce { key_id:'id_duakce'
  number id_duakce
  number typ
  number statistika
}

table join_akce { // spojení číselníku akcí s MS
  number g_rok     { help:"rok akce podle číselníku akcí" }
  number g_kod     { help:"kód akce podle číselníku akcí" }
  text   source    { help:"L:Lídino M:Milošovo MS.EXE" }
  number akce      { help:"staré číslo akce podle MS.EXE" }
}

# kopie číselníků akcí z intranetu YS
table uakce { key_id:'id_uakce'
  number id_uakce
  number id_akce
  text ms_source
  number ms_akce
  number rok
  text akce
  text nazev_akce
  date datum { sql_pipe:'sql_date1' }
  number dnu
  number osob
  number typ
  text kapitola
}

table ms_akce { key_id:'id_akce'  // Sey
  number id_akce
  text source
  text nazev
  number akce
  number druh
  number poradatel
  date datum_od        // SQL tvar se používá v ys.akce
  date datum_do
  text misto
}

table g_akce { key_id:'id_gakce' // kopie číselníku akcí z intranetu
  number id_gakce
  number g_rok     { help:"rok akce podle číselníku akcí" }
  number g_kod     { help:"kód akce podle číselníku akcí" }
  text   g_nazev   { help:"název akce z číselníku akcí" }
  date   g_od      { help:"začátek akce z číselníku akcí", sql_pipe:'sql_date1' }
  date   g_do      { help:"ukončení akce z číselníku akcí", sql_pipe:'sql_date1' }
  number g_ucast   { help:"počet účastníků akce z číselníku akcí" }
  text   g_typ     { help:"typ akce z číselníku akcí" }
  text   g_kap     { help:"účetní kapitola akce z číselníku akcí" }
}

table mail { key_id:'id_mail'
  number id_mail { key:'primary' },
  number id_dopis { help:'mail|index mailu' }, // nepoužívá se pro dopisy s druh!='@'
  number id_davka
  number id_znacka                             // nepoužívá se pro dopisy s druh=='@'
  number id_clen
  text znacka
  text email
  number stav
  text msg
  text body
}

table dopis { key_id:'id_dopis'
  number id_dopis { key:'primary' },
  number id_davka { help:'dávka|číslo rozesílání dávky dopisů' },
  text nazev
  text prilohy
  number potvrzeni { help:"potvrzení|vyžádat zaslání potvrzení o přečtení" }
  number kopie	{ help:"poslat slepou kopii 'odesílateli'" }
  text druh { help:'druh|D-dopis, S-samolepka,legitimace, N-nesamostatná složka' },
  text typ { help:'typ|značka dopisu' },
  number nw
  number nh
  number komu     // pouze pro maily
  number pocet    // pouze pro maily
  text report { help:'vzor|identifikátor reportu' },
  number aktualni { help:'aktualni|text dopisu je připraven k tisku' },
  date datum { help:'datum|vročení dopisu', sql_pipe:'sql_date1' },
  text obsah { help:'obsah|text dopisu' },
  text var_list { help:'seznam proměnných'}
  text post_vars { help:'seznam proměnných počítaných po generování'}
  text nest_list { help:'seznam složek (druh=N)'}
  text add_list
}

table _user { key_id:'id_user', db:'ezer_system'
  number id_user
  text deleted,
  text abbr,
  text skills,
  text options { help:'privátní nastavení|ve formátu JSON' }
  text username,
  text password,
  number state,
  text forename,
  text surname,
  text login,
  text history,
  text sess_id,
  text sess_time,
  text sess_data,
  number zmena_kdo,
  date zmena_kdy {  },
}

table _file_ {
  text fid
  date fdate
  text fname
  text ftype
  text finfo
}

table _cis { key_id:'id_cis'
  number id_cis text druh, text data, text zkratka, number poradi,
  text hodnota, text popis, text barva, text ikona
}

map ms_akce_clen:     table _cis {where:"druh='ms_akce_clen'",     order:'poradi', key_id:'data'}
map ms_akce_pouze:    table _cis {where:"druh='ms_akce_pouze'",    order:'poradi', key_id:'data'}
map ms_akce_funkce:   table _cis {where:"druh='ms_akce_funkce'",   order:'poradi', key_id:'data'}
map ms_akce_prednasi: table _cis {where:"druh='ms_akce_prednasi'", order:'poradi', key_id:'data'}
map ms_akce_cirkev:   table _cis {where:"druh='ms_akce_cirkev'",   order:'poradi', key_id:'data'}
map ms_akce_vzdelani: table _cis {where:"druh='ms_akce_vzdelani'", order:'poradi', key_id:'data'}
map ms_akce_platba:   table _cis {where:"druh='ms_akce_platba'",   order:'poradi', key_id:'data'}

