# Systém Ans(w)er pro YMCA Familia a YMCA Setkání
# (c) 2008-2021 Martin Šmídek <martin@smidek.eu>

menu db2 { type:'main', title:'db2', active:*, _sys:'*'
  func onstart() { php.table_lock('none'); } // uvolni všechny zámky řádků tabulek přihlášeného 
  var global: text,
  tabs pok {title:"[fa-eur] Pokladna", _sys:'*',  include:'onclick,db2.pok', active:*, skill:'yp' }
  tabs eko {title:"[fa-stack-overflow] Ekonomika", _sys:'*',  include:'onclick,ys2.eko', active:*, skill:'ye' }
  tabs ds {title:"[fa-home] Dům", _sys:'*',  include:'onclick', active:*, skill:'yd' }
  tabs akce2 {title:"[fa-database] Databáze", _sys:'*', include:'onload,db2.akce2', active:*, skill:'fa;ya;mrop'
    proc reaccess() {}
    panel track { type:'popup'
      proc back_show(kde,klic) {}
    }
  }
  tabs syst {title:"[fa-cog] Systém", _sys:'sys',  include:'onload,ezer2.syst', active:*
    proc reaccess() {}
    panel oso {type:'right', title:'Osobní nastavení', _sys:'*', include:'onclick,ezer2.pers' }
    panel dat {type:'right', title:'[fa-database] Schema DB', _sys:'*', include:'onload,ezer3.db', skill:'a;ben'
      par:{
        infos: [
          {title:'[fa-object-group] Schema databáze',
           html: "Schema databáze<br><br><img src='db2/img/schema_db_2019.png'>"},
          {title:'[fa-object-group] ... zjednodušené', 
           html: "Schema databáze<br><br><img src='db2/img/schema_db_2015.png'>"}
        ],
        tables: {
          akce:  "id_duakce>*,nazev,datum_od",
          pobyt: "id_pobyt>*,id_akce>akce,i0_rodina>rodina,hnizdo,funkce",
          uhrada:"id_uhrada>*,id_pobyt>pobyt,u_poradi,u_castka,u_datum,u_zpusob,u_stav,u_za",
          spolu: "id_spolu>-,id_pobyt>pobyt,id_osoba>osoba,s_hnizdo,s_role",
          osoba: "id_osoba>*,jmeno,prijmeni",
          osoba_geo: "id_osoba>osoba,kod_obec,stav",
          tvori: "id_tvori>-,id_osoba>osoba,id_rodina>rodina,role",
          rodina:"id_rodina>*,nazev",
          dopis: "id_dopis>*,hnizdo,nazev",
          mail:  "id_mail>*,id_dopis>dopis,id_clen>osoba",
          dotaz: "id_dotaz>*,dotaznik,id,page,hnizdo"
        },
        css: 'stat' // styl tabulek
    }}
    panel nas {type:'right', title:'Data', _sys:'nas', skill:'a'
      proc track_osoba(ido)  { akce2.track.back_show('osoba',ido) }
      proc track_rodina(idr) { akce2.track.back_show('rodina',idr) }
      proc smaz_tvori(idt)   {
        confirm("smazat záznam ",idt," v tabulce tvori?");
        ask('query',conc("DELETE FROM tvori WHERE id_tvori=",idt));
      }
      menu m {type:'left', format:'f+'
        menu {title:'Přehled',type:'group', _sys:'*'
          item {title:'Záznamy', par:{org:'3',cmd:'show'}}
          item {title:'Ulož ezer_answer.db_osoba', par:{org:'3',cmd:'save'}, skill:'m'}
          item {title:'Doplň geolokace - Šmídek', par:{cond:"prijmeni='Šmídek'"}, skill:'a'}
          item {title:'Doplň geolokace - iniciovaní', par:{cond:'ISNULL(stav) AND iniciace!=0'}, skill:'a'}
          item {title:'... opravy', par:{errata:1,cond:'NOT ISNULL(stav) AND stav<0'}, skill:'a'}
          func onclick (i:ezer) {
            clear(); info.display(0,'t'); info.header(i);
            info.fill('','... probíhá výpočet ...');
            if (eq(i.par.cmd,'show','save'))
              info.fill('',php.db2_info(i.par)) 
            else {
               Proces.Start({x:10,y:1,par:i.par},"Lokalizace adres") 
            }
          }
          item {title:'TEST url', skill:'m'
            func onclick (i:ezer) {
              clear(); info.display(1,'t'); info.header(i);
            }
          }
        }
        menu { title:'Import a záloha databáze',type:'group',skill:'m'
          item { title:"[fa-question] přehled záloh"          par:°{typ:'listing',direct:'1'}}
          item { title:"[fa-upload] zálohuj teď"              par:°{typ:'special',direct:'0'}}
          proc onclick(i) {
            clear; info.display(0,'t');
            info.fill(replace_fa(conc(i.owner.title,' - ',i.title)),' ');
            info.append(ask('sys_backup_make',i.par)) 
          }
          item { title:"obnova ze zálohy (jen local)" par:°{typ:'restore',direct:'1'}
            proc onclick(i) { var heslo:text
              clear; info.display(0,'t');
              info.fill(conc(i.owner.title,' - ',i.title),' ');
              eq(sys('ezer','server'),0,1); // test na lokální server na NTB nebo Synology DOMA
              confirm('Opravdu mám lokální databázi přepsat?'); 
              info.append(ask('sys_backup_make',i.par))
            | alert('obnovu ze zálohy lze provádět pouze na lokální kopii systému');
            }
          }
        }
        menu { title:'Export a import polí',type:'group',skill:'m'
          item { title:"[fa-download] export adres pro geo / osoba"  par:°{tab:'osoba'}}
          item { title:"[fa-download] export adres pro geo / rodina"  par:°{tab:'rodina'}}
          func onclick(i:ezer) { var own:ezer
            own= i.owner(); info.display(0,'t');
            info.fill(replace_fa(`${own.title} - ${i.title}`),' ');
            info.append(php.mapa2_geo_export(i.par)) 
          }
        }
        menu o {title:'Ostrá databáze',type:'group', _sys:'*'
          item {title:'Stav sjednocování kopií', par:°{db:'db2'}
            proc onclick (i) {
              panel.display(2,'i'); info.display(0,'t');
              info.fill(replace_fa(conc(i.owner.title,' - ',i.title)),'... probíhá výpočet ...');
              info.fill('',ask('db2_stav',i.par.db)) }}
          item {title:'obnova pobyt.web_changes' 
            proc onclick() { 
              panel.display(2,'i'); info.display(0,'t');
              confirm('opravit pobyt.web_changes ?'); ask('update_web_changes') 
          }}
//          item {title:'smazat platební údaje LK2019' skill:'m'
//            proc onclick() { confirm('smazat platební údaje LK2019 ?'); ask('clear_platby_lk2019') }
//          }
        }
        menu k {title:'Kontroly konzistence dat',type:'group', _sys:'*'
          item {title:'kontroly databáze - test'        ,par:°{opravit:0,test:'test'}, skill:'m'}
          item {title:'kontroly databáze - test+oprava' ,par:°{opravit:1,test:'test'}, skill:'m'}
          item {title:'kontroly databáze'               ,par:°{opravit:0}}
          item {title:'kontroly databáze s opravou'     ,par:°{opravit:1}, skill:'m'}
          proc onclick (i) {
            panel.display(2,'i'); info.display(0,'t');
            info.fill(replace_fa(conc(i.owner.title,' - ',i.title)),' ... probíhá kontrola dat ...');
            info.fill('',ask('db2_kontrola_dat',i.par));
          }
        }
        menu t {title:'Testovací databáze',type:'group', _sys:'*'
          item {title:'stav sjednocování kopií', par:°{db:'db2_test'}
            proc onclick (i) {
              panel.display(2,'i'); info.display(0,'t');
              info.fill(replace_fa(conc(i.owner.title,' - ',i.title)),'... probíhá výpočet ...');
              info.fill('',ask('db2_stav',i.par.db)) }}
          item {title:'obnovit testovací db z ostré', par:°{db:'db2'}
            proc onclick (i) {
              panel.display(2,'i'); info.display(0,'t');
              confirm("vytvořit znovu testovací databázi z aktuálního stavu ostré databáze?");
              info.fill(replace_fa(conc(i.owner.title,' - ',i.title)),' ... probíhá update ...');
              info.fill('',ask('db2_copy_test_db',i.par.db)); }}
        }
      }
      use info: form right [12,4,,] { tag:'i' 
        field url [40,29,100,17] {tag:'t', format:'t', style:'width:calc(100% - 60px)'}
        button [0,29,,] {tag:'t', title:'GET', func onclick() { var html:text
          html= php.curl_get_contents(url);
          form.note= php.htmlentities(html);
        }}
//        select tab [-253,5,150,] {tag:'t', title:'tabulka', format:'t'}
//        field idt [-152,5,70,] {tag:'t', title:'klíč', format:'t'}
//        button [-82,5,,] {tag:'t', title:'zobraz'
//          func onclick() {
//            clear(); form.note= conc(form.note,php.tab_append(tab,`*='${idt}'`)); // * bude primary_key
//        }}
//        button [-22,5,,] {tag:'t', title:'vymaž'
//          func onclick() { form.note= '';
//        }}
      }
      // ==> proces lokalizace adres
      panel Proces [0,0,300,50] { type:'popup', 
          //par:°{close:'no'} 
        use it: form _it 
        // stavové proměnné
        var y:object, // střadač zajištující komunikaci mezi klientem a serverem
            continue=1
        // Start - odstartuje výpočet definovaný objektem par a zobrazí jeho průběh 
        func Start(par,title) { 
          clear(); continue= 1; it.msg= ''; Show(0,1); it.display(1,'t'); it.display(0,'e'); 
          panel.title= title;
          // inicializace cyklu a předání prametrů - počáteční hodnota todo bude určena z par
          y= {todo:0,done:0,par:par}; 
          // zahájení cyklu 
          fork.panel.Step(); // asynchronní spuštění, aby bylo provedeno panel.modal
          panel.modal() 
        }
        // Step - jeden krok procesu
        func Step() { 
          while (continue) {  // po Stop (třeba stisku Cancel) se výpočet přeruší
            y= php.geo_fill(y); // funkce serveru odpovídá  za done<=todo 
            info.append(y.note);
            it.msg= !continue ? `PŘERUŠENO UŽIVATELEM -- ${y.msg}` : y.msg;
            if (y.error) {
              Stop(); it.msg= `PŘERUŠENO -- CHYBA: ${y.error}`
            };
            Show(y.done,y.todo); // posune termometr
            if (y.done==y.todo)  
              Stop(); 
          }
          End()
        }
        // Show(cast,celek) - posune termometr
        func Show(cast,celek) { 
          it.thermo1.property(object('width',(cast*290)/celek)) }
        // Stop - zastaví a nechá zobrazené
        func Stop() { continue= 0; it.display(0,'b'); }
        // End - zruší zobrazení
        func End() { it.display(0,'b'); it.display(1,'e') }
        form _it { 
          label thermo1 [6,1,0,8]   { tag:'t', title:'' style:'background-color:#ef7f13' }
          label thermo2 [5,0,290,8] { tag:'t', title:'' style:'border:1px dotted grey' }
          label msg [0,15,300,40]   { tag:'t' format:'c' }
          button cancel [120,30,,]  { tag:'t,b', title:'Přerušit' func onclick () { Stop() } }
          button ok     [136,30,,]  { tag:'e', title:'Ok' func onclick () { panel.hide(1) } }
        }
      }
    }
    panel str {type:'right', title:'Aplikace', _sys:'str', skill:'a;aa'
      menu m {type:'left', format:'f+'
        menu { title:'GitHub',type:'group',skill:'m'
//          item { title:'[fa-question] Answer: git status'  par:°{folder:'.',op:'cmd',cmd:'status'}}
//          item { title:'[fa-gear] Answer: git log'         par:°{folder:'.',op:'cmd',cmd:'log'}}
//          item { title:'[fa-gear] Answer: git pull'        par:°{folder:'.',op:'cmd',cmd:'pull'}}
//          item { title:'[fa-gear] Answer: git fetch ...'   help:"po fetch>git reset --hard origin/master"
//                                                           par:°{folder:'.',op:'cmd',cmd:'fetch'}}
//          item { title:'[fa-question] Ezer: git status' par:°{folder:'ezer',op:'cmd',cmd:'status'}}
//          item { title:'[fa-gears] Ezer: git log'       par:°{folder:'ezer',op:'cmd',cmd:'log'}}
//          item { title:'[fa-gears] Ezer: git pull'      par:°{folder:'ezer',op:'cmd',cmd:'pull'}}
//          item { title:'[fa-gears] Ezer: git fetch ...' help:"po fetch>git reset --hard origin/master"
//                                                        par:°{folder:'ezer',op:'cmd',cmd:'fetch'}}
          item { title:'[fa-question] show last log'         par:{folder:'.',op:'show'}}
          item { title:'[fa-gear] Aplikace: git status'      par:{folder:'.',op:'cmd',cmd:'status'}}
          item { title:'[fa-gear] Aplikace: git log'         par:{folder:'.',op:'cmd',cmd:'log'}}
          item { title:'[fa-gear] Aplikace: git pull'        par:{folder:'.',op:'cmd',cmd:'pull'}}
          item { title:'[fa-gear] Aplikace: git fetch ...'    help:"po fetch>git reset --hard origin/master"
                                                             par:{folder:'.',op:'cmd',cmd:'fetch'}}
          // ezer
          item { title:'[fa-gears] Ezer: git status'     par:{folder:'ezer',op:'cmd',cmd:'status'}}
          item { title:'[fa-gears] Ezer: git log'        par:{folder:'ezer',op:'cmd',cmd:'log'}}
          item { title:'[fa-gears] Ezer: git pull'       par:{folder:'ezer',op:'cmd',cmd:'pull'}}
          item { title:'[fa-gears] Ezer: git fetch ...'  help:"po fetch>git reset --hard origin/ezer3.1"
                                                         par:{folder:'ezer',op:'cmd',cmd:'fetch'}}
          // skins
          item { title:'[fa-question] Skins: git status' par:{folder:'skins',op:'cmd',cmd:'status'}}
          item { title:'[fa-gear] Skins: git log'        par:{folder:'skins',op:'cmd',cmd:'log'}}
          item { title:'[fa-gears] Skins: git pull'      par:{folder:'skins',op:'cmd',cmd:'pull'}}
          item { title:'[fa-gears] Skins: git fetch ...' help:"po fetch>git reset --hard origin/master"
                                                         par:{folder:'skins',op:'cmd',cmd:'fetch'}}
          proc onclick (i) {  
            panel.display(2,'i'); clear;
            info.fill(replace_fa(conc(i.owner.title,' - ',i.title)),ask('git_make',i.par));
          }
        }
        menu m {title:'Struktura modulů a funkcí',type:'group'
          item {title:'Ezer moduly'         proc onclick () { clear; info.Set(ask('doc_ezer')) } }
          item {title:'PHP moduly'          proc onclick () { clear; info.Set(ask('doc_php')) } }
          item {title:'Seznam PHP funkcí'   proc onclick () { clear; info.Set(ask('doc_called')) } }
        }
      }
      use info: form right [12,4,,]     { tag:'i' }
    }
  }
  tabs doc  {title:"[fa-question] Nápověda", _sys:'*',  include:'onload,ezer2.help' }
  tabs off  {title:"[fa-power-off] Odhlásit", type:'logoff' }
  // přepnutí access - refresh formulářů
  proc spinner(on) {
    [ function('on',"waiting(on);",on) ];
  }
  proc reaccess(msg) {
    echo("jsou přístupná data ",msg); //set_trace('E',1,'spinner,reaccess');
    spinner(1);
    akce2.reaccess;
    syst.reaccess;
    spinner(0); //set_trace('E',0,'spinner,reaccess');
  }
  // univerzální procedury
  proc the_formsave (f,b) {
    f.same
  | f.key; f.save; f.load; b.browse_seek
  | f.insert; f.load;
    b.raise('onrowclick',b.browse_seek(conc(f.key_id,'=',f.key)))
  }

}
// univerzální formulář pro levostranné menu
form right [,,*,*] {
  label info [0,0,*,600] { title:'' style:'padding-right:10px'}
  label head [0,0,*,50]  { title:'' style:'padding-right:10px'}
  label note [0,50,,*] { title:'' style:'padding-right:10px;width:calc(100% - 15px)'}
  proc Set(x) {
    head.set(''); note.set(''); info.set(x)
  }
  proc fill(x,y) {
    info.set('');
    [ x; head.set(conc("<div class='karta'>",x,"</div>")) ];
    [ y; note.set(y) ]
  }
  proc append(x) { note.get; note.set(conc(note.get,'<br>',x)) | note.set(x) }
  func header(i:ezer) { var i_owner:ezer
    clear(); i_owner= i.owner();
    info= ''; note= '';
    head= replace_fa(`<div class='karta'>${i_owner.title} - ${i.title}</div>`);
  }
}

table akce { key_id:'id_duakce'
  number id_duakce
  number typ
  number statistika
}

table join_akce { // spojení číselníku akcí s MS
  number g_rok     { help:"rok akce podle číselníku akcí" }
  number g_kod     { help:"kód akce podle číselníku akcí" }
  text   source    { help:"L:Lídino M:Milošovo MS.EXE" }
  number akce      { help:"staré číslo akce podle MS.EXE" }
}

# kopie číselníků akcí z intranetu YS
table uakce { key_id:'id_uakce'
  number id_uakce
  number id_akce
  text ms_source
  number ms_akce
  number rok
  text akce
  text nazev_akce
  date datum { sql_pipe:'sql_date1' }
  number dnu
  number osob
  number typ
  text kapitola
}

table ms_akce { key_id:'id_akce'  // Sey
  number id_akce
  text source
  text nazev
  number akce
  number druh
  number poradatel
  date datum_od        // SQL tvar se používá v ys.akce
  date datum_do
  text misto
}

table g_akce { key_id:'id_gakce' // kopie číselníku akcí z intranetu
  number id_gakce
  number g_rok     { help:"rok akce podle číselníku akcí" }
  number g_kod     { help:"kód akce podle číselníku akcí" }
  text   g_nazev   { help:"název akce z číselníku akcí" }
  date   g_od      { help:"začátek akce z číselníku akcí", sql_pipe:'sql_date1' }
  date   g_do      { help:"ukončení akce z číselníku akcí", sql_pipe:'sql_date1' }
  number g_ucast   { help:"počet účastníků akce z číselníku akcí" }
  text   g_typ     { help:"typ akce z číselníku akcí" }
  text   g_kap     { help:"účetní kapitola akce z číselníku akcí" }
}

table mail { key_id:'id_mail'
  number id_mail { key:'primary' },
  number id_dopis { help:'mail|index mailu' }, // nepoužívá se pro dopisy s druh!='@'
  number id_davka
  number id_znacka                             // nepoužívá se pro dopisy s druh=='@'
  number id_clen
  text znacka
  text email
  number stav
  text msg
  text body
}

table dopis { key_id:'id_dopis'
  number id_dopis { key:'primary' },
  number id_davka { help:'dávka|číslo rozesílání dávky dopisů' },
  text nazev
  text prilohy
  number potvrzeni { help:"potvrzení|vyžádat zaslání potvrzení o přečtení" }
  number kopie	{ help:"poslat slepou kopii 'odesílateli'" }
  text druh { help:'druh|D-dopis, S-samolepka,legitimace, N-nesamostatná složka' },
  text typ { help:'typ|značka dopisu' },
  number nw
  number nh
  number komu     // pouze pro maily
  number pocet    // pouze pro maily
  text report { help:'vzor|identifikátor reportu' },
  number aktualni { help:'aktualni|text dopisu je připraven k tisku' },
  date datum { help:'datum|vročení dopisu', sql_pipe:'sql_date1' },
  text obsah { help:'obsah|text dopisu' },
  text var_list { help:'seznam proměnných'}
  text post_vars { help:'seznam proměnných počítaných po generování'}
  text nest_list { help:'seznam složek (druh=N)'}
  text add_list
}

table _user { key_id:'id_user', db:'ezer_system'
  number id_user
  text deleted,
  text abbr,
  text skills,
  text options { help:'privátní nastavení|ve formátu JSON' }
  text username,
  text password,
  number state,
  text forename,
  text surname,
  text login,
  text history,
  text sess_id,
  text sess_time,
  text sess_data,
  number zmena_kdo,
  date zmena_kdy {  },
}

table _file_ {
  text fid
  date fdate
  text fname
  text ftype
  text finfo
}

table _cis { key_id:'id_cis'
  number id_cis text druh, text data, text zkratka, number poradi,
  text hodnota, text popis, text barva, text ikona
}

map ms_akce_clen:     table _cis {where:"druh='ms_akce_clen'",     order:'poradi', key_id:'data'}
map ms_akce_pouze:    table _cis {where:"druh='ms_akce_pouze'",    order:'poradi', key_id:'data'}
map ms_akce_funkce:   table _cis {where:"druh='ms_akce_funkce'",   order:'poradi', key_id:'data'}
map ms_akce_prednasi: table _cis {where:"druh='ms_akce_prednasi'", order:'poradi', key_id:'data'}
map ms_akce_cirkev:   table _cis {where:"druh='ms_akce_cirkev'",   order:'poradi', key_id:'data'}
map ms_akce_vzdelani: table _cis {where:"druh='ms_akce_vzdelani'", order:'poradi', key_id:'data'}
map ms_akce_platba:   table _cis {where:"druh='ms_akce_platba'",   order:'poradi', key_id:'data'}

