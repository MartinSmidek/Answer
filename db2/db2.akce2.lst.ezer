#pragma test
# ------------------------------------------------------------------------------------------------ #
# Karta pro výběr akce                                                                             #
#                                                                                                  #
# Ans(w)er/Ezer                                     (c) 2007-2015 Martin Šmídek <martin@smidek.eu> #
# ------------------------------------------------------------------------------------------------ #

# ------------------------------------------------------------------------------------------==> stav
var zalozka: text               // inf | cor | cen
var the_omezeni: text           // podmínka omezující akce podle druhu - 1,2,3,4 jsou MS
var menu_item:object            // aktivní menu
# ----------------------------------------------------------------------------------==> inicializace
# inicializace panelu proběhne po akce.onstart, zobrazí seznam akcí daného roku podle the_source
proc onfirstfocus() {
#   spinner(1);
  [ dirty.get | akce_onstart ]; // zavolá akce_work
  // detekce rozměrů
  curr_w.set(cconc(lt(sys('screen','width'),1280),1024,1280));
  // zobrazovat mapu?
  info.mapka.enable(sys('options','gmap'));
  // zobrazení akce
  info.fill(the_title.get);
  // inicializace zobrazení
  info.rok.selects(javascript(conc(
    "var x='';for(let i=",sum(fdate('Y'),1),";i>=1990;i--){x+=i+',';};x+='0'")));
  info.rok.set(the_rok.get);
  page.cen_copy.selects("neměnit:0");
  zalozka.set('inf');
  // zobrazení seznamu akcí
  [ has_skill('y;f;s;crz');  m.g.sey.click
  | has_skill('caam'); m.c.m.click
  | has_skill('caar'); m.c.r.click
  | has_skill('a');    m.c.a.click
  ];
  // zobrazení vazby na objednávky DS
  [ has_skill('y') | page.ds_order.set_css('hidden','') ];
  page.DocInit;
  page.inf.onclick;
#   spinner(0);
}
# -------------------------------------------------------------------------------------------==> use
use info: form _info [12,4,,]
use page: form _page [512,40,,]
# use fil:  form file._fil [512,72,,]  { tag:'file', format:'n' }

# ----------------------------------------------------------------------------==> globální procedury
# nastaví akci jako pracovní
proc akce_work(id_akce) {
  info.volba_akce(id_akce);
  info.rok.set(the_rok.get);
}
# zobrazí přehled hnízd
func ukaz_hnizda(ida) { 
  HNIZDA.prehled(ida)
}                          
proc reaccess() { #==> . reaccess - změna
  # intranet povol jen Setkání
  [ m.g.part('goo'); m.g.goo.enable(eq(my_org.get,1)) ];
  # přenastav pracovní akci podle my_org
  { eq(my_org.get,1);
    akce_work(the_akce_ys.get)
  | eq(my_org.get,2);
    akce_work(the_akce_fa.get)
  | akce_work(the_duakce.get)
  };
  # překresli aktivní kartu
  menu_item.get;
  m.onclick(menu_item.get);
  { eq(the_source.get,'sey'); info.sey.pracovni | info.google.pracovni }
}
# -----------------------------------------------------------------------------==> lokální procedury
# vrátí hodnotu položky z google resp. sey
proc akce_get(_fld) { var fld:object
  { eq(the_source.get,'sey'); fld.set(info.sey.part(_fld).get) | fld.set(info.google.part(_fld).get) };
  return(fld);
}
# zapíše hodnotu akce.archiv (a zapíše ji i do google resp. sey)
proc akce_set_archiv(cloud,folder) { var path:text
  path.set(conc(cloud,folder));
  ask('query',conc("UPDATE akce SET archiv='",path,"' WHERE id_duakce=",page.a_akce.get));
  { eq(the_source.get,'sey'); info.sey.archiv.let(path) | info.sey.google.let(path) };
}
# obnoví zobrazení seznamu
proc lst_refresh() {
  eq(the_source.get,'sey');    [ info.sey.browse_refresh ]
| eq(the_source.get,'google'); [ info.google.browse_refresh ]
}
# -----------------------------------------------------==> . reakce na změny rozměrů okna prohlížeče
const min_h=550           // minimální výška (nelze jednoduše měnit - jsou k ní vztaženy souřadnice)
var   curr_h: number      // výška nad základní výšku
var   curr_w: number      // 1024 nebo 1280

proc onresize(w,h) { // echo('lst onresize(',w,',',h,')');
  [ and(lt(h,min_h.get),not(eq(curr_h.get,min_h.get)));
    curr_h.set(0); h_change_all()
  | and(gt(h,min_h.get),not(eq(h,sum(curr_h.get,min_h.get))));
    curr_h.set(minus(h,min_h.get)); h_change_all() ];
  [ lt(w,1220); m.click(2,1) | m.click(1,1) ];
  [ and(lt(w,1280),eq(curr_w.get,1280)); narrow
  | and(gt(w,1279),eq(curr_w.get,1024)); wide ];
}

# změna šířky 1024/1280
proc narrow() { curr_w.set(1024); w_change_all  }
proc wide() { curr_w.set(1280); w_change_all }
proc w_change_all() {}

# změna výšky oproti základní min_h
proc h_change_all() {
//  page.property(object('down',curr_h.get),'b');      // posunutí proti původnímu - omezeno na min_h
  page.frame.property(object('stretch',curr_h.get)); // protažení proti původnímu
  page.drop.property(object('stretch',curr_h.get));
  h_change_b;
}
# změna počtu řádků browse oproti aktuálním řádkům
proc h_change_b() { var r:number, h:number
  h.set(sys('screen','height'));
  r.set(divide(sum(h.get,-173),17));
  not(eq(r,info.sey.rows));
  info.google.set_attrib('rows',r);
  info.sey.set_attrib('rows',r);
  page.c.set_attrib('rows',sum(r,-2));
}

# ------------------------------------------------------------------------------------------==> MENU
# Výběr akce má dvě možnosti, uchované v proměnné the_source:
#   'google' používá jako primárná data spreadsheet v intranetu YS a z něj upravuje tabulku AKCE
#   'sey' používá pouze tabulku AKCE

menu m {type:'left', format:'f+'
  # it: info.tag
  # s:  source
  # o:  omezeni  org|access
  # a:  rok=akce_roku | akce_vsechny
  # cond
  menu c {title:'Výběr akce',type:'group',                                                 skill:'c#crz;s'
    // jen pro CR
    item m {title:'[MS] program Manželská setkání', skill:'caam', par:°{omez:'da2.druh IN (1,2,3,4)'} }
    item r {title:'[R] programy Rodina mimo MS',    skill:'caar', par:°{omez:'da2.druh NOT IN (1,2,3,4)'} }
    item a {title:'[...] bez omezení',              skill:'a',    par:°{omez:'1'} }
      proc onclick (i) {
        info.rok.display(1); info.sey.display(1); info.google.display(0);
        the_omezeni.set(i.par.omez);
        the_source.set('sey'); akce_roku(info.rok.get);
  } }
  menu g {title:'Výběr akce zadaného roku',type:'group',                                 skill:'y;f;s;crz'
    item goo {title:'[fa-google] podle číselníku akcí z intranetu',                      skill:'y'
      par:°{it:'g|r',s:'google',o:'',a:'rok'}}
    item sey {title:'[fa-search] ze seznamu všech našich akcí'
      par:°{it:'s|r',s:'sey',o:'org',a:'rok'}}
    item {title:'[fa-search] ze seznamu všech akcí'                                      skill:'#crz;s'
      par:°{it:'s|r',s:'sey',o:'access',a:'rok'}}
  }
  menu {title:'Výběr ze všech roků',type:'group',                                        skill:'y;f;c;s'
    item sey {title:'[fa-search] ze seznamu všech akcí',    par:°{it:'s',cond:'1'} }
    item  {title:'[fa-search-plus]... opravdové akce',      par:°{it:'s',cond:'da2.spec=0'} }
    item  {title:'[fa-search-minus] ... jen různé seznamy', par:°{it:'s',cond:'da2.spec=1'} }
  }
  proc onclick (i) {
    menu_item.set(i);
    info.display(2,i.par.it); the_source.set(i.par.s);
    [ i.par.o; the_omezeni.set(conc(
        "da2.access & ",cconc(eq(i.par.o,'org'),my_org.get,my_access.get))) ];
    { i.par.a; akce_roku(info.rok.get) | akce_vsechny(i.par.cond) }
  }
}
#==> . akce definovaného roku
func akce_roku(rok) {
  page.init_page();
  if (the_source=='google') {
    info.google.browse_init();
    if (rok>2009)  // intranet YS obsahuje akce od roku 2010 včetně
      info.google.browse_load(`g_kat='a' AND g_rok=${rok}`,
          '','','','','',`SET @curr:=${the_duakce}`);
    else
      alert("V intranetu jsou v přesném formátu popsány jen akce od roku 2010")
  }
  elseif (the_source=='sey') {
    info.sey.browse_load(`YEAR(da2.datum_od)='${rok}' AND da2.spec=0 AND ${the_omezeni}`,
        '','','','','',`SET @curr:=${the_duakce}`);
    info.sey.raise('onrowclick',info.sey.browse_seek(`da2.id_duakce=${the_duakce}`));
  }
}
//proc akce_roku(rok) {
//  page.init_page;
//  [ eq(the_source.get,'google');
//    { gt(rok,2009);  // intranet YS obsahuje akce od roku 2010 včetně
//      info.google.browse_load(conc("g_kat='a' AND g_rok=",rok),
//      '','','','','',
//      conc('SET @curr:=',the_duakce.get));
//    | alert("V intranetu jsou v přesném formátu popsány jen akce od roku 2010") }
//  | info.google.browse_init];
//  [ eq(the_source.get,'sey');
//    info.sey.browse_load(conc("YEAR(da2.datum_od)='",rok,"' AND da2.spec=0 AND ",the_omezeni.get),
//      '','','','','',
//      conc('SET @curr:=',the_duakce.get));
//    info.sey.raise('onrowclick',info.sey.browse_seek(conc("da2.id_duakce=",the_duakce.get)));
////    [ has_skill('yas'); page.DocBrowse ]
//  ]
//}
proc akce_vsechny(cond) {
  the_source.set('sey');
  [ info.sey.browse_load(cond) ];
}
# ------------------------------------------------------------------------------------------==> AKCE
form _page [,,275,360] {
  var a_akce: number, zmena_web:number
  # ---------------------------==> . ochrana editace více uživateli
  func onchanged() { akce_lock('on') }
  func akce_lock(on) { var lock:object 
    if (key_akce) {
      lock= php.table_lock(on,'akce',key_akce); 
      if ((on=='on') && (!lock.ok)) {
        alert(conc(lock.info,lock.note));
        akce_zpet();
      }
    }
  }
  func akce_zpet() { 
    akce_lock('off'); 
    form.load(page.a_akce); zmena_web= 0; 
  }
  # ---------------------------==> . záložky
  proc tags(a,on,off) {
    form.set_css('ae_butt_off','ae_butt_on','p');
    a.set_css('ae_butt_on','ae_butt_off');
    form.display(0,off); form.display(1,on);
  }
  label inf [ 12,0,80,20] {tag:'p', title:'<b>Info</b>', format:'c', css:'ae_butt_on'
    proc onclick() { tags(this,'inf','cor|cen|doc|syn'); zalozka.set('inf'); show_akce(a_akce.get)
  } }
  label cor [100,0,80,20] {tag:'p', title:'<b>Úprava</b>', format:'c'
    proc onclick() { tags(this,'cor','inf|cen|doc|syn'); zalozka.set('cor'); show_akce(a_akce.get)
  } }
  label cen [188,0,80,20] {tag:'p', title:'<b>Ceník</b>', format:'c',
    proc onclick() { tags(this,'cen','inf|cor|doc|syn'); zalozka.set('cen'); show_akce(a_akce.get)
  } }
  label doc [364,0,80,20] {tag:'p', title:'<b>Archiv</b>', format:'c', skill:'xy;xf'
    proc onclick() { tags(this,'doc','inf|cor|cen|syn'); zalozka.set('doc'); show_akce(a_akce.get)
  } }
  label syn [276,0,80,20] {tag:'p', title:'<b>Dokumenty</b>', format:'c', skill:'yas'
    proc onclick() { tags(this,'syn','inf|cor|cen|doc'); zalozka.set('syn'); show_akce(a_akce.get)
  } }
  func show_akce(id_akce) {
    page.a_akce= id_akce;
    if (the_source=='sey') { if (info.sey.browse_count()) show_page(id_akce) else init_page() }
    if (the_source=='google') { if (id_akce) show_page(id_akce) else init_page() }
  }
  func show_page(id_akce) {
    cen_copy.enable(0); cen_copy.key(0); key_akce= id_akce;
    form.load(id_akce); zmena_web= 0; 
    if (has_skill('y')) ds_order= php.ds_objednavka(id_akce);
    switch (zalozka) {
      case 'inf': clear(); note= php.akce2_info(id_akce); break;
      case 'cor': 
        is_hnizda= hnizda?1:0; hnizda.display(hnizda?1:0);
        if (has_skill('faan')) {
          form.display(0,'corm|corf'); 
          if (mrop) form.display(1,'corm'); 
          if (firm) form.display(1,'corf'); 
        };
        if (has_skill('f')) soubezna= id_hlavni?1:0;
//        cen_ma.onchange();
        info.sey.browse_focus();
        break;
      case 'cen': 
        hnizdo.display(hnizda);
        hnizdo.key(0);
        if (hnizda) {
          hnizdo.set_attrib('options','ms_akce_hnizda.nazev');
          hnizdo.key(1);
        }
        c.Load();
        break;
      case 'doc': archiv.ukaz(akce_get('archiv')); break;
      case 'syn': if (has_skill('yas')) DocShow(akce_get('rok'),akce_get('kod')); break;
    };
    c_show();
  }
//  proc show_page(id_akce) {
//    cen_copy.enable(0); cen_copy.key(0); key_akce.set(id_akce); //fil.display(0);
//    form.load(id_akce); zmena_web.set(0); 
//    [ has_skill('y'); ds_order.set(ask('ds_objednavka',id_akce)) ];
//    switch(zalozka.get,
//    'inf',{ clear; note.set(ask('akce2_info',id_akce));  },
//    'cor',{ //form.load(id_akce);
//            { hnizda.get; is_hnizda.set(1); hnizda.display(1)
//            | is_hnizda.set(0); hnizda.display(0)
//            };
//            [ has_skill('faan'); 
//              form.display(0,'corm|corf'); 
//              [ mrop.get; form.display(1,'corm') ]; 
//              [ firm.get; form.display(1,'corf') ]; 
//            ];
//            [ has_skill('f'); soubezna.set(cconc(id_hlavni.get,1,0)) ];
//            cen_ma.onchange;
//            info.sey.browse_focus },
//    'cen',{ c.browse_load(conc("id_akce=",id_akce)); },
//    'doc',{ archiv.ukaz(akce_get('archiv')) },
//    'syn',{ has_skill('yas'); DocShow(akce_get('rok'),akce_get('kod')) }
//    );
////    echo(id_akce,':',access.get,cen_ver.key);
//    c_show;
//  }
  proc init_page() {
    page.a_akce.set(0);
    cen_copy.enable(0); cen_copy.key(0);
    form.init; zmena_web.set(0); ds_order.set(0);
    note.set(''); key_akce.set(' ');
  }
  # ------------------------==> . informace
  field key_akce [408,19,46,] {format:'or', css:'Label',
    style:'color:white', help:' ', tabindex:0 }
  label note [10,35,455,193] { tag:'inf', title:'', style:'color:black;font-size:12px' }
  # ---------------------------==> . položky akce
  field access { data:akce.access }
  field id_hlavni { data:akce.id_hlavni }
  // ------------------------------------- základní údaje o akci
  field nazev [17,49,218,] { tag:'cor', title:'^název akce *', data:akce.nazev }
  field [250,49,50,] { tag:'cor', title:'^pro max.účastníků', data:akce.web_maximum, format:':e' }
  field misto [17,89,218,] { tag:'cor', title:'^místo', data:akce.misto }
  check ds_order [250,89,200,] { tag:'cor', title:'objednávka Domu setkání', format:'nd' } 
  field datum_od [18,133,83,] { tag:'cor', title:'^začátek *', type:'date', data:akce.datum_od }
  field datum_do [118,133,83,] { tag:'cor', title:'^ukončení', type:'date', data:akce.datum_do }
  check [213,157,90,] { tag:'cor', title:'jen seznam', data:akce.spec }
  check [213,177,90,] { tag:'cor', title:'akce zrušena', data:akce.zruseno }
  check mrop [300,157,60,] { tag:'cor', title:'mrop', data:akce.mrop, title:'mrop', skill:'faan|faa:m' }
  check firm [300,177,60,] { tag:'cor', title:'firm', data:akce.firm, title:'firming', skill:'faan|faa:m' }
  select [367,167,89,] { tag:'cor', title:'^statistika muži', type:'map0', data:akce.statistika,
    options:ms_akce_stat.zkratka, format:'n', skill:'f|f' }
  select [71,169,139,] { tag:'cor', title:'typ akce *', type:'map', data:akce.druh,
    options:ms_akce_typ.zkratka, format:'n' }
  radio [213,117,86,35] { tag:'cor', data:akce.strava_oddo, value:'vo'
    case [0,2,83,] { title:'večeře-oběd', expr:'vo' }
    case [0,17,76,] { title:'oběd-oběd', expr:'oo' }
    proc onchange() {} //echo('strava=',this.get) }
  }
  field [96,202,54,] { tag:'cor', title:'účetní symbol:', data:akce.ciselnik_akce,
    skill:'faa|faa;caa|caa' }
  // zobrazení a nastavení akce jako souběžné (stejné datum a místo)
  check soubezna [157,200,120,] { tag:'cor', title:'akce je souběžná', skill:'faa|faan',
    proc onchange() {
      { info.sey.id_hlavni.get;
        [ confirm("opravdu zrušit souběh této akce?"); ask('akce2_soubeh_nastav',form.key,0) ]
      | alert(ask('akce2_soubeh_nastav',form.key)) };
      info.sey.browse_seek; show_akce(form.key);
  }}
  // Změna ceníku
  check cen_ma [13,254,93,] { tag:'cor', title:'používat ceník', data:akce.ma_cenik,
                        format:'', skill:'yaa|yaan;caa|caan;faa|faan'
//    proc onchange() { cen_ver.display(this.get) }
  }
//  select cen_ver [109,254,100,] {tag:'cor', type:'map', data:akce.ma_cenik_verze,
//    help:'verze ceníků', options:ms_cenik_verze.zkratka, format:'n' }
  button [14,230,,] { tag:'cor', title:'Změnit ceník podle', skill:'yaa|yaan;caa|caan;faa|faan'
    func onclick() {
      cen_copy.enable(1);
      cen_copy.selects(php.akce2_select_cenik(form.key()),',:',0,1)
  }}
  select cen_copy [138,232,139,] {tag:'cor', format:'ntd', help:'vzory ceníků'
    func onchanged() { var msg:text
      msg= php.akce2_change_cenik('dotaz',form.key(),cen_copy.key());
      if (substr(msg,-1,1)=='?') {
        if (confirm(msg)) alert(php.akce2_change_cenik('proved',form.key(),cen_copy.key())); 
      }
      else alert(msg);
  }}
  // jednotná cena / dospělý
  check [13,276,93,] { tag:'cor', title:'používat cenu', data:akce.ma_cenu,
                        format:'', skill:'yaa|yaan;caa|caan;faa|faan' }
  field [109,278,43,] { tag:'cor', data:akce.cena, format:'r', skill:'yaa|yaan;caa|caan;faa|faan',
                        help:'pro 1 dospělého' }
  label [158,281,20,] { tag:'cor', title:'Kč' }
  // ------------------------------------- hnízda
  var hnizda_ok=1
  check is_hnizda [292,230,161,] { tag:'cor' title:'akce je v hnízdech' format:'t'
      skill:'yaa|yaan;caa|caan;faa|faan' 
    proc onchange() { this.get; hnizda.display(1) | hnizda.set(''); hnizda.change }}
  edit hnizda [292,262,161,40] { tag:'cor' title:'^jména hnízd oddělená čárkami' 
      data:akce.hnizda format:'n' 
    proc onchanged() { var msg:text
      msg.set(ask('akce2_hnizda_test',hnizda.get));
      msg; hnizda_ok.set(0); alert(msg);
    | hnizda_ok.set(1)
  }}
  // ------------------------------------- web setkani.org a chlapi.online
  label [10,309,384,92] { tag:'cor', css:'bily_ram', skill:'yaaw;faaw' 
                        title:" součinnost s www.setkani.org a chlapi.online" }
  button [381,316,,] { tag:'cor', title:'[fa-question]', skill:'yaa+|yaaw'
    help:'pokyny pro přihlašování na www.setkani.org'
    proc onclick() { alert("V popisu akce na www.setkani.org je zapotřebí vyplnit ID akce=",a_akce.get)
  }}
  field web_url [82,328,287,] { tag:'cor', title:'url pozvánky', data:akce.web_url, skill:'yaa|yaaw;faa|faaw' 
    proc onchanged() {
      match('page=',this.get); 
      warning("odkaz na akci musí být ve formátu pro uživatele nepřihlášeného k editaci webu!",
              "<br>Tzn. ne ve tvaru www.setkani.org/index.php?page=...");
      this.set(''); this.plain;
    | zmena_web.set(1);
  }}
  check web_prihl[16,344,243,] { tag:'cor', data:akce.web_prihlasky,
                        title:"ONLINE <span class='zluty'>přihlašování</span> na www.setkani.org", skill:'yaa+|yaaw' 
    proc onchange() { zmena_web.set(1);
  }}
  select garant [293,355,79,] {tag:'cor', type:'map', title:'garant' , data:akce.poradatel,
      help:'garant akce, příjemce organizačních mailů', options:akce_garant.zkratka 
    proc onchanged() { zmena_web.set(1);
  }}
  check [16,361,214,21] { tag:'cor', title:"<span class='cerveny'>OBSAZENO</span> - další jako náhradníci", data:akce.web_obsazeno,
                        skill:'yaa|yaaw;faa|faaw', help:"a to i tehdy, když akce není ještě naplněná" }
  check [16,378,135,] { tag:'cor', title:"kalendář <span class='azurovy'>chlapi.online</span>", data:akce.web_kalendar,
                        skill:'yaa|yaaw;faa|faaw' }
  button [184,380,,] { tag:'cor', title:'Upravit anotaci pro chlapi.online', skill:'yaa+|yaaw;faa+|faaw'
    help:'úprava textu v kalendáři chlapi.online'
    proc onclick() { 
      page.a_akce.get; ANOTACE.oprava(page.a_akce.get);
  }}
  // ------------------------------------- Zpět a Uložit
  label  [193,417,98,30] { tag:'cor', css:'ae_parm', style:'z-index:1', skill:'yaa+;faa+;caa+' }
  button [199,423,,] { tag:'cor', title:'Zpět', skill:'yaa+|yaa+;faa+|faa+;caa+|caa+'
    help:'Neměnit změněná data'
    func onclick() { akce_zpet() }
  }
  button [245,423,,] { tag:'cor', title:'Uložit', skill:'yaa+|yaan;faa+|faan;caa+|caan'
    help:'Uložit změněná data',
    proc onclick() {
      akce_lock('off'); 
      not(hnizda_ok.get); warning("oprav hnízda")
    | form.same
    | // oprava
      ucast.the_duakce_local.set(0); // inicializace Účastníci
      [ zmena_web.get; alert(ask('web_prihlaska',page.a_akce.get,web_url.get,web_prihl.get,garant.key)) ]; 
      form.key; form.save; form.load; info.sey.browse_seek
    | // přidání Setkání
      has_skill("yaa+");
      { eq(fdate("Y",datum_od.get),info.rok.get);
        [ form.insert; form.load;
          info.sey.raise('onrowclick',info.sey.browse_seek(conc('da2.id_duakce=',form.key)))
        | alert("Akci se nepovedlo po uložení najít")
        ]
      | alert("akce musí začínat v nastaveném roce")
      }
    | // přidání Familia
      has_skill("faa+");
      { eq(fdate("Y",datum_od.get),info.rok.get);
        [ form.insert; form.load;
          info.sey.raise('onrowclick',info.sey.browse_seek(conc('da2.id_duakce=',form.key)))
        | alert("Akci se nepovedlo po uložení najít")
        ]
      | alert("akce musí začínat v nastaveném roce")
      }
    | // přidání CPR
      has_skill("caa+");
      { eq(fdate("Y",datum_od.get),info.rok.get);
        [ form.insert; form.load;
          info.sey.raise('onrowclick',info.sey.browse_seek(conc('da2.id_duakce=',form.key)))
        | alert("Akci se nepovedlo po uložení najít - není skryta v jiném levém menu?")
        ]
      | alert("akce musí začínat v nastaveném roce")
      }
  } }
  // jen pro YF,CR - přidání a zrušení akce
  label  [10,417,164,30] { tag:'cor', css:'ae_parm', style:'z-index:1', skill:'faan;caan;yaan' }
  button [16,423,,] {tag:'cor', title:'Nová akce', format:'c', skill:'faan|faan;caan|caan;yaan|yaan'
    proc onclick() {
      eq(my_org.get,1);
      // Setkání
      { has_skill('yaan'); {
          eq(the_source.get,'google'); alert("Vkládat lze jen do seznamu všech akcí")
        | gt(info.rok.get,2009); alert("Vkládat lze jen 'historické akce', po roce 2010 jen přes intranet")
        | form.init; access.set(1); access.change; info.sey.blur(1)
        }
      | alert("Nemáš oprávnění vytvořit novou akci (kontaktuj administrátora např. Martina)")
      }
    | eq(my_org.get,2);
      // Familia
      form.init; access.set(2); access.change; info.sey.blur(1)
    | eq(my_org.get,4,8);
      // CPR, MS - samostatné db
      form.init; access.set(my_org.get); access.change; 
      nazev.change; misto.change; datum_od.change; datum_do.change;
      info.sey.blur(1)
  } }
  button [91,423,,] {tag:'cor', title:'Smazat akci', format:'c', skill:'faan|faan;caan|caan;yaan|yaan'
    proc onclick() {
      var ret:object
      var msg:text
      ret.set(ask('akce2_delete_confirm',form.key));
      ret.zrusit; confirm(ret.zrusit);                  // fakt zrušit + název
      { not(ret.ucastnici) | confirm(ret.ucastnici) };  // ale akce má účastníky
      { ret.platby; alert(ret.platby)                   // nelze smazat, jsou-li platby
      | msg.set(ask('akce2_delete',form.key,ret));
        alert(msg);
        lst_refresh;
  } } }
  // jen pro YF - zápis roku iniciace účastníkům akce - musí být akce.mrop=1 
  label  [310,417,118,31] { tag:'corm,corf', css:'ae_parm', format:'n', style:'z-index:1',         skill:'faan' }
  button [316,423,,] {tag:'corm', title:'Potvrzení iniciace', format:'nc', style:'z-index:1', skill:'faan|faa:m'
    proc onclick() { var ret:object
      has_skill('faa:m');
      ret.set(ask('akce2_confirm_mrop',form.key,0));
      { ret.ok;
        [ confirm(ret.msg); ret.set(ask('akce2_confirm_mrop',form.key,1)); alert(ret.msg) ]
      | alert(ret.msg)
      }
  } }
  button [316,423,,] {tag:'corf', title:'Potvrzení firmingu', format:'nc', style:'z-index:1', skill:'faan|faa:m'
    proc onclick() { var ret:object
      has_skill('faa:m');
      ret.set(ask('akce2_confirm_firm',form.key,0));
      { ret.ok;
        [ confirm(ret.msg); ret.set(ask('akce2_confirm_firm',form.key,1)); alert(ret.msg) ]
      | alert(ret.msg)
      }
  } }
  # ------------------------==> . ceník
  proc c_show() { // výběr a šířky sloupců podle akce.access a akce.ma_cenik_verze
    // access: 1,2 pro db2 - nepoužívá cenik.typ,   používá akce.ma_cenik_verze s hodnotou 0|1
    //                       lze měnit sloupce od-do za dph
    eq(access.get,1,2);
    c.typ.width(0);
//    { eq(cen_ver.key,0,1); // verze 2016 i 2017
      c.pol.width(156); c.za_2016.width(38); form.display(eq(zalozka.get,'cen'),'ex0');
//      c.za_2017.width(0); c.pro_2017.width(0); form.display(0,'ex1');
//#     | eq(cen_ver.key,1); // verze 2017
//#       c.pol.width(121); c.pro_2017.width(45); c.za_2017.width(27); form.display(eq(zalozka.get,'cen'),'ex1');
//#       c.za_2016.width(0); form.display(0,'ex0');
//    }
    // access: 4|8 pro cr|ms    -   používá cenik.typ, nepoužívá akce.ma_cenik_verze
  | eq(access.get,4,8);
    c.pol.width(100); c.typ.width(50); c.za_2016.width(40);
    form.display(0,'ex');
  | warning("chybně definovaný ceník");
  }
  browse c [10,25,,] { tag:'cen', rows:26, qry_rows:1, css_rows:"barva,1:sedy"
    // pro y;f;c
    show id_cenik { data:cenik.id_cenik }
    show id_akce  { data:cenik.id_akce }
    show polozka  { data:cenik.polozka }
    show poradi [,,25,] { title:'N', help:'pořadí ve faktuře', data:cenik.poradi, format:'ruq*s+'
      proc onsubmit() { this.save } }
    show barva { expr:"IF(LEFT(polozka,1)='-',1,0)" }
    show pol [,,155,] { title:'položka', help:'... do faktury', data:cenik.polozka, format:'tuq*s'
      proc onsubmit() { this.save } }
//     pro y;f verze 2017
//    show pro_2017 [,,0,] { title:'pro', data:cenik.pro, format:'urq=s',
//      help:'U=účastník, S=~VPS, D=dítě, d=dítě s chůvou, C=chůva, p=pom.peč., H=host'
//      proc onsubmit() { this.save } }
//    show za_2017 [,,0,] { title:'za', data:cenik.za, format:'urq=s',
//      help:'n=noc, s=snídaně, o=oběd, v=večeře, p=pobyt, a=aktivita'
//      proc onsubmit() { this.save } }
    // pro c a y;f verze 2016
    show za_2016 [,,0,] { title:'za', data:cenik.za, format:'urq=s',
      help:'noc:N, strava:(s=snídaně/o=oběd/v=večeře)(c=celá/p=poloviční), pobyt:P, slevy:S'
      proc onsubmit() { this.save } }
    // pro c
    show typ [,,0,] { title:'typ', data:cenik.typ, map_pipe:ms_akce_ubytovan.zkratka,
      format:'urq#s', help:'typ ubytování: 1,2,3,...'
      proc onsubmit() { this.save } }
    // pro y;f;c
    show cena [,,43,] { title:'cena', help:'cena v Kč vč. DPH', data:cenik.cena, format:'urq=s:e'
      proc onsubmit() { this.save } }
    // jen y;f
    show dph [,,0,] { title:'dph', help:'DPH v Kč', data:cenik.dph, format:'urq=s:e'
      proc onsubmit() { this.save } }
    // pro y;f;c
    show od [,,21,] { title:'≥', data:cenik.od, format:'urq=:e', help:'od tohoto věku (včetně narozenin)'
      proc onsubmit() { this.save } }
    show do [,,21,] { title:'<', data:cenik.do, format:'urq=:e', help:'do tohoto věku (do narozenin)'
      proc onsubmit() { this.save } }
    // funkce a menu
    func Load() {
      if (hnizda)
        c.browse_load(`id_akce=${the_duakce} AND hnizdo=${hnizdo.key()}`); 
      else
        c.browse_load(`id_akce=${the_duakce}`); 
    }
    menu { type:'context', skill:'yaa+;faa+;caa+'
      item { title:'přidat nový řádek'
        func onclick() { 
          c.browse_seek(conc('id_cenik=',cenik.insert_record(
          {id_akce:the_duakce, polozka:'?', hnizdo:hnizda ? hnizdo.key() : 0})));
      } }
      item { title:'odebrat běžný řádek'
        func onclick() { 
          if (confirm(`opravdu odebrat položku ${polozka} ?`)) {
            cenik.delete_record(conc('id_cenik=',id_cenik));
            Load()
          }
      } }
    }
    proc onclick() {
      eq(access.get,1,2);
      od.width; od.width(0); do.width(0); dph.width(43)
    | od.width(21); do.width(21); dph.width(0)
    }
  }
  // export ceníku a výběr hnízda
  const bc=40, lc=335
  label  [lc+19,24,102,57] { tag:'b,cen', css:'ae_parm', style:'z-index:1' }
  select hnizdo [lc+28,31,86,] { tag:'b,cen', title:'' format:'t' type:'map' 
    func onchanged() { 
      c.Load(); 
    }
  }
  button [lc+28,bc+16,,] { tag:'b,cen', title:'Export ceníku', format:'c'
    proc onclick() {
      var ret: object
      ret.set(ask('akce2_platby_xls',a_akce.get));
      ret.ok; alert(ret.xhref);
    | warning(ret.msg);
  } }
  label  [lc+20,bc+52,211,] { tag:'b,cen,ex0', title:"příklady poplatků" }
  button [lc+20,bc+68,,] { tag:'b,cen,ex0', title:'pár', proc onclick() { test_ceniku(2,0,0,0) }}
  button [lc+20,bc+95,,] { tag:'b,cen,ex0', title:'pár+dítě', proc onclick() { test_ceniku(2,1,0,0) }}
  button [lc+20,bc+123,,] { tag:'b,cen,ex0', title:'pár+kojenec', proc onclick() { test_ceniku(2,0,1,0) }}
  button [lc+20,bc+150,,] { tag:'b,cen,ex0', title:'dítě do 10 let', proc onclick() { test_ceniku(0,1,0,0) }}
  button [lc+20,bc+177,,] { tag:'b,cen,ex0', title:'kojenec', proc onclick() { test_ceniku(0,0,1,0) }}
  button [lc+20,bc+204,,] { tag:'b,cen,ex0', title:'pečovatel', skill:'y|y', proc onclick() { test_ceniku(0,0,0,1) }}
  func test_ceniku(dosp,deti,koje,peco) { var cena:object
    if (the_duakce==a_akce) {
      if (the_soubeh==1)
        cena= php.akce2_vzorec_soubeh(0,the_duakce,the_soubezna,hnizdo.key(),dosp,deti,koje,peco);
      elseif (the_soubeh==2)
        cena= php.akce2_vzorec_soubeh(0,the_hlavni,the_duakce,hnizdo.key(),dosp,deti,koje,peco);
      else
        cena= php.akce2_vzorec_test(a_akce,hnizdo.key(),dosp,deti,koje,peco);
      alert(cena.navrh)
    }
    else alert("pro testování ceníku musí být akce nastavena jako pracovní")
  }
  // výpočet expr
  label  [lc+20,bc+230,211,] { tag:'b,cen', title:"+ ceny na web" }
  button [lc+20,bc+247,,] { tag:'b,cen', title:'manželský pár',    
    proc onclick() { expr_ceniku("2*N*Nl+2*N*sc+2*O*oc+2*N*vc+2*P",99,this.title) }}
  button [lc+20,bc+267,,] { tag:'b,cen', title:'dítě od 10,celá',  
    proc onclick() { expr_ceniku("  N*Nl  +N*sc  +O*oc  +N*vc+1*Pd",99,this.title) }}
  button [lc+20,bc+287,,] { tag:'b,cen', title:'dítě 6-10,dětská', 
    proc onclick() { expr_ceniku("  N*Nl  +N*sp  +O*op  +N*vp+1*Pd",99,this.title) }}
  button [lc+20,bc+307,,] { tag:'b,cen', title:'dítě do 6,dětská', 
    proc onclick() { expr_ceniku("         N*sp  +O*op  +N*vp+1*Pm",99,this.title) }}
  button [lc+20,bc+327,,] { tag:'b,cen', title:'dítě do 3,bez',    
    proc onclick() { expr_ceniku("                            1*Pk",99,this.title) }}
  button [lc+20,bc+347,,] { tag:'b,cen', title:'VPS se slevou',    
    proc onclick() { expr_ceniku("2*N*Nl+2*N*sc+2*O*oc+2*N*vc+2*P-1*Sv",99,this.title) }}
  button [lc+20,bc+367,,] { tag:'b,cen', title:'PPS se slevou',    
    proc onclick() { expr_ceniku("2*N*Nl+2*N*sc+2*O*oc+2*N*vc+2*P-1*Sp",99,this.title) }}
//#   // výpočet expr - EXPERIMENTÁLNÍ
//#   label  [lc+20,bc+39,211,] { tag:'b,cen,ex1', title:"+ ceny na web" }
//#   button [lc+20,bc+56,,] { tag:'b,cen,ex1', title:'manželský pár',    proc onclick() {
//#     expr_ceniku("2*N*Un +2*N*US+2*O*UO+2*N*UV +2*Up",30,this.title) }}
//#   button [lc+20,bc+76,,] { tag:'b,cen,ex1', title:'dítě od 10,celá',  proc onclick() {
//#     expr_ceniku("  N*Dn   +N*DS +O*DO  +N*DV +1*Dp",10,this.title) }}
//#   button [lc+20,bc+96,,] { tag:'b,cen,ex1', title:'dítě 6-10,dětská', proc onclick() {
//#     expr_ceniku("  N*Dn   +N*Ds  +O*Do  +N*Dv +1*Dp",6,this.title) }}
//#   button [lc+20,bc+116,,] { tag:'b,cen,ex1', title:'dítě do 6,dětská' proc onclick() {
//#     expr_ceniku("          N*Ds  +O*Do  +N*Dv +1*Dp",3,this.title) }}
//#   button [lc+20,bc+136,,] { tag:'b,cen,ex1', title:'dítě do 3,bez'    proc onclick() {
//#     expr_ceniku("                            1*Dp",2,this.title) }}
//#   button [lc+20,bc+156,,] { tag:'b,cen,ex1', title:'VPS ve službě',    proc onclick() {
//#     expr_ceniku("2*N*Sn +2*N*SS+2*O*SO+2*N*SV +2*Sp + 1*Sa",30,this.title) }}
  func expr_ceniku(expr,vek,tit) { // verze 2016 i verze 2017
    alert(conc('<b>',tit,'</b><br><br>',php.akce2_vzorec_expr(a_akce,hnizdo.key(),expr),
      '<br><br>(N je počet nocí, O je počet obědů)'))
  }
//  proc expr_ceniku(expr,vek,tit) {
//    eq(cen_ver.key,0,1); // verze 2016 i verze 2017
//    alert(conc('<b>',tit,'</b><br><br>',ask('akce2_vzorec_expr',a_akce.get,expr),
//      '<br><br>(N je počet nocí, O je počet obědů)'))
//#   | eq(cen_ver.key,1); // verze 2017
//#     alert(conc(tit,': ',ask('akce2_vzorec_expr_2017',a_akce.get,expr,vek),
//#       '<br><br>(N je počet nocí, O je počet obědů)'))
//  }
  # ------------------------==> . archiv
  # tags:   doc_show=pro zobrazení složky, doc_crea=nabídka vytvoření složky
  var autorized= 0
  var archiv_slozka:object
  var cloud:text, folder:text
  // výzva k přihlášení do cloudu
  button login [15,25,,] { tag:'doc_log', title:'přihlásit k Google Disk (intranet)', format:'n', skill:'y|y'
    proc onclick() {
      warning("answer@smidek.eu: ",function('caller','return Ezer.Google.authorize(caller);',this));
    }
    proc onautorize(ok) { autorized.set(ok);
      ok; form.display(0,'doc_log'); archiv.ukaz(akce_get('archiv'));
      warning("přístup na Goggle Disk povolen")
    | warning("přístup na Goggle Disk zamítnut")
  }}
  // vytvoření archivu
  button archiv [15,45,,] { tag:'doc_crea', title:'vytvořit', format:'n',
    skill:'faa|faan;caa|caan;yaa|yaan'
    const gd = "https://drive.google.com/folderview?id="
    proc ukaz(_folder) { // ukáže tlačítko jako vytvořit pokud akce.archiv='' jinak zobrazí folder
      has_skill('y'); not(autorized.get);
      form.display(1,'doc_log'); form.display(0,'doc_crea|doc_show');
      return
    | ukaz_syn(_folder)
    }
    proc ukaz_syn(_folder) { // ukáže tlačítko jako vytvořit pokud akce.archiv='' jinak zobrazí folder
      _folder; cloud.set(substr(_folder,0,2)); folder.set(substr(_folder,2));
      { eq(cloud.get,'H:');
        popis.set(conc("Archiv pro akce roku ",akce_get('rok')," s kódem ",akce_get('kod'),
          " je obsažen ve složce <b>",folder.get,"</b> na serveru"));
        drop.init(folder.get,'H:','.*');
      | eq(cloud.get,'G:');
        popis.set(conc("Archiv pro akce roku ",akce_get('rok')," s kódem ",akce_get('kod'),
          " je obsažen na <a target='GD' href='",gd.get,folder.get,"'>Google Disku</a>"));
        drop.init(folder.get,'G:');
      | warning("archiv '",_folder,"' nelze zobrazit");
      };
      drop.lsdir;
      form.display(1,'doc_show'); form.display(0,'doc_crea');
    | drop.init('?');
      form.display(0,'doc_show'); form.display(1,'doc_crea');
    }
    proc onclick() { var path: text, _folder:text
      [ koren.key | alert("Před vytvořením je nutné zvolit kořen složky"); return ];
      path.set(conc(koren.get,'/',path_rok.get,'/',path_kod.get));
      confirm("Mám vytvořit složku pro dokumenty akce<br><b>",
        akce_get('nazev'),'</b> ve složce<br><b>',path,'</b> ?');
      // vytvoření složky na serveru
      cloud.set(archiv_slozka.get('hodnota'));
      drop.init(archiv_slozka.get('popis'),cloud.get);
      _folder.set(drop.mkdir(path_rok.get)); // přepne na vytvořenou složku
      _folder.set(drop.mkdir(path_kod.get)); // takže tato je v ní vnořená
      akce_set_archiv(cloud.get,_folder); ukaz(conc(cloud.get,_folder))
    }
  }
  select koren [90,45,150,] { tag:'doc_crea', title:'^kořenná složka', type:'map', format:'nt',
    options:akce_slozka.zkratka
    proc onchanged() {
      archiv_slozka.set(ask('select_object','hodnota,ikona,barva,popis','_cis',
        conc("druh='akce_slozka' AND data=",this.key)));
      path_rok.set(replace(archiv_slozka.get('ikona'),'{rok}',akce_get('rok')));
      path_kod.set(replace(archiv_slozka.get('barva'),
        '{kod}',akce_get('kod'),'{nazev}',akce_get('nazev')));
    }
  }
  field path_rok [250,45,95,] { tag:'doc_crea', title:'^složka pro rok', format:'dn' }
  field path_kod [355,45,95,] { tag:'doc_crea', title:'^podsložka akcí', format:'dn' }
  // zobrazení archivu
  label popis [15,45,440,] { tag:'doc_show', format:'n' }
  label [15,60,455,] { tag:'doc_show', format:'n',
    title:'archivované soubory svázané s akcí - lze k nim přidat další přetáhnutím myší, zrušit a stáhnout je lze kontextovým menu' }
  label drop [15,95,435,310] { tag:'doc_show', type:'drop', format:'n'
    proc ondrop(f) {
      gt(f.size,5242880); alert('odmítnuto ',f.name,' má více jak 5MB');
      return(0)
    | return(1); // return(conc(prefix.get,f.name)) - případné přejmenování
    }
    proc onload(f) { warning("přeneseno:",f.name) } // po dokončení přenosu
    proc onerror(e) { warning(e.msg); return(1) } // vrácení 0 způsobí standardní hlášení chyby
    proc onmenu(op,title,ref) { // op=remove|remove-all
      eq(cloud.get,'G:');
      [ eq(op,'viewer'); [ function('ref',"window.open(ref,'viewer')",ref) ]
      | eq(op,'remove'); [ function('ref',"var request=gapi.client.drive.files.trash({'fileId':ref});request.execute(function(resp){});",ref) ]
      | warning('zatím neimplementovaná operace') ]
    | eq(cloud.get,'H:');
      [ eq(op,'viewer'); [ function('ref',"window.open(ref,'viewer')",ref) ]
      | eq(op,'remove'); [ confirm("Opravdu smazat ",title," ?");
                           warning(ask('drop_unlink',folder.get,ref,'H:')) ] // odebrání souboru
      | warning('zatím neimplementovaná operace') ];
      drop.lsdir;
    }
  }
  # ------------------------==> . dokumenty na Synology
  use folders:  area _page._folders { tag:'syn' }
  var dcloud='U:',    // U: je třeba nastavit Ezer.options.path_files_u na složku obsahující droot
      abase:text,     // Ezer.options.path_akce
      aroot:text,     // absolutní adresa Ezer.options.path_files_u (má / na konci)
      droot='docs',   // kořen pro filebrowser 
      dfold='docs'    // zobrazená podsložka droot (bez koncového /)
  label [18,25,300,40] {tag:'syn' title:"Přidávat soubory lze přetažením myší, pravým tlačítkem lze přidávat podsložky, kliknutím stáhnout nebo zobrazit (sdílet a mazat lze jen z prostředí Synology)" }
  button [324,42,,] {tag:'syn' title:'[fa-briefcase] založit archiv' 
    proc onclick() { var kod:number
      kod.set(akce_get('kod'));
      kod;
      ask('tut_mkdir',abase.get,akce_get('rok'),kod,akce_get('nazev'));
      DocRefresh; DocBrowse
    | not(kod); alert("akce musí mít definovaný číselný kód tzn. účetní symbol")
  }}
  button [424,42,,] {tag:'syn' title:'[fa-question]' 
    proc onclick() {
      DocBrowse
  }}
  proc DocInit() { 
    abase.set(function("return Ezer.options.path_akce;"))
  }
  proc DocBrowse() { 
    fdrop.off;
    info.sey.ma_archiv.load('tut_ma_archiv','akce','id_duakce',abase.get);
  }
  proc DocRefresh() {
    DocShow(akce_get('rok'),akce_get('kod'))
  }
  proc DocShow(_rok,_kod) { var y:object
//    clear;
    _rok; _kod;
    y.set(ask('tut_dir_find',abase.get,_rok,_kod));
    y.ok; 
//    echo(debug(y));
    aroot.set(function('aroot',"return Ezer.options.path_files_u=aroot;",y.aroot)); 
    droot.set(y.droot); 
    dfold.set(droot.get);
    folders.Load(aroot.get,droot.get); 
    files.Load;
    fdrop.Init;
    fdrop.on;
  | folders.Init;
    files.Init;
  }
  # složky - tree area
  area _folders { title:"<div id='files' style='left:530px;top:112px;width:160px;height:370px;border-right:1px solid grey;position:absolute;z-index:1'></div>"
    var click_path:text
    proc tree_onclick(fid,id,dat,com,x,path,txt) { var i:text
//      echo("tree_onclick: txt=",txt," id=",i," path=",path);
      page.dfold.set(replace(path,'\|','/'));
      page.fdrop.Init;
      page.files.Load;
    }
    proc tree_oncontextmenu(fid,id,dat,com,x,path,txt) { 
      click_path.set(replace(path,'\|','/'));
      echo('tree:',click_path.get);
    }
    menu { type:'context'
      item { title:'vložit podsložku', proc onclick() { var folder:text
        folder.set(prompt2('název podsložky:'));
        ask('tut_mkdir',page.abase.get,akce_get('rok'),0,click_path.get,folder);
        page.DocRefresh
      }}
    }
    proc Load(_a,_b) { 
      this.tree_show(ask('tut_dir',_a,_b),'files',0,°{mode:'folders',theme:'mootree.gif'}); 
      this.tree_expand(1) 
    }
    proc Init() { 
      this.tree_show(°{prop:°{id:'<žádný dokument>'}},'files',0,°{mode:'folders',theme:'mootree.gif'}); 
    }
  }
  # soubory - seznam
  label files [179,72,258,360] {tag:'syn' css:'files' style:"z-index:3;padding:5px"
    proc Load() { fdrop.off; this.set(ask('tut_files',aroot.get,dfold.get)); echo(this.self)}
    proc Init() { this.set(''); }
    proc Test() { alert('au')}
    proc Menu(op,name,ref) { 
      eq(op,'viewer'); function('url',"window.open(url,'viewer')",ask('tut_file_url',ref,name))
    }
  }
  # soubory - drop area
  label fdrop [192,90,241,50] {tag:'syn' type:'drop' title:'vlož soubor sem' css:'drop' style:"z-index:2"
    proc Init() { // podsložka {root} s lomítkem jen na konci
      fdrop.init(dfold.get,dcloud.get); 
    }
//    proc ondrop(f) {                                                  // možnost odmítnutí přenosu
//      eq(f.name,'ugly.jpg'); warning('fuj ',f.name); return(0)        // navrácením 0
//    | gt(f.size,5242880); warning('odmítnuto ',f.name,' má více jak 5MB'); return(0)
////      | warning('přenášeno:',f.name)                                   // jméno nemusí být ASCII
//    | return(f.name);
//    }
    proc onload(f) { // po dokončení přenosu, jméno bude ASCII
      files.Load;
      off;
    } 
    proc onerror(e) { // funkce onerror nesmí obsahovat asynchronní kód a lokální proměnné, ale přes call lze zavolat už cokoliv
      warning(e.msg); return(1) // vrácení 0 způsobí standardní hlášení chyby
    }
    proc on() {
      function('f',
        "jQuery('div.files').on({dragover: evt => { jQuery('div.drop').css({zIndex:4});return false; }})",
        page.files);
    }
    proc off() {
      function('f',
        "jQuery('div.drop').css({zIndex:2});",
        page.files);
    }
  }
  # --------------------------- pozadí stránky a kontextové menu nakonec
  label frame [2,16,443,385] { title:'', css:'ae_work page' }
  menu detail { type:'context' join:frame
    item { title:'ukázat změny', func onclick () {
      if (key_akce) track.back_show('akce',key_akce) } }
  }
}
# ==> VÝBĚR AKCE
form _info [,,*,50] {
  # -----------------------------------------------------------------------------==> . seznam GOOGLE
  # kopie číselníku z intranetu -  pro ys
  view ga: table g_akce
  view ja: table join_akce {join_type:'LEFT',join:'USING(g_rok,g_kod)'}
  view da: table akce      {join_type:'LEFT',join:'ON ja.id_akce=da.id_duakce'}
  view pa: table pobyt     {join_type:'LEFT',join:'ON pa.id_akce=da.id_duakce'}
  browse google [0,36,200,500] { tag:'g', rows:27, qry_rows:1, buf_rows:120, group_by:"id_gakce"
    show id_gakce {data:ga.id_gakce}
    show id_akce [,,30,] {title:'akce', data:ja.id_akce, format:'rsqu', css_cell:'id_akce,0:yellow'}
    show org {data:da.access}
    show naz_akce {data:da.nazev}
    show ma_cenik {data:da.ma_cenik}
    show ma_cenu {data:da.ma_cenu}
    show cena {data:da.cena}
    show      {data:pa.id_pobyt}
    show rok  {data:ga.g_rok}
    show archiv {data:da.archiv}
    show curr {expr:"IF(da.id_duakce=@curr,1,0)" }
    show kod   [,,36,] {title:'kód účetní',    data:ga.g_kod, format:'rs+q*'
      css_cell:'org,0:,1:ezer_ys,2:ezer_fa,3:ezer_db' }
    show nazev [,,205,] {title:'název', data:ga.g_nazev, format:'tsq*'
      css_cell:"curr,1:curr_akce"}
    show ucast [,,30,] {title:'úč.', format:'rtsq=', help:'počet skupin účastníků',
      expr:"SUM((SELECT IF(COUNT(*)>0,1,0) FROM spolu WHERE id_pobyt=pa.id_pobyt AND funkce!=99))"}
    show pecou [,,30,] {title:'pč.', format:'rtsq=', help:'počet pečounů'
      expr:"SUM((SELECT COUNT(*) FROM spolu WHERE id_pobyt=pa.id_pobyt AND funkce=99))"}
    show od    [,,65,] {title:'od',     data:ga.g_od, format:'rsq*'}
    show do    [,,65,] {title:'do',     data:ga.g_do, format:'rsq*'}
    show stat  [,,0,] {title:'stat.',   data:da.statistika, map_pipe:ms_akce_stat.zkratka, format:'sq#'}
    proc onclick() { stat.width; stat.width(0); do.width(65) | stat.width(65); do.width(0) }
    proc onsubmit() { pracovni }
    proc onrowclick() { page.show_akce(id_akce.get); }
    menu { type:'context'
      item { title:'Vybrat akci jako pracovní', proc onclick() { pracovni }}
    }
    // označení akce jako pracovní
    proc pracovni() { #==> .. pracovní YS
      var org_ys:number
      org_ys.set(1);
      not(accessed(org_ys)); // jen YS používá intranet
      alert("nemáte oprávnění označit jako pracovní akci jiné organizace");
    | id_akce.get; volba_akce(id_akce.get)
    | not(eq(fdate('Y',od.get),rok.get));
      alert('Akce má na intranetu uvedeno chybné datum zahájení (např. chybějící nebo z jiného roku)');
    | confirm('akce ',nazev.get,' zatím není v databázi, mám ji založit?');
      the_duakce.set(akce.insert_record(
        object('access',org_ys,'nazev',nazev.get,'datum_od',date2sql(od.get),'datum_do',date2sql(do.get))));
      join_akce.insert_record(object('id_akce',the_duakce.get,'g_rok',rok.get,'g_kod',kod.get));
      google.browse_row; volba_akce(id_akce.get) //,nazev.get,ma_cenik.get,ma_cenu.get,cena.get)
    }
  }
  # --------------------------------------------------------------------------------==> . seznam SEY
  # přechod na účastníky je omezen datovými právy
  view da2: table akce
  view ja2: table join_akce {join_type:'LEFT',join:'ON ja2.id_akce=da2.id_duakce'}
  view ga2: table g_akce    {join_type:'LEFT',join:'USING(g_rok,g_kod)'}
  view sa2: table akce      {join_type:'LEFT',join:'ON sa2.id_hlavni=da2.id_duakce'}
  view pa2: table pobyt     {join_type:'LEFT',join:'ON pa2.id_akce=da2.id_duakce'}
  browse sey [0,36,200,500] { tag:'s', rows:27, qry_rows:1, buf_rows:120, group_by:"da2.id_duakce"
      css_rows:"barva,1:cerveny,2:zluty,3:skrtnuty" // akce je: 1=hlavní | 2=souběžná | 3=zrušená
      optimize:°{fetch:'all'}
    show id_akce  {data:da2.id_duakce}
    show          {data:ja2.id_akce}
    show          {data:pa2.id_pobyt}
    show ma_archiv // [,,10,] 
    show ma_cenik {data:da2.ma_cenik}
    show ma_cenu {data:da2.ma_cenu}
    show cena {data:da2.cena}
    show id_gakce {data:ga2.id_gakce}
    show id_hlavni {data:da2.id_hlavni}
    show id_soubeh {data:sa2.id_duakce}
    show archiv {data:da2.archiv}
    show rok {expr:"YEAR(da2.datum_od)"}
    show curr { expr:"IF(da2.id_duakce=@curr,1,0)" }
    show org { data:da2.access }
    show web_k { data:da2.web_kalendar }
    show web_p { data:da2.web_prihlasky }
    show web_o { expr:"da2.web_obsazeno OR IF(da2.web_maximum,da2.web_maximum<=SUM((SELECT 1 FROM pobyt WHERE id_pobyt=pa2.id_pobyt AND funkce!=99)),0)" }
    show web_m { expr:"IF(da2.mrop OR da2.firm,1,0)" }
    show SF [,,20,] {title:'SF', expr:"IF(da2.access=1,'S',IF(da2.access=2,'F','?'))"
      help:"pořádá S=YMCA Setkání, F=YMCA Familia"
      css_cell:'org,0:,1:ezer_ys,2:ezer_fa,3:ezer_db', format:'rsq*' }
//  show barva { expr:"CASE WHEN da2.id_hlavni THEN 2 WHEN sa2.id_duakce THEN 1 ELSE 0 END" }
    show barva { expr:"IF(da2.zruseno,3,IF(da2.id_hlavni,2,IF(sa2.id_duakce,1,0)))" }
    show kod   [,,36,] {title:'kód účetní', expr:"IF(ga2.g_kod,ga2.g_kod,da2.ciselnik_akce)"
      css_cell:'ma_archiv,0:,1:oranzovy', format:'rsq*' }
    show druh  [,,60,] {title:'typ',    data:da2.druh, map_pipe:ms_akce_typ.zkratka, format:'sq#'
      css_cell:'web_p,0:,1:zluty' }
    show nazev [,,104,] {title:'název', expr:"IF(da2.nazev='',ga2.g_nazev,da2.nazev)", format:'tsq*',
      css_cell:"curr,1:curr_akce"}
    show ucast [,,30,] {title:'úč.', format:'rtsq=', help:'počet skupin účastníků',
      expr:"SUM((SELECT IF(COUNT(*)>0,1,0) FROM spolu WHERE id_pobyt=pa2.id_pobyt AND funkce!=99))"
      css_cell:'web_o,0:,1:cerveny' }
    show pecou [,,30,] {title:'pč.', format:'rtsq=', help:'počet pečounů'
      expr:"SUM((SELECT COUNT(*) FROM spolu WHERE id_pobyt=pa2.id_pobyt AND funkce=99))"}
    show misto [,,50,] {title:'místo',  data:da2.misto, format:'tsq*'}
    show od    [,,65,] {title:'od',     data:da2.datum_od, format:'rs+q*'
      css_cell:'web_k,0:,1:azurovy' }
    show do    [,,65,] {title:'do',     data:da2.datum_do, format:'rsq*'
      css_cell:'web_k,0:,1:azurovy' }
    show stat  [,,0,] {title:'stat.',    data:da2.statistika, map_pipe:ms_akce_stat.zkratka, format:'sq#'
      css_cell:'web_m,0:,1:zluty' }
    show zen   [,,0,] {title:'žen', format:'rtsq=', help:'počet žen',
      expr:"SUM((SELECT IF(COUNT(*)>0,1,0) FROM spolu JOIN pobyt USING (id_pobyt) JOIN osoba USING (id_osoba) WHERE id_pobyt=pa2.id_pobyt AND sex=2 AND funkce=0))" }
    proc onclick() { 
      stat.width; stat.width(0); zen.width(0) ; do.width(65)
    | stat.width(35); zen.width(29); do.width(0)
    }
    proc onchange() {
      [ has_skill('yas'); page.DocBrowse ]
    }
    proc onsubmit() { pracovni }
    proc onrowclick() { 
      page.show_akce(id_akce.get); 
      [ has_skill('yas'); page.DocBrowse ] 
    }
    menu { type:'context'
      item { title:'Vybrat akci jako pracovní', proc onclick() { pracovni }}
    }
    // označení akce jako pracovní
    proc pracovni() { #==> .. pracovní
      not(accessed(org.get));
      alert("nemáte oprávnění označit jako pracovní akci jiné organizace");
    | pracovni_curr
    }
    proc pracovni_curr() {
      volba_akce(id_akce.get);
      curr.set_attrib('expr',conc("IF(da2.id_duakce=",id_akce.get,",1,0)"));
      this.browse_seek;
    }
    proc onstart() {
      gt(sys('options','watch_access'),3);
      SF.width(0); kod.width(0); nazev.width(140); misto.width(70);
    }
  }
  # --------------------------------------------------------------------------------------==> . akce
  select rok [4,5,52,] { tag:'r', format:'t', proc onchanged() { akce_roku(this.get) }}
  # je voláno s kontextem správného the_source
  proc nazev_akce(id_akce) {
    var nazev: text
    { and(id_akce,not(eq(id_akce,'0')));
      { eq(the_source.get,'google');
        nazev.set(conc(' Akce ',google.kod.get,'/',google.rok.get,' - ',google.nazev.get));
      | eq(the_source.get,'sey');
        nazev.set(conc(' ',conc(sey.nazev.get,', ',fdate('Y',sey.od.get))));
      | alert('V levém menu je třeba vybrat typ akcí (na které máte oprávnění)')
      };
    };
    return (nazev);
  }
  proc volba_akce(id_akce) { #==> . volba_akce
    akce_choose(id_akce);
    lst.info.fill(conc(the_title.get,' '));
    [ ucast.call('clear_ucast2') ];
    [ has_skill('yam;fam;cam'); maia.call('init_ucast') ];
    [ has_skill('crz') | this.call('pece.init_pece') ]; // CRŽ
    // ošetření souběžných akcí - 0=normální akce, 1=hlavní akce, 2=souběžná akce
    [ has_skill('f'); page.soubezna.set(eq(the_soubeh.get,2)) ];
    // nastavení cookies podle organizace
    id_akce;
    { eq(the_org.get,1);
      the_akce_ys.set(the_duakce.get);
      set_cookie(conc(sys('root'),'_akce_ys'),the_akce_ys.get)
    | eq(the_org.get,2);
      the_akce_fa.set(the_duakce.get);
      set_cookie(conc(sys('root'),'_akce_fa'),the_akce_fa.get)
    | // mimo YS+FA
      set_cookie(conc(sys('root'),'_akce'),the_duakce.get)
    };
    ucast.ucast_hnizda();
    pece.pece_hnizda();
    [ has_skill('yam;fam;cam'); maia.mail_hnizda() ];
    tisk.tisk_hnizda()
  }
  label head [0,0,*,30] { title:'', style:'z-index:0' }
  # volba pracovní akce
  button [376,5,,] { title:'Pracovní', proc onclick() {
    eq(the_source.get,'sey'); info.sey.pracovni | info.google.pracovni
  }}
  # ==> .. načtení akcí z intranetu
  button [-280,5,,] { title:'Obnovit rok z intranetu', skill:'y|yaa+'
    help:"obnoví akce roku z http://intranet.setkani.org"
    # získání seznamu akcí z docs.google.com pomocí API spredsheet
    proc onclick() {
      lt(rok.get,2010); alert("Z intranetu lze obnovovat jen akce roku 2010 a starší");
    | confirm("Mám pro rok ",rok.get," obnovit seznam akcí (musí mít značku 'a')?",
            "<hr>POZOR: v souboru <a target='cis' href='https://docs.google.com/spreadsheets/d/16nu8StIpIPD9uY-cskQOAXCnVY6hL9OTaTBdpPoUrnA/edit#gid=275004412'>ciselnik_akci</a> musí být obnovovaný rok zcela vlevo");
      alert('bylo obnoveno ',ask('akce2_roku_update',rok.get),' záznamů o akcích');
      google.browse_seek;
    }
  }
  # export vlastností akcí nastaveného roku
  button [-226,5,,] { title:'Export', func onclick() {
    sey.browse_count(); sey.browse_export({file:'akce',type:'xls'});
    alert("vygenerovaný sešit lze stáhnout <a href='docs/akce.xls' target='xls'>zde</a>");
  }}
  # ==> .. graf průchodu účastníků akcemi
  button cesty [-123,5,,] { title:'Akce účastníků'                                       skill:'y|y;f|f'
    func onclick() { HNIZDA.cesty(page.key_akce) }}
  # ==> .. zobrazení mapy s pozicí míst účastníků dané akce a oprava PSČ
  button mapka [-21,5,,] { title:'Mapa účastníků',                                  skill:'#crz'
    help:"mapa s bydlišti účastníků akcí"
    func onclick() {
      if ( the_source=='google' ) { 
        mapa.id_akce= google.id_akce;
        mapa.pecouni= google.pecou;
      }
      else { // the_source=='sey' 
        mapa.id_akce= sey.id_akce;
        mapa.pecouni= sey.pecou;
      }
      if ( mapa.id_akce ) {
        mapa.metoda= 'akce';
        mapa.modal(20,100,nazev_akce(mapa.id_akce));
      }
    }
  }
  proc fill(x) {
    [ x; head.set(conc("<div class='karta' style='padding-left:60px'>",x,"</div>")) ];
  }
}
# =========================================================================================> anotace
# oprava textu anotace na webu
panel ANOTACE [0,0,645,520] { title:' Úprava textu anotace', type:'popup', css:'dialog'
  use d: form _d [0,0,,],
  proc oprava(_ida) {
    _ida;
    d.load(_ida);
    [ panel.modal ] 
  | alert("napřed novou akci ulož");
  }
  form _d [10,10,600,460] {
    button  [540,9,45,20] { title:'Uložit', help:'ukončit editor a uložit změny'
      proc onclick() { form.save; panel.hide(1); } }
    button  [600,9,45,20] { title:'Zpět', help:'ukončit editor bez uložení změn'
      proc onclick() { panel.hide(0); }
    }
    edit html [0,40,655,480] {type:'html', data:akce.web_anotace, par:°{toolbar:'EzerLetter'} },
  }
}
# ==========================================================================================> hnízda
# přehled obsazení hnízd
panel HNIZDA [0,0,640,520] { title:' ', type:'popup', css:'dialog',
    style:"height:calc(100% - 130px)"
  func prehled(ida) { var y:object
    clear(); panel.title= "Přehled obsazenosti hnízd této akce";
    y= php.akce2_hnizda(ida);
    h.txt= y.html;
    panel.modal(210,60);
  }
  func cesty(ida) { var y:object
    clear(); panel.title= "průchod účastníků (vč. PPS) této akce přechozími akcemi";
    js.highcharts_load(); 
    h.txt= "<div id='akce_ucastnici' style='width:445px'></div>";
    y= php.akce_ucastnici(ida,'matrix');
    if (y.chart) js.highcharts_show(y.chart,'akce_ucastnici');
    panel.modal(210,60);
  }
  use h: form {
    label txt [0,0,*,*] { style:'overflow:auto' }
  }
}
map akce_cenik:  table akce {where:"ma_cenik=1", order:'datum_od', key_id:'id_duakce'}
map akce_slozka: table _cis {where:"druh='akce_slozka'", order:'poradi', key_id:'data'}
map ms_cenik_verze: table _cis {where:"druh='ms_cenik_verze'", order:'poradi', key_id:'data'}
map ms_akce_stat:   table _cis {where:"druh='ms_akce_stat'", order:'poradi', key_id:'data'}
map akce_garant:    table _cis {where:"druh='akce_garant'", order:'poradi', key_id:'data'}