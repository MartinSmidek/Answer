# ------------------------------------------------------------------------------------------------ #
# Karta pro výběr akce                                                                             #
#                                                                                                  #
# Ans(w)er/Ezer                                     (c) 2007-2015 Martin Šmídek <martin@smidek.eu> #
# ------------------------------------------------------------------------------------------------ #

# ------------------------------------------------------------------------------------------==> stav
var zalozka: text               // inf | cor | cen
var the_omezeni: text           // podmínka omezující akce podle my_access
var menu_item:object            // aktivní menu
# ----------------------------------------------------------------------------------==> inicializace
# inicializace panelu proběhne po akce.onstart, zobrazí seznam akcí daného roku podle the_source
proc onfirstfocus() {
#   spinner(1);
  [ dirty.get | akce_onstart ]; // zavolá akce_work
  // detekce rozměrů
  curr_w.set(cconc(lt(sys('screen','width'),1280),1024,1280));
  // zobrazení akce
  info.fill(the_title.get);
  // inicializace zobrazení
  info.rok.selects(javascript(conc(
    "var x='';for(i=",sum(fdate('Y'),1),";i>=1990;i--){x+=i+',';};x+='0'")));
  info.rok.set(the_rok.get);
  page.cen_copy.selects("neměnit:0");
  zalozka.set('inf');
  // zobrazení seznamu akcí
  [ has_skill('y;f;crz');  m.g.sey.click
  | has_skill('caam'); m.c.m.click
  | has_skill('caar'); m.c.r.click
  | has_skill('a');    m.c.a.click
  ];
  page.inf.onclick;
#   spinner(0);
}
# -------------------------------------------------------------------------------------------==> use
use info: form _info [12,4,,]
use page: form _page [512,40,,]
# use fil:  form file._fil [512,72,,]  { tag:'file', format:'n' }

# ----------------------------------------------------------------------------==> globální procedury
# nastaví akci jako pracovní
proc akce_work(id_akce) {
  info.volba_akce(id_akce);
  info.rok.set(the_rok.get);
}
proc reaccess() { #==> . reaccess - změna
  # intranet povol jen Setkání
  [ m.g.part('goo'); m.g.goo.enable(eq(my_org.get,1)) ];
  # přenastav pracovní akci podle my_org
  { eq(my_org.get,1);
    akce_work(the_akce_ys.get)
  | eq(my_org.get,2,4);
    akce_work(the_akce_fa.get)
  | akce_work(the_duakce.get)
  };
  # překresli aktivní kartu
  menu_item.get;
  m.onclick(menu_item.get);
  { eq(the_source.get,'sey'); info.sey.pracovni | info.google.pracovni }
}
# -----------------------------------------------------------------------------==> lokální procedury
# vrátí hodnotu položky z google resp. sey
proc akce_get(_fld) { var fld:object
  { eq(the_source.get,'sey'); fld.set(info.sey.part(_fld).get) | fld.set(info.google.part(_fld).get) };
  return(fld);
}
# zapíše hodnotu akce.archiv (a zapíše ji i do google resp. sey)
proc akce_set_archiv(cloud,folder) { var path:text
  path.set(conc(cloud,folder));
  ask('query',conc("UPDATE akce SET archiv='",path,"' WHERE id_duakce=",page.a_akce.get));
  { eq(the_source.get,'sey'); info.sey.archiv.let(path) | info.sey.google.let(path) };
}
# -----------------------------------------------------==> . reakce na změny rozměrů okna prohlížeče
const min_h=550           // minimální výška (nelze jednoduše měnit - jsou k ní vztaženy souřadnice)
var   curr_h: number      // výška nad základní výšku
var   curr_w: number      // 1024 nebo 1280

proc onresize(w,h) { // echo('lst onresize(',w,',',h,')');
  [ and(lt(h,min_h.get),not(eq(curr_h.get,min_h.get)));
    curr_h.set(0); h_change_all()
  | and(gt(h,min_h.get),not(eq(h,sum(curr_h.get,min_h.get))));
    curr_h.set(minus(h,min_h.get)); h_change_all() ];
  [ lt(w,1220); m.click(2,1) | m.click(1,1) ];
  [ and(lt(w,1280),eq(curr_w.get,1280)); narrow
  | and(gt(w,1279),eq(curr_w.get,1024)); wide ];
}

# změna šířky 1024/1280
proc narrow() { curr_w.set(1024); w_change_all  }
proc wide() { curr_w.set(1280); w_change_all }
proc w_change_all() {}

# změna výšky oproti základní min_h
proc h_change_all() {
//  page.property(object('down',curr_h.get),'b');      // posunutí proti původnímu - omezeno na min_h
  page.frame.property(object('stretch',curr_h.get)); // protažení proti původnímu
  page.drop.property(object('stretch',curr_h.get));
  h_change_b;
}
# změna počtu řádků browse oproti aktuálním řádkům
proc h_change_b() { var r:number, h:number
  h.set(sys('screen','height'));
  r.set(divide(sum(h.get,-173),17));
  not(eq(r,info.sey.rows));
  info.google.set_attrib('rows',r);
  info.sey.set_attrib('rows',r);
  page.c.set_attrib('rows',sum(r,-2));
}

# ------------------------------------------------------------------------------------------==> MENU
# Výběr akce má dvě možnosti, uchované v proměnné the_source:
#   'google' používá jako primárná data spreadsheet v intranetu YS a z něj upravuje tabulku AKCE
#   'sey' používá pouze tabulku AKCE

menu m {type:'left', format:'f+'
  # it: info.tag
  # s:  source
  # o:  omezeni  org|access
  # a:  rok=akce_roku | akce_vsechny
  # cond
  menu c {title:'Výběr akce',type:'group',                                                 skill:'c#crz'
    // jen pro CR
    item m {title:'[MS] program Manželská setkání', skill:'caam', par:°{omez:'da2.druh IN (1,2,3,4)'} }
    item r {title:'[R] programy Rodina mimo MS',    skill:'caar', par:°{omez:'da2.druh NOT IN (1,2,3,4)'} }
    item a {title:'[...] bez omezení',              skill:'a',    par:°{omez:'1'} }
      proc onclick (i) {
        info.rok.display(1); info.sey.display(1); info.google.display(0);
        the_omezeni.set(i.par.omez);
        the_source.set('sey'); akce_roku(info.rok.get);
  } }
  menu g {title:'Výběr akce zadaného roku',type:'group',                                 skill:'y;f;crz'
    item goo {title:'[fa-google] podle číselníku akcí z intranetu',                      skill:'y'
      par:°{it:'g|r',s:'google',o:'',a:'rok'}}
    item sey {title:'[fa-search] ze seznamu všech našich akcí'
      par:°{it:'s|r',s:'sey',o:'org',a:'rok'}}
    item {title:'[fa-search] ze seznamu všech akcí'                                      skill:'#crz'
      par:°{it:'s|r',s:'sey',o:'access',a:'rok'}}
  }
  menu {title:'Výběr ze všech roků',type:'group',                                        skill:'y;f;c'
    item sey {title:'[fa-search] ze seznamu všech akcí',    par:°{it:'s',cond:'1'} }
    item  {title:'[fa-search-plus]... opravdové akce',      par:°{it:'s',cond:'da2.spec=0'} }
    item  {title:'[fa-search-minus] ... jen různé seznamy', par:°{it:'s',cond:'da2.spec=1'} }
  }
  proc onclick (i) {
    menu_item.set(i);
    info.display(2,i.par.it); the_source.set(i.par.s);
    [ i.par.o; the_omezeni.set(conc(
        "da2.access & ",cconc(eq(i.par.o,'org'),my_org.get,my_access.get))) ];
    { i.par.a; akce_roku(info.rok.get) | akce_vsechny(i.par.cond) }
  }
}
#==> . akce definovaného roku
proc akce_roku(rok) {
  page.init_page;
  [ eq(the_source.get,'google');
    { gt(rok,2009);  // intranet YS obsahuje akce od roku 2010 včetně
      info.google.browse_load(conc("g_kat='a' AND g_rok=",rok),
      '','','','','',
      conc('SET @curr:=',the_duakce.get));
    | alert("V intranetu jsou v přesném formátu popsány jen akce od roku 2010") }
  | info.google.browse_init];
  [ eq(the_source.get,'sey');
    info.sey.browse_load(conc("YEAR(da2.datum_od)='",rok,"' AND da2.spec=0 AND ",the_omezeni.get),
      '','','','','',
      conc('SET @curr:=',the_duakce.get));
    info.sey.browse_seek(conc("da2.id_duakce=",the_duakce.get));
  ]
}
proc akce_vsechny(cond) {
  [ info.sey.browse_load(cond) ];
}
# ------------------------------------------------------------------------------------------==> AKCE
form _page [,,275,360] {
  var a_akce: number
  # ---------------------------==> . záložky
  proc tags(a,on,off) {
    form.set_css('ae_butt_off','ae_butt_on','p');
    a.set_css('ae_butt_on','ae_butt_off');
    form.display(0,off); form.display(1,on);
  }
  label inf [ 12,0,80,20] {tag:'p', title:'<b>Info</b>', format:'c', css:'ae_butt_on'
    proc onclick() { tags(this,'inf','cor|cen|doc'); zalozka.set('inf'); show_akce(a_akce.get)
  } }
  label cor [100,0,80,20] {tag:'p', title:'<b>Úprava</b>', format:'c'
    proc onclick() { tags(this,'cor','inf|cen|doc'); zalozka.set('cor'); show_akce(a_akce.get)
  } }
  label cen [188,0,80,20] {tag:'p', title:'<b>Ceník</b>', format:'c',
    proc onclick() { tags(this,'cen','inf|cor|doc'); zalozka.set('cen'); show_akce(a_akce.get)
  } }
  label doc [276,0,80,20] {tag:'p', title:'<b>Archiv</b>', format:'c',
    proc onclick() { tags(this,'doc','inf|cor|cen'); zalozka.set('doc'); show_akce(a_akce.get)
  } }
#   label doc [364,0,80,20] {tag:'p', title:'<b>Dokumenty</b>', format:'c',
#     proc onclick() { tags(this,'doc','inf|cor|cen|eko'); zalozka.set('doc'); show_akce(a_akce.get)
#   } }
  proc show_akce(id_akce) {
    page.a_akce.set(id_akce);
    eq(the_source.get,'sey');    [ info.sey.browse_count; show_page(id_akce) | init_page ]
  | eq(the_source.get,'google'); [ id_akce; show_page(id_akce) | init_page ]
  }
  proc show_page(id_akce) {
    cen_copy.enable(0); cen_copy.key(0); key_akce.set(id_akce); //fil.display(0);
    switch(zalozka.get,
    'inf',{ note.set(ask('akce2_info',id_akce));  },
    'cor',{ form.load(id_akce);
            [ has_skill('faan'); form.display(mrop.get,'corm') ];
            [ has_skill('f'); soubezna.set(cconc(id_hlavni.get,1,0)) ];
            info.sey.browse_focus },
    'cen',{ c.browse_load(conc("id_akce=",id_akce)) },
    'doc',{ archiv.ukaz(akce_get('archiv')) },
    );
  }
  proc init_page() {
    page.a_akce.set(0);
    cen_copy.enable(0); cen_copy.key(0);
    form.init;
    note.set(''); key_akce.set(' ');
  }
  # ------------------------==> . informace
  field key_akce [408,19,46,] {format:'or', css:'Label',
    style:'color:white', help:' ', tabindex:0 }
  label note [10,35,455,193] { tag:'inf', title:'', style:'color:black;font-size:12px' }
  # ---------------------------==> . položky akce
  field access { data:akce.access }
  field id_hlavni { data:akce.id_hlavni }
  field [17,49,218,] { tag:'cor', title:'^název akce *', data:akce.nazev }
  field [17,89,218,] { tag:'cor', title:'^místo', data:akce.misto }
  field datum_od [18,133,83,] { tag:'cor', title:'^začátek *', type:'date', data:akce.datum_od }
  field [118,133,83,] { tag:'cor', title:'^ukončení', type:'date', data:akce.datum_do }
  check [213,166,90,] { tag:'cor', title:'jen seznam', data:akce.spec }
  check mrop [300,166,90,] { tag:'cor', title:'mrop', data:akce.mrop, title:'mrop', skill:'faan|faa:m' }
  select [71,169,139,] { tag:'cor', title:'typ akce *', type:'map', data:akce.druh,
    options:ms_akce_typ.zkratka, format:'n' }
  radio [213,117,86,35] { tag:'cor', data:akce.strava_oddo, value:'vo'
    case [0,2,83,] { title:'večeře-oběd', expr:'vo' }
    case [0,17,76,] { title:'oběd-oběd', expr:'oo' }
    proc onchange() { echo('strava=',this.get) }
  }
  field [96,202,54,] { tag:'cor', title:'účetní symbol:', data:akce.ciselnik_akce,
    skill:'faa|faa;caa|caa' }
  // zobrazení a nastavení akce jako souběžné (stejné datum a místo)
  check soubezna [157,200,120,] { tag:'cor', title:'akce je souběžná', skill:'faa|faan',
    proc onchange() {
      { info.sey.id_hlavni.get;
        [ confirm("opravdu zrušit souběh této akce?"); ask('akce2_soubeh_nastav',form.key,0) ]
      | alert(ask('akce2_soubeh_nastav',form.key)) };
      info.sey.browse_seek; show_akce(form.key);
  }}
  // Změna ceníku
  check [134,254,93,] { tag:'cor', title:'používat ceník', data:akce.ma_cenik,
                        format:'', skill:'yaa|yaan;caa|caan;faa|faan' }
  button [14,230,,] { tag:'cor', title:'Změnit ceník podle', skill:'yaa|yaan;caa|caan;faa|faan'
    proc onclick() {
      cen_copy.enable(1);
      cen_copy.selects(ask('akce2_select_cenik',form.key))
  }}
  select cen_copy [138,232,158,] {tag:'cor', format:'ntd', help:'vzory ceníků'
    proc onchange() {
      cen_copy.key;
      confirm("opravdu zaměnit ceník za vybraný?");
      alert(ask('akce2_change_cenik',form.key,cen_copy.key))
  }}
  // jednotná cena / dospělý
  check [134,284,93,] { tag:'cor', title:'používat cenu', data:akce.ma_cenu,
                        format:'', skill:'yaa|yaan;caa|caan;faa|faan' }
  field [230,286,43,] { tag:'cor', data:akce.cena, format:'r', skill:'yaa|yaan;caa|caan;faa|faan',
                        help:'pro 1 dospělého' }
  label [279,289,20,] { tag:'cor', title:'Kč' }
  // Zpět a Uložit
  label  [193,350,98,30] { tag:'cor', css:'ae_parm', style:'z-index:1', skill:'yaa+;faa+;caa+' }
  button [199,356,,] { tag:'cor', title:'Zpět', skill:'yaa+|yaa+;faa+|faa+;caa+|caa+'
    help:'Neměnit změněná data'
    proc onclick() { form.load(page.a_akce.get);
  }}
  button [245,356,,] { tag:'cor', title:'Uložit', skill:'yaa+|yaan;faa+|faan;caa+|caan'
    help:'Uložit změněná data',
    proc onclick() {
      form.same
    | // oprava
      form.key; form.save; form.load; info.sey.browse_seek
    | // přidání
      or(has_skill("faa+;caa+"),lt(fdate("Y",datum_od.get),2011));
      form.insert; form.load;
      info.sey.raise('onrowclick',info.sey.browse_seek(conc('da2.id_duakce=',form.key)))
    | alert("Vkládat lze jen 'historické akce', po roce 2010 jen přes intranet")                    //ys
  } }
  // jen pro YF,CR - přidání a zrušení akce
  label  [10,350,164,30] { tag:'cor', css:'ae_parm', style:'z-index:1', skill:'faan;caan;yaan' }
  button [16,356,,] {tag:'cor', title:'Nová akce', format:'c', skill:'faan|faan;caan|caan;yaan|yaan'
    proc onclick() {
      eq(my_org.get,1);
      // Setkání
      { has_skill('yaan'); {
          eq(the_source.get,'google'); alert("Vkládat lze jen do seznamu všech akcí")
        | gt(info.rok.get,2009); alert("Vkládat lze jen 'historické akce', po roce 2010 jen přes intranet")
        | form.init; access.set(1); access.change; info.sey.blur(1)
        }
      | alert("Nemáš oprávnění vytvořit novou akci (kontaktuj administrátora např. Martina)")
      }
    | eq(my_org.get,2,4);
      // Familia
      form.init; access.set(2); access.change; info.sey.blur(1)
  } }
  button [91,356,,] {tag:'cor', title:'Smazat akci', format:'c', skill:'faan|faan;caan|caan;yaan|yaan'
    proc onclick() {
      var ret:object
      var msg:text
      ret.set(ask('akce2_delete_confirm',form.key));
      ret.zrusit; confirm(ret.zrusit);                  // fakt zrušit + název
      { not(ret.ucastnici) | confirm(ret.ucastnici) };  // ale akce má účastníky
      { ret.platby; alert(ret.platby)                   // nelze smazat, jsou-li platby
      | msg.set(ask('akce2_delete',form.key,ret));
        akce_roku(info.rok.get); alert(msg.get);
  } } }
  // jen pro YF - zápis roku iniciace účastníkům akce - musí být akce.mrop=1
  label  [310,350,118,30] { tag:'corm', css:'ae_parm', format:'n', style:'z-index:1',         skill:'faan' }
  button [316,356,,] {tag:'corm', title:'Potvrzení iniciace', format:'nc', style:'z-index:1', skill:'faan|faa:m'
    proc onclick() { var ret:object
      has_skill('faa:m');
      ret.set(ask('akce2_confirm_mrop',form.key,0));
      { ret.ok;
        [ confirm(ret.msg); ret.set(ask('akce2_confirm_mrop',form.key,1)); alert(ret.msg) ]
      | alert(ret.msg)
      }
  } }
  # ------------------------==> . ceník
  browse c [10,25,,] { tag:'cen', rows:26, qry_rows:1, css_rows:"barva,1:grey"
    show id_cenik { data:cenik.id_cenik }
    show id_akce  { data:cenik.id_akce }
    show polozka  { data:cenik.polozka }
    show poradi [,,25,] { title:'N', help:'pořadí ve faktuře', data:cenik.poradi, format:'ruq*s+'
      proc onsubmit() { this.save } }
    show barva { expr:"IF(LEFT(polozka,1)='-',1,0)" }
    show [,,155,] { title:'položka', help:'... do faktury', data:cenik.polozka, skill:'y|y;f|f',
      format:'tuq*s'
      proc onsubmit() { this.save } }
    show [,,105,] { title:'položka', help:'... do faktury', data:cenik.polozka,  skill:'c|c',
      format:'tuq*s'
      proc onsubmit() { this.save } }
    show za [,,40,] { title:'za', data:cenik.za, format:'urq=s',
      help:'noc:N, strava:(s=snídaně/o=oběd/v=večeře)(c=celá/p=poloviční), pobyt:P, slevy:S'
      proc onsubmit() { this.save } }
    show typ [,,50,] { title:'typ', data:cenik.typ, map_pipe:ms_akce_ubytovan.zkratka,
      format:'urq#s',                                                                    skill:'c|c'
      help:'typ ubutování: 1,2,3,...'
      proc onsubmit() { this.save } }
    show cena [,,45,] { title:'cena', help:'cena v Kč vč. DPH', data:cenik.cena, format:'urq=s'
      proc onsubmit() { this.save } }
    show dph [,,35,] { title:'dph', help:'DPH v Kč', data:cenik.dph, format:'urq=s'
      proc onsubmit() { this.save } }
    menu { type:'context', skill:'yaa+;faa+;caa+'
      item { title:'přidat nový řádek'
        proc onclick() {
          c.browse_seek(conc('id_cenik=',cenik.insert_record(
            object('id_akce',a_akce.get,'polozka','?'))));
      } }
      item { title:'odebrat běžný řádek'
        proc onclick() { confirm('opravdu odebrat položku ',polozka.get,'?');
          cenik.delete_record(conc('id_cenik=',id_cenik.get));
          c.browse_load
      } }
    }
  }
  // export ceníku
  const bc=40, lc=335
  label  [lc+16,bc,102,31] { tag:'b,cen', css:'ae_parm', style:'z-index:1' }
  button [lc+25,bc+7,,] { tag:'b,cen', title:'Export ceníku', format:'c'
    proc onclick() {
      var ret: object
      ret.set(ask('akce2_platby_xls',a_akce.get));
      ret.ok; alert(ret.xhref);
    | warning(ret.msg);
  } }
  label  [lc+20,bc+49,211,] { tag:'b,cen', title:"příklady poplatků" }
  button [lc+20,bc+68,,] { tag:'b,cen', title:'pár', proc onclick() { test_ceniku(2,0,0) }}
  button [lc+20,bc+95,,] { tag:'b,cen', title:'pár+dítě', proc onclick() { test_ceniku(2,1,0) }}
  button [lc+20,bc+123,,] { tag:'b,cen', title:'pár+kojenec', proc onclick() { test_ceniku(2,0,1) }}
  button [lc+20,bc+150,,] { tag:'b,cen', title:'dítě do 10 let', proc onclick() { test_ceniku(0,1,0) }}
  button [lc+20,bc+177,,] { tag:'b,cen', title:'kojenec', proc onclick() { test_ceniku(0,0,1) }}
  proc test_ceniku(dosp,deti,koje) { var cena:object
    eq(the_duakce.get,a_akce.get);
    { eq(the_soubeh.get,1);
      cena.set(ask('akce2_vzorec_soubeh',0,the_duakce.get,the_soubezna.get,dosp,deti,koje))
    | eq(the_soubeh.get,2);
      cena.set(ask('akce2_vzorec_soubeh',0,the_hlavni.get,the_duakce.get,dosp,deti,koje))
    | cena.set(ask('akce2_vzorec_test',a_akce.get,dosp,deti,koje)) };
    alert(cena.navrh)
  | alert("pro testování ceníku musí být akce nastavena jako pracovní")
  }
  # ------------------------==> . archiv
  # tags:   doc_show=pro zobrazení složky, doc_crea=nabídka vytvoření složky
  var autorized= 0
  var archiv_slozka:object
  var cloud:text, folder:text
  // výzva k přihlášení do cloudu
  button login [15,25,,] { tag:'doc_log', title:'přihlásit k Google Disk (intranet)', format:'n', skill:'y|y'
    proc onclick() {
      warning("answer@smidek.eu: ",function('caller','return Ezer.Google.authorize(caller);',this));
    }
    proc onautorize(ok) { autorized.set(ok);
      ok; form.display(0,'doc_log'); archiv.ukaz(akce_get('archiv'));
      warning("přístup na Goggle Disk povolen")
    | warning("přístup na Goggle Disk zamítnut")
  }}
  // vytvoření archivu
  button archiv [15,45,,] { tag:'doc_crea', title:'vytvořit', format:'n',
    skill:'faa|faan;caa|caan;yaa|yaan'
    const gd = "https://drive.google.com/folderview?id="
    proc ukaz(_folder) { // ukáže tlačítko jako vytvořit pokud akce.archiv='' jinak zobrazí folder
      has_skill('y'); not(autorized.get);
      form.display(1,'doc_log'); form.display(0,'doc_crea|doc_show');
      return
    | _folder; cloud.set(substr(_folder,0,2)); folder.set(substr(_folder,2));
      { eq(cloud.get,'H:');
        popis.set(conc("Archiv pro akce roku ",akce_get('rok')," s kódem ",akce_get('kod'),
          " je obsažen ve složce <b>",folder.get,"</b> na serveru"));
        drop.init(folder.get,'H:','.*');
      | eq(cloud.get,'G:');
        popis.set(conc("Archiv pro akce roku ",akce_get('rok')," s kódem ",akce_get('kod'),
          " je obsažen na <a target='GD' href='",gd.get,folder.get,"'>Google Disku</a>"));
        drop.init(folder.get,'G:');
      | warning("archiv '",_folder,"' nelze zobrazit");
      };
      drop.lsdir;
      form.display(1,'doc_show'); form.display(0,'doc_crea');
    | drop.init('?');
      form.display(0,'doc_show'); form.display(1,'doc_crea');
    }
    proc onclick() { var path: text, _folder:text
      [ koren.key | alert("Před vytvořením je nutné zvolit kořen složky"); return ];
      path.set(conc(koren.get,'/',path_rok.get,'/',path_kod.get));
      confirm("Mám vytvořit složku pro dokumenty akce<br><b>",
        akce_get('nazev'),'</b> ve složce<br><b>',path,'</b> ?');
      // vytvoření složky na serveru
      cloud.set(archiv_slozka.get('hodnota'));
      drop.init(archiv_slozka.get('popis'),cloud.get);
      _folder.set(drop.mkdir(path_rok.get)); // přepne na vytvořenou složku
      _folder.set(drop.mkdir(path_kod.get)); // takže tato je v ní vnořená
      akce_set_archiv(cloud.get,_folder); ukaz(conc(cloud.get,_folder))
    }
  }
  select koren [90,45,150,] { tag:'doc_crea', title:'^kořenná složka', type:'map', format:'nt',
    options:akce_slozka.zkratka
    proc onchange() {
      archiv_slozka.set(ask('select_object','hodnota,ikona,barva,popis','_cis',
        conc("druh='akce_slozka' AND data=",this.key)));
      path_rok.set(replace(archiv_slozka.get('ikona'),'{rok}',akce_get('rok')));
      path_kod.set(replace(archiv_slozka.get('barva'),
        '{kod}',akce_get('kod'),'{nazev}',akce_get('nazev')));
    }
  }
  field path_rok [250,45,95,] { tag:'doc_crea', title:'^složka pro rok', format:'dn' }
  field path_kod [355,45,95,] { tag:'doc_crea', title:'^podsložka akcí', format:'dn' }
  // zobrazení archivu
  label popis [15,45,440,] { tag:'doc_show', format:'n' }
  label [15,60,455,] { tag:'doc_show', format:'n',
    title:'archivované soubory svázané s akcí - lze k nim přidat další přetáhnutím myší, zrušit a stáhnout je lze kontextovým menu' }
  label drop [15,95,435,310] { tag:'doc_show', type:'drop', format:'n'
    proc ondrop(f) {
      gt(f.size,5242880); alert('odmítnuto ',f.name,' má více jak 5MB');
      return(0)
    | return(1); // return(conc(prefix.get,f.name)) - případné přejmenování
    }
    proc onload(f) { warning("přeneseno:",f.name) } // po dokončení přenosu
    proc onerror(e) { warning(e.msg); return(1) } // vrácení 0 způsobí standardní hlášení chyby
    proc onmenu(op,title,ref) { // op=remove|remove-all
      eq(cloud.get,'G:');
      [ eq(op,'viewer'); [ function('ref',"window.open(ref,'viewer')",ref) ]
      | eq(op,'remove'); [ function('ref',"var request=gapi.client.drive.files.trash({'fileId':ref});request.execute(function(resp){});",ref) ]
      | warning('zatím neimplementovaná operace') ]
    | eq(cloud.get,'H:');
      [ eq(op,'viewer'); [ function('ref',"window.open(ref,'viewer')",ref) ]
      | eq(op,'remove'); [ confirm("Opravdu smazat ",title," ?");
                           warning(ask('drop_unlink',folder.get,ref,'H:')) ] // odebrání souboru
      | warning('zatím neimplementovaná operace') ];
      drop.lsdir;
    }
  }
  # --------------------------- pozadí stránky nakonec
  label frame [2,16,443,385] { title:'', css:'ae_work page' }
}
# ==> VÝBĚR AKCE
form _info [,,*,50] {
  # -----------------------------------------------------------------------------==> . seznam GOOGLE
  # kopie číselníku z intranetu -  pro ys
  view ga: table g_akce
  view ja: table join_akce {join_type:'LEFT',join:'USING(g_rok,g_kod)'}
  view da: table akce      {join_type:'LEFT',join:'ON ja.id_akce=da.id_duakce'}
  view pa: table pobyt     {join_type:'LEFT',join:'ON pa.id_akce=da.id_duakce'}
  browse google [0,36,200,500] { tag:'g', rows:27, qry_rows:1, buf_rows:120, group_by:"id_gakce"
    show id_gakce {data:ga.id_gakce}
    show id_akce [,,30,] {title:'akce', data:ja.id_akce, format:'rsqu', css_cell:'id_akce,0:yellow'}
    show org {data:da.access}
    show naz_akce {data:da.nazev}
    show ma_cenik {data:da.ma_cenik}
    show ma_cenu {data:da.ma_cenu}
    show cena {data:da.cena}
    show      {data:pa.id_pobyt}
    show rok  {data:ga.g_rok}
    show archiv {data:da.archiv}
    show curr {expr:"IF(da.id_duakce=@curr,1,0)" }
    show kod   [,,36,] {title:'kód účetní',    data:ga.g_kod, format:'rs+q*'
      css_cell:'org,0:,1:ezer_ys,2:ezer_fa,3:ezer_db' }
    show nazev [,,205,] {title:'název', data:ga.g_nazev, format:'tsq*'
      css_cell:"curr,1:curr_akce"}
    show ucast [,,30,] {title:'úč.', format:'rtsq=', help:'počet skupin účastníků',
      expr:"SUM((SELECT IF(COUNT(*)>0,1,0) FROM spolu WHERE id_pobyt=pa.id_pobyt AND funkce!=99))"}
    show pecou [,,30,] {title:'pč.', format:'rtsq=', help:'počet pečounů'
      expr:"SUM((SELECT COUNT(*) FROM spolu WHERE id_pobyt=pa.id_pobyt AND funkce=99))"}
    show od    [,,65,] {title:'od',     data:ga.g_od, format:'rsq*'}
    show do    [,,65,] {title:'do',     data:ga.g_do, format:'rsq*'}
    proc onsubmit() { pracovni }
    proc onrowclick() { page.show_akce(id_akce.get); }
    menu { type:'context'
      item { title:'Vybrat akci jako pracovní', proc onclick() { pracovni }}
    }
    // označení akce jako pracovní
    proc pracovni() { #==> .. pracovní YS
      var org_ys:number
      org_ys.set(1);
      not(accessed(org_ys)); // jen YS používá intranet
      alert("nemáte oprávnění označit jako pracovní akci jiné organizace");
    | id_akce.get; volba_akce(id_akce.get)
    | not(eq(fdate('Y',od.get),rok.get));
      alert('Akce má na intranetu uvedeno chybné datum zahájení (např. chybějící nebo z jiného roku)');
    | confirm('akce ',nazev.get,' zatím není v databázi, mám ji založit?');
      the_duakce.set(akce.insert_record(
        object('access',org_ys,'nazev',nazev.get,'datum_od',date2sql(od.get),'datum_do',date2sql(do.get))));
      join_akce.insert_record(object('id_akce',the_duakce.get,'g_rok',rok.get,'g_kod',kod.get));
      google.browse_row; volba_akce(id_akce.get) //,nazev.get,ma_cenik.get,ma_cenu.get,cena.get)
    }
  }
  # --------------------------------------------------------------------------------==> . seznam SEY
  # přechod na účastníky je omezen datovými právy
  view da2: table akce
  view ja2: table join_akce {join_type:'LEFT',join:'ON ja2.id_akce=da2.id_duakce'}
  view ga2: table g_akce    {join_type:'LEFT',join:'USING(g_rok,g_kod)'}
  view sa2: table akce      {join_type:'LEFT',join:'ON sa2.id_hlavni=da2.id_duakce'}
  view pa2: table pobyt     {join_type:'LEFT',join:'ON pa2.id_akce=da2.id_duakce'}
  browse sey [0,36,200,500] { tag:'s', rows:27, qry_rows:1, buf_rows:120, group_by:"da2.id_duakce"
      css_rows:"barva,1:cerveny,2:zluty" // 1=hlavní akce, 2=souběžná akce
    show id_akce  {data:da2.id_duakce}
    show          {data:ja2.id_akce}
    show          {data:pa2.id_pobyt}
    show ma_cenik {data:da2.ma_cenik}
    show ma_cenu {data:da2.ma_cenu}
    show cena {data:da2.cena}
    show id_gakce {data:ga2.id_gakce}
    show id_hlavni {data:da2.id_hlavni}
    show id_soubeh {data:sa2.id_duakce}
    show archiv {data:da2.archiv}
    show rok {expr:"YEAR(da2.datum_od)"}
    show curr { expr:"IF(da2.id_duakce=@curr,1,0)" }
    show org { data:da2.access }
    show SF [,,20,] {title:'SF', expr:"IF(da2.access=1,'S',IF(da2.access=2,'F','?'))"
      help:"pořádá S=YMCA Setkání, F=YMCA Familia"
      css_cell:'org,0:,1:ezer_ys,2:ezer_fa,3:ezer_db', format:'rsq*' }
    show barva { expr:"CASE WHEN da2.id_hlavni THEN 2 WHEN sa2.id_duakce THEN 1 ELSE 0 END" }
    show kod   [,,36,] {title:'kód účetní', expr:"IF(ga2.g_kod,ga2.g_kod,da2.ciselnik_akce)"
      css_cell:'org,0:,1:ezer_ys,2:ezer_fa,3:ezer_db', format:'rsq*' }
    show druh  [,,60,] {title:'typ',    data:da2.druh, map_pipe:ms_akce_typ.zkratka, format:'sq#'}
    show nazev [,,104,] {title:'název', expr:"IF(da2.nazev='',ga2.g_nazev,da2.nazev)", format:'tsq*',
      css_cell:"curr,1:curr_akce"}
    show ucast [,,30,] {title:'úč.', format:'rtsq=', help:'počet skupin účastníků',
      expr:"SUM((SELECT IF(COUNT(*)>0,1,0) FROM spolu WHERE id_pobyt=pa2.id_pobyt AND funkce!=99))"}
    show pecou [,,30,] {title:'pč.', format:'rtsq=', help:'počet pečounů'
      expr:"SUM((SELECT COUNT(*) FROM spolu WHERE id_pobyt=pa2.id_pobyt AND funkce=99))"}
    show misto [,,50,] {title:'místo',  data:da2.misto, format:'tsq*'}
    show od    [,,65,] {title:'od',     data:da2.datum_od, format:'rs+q*'}
    show do    [,,65,] {title:'do',     data:da2.datum_do, format:'rsq*'}
    proc onsubmit() { pracovni }
    proc onrowclick() { page.show_akce(id_akce.get); }
    menu { type:'context'
      item { title:'Vybrat akci jako pracovní', proc onclick() { pracovni }}
    }
    // označení akce jako pracovní
    proc pracovni() { #==> .. pracovní
      not(accessed(org.get));
      alert("nemáte oprávnění označit jako pracovní akci jiné organizace");
    | pracovni_curr
    }
    proc pracovni_curr() {
      volba_akce(id_akce.get);
      curr.set_attrib('expr',conc("IF(da2.id_duakce=",id_akce.get,",1,0)"));
      this.browse_seek;
    }
    proc onstart() {
      gt(sys('options','watch_access'),3);
      SF.width(0); kod.width(0); nazev.width(140); misto.width(70);
    }
  }
  # --------------------------------------------------------------------------------------==> . akce
  select rok [4,5,52,] { tag:'r', format:'t', proc onchange() { akce_roku(this.get) }}
  # je voláno s kontextem správného the_source
  proc nazev_akce(id_akce) {
    var nazev: text
    { and(id_akce,not(eq(id_akce,'0')));
      { eq(the_source.get,'google');
        nazev.set(conc(' Akce ',google.kod.get,'/',google.rok.get,' - ',google.nazev.get));
      | eq(the_source.get,'sey');
        nazev.set(conc(' ',conc(sey.nazev.get,', ',fdate('Y',sey.od.get))));
      | alert('V levém menu je třeba vybrat typ akcí (na které máte oprávnění)')
      };
    };
    return (nazev);
  }
  proc volba_akce(id_akce) { #==> . volba_akce
    akce_choose(id_akce);
    lst.info.fill(conc(the_title.get,' '));
    [ ucast.call('clear_ucast2') ];
    [ has_skill('yam;fam;cam'); maia.call('init_ucast') ];
    [ has_skill('crz') | this.call('pece.init_pece') ]; // CRŽ
    // ošetření souběžných akcí - 0=normální akce, 1=hlavní akce, 2=souběžná akce
    [ has_skill('f'); page.soubezna.set(eq(the_soubeh.get,2)) ];
    // nastavení cookies podle organizace
    id_akce;
    { eq(the_org.get,1);
      the_akce_ys.set(the_duakce.get);
      set_cookie(conc(sys('root'),'_akce_ys'),the_akce_ys.get)
    | eq(the_org.get,2);
      the_akce_fa.set(the_duakce.get);
      set_cookie(conc(sys('root'),'_akce_fa'),the_akce_fa.get)
    | // mimo YS+FA
      set_cookie(conc(sys('root'),'_akce'),the_duakce.get)
    };
  }
  label head [0,0,*,30] { title:'', style:'z-index:0' }
  # volba pracovní akce
  button [376,3,,] { title:'Pracovní', proc onclick() {
    eq(the_source.get,'sey'); info.sey.pracovni | info.google.pracovni
  }}
  # ==> .. načtení akcí z intranetu
  button [-160,3,,] { title:'Obnovit rok z intranetu', skill:'y|yaa+'
    help:"obnoví akce roku z http://intranet.setkani.org"
    # získání seznamu akcí z docs.google.com pomocí API spredsheet
    proc onclick() {
      lt(rok.get,2010); alert("Z intranetu lze obnovovat jen akce roku 2010 a starší");
    | confirm("Mám pro rok ",rok.get," obnovit seznam akcí (musí mít značku 'a')?");
      alert('bylo obnoveno ',ask('akce2_roku_update',rok.get),' záznamů o akcích');
      google.browse_seek;
    }
  }
  # export vlastností akcí nastaveného roku
  button [-88,3,,] { title:'Export', proc onclick() {
    sey.browse_count; sey.browse_export(°{file:'akce',type:'xls'});
    alert("vygenerovaný sešit lze stáhnout <a href='docs/akce.xls' target='xls'>zde</a>");
  }}
  # ==> .. zobrazení mapy s pozicí míst účastníků dané akce
  button [-30,3,,] { title:'Mapa',                                                      skill:'#crz'
    help:"mapa s bydlišti účastníků akcí"
    proc onclick() {
      [ eq(the_source.get,'google'); mapa.id_akce.set(google.id_akce.get)
      | eq(the_source.get,'sey'); mapa.id_akce.set(sey.id_akce.get) ];
      mapa.id_akce.get;
      mapa.metoda.set('akce');
      mapa.modal(20,100,nazev_akce(mapa.id_akce.get))
    }
  }
  proc fill(x) {
    [ x; head.set(conc("<div class='karta' style='padding-left:60px'>",x,"</div>")) ];
  }
}

map akce_cenik:  table akce {where:"ma_cenik=1", order:'datum_od', key_id:'id_duakce'}
map akce_slozka: table _cis {where:"druh='akce_slozka'", order:'poradi', key_id:'data'}
