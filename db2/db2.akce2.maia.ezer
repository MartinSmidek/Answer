#pragma test
# ------------------------------------------------------------------------------------------------ #
# Karta pro rozesílání mailů účastníků akce                                                        #
#                                                                                                  #
# Ans(w)er/Ezer                                     (c) 2008-2015 Martin Šmídek <martin@smidek.eu> #
# ------------------------------------------------------------------------------------------------ #

# ----------------------------------------------------------------------------------==> inicializace
proc onfirstfocus() {
#   spinner(1);
  [ dirty.get | akce_onstart ];
  m.g.i.click;
#   spinner(0);
}
proc init_ucast() { echo('mai:init_ucast ',the_title.get);
  vy.fill(the_title.get);
}

# ------------------------------------------------------------------------------------------==> stav
var the_duakce_local: number    // zobrazená akce - pokud se liší od the_duakce, je třeba překreslit
var E_info: text
var dopis_id: text
var dopis_var: text             // typ rozesílání: U/U2/U3/U4 - účastníci akce
var dopis_akce: number          // id_akce|0
var karta: text                 // et|es
var menu_item=0

# ----------------------------------------------------------------------------==> globální procedury
proc reaccess() { #==> reaccess - změna
  menu_item.get;
  m.onclick(menu_item.get);
}
# -------------------------------------------------------------------------------------------==> use
use ii: form right [12,4,,]
use vy: form _vyber [10,30,,]   // výběr skupiny | zobrazené akce
use et: form _etext [10,40,,]   // editace mailu
use es: form _esady [10,74,,]   // rozesílání

# ------------------------------------------------------------------------------------------==> MENU
menu m {type:'left',
  menu g {title:'Vytvoření mailu k akci', type:'group'
    item i {title:'všem účastníkům této akce', _sys:'akc'
      par:°{k:'et',uq:'U',on:'akce',off:'gen|skup'} }
    item {title:'... VPS,SVPS,hospodářům akce'
      par:°{k:'et',uq:'U2',on:'akce',off:'gen|skup'} }
    item {title:'... dlužníkům akce'
      par:°{k:'et',uq:'U3',on:'akce',off:'gen|skup'} }
    item {title:'... s divnou/žádnou občankou'
      par:°{k:'et',uq:'U4',on:'akce',off:'gen|skup'} }
  }
  menu {title:'Rozesílání mailů ', type:'group', _sys:'snd'
    item {title:'všem účastníkům této akce',
      par:°{k:'es',uq:'U',on:'akce',off:'gen|skup'} }
    item {title:'... VPS,SVPS,hospodářům akce'
      par:°{k:'es',uq:'U2',on:'akce',off:'gen|skup'} }
    item {title:'... dlužníkům akce'
      par:°{k:'es',uq:'U3',on:'akce',off:'gen|skup'} }
    item {title:'... staré nebo žádné OP'
      par:°{k:'es',uq:'U4',on:'akce',off:'gen|skup'} }
  }
  proc onclick (i) {
    menu_item.set(i);
    ii.fill(conc(i.owner.title,' - ',i.title),' ');
    vy.get.display(1,i.par.on); vy.get.display(0,i.par.off);
    dopis_var.set(i.par.uq); karta.set(i.par.k);
    dopis_id.set(0); dopis_akce.set(the_duakce.get);
    et.form_init;
    switch(i.par.k,
    'et',{et.get.display(1); es.get.display(0); vy.get.display(0,'es'); et.reload},
    'es',{et.get.display(0); es.get.display(1); vy.get.display(1,'es'); es.reload},
         {et.get.display(0); es.get.display(0)});
    vy.fill(the_title.get);
  }
}
# ------------------------------------------------------------------------------------------- _vyber
form _vyber {
  # --------------------------- jméno akce
  label txt [0,10,380,28] { tag:'akce' }
  proc fill(x) { txt.set(conc("<div class='karta'>",x,"</div>")) }
  # --------------------------- rozesílání
  label popis [391,19,240,19] { tag:'es', format:'n', title:"
    <span class='zeleny'> posláno </span>&nbsp;&nbsp;
    <span class='cerveny'> chyba resp. neposílat </span>&nbsp;&nbsp;
    <span class='zluty'> poslat znovu </span>
   "}
  # vynechání adresátů daného dopisu
  button [644,0,,] { tag:'es', title:'neposlat',
    help:'vynechání adresátů z mailu s daným id_dopis'
    proc onclick () {
      es.msg.set('čekej ...');
      es.msg.set(ask('mail2_mai_omitt',dopis_id.get,vynech_id.get));
    }
  }
  field vynech_id [711,3,60,] { tag:'es', format:'t' }
  # vynechání adresátů daných adres
  button [644,20,,] { tag:'es', title:'neposlat',
    help:'vynechání adresátů daných adres'
    proc onclick () {
      es.msg.set('čekej ...');
      es.msg.set(ask('mail2_mai_omitt2',dopis_id.get,vynech_emaily.get));
    }
  }
  field vynech_emaily [711,23,60,] { tag:'es', format:'t' }
}
# ----------------------------------------------------------------------------------------==> _etext
# správa mailů
form _etext  {
  var adopis: number
  # obslužné procedury stavu tlačítek
  proc form_init() {
    texty.browse_init; form.init; drop.set(''); form_state('n','u|z|s') }
  proc form_state(on,off) {
    form.enable(1,on); form.enable(0,off); }
  proc onchanged () {
    or(uloz.enable,oprav.enable); form_state('u|z','n|s')
  }
  # --------------------------- seznam mailů
  view etd: table dopis
  view etm: table mail {join_type:'LEFT' join:'USING(id_dopis)'}
  view eta: table akce {join_type:'LEFT' join:'USING(id_duakce)'}
  view etc: table _cis {join_type:'LEFT' join:"ON etd.cis_skupina=etc.data AND etc.druh='db_maily_sql'"}
  browse texty [0,34,150,200] { rows:10, qry_rows:1, group_by:'id_dopis', css_rows:'stav1,3:zluty,4:zeleny,5:cerveny'
    show id_dopis { data:etd.id_dopis }
    show x { data:etm.id_davka },
    show datum [,,60,] { title:'datum', data:etd.datum, format:'rs-' }
    show komu_U [,,100,] { title:'účastníkům', data:eta.nazev, format:'s' },
    show predmet [,,160,] { title:'předmět', data:etd.nazev, format:'sq', css_cell:'stav2,3:zluty,4:zeleny,5:cerveny' },
    show stav1 { expr:'min(etm.stav)', format:'rsq' }
    show stav2 { expr:'max(etm.stav)', format:'rsq' }
    show pocet [,,30,] { title:'#', data:etd.pocet, format:'rs' }
    proc onrowclick () {
      dopis_id.set(id_dopis.get);
      form.load(dopis_id.get);
      drop_init;
      obsah.set(ask('mail2_mai_text',dopis_id.get));
      adopis.set(id_dopis.get);
      form_state('n|s','u|z');
      generovat.set(conc(cconc(pocet.get,"Přegenerovat","Vygenerovat")," rozesílací seznam"))
    }
  }
  # nezobrazené údaje mailu
  field druh    { data:dopis.druh }
  field id_duakce { data:dopis.id_duakce }
  field id_mailist { data:dopis.id_mailist }
  field cis_skupina { data:dopis.cis_skupina }
  field komu  { data:dopis.komu }       // 0:všem, 1:VPS..., 2:dlužníci, 3:OP
  # společné údaje mailu
  const L=390
  field id_davka { data:dopis.id_davka }
  label [L+0,20,365,150] { css:'ae_work' }
  label [L+10,23,100,17] { title:'datum odeslání' }
  field datum [L+10,38,80,17] { data:dopis.datum, help:'datum|datum emailu', skill:'yam|yams;fam|fams;cam|cams' }
  label [L+307,23,45,17] { title:'ID dopisu' }
  field id_dopis [L+307,38,43,17] { data:dopis.id_dopis, format:'rd' }
  label [L+10,62,100,17] { title:'předmět mailu' }
  field predmet [L+10,77,340,17] { data:dopis.nazev, help:'předmět|předmět emailu', skill:'yam|yams;fam|fams;cam|cams' }
  // ==> připojení příloh
  field attach {data:dopis.prilohy}                     // datové pole se seznamem příloh
  label [L+10,100,,] {title:'sem lze přetáhnout přílohy, zrušit a stáhnout pak kontextovým menu'}
  label drop [L+10,115,340,45] { type:'drop'
    proc onload(f) {                                    // po dokončení přenosu
      ask('mail2_mai_attach',form.key,f);                 // přidání souboru k dávce
    }
    proc onmenu(op,name,ref) {  // op=remove|remove-all
      { eq(op,'remove');     ask('mail2_mai_detach',form.key,name) // odebrání přílohy
      | eq(op,'remove-all'); ask('mail2_mai_detach_all',form.key)  // odebrání všech příloh
      | warning('zatím neimplementovaná operace')
      };
      attach.set(ask('select','prilohy','dopis',conc("id_dopis=",form.key)));
      drop_init
    }
  }
  proc drop_init() {                                    // inicializace - je třeba na začátku
    drop.init('','S:');                                 // naplnit pole drag&drop a definovat dir
    drop.set(attach.get);
  }
  # příkazy 1
  label  [L+0,180,200,78] { css:'ae_parm' }
  label [L+8,232,184,] { title:"přegenerováním jsou také přepočteny všechny {proměnné}" }
  button uloz [L+8,186,,] { tag:'u', type:'submit', title:'Uložit',
    help:'uložit opravené hodnoty', skill:'yam|yams;fam|fams;cam|cams'
    proc onclick () {
      form.same
    | // oprava
      form.key; form.save; form.load; texty.browse_seek;
      form_state('n|s','u|z'); texty.browse_focus;
    | // přidání
      form.insert; form.load;
      texty.raise('onrowclick',texty.browse_seek(conc(form.id_key,'=',form.key)));
      form_state('n|s','u|z');
    }
  }
  button [L+64,186,,] { tag:'z', title:'Zpět', help:'neukládat hodnoty', skill:'yam|yams;fam|fams;cam|cams'
    proc onclick () {
      adopis.get; form.load(adopis.get); form_state('n|s','u|z'); texty.browse_focus
    | form.init; drop.set(''); form_state('n','u|z|s')
    }
  }
  button oprav [L+113,186,,] { tag:'s', title:'Opravit text', help:'opravit text mailu',
    skill:'yam|yams;fam|fams;cam|cams'
    proc onclick () {
      gt(texty.stav2.get,2); not(confirm("Tento dopis již byl rozesílán, opravdu chceš opravit jeho text?"))
    | E_dopis.modal(10,10); texty.raise('onrowclick',dopis_id.get)
    }
  }
  # příkazy 2
  label  [L+246,178,120,32] { css:'ae_parm' }
  button novy [L+254,184,,] { tag:'n', title:'Nový', help:'vytvořit nový e-mail',
    skill:'yam|yams;fam|fams;cam|cams'
    proc onclick () {
      form.init; drop.set(''); form_state('u|z','n|s'); drop_init;
      texty.blur(1); adopis.set(0);
      datum.set(now); datum.change; id_davka.set(1); id_davka.change; druh.set('@'); druh.change;
      cis_skupina.init; id_mailist.init; id_duakce.init; komu.init;
      switch(dopis_var.get,
      'U', { id_duakce.set(dopis_akce.get); komu.set(0); },
      'U2',{ id_duakce.set(dopis_akce.get); komu.set(1); },
      'U3',{ id_duakce.set(dopis_akce.get); komu.set(2); },
      'U4',{ id_duakce.set(dopis_akce.get); komu.set(3); }
      );
      id_duakce.change; komu.change; cis_skupina.change; id_mailist.change;
    }
  }
  button smaz [L+303,184,,] { tag:'s', title:'Smazat', help:'smazat mail a jeho rozesílání',
    skill:'yam|yams;fam|fams;cam|cams'
    proc onclick () {
      confirm(conc("Opravdu smazat mail '",texty.predmet.get,"' a jeho rozesílací seznam?"));
      ask('mail2_mai_smaz',texty.id_dopis.get);
      reload
    }
  }
  var et_prazdy: number
  button generovat [L+8,211,,] { tag:'s', title:'Vygenerovat rozesílací seznam',
    help:'připravit rozesílání', skill:'yam|yams;fam|fams;cam|cams'
    proc onclick () {
      var conf: object
      var blokovano: number
      var pocet: number
      var err: text
      eq(dopis_var.get,'U','U2','U3','U4');
      # rozesílání podle nastavené akce
        prazdny; et_prazdy.get;
        E_info.set(ask('mail2_mai_pocet',dopis_id.get,dopis_var.get));
        confirm(E_info.get('_html'));
        E_info.get('_count');
        err.set(ask('mail2_mai_posli',dopis_id.get,E_info.get));
        [ err; alert(err) ];
        texty.browse_seek; texty.browse_focus;
    }
  }
  proc prazdny() {
    E_info.set(ask('mail2_mai_prazdny',texty.id_dopis.get));
    et_prazdy.set(E_info.get('_prazdny')); et_prazdy.get
  | et_prazdy.set(confirm(E_info.get('_html')))
  }
  # -------------------------- načtení celé karty přípravy mailů
  proc reload() {
    form.init;
    obsah.set(''); texty.browse_init;
    switch(dopis_var.get,
    // účastníci akce  (dopis.komu=0)
    'U',{ texty.browse_load(conc("etd.id_duakce='",dopis_akce.get,"'"," AND etd.komu=0")); },
    // organizátoři akce (dopis.komu=1)
    'U2',{ texty.browse_load(conc("etd.id_duakce='",dopis_akce.get,"'"," AND etd.komu=1")); },
    // dlužníci akce (dopis.komu=2)
    'U3',{ texty.browse_load(conc("etd.id_duakce='",dopis_akce.get,"'"," AND etd.komu=2")); },
    // špatné OP (dopis.komu=3)
    'U4',{ texty.browse_load(conc("etd.id_duakce='",dopis_akce.get,"'"," AND etd.komu=3")); }
    );
    reload2
  }
  proc reload2() {
    texty.browse_count; drop.set(''); drop_init;
    { dopis_id.get; texty.raise('onrowclick',dopis_id.get) | texty.raise('onrowclick') }
  }
  # text mailu
  label obsah [0,263,645,200] { title:'zobrazení předmětu a textu mailu' }
}
# ========================================================================================== E_dopis
panel E_dopis [0,0,645,520] { title:' Úprava textu mailu', type:'popup', css:'dialog', skill:'yams;fams;cams'
  use mail: form _edopis [0,0,,],
  proc onfocus () {
    dopis_id.get; mail.load(dopis_id.get);
  | error('nedefinovaný dopis')
  }
  # ---------------------------------------------------------------------------- _edopis
  # oprava textu, data, předmětu a adresátů mailu v popup menu
  form _edopis {
    label [0,12,60,20] { title:'Předmět:' }
    field nazev [50,10,480,20] { data:dopis.nazev },
    button  [540,9,,] { title:'Uložit', help:'ukončit editor a uložit změny'
      proc onclick() { form.save; panel.hide(1); }
    }
    button  [600,9,,] { title:'Zpět', help:'ukončit editor bez uložení změn'
      proc onclick() { panel.hide(0); }
    }
    edit [0,40,645,480] {type:'html', data:dopis.obsah, par:°{toolbar:'EzerMail'} },
  }
}
# ------------------------------------------------------------------------------------------- _esady
# rozesílání připraveného mailu - zakládání, aktualizace a rušení - obsluha front s maily
form _esady  {
  # ------------------------ připravené dopisy
  view etd: table dopis
  view etm: table mail  {join_type:'LEFT' join:'USING(id_dopis)'}
  view eta: table akce  {join_type:'LEFT' join:'USING(id_duakce)'}
  view etc: table _cis  {join_type:'LEFT' join:"ON etd.cis_skupina=etc.data AND etc.druh='db_maily_sql'"}
#   view etl: table mailist {join_type:'LEFT' join:"ON etl.id_mailist=etd.id_mailist"}
  view eto: table osoba {join_type:'LEFT' join:'ON id_clen=eto.id_osoba'}
  view etr: table rodina {join_type:'LEFT' join:'ON id_clen=etr.id_rodina'}
  browse texty [0,0,150,200] { rows:9, qry_rows:1, group_by:'id_dopis', css_rows:'stav1,3:zluty,4:zeleny,5:cerveny'
    show id_dopis  { data:etd.id_dopis },
    show id_duakce { data:etd.id_duakce }
    show cis_skupina { data:etd.cis_skupina }
    show datum [,,60,] { title:'datum', data:etd.datum, format:'s-' },
    show komu_U [,,100,] { title:'účastníkům' , data:eta.nazev, format:'s' },
    show predmet [,,160,] { title:'předmět', data:etd.nazev, format:'sq', css_cell:'stav2,3:zluty,4:zeleny,5:cerveny' },
    show stav1 [,,0,] { expr:'min(etm.stav)', format:'rsq' },
    show stav2 [,,0,] { expr:'max(etm.stav)', format:'rsq' },
    show pocet [,,30,] { title:'#', data:etd.pocet, format:'rs' },
    proc onrowclick () {
      eq(this.browse_count,0);
      dopis_id.set(0);
    | dopis_id.set(id_dopis.get);
      maily.browse_load(conc("etmm.id_dopis=",dopis_id.get));
      zkus.set(sys('user','options','email'));
      from.set(sys('user','options','email'));
      name.set(sys('user','options','vyrizuje'));
      davka.set(20);
    }
  }
  label [12,214,92,17] { title:'Testovací adresa:' }
  label [35,247,66,17] { title:'Odeslat jako:' }
  field zkus [105,210,200,17] { skill:'yam|yams;fam|fams;cam|cams', help:'email z osobního nastavení', format:'t' }
  field from [105,236,200,17] { skill:'yam|yams;fam|fams;cam|cams', help:'email z osobního nastavení', format:'t'  }
  field name [105,255,200,17] { skill:'yam|yams;fam|fams;cam|cams', help:"'vyřizuje' z osobního nastavení", format:'t'  }
  button test [0,280,,] { title:'1 mail testovací', skill:'yam|yams;fam|fams;cam|cams',
    help:'pro kontrolu - posílá se na testovací adresu'
    proc onclick () {
      msg.set('');
      confirm('Poslat zkušební mail na ',zkus.get,' ?');
      E_info.set(ask('mail2_mai_send',dopis_id.get,0,from.get,name.get,zkus.get,maily.id_mail.get));
      msg.set(E_info.get('_html'));
    }
  }
  field davka [105,281,30,17] { format:'rt', value:'10', skill:'yam|yams;fam|fams;cam|cams',
    proc onchange () {
      send.set(conc('až ',davka.get,'  ještě neposlaných'));
    }
  }
  button send [150,280,,] { title:'xx ještě neposlaných', skill:'yam|yams;fam|fams;cam|cams',
    help:'pošle další dávku - počet lze měnit políčkem vlevo'
    proc onclick () {
      msg.set('');
      confirm(conc('Opravdu poslat dalších ',davka.get,' mailů "z adresy" ',from.get,'?'));
      E_info.set(ask('mail2_mai_send',dopis_id.get,davka.get,from.get,name.get));
      msg.set(E_info.get('_html'));
      maily.browse_seek; texty.browse_seek;
    }
  }
  # ------------------- informace
  label msg [0,305,377,67]
  label info [0,373,367,90]   { style:"overflow:auto;border:1px solid grey;padding:5px" }
  label obsah [0,474,716,246] { style:"overflow:auto;border:1px solid grey;padding:5px" }
  # ------------------- seznam adres
  view etmm: table mail
  view etmo: table osoba  {join_type:'LEFT' join:'ON id_clen=etmo.id_osoba'}
  view etmr: table rodina {join_type:'LEFT' join:'ON id_clen=etmr.id_rodina'}
  view etmd: table dopis  {join_type:'LEFT' join:'USING(id_dopis)'}
  view etmc: table _cis   {join_type:'LEFT' join:"ON etmd.cis_skupina=etmc.data AND etmc.druh='db_maily_sql'"}
  browse maily [390,0,120,200] { rows:24, qry_rows:1, css_rows:'stav,3:zluty,4:zeleny,5:cerveny'
    show id_mail          { data:etmm.id_mail }
    show                  { data:etmo.id_osoba }
    show                  { data:etmr.id_rodina }
    show id_dopis         { data:etmd.id_dopis }
    show                  { data:etmc.id_cis }
    show stav    [,,20,]  { title:'s.', data:etmm.stav, format:'rsq' },
    show id_clen [,,50,]  { title:'člen', data:etmm.id_clen, format:'rsq' },
    show prijmeni [,,50,] { title:'příjmení', expr:"'-'", format:'s+q' },  // nastavuje se dynamicky v reload
    show email   [,,180,] { title:'email', data:etmm.email, format:'tsq' },
    show msg     [,,50,]  { title:'chyba', data:etmm.msg, format:'tsq' }
    show body             { data:etmm.body }
    proc onsubmit() {
      ask('mail2_mai_stav',id_mail.get,cconc(eq(stav.get,5),3,5));
      this.browse_row;
    }
    proc onrowclick () {
      info.set(ask('mail2_mai_info',id_clen.get,email.get,dopis_id.get,dopis_var.get,id_mail.get));
      [ body.get; obsah.set(body.get)
      | obsah.set(ask('select','obsah','dopis',conc('id_dopis=',id_dopis.get))) ]
    }
  }
  # --------------------- načtení celé karty
  proc reload() {
    form.init; davka.set(20); davka.change; msg.set(''); info.set('');
    maily.prijmeni.set_attrib('expr',"etmo.prijmeni");
    maily.browse_init;
    texty.browse_init;
    switch(dopis_var.get,
    // účastnící akce  (dopis.komu=0)
    'U',{ texty.browse_load(conc("etd.id_duakce='",dopis_akce.get,"'"," AND etd.komu=0")); },
    // organizátoři akce (dopis.komu=1)
    'U2',{ texty.browse_load(conc("etd.id_duakce='",dopis_akce.get,"'"," AND etd.komu=1")); },
    // dlužníci akce (dopis.komu=2)
    'U3',{ texty.browse_load(conc("etd.id_duakce='",dopis_akce.get,"'"," AND etd.komu=2")); },
    // špatné OP (dopis.komu=3)
    'U4',{ texty.browse_load(conc("etd.id_duakce='",dopis_akce.get,"'"," AND etd.komu=3")); }
    );
    reload2
  }
  proc reload2() {
    texty.browse_count;
    { dopis_id.get; texty.raise('onrowclick',dopis_id.get) | texty.raise('onrowclick') }
  }
}
