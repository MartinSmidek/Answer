#pragma test
# ------------------------------------------------------------------------------------------------ #
# Karta pro grafické zobrazení různých statistik                                                   #
#                                                                                                  #
# Ans(w)er/Ezer                                          (c) 2021 Martin Šmídek <martin@smidek.eu> #
# ------------------------------------------------------------------------------------------------ #
# ----------------------------------------------------------------------------------- globální jména
# ----------------------------------------------------------------------------------==> inicializace
use info: form _right
func onfirstfocus() { 
  info.Init();
  js.highcharts_load();
  // ys:1 fa:2 cr:4 ms:8
  switch (my_org) {
    case 1: case 2: info.org.selects("YMCA Setkání:1,YMCA Familia:2,Setkání + Familia:3"); break;
    case 4: info.org.selects("Centrum pro rodinu ...:4"); break;
    case 8: info.org.selects("Šance pro manželství:8"); break;
  }
  info.org.key(my_org);
  info.do= fdate('Y');
  info.od=  info.do - 5;
  m.s.prih.click(m.s.prih);
} 
# ------------------------------------------------------------------------------------------==> stav
# ----------------------------------------------------------------------------==> globální procedury
proc reaccess() { #==> reaccess - změna
}
# ------------------------------------------------------------------------------------------==> MENU
var last_par:object, last_menu:ezer
menu m {type:'left', format:'f+' 
  menu s {title:'Infografika LK MS',type:'group'
    item  {title:"[fa-bar-chart] Skladba účastníků kurzu", par:{type:'skladba',pro:'ms',
        chart:{chart:'column',plotOptions:{column:{stacking:'value'}}}}}
    item  {title:"[fa-bar-chart] Skladba účastníků kurzu v %", par:{type:'skladba',pro:'ms',
        chart:{chart:'column',plotOptions:{column:{stacking:'percent'}}}}}
    item  {title:"[fa-bar-chart] Délka manželství nováčků", par:{type:'novacci',pro:'ms',
        chart:{chart:'column',plotOptions:{column:{stacking:'value'}}}}}
    item skup {title:"[fa-line-chart] Velikost a počet skupinek", par:{type:'skupinky',pro:'ms'}}
    item prih {title:"[fa-line-chart] postupný součet přihlášek", par:{type:'prihlasky',ukaz:'celkem',pro:'ms'}}
    item  {title:"[fa-line-chart] ... přírůstky přihlášek",   par:{type:'prihlasky',ukaz:'prubeh',pro:'ms'}}
  }
  menu {title:'Infografika MROP',type:'group'                                            skill:'y;f'
    item  {title:"[fa-bar-chart] na našich akcích před iniciaci", par:{type:'pred_mrop',
        chart:{chart:'bar',plotOptions:{series:{stacking:'normal'}}}}}
    item  {title:"[fa-bar-chart] na našich akcích po iniciaci", par:{type:'po_mrop',
        chart:{chart:'bar',plotOptions:{series:{stacking:'normal'}}}}}
    item  {title:"[fa-bar-chart] aktivita YS/FA před iniciací", par:{type:'ys_fa',
        chart:{chart:'bar',plotOptions:{series:{stacking:'normal'}}}}}
    item  {title:"[fa-bar-chart] ... YMCA/CPR+ŠM před iniciací", par:{type:'y_x pred',
        chart:{chart:'bar',plotOptions:{series:{stacking:'normal'}}}}}
    item  {title:"[fa-bar-chart] ... YMCA/CPR+ŠM po iniciací", par:{type:'y_x po',
        chart:{chart:'bar',plotOptions:{series:{stacking:'normal'}}}}}
    item  {title:"[fa-line-chart] postupný součet přihlášek", par:{type:'prihlasky',ukaz:'celkem',pro:'mrop'}}
    item  {title:"[fa-line-chart] ... přírůstky přihlášek",   par:{type:'prihlasky',ukaz:'prubeh',pro:'mrop'}}
  }
  menu {title:'Infografika FIRMING',type:'group'                                            skill:'y;f'
    item  {title:"[fa-line-chart] postupný součet přihlášek", par:{type:'prihlasky',ukaz:'celkem',pro:'firm'}}
    item  {title:"[fa-line-chart] ... přírůstky přihlášek",   par:{type:'prihlasky',ukaz:'prubeh',pro:'firm'}}
  }
  func onclick(i:ezer) {  var y:object, par:object
    if (i) { last_par= i.par; last_menu= i; }
    info.header(last_menu);
    info.spinner(last_menu);
    last_par.od= info.od;
    last_par.do= info.do;
    last_par.prc= info.prc;
    if (last_par.pro!='ms') 
      info.org.key(3);
    last_par.org= info.org.key();
    y= php.chart_akce(last_par);
    if (y.err) 
      alert(y.err)
    else {
      js.highcharts_show(y.chart);
      info.fill('',y.note);
    }
    info.spinner(0);
  }
}
# ------------------------------------------------==> .. _right
# univerzální formulář pro levostranné menu
form _right [,,*,50] {
  label head  [10,0,*,50]  { title:'' }
  label chart [10,50,800,400] { title:'' }
  label note  [50,450,700,500] { title:'' }
  // parametry
  field od [-305,5,40,] { title:'graf od roku', format:'tr', css:'bila' }
  field do [-215,5,40,] { title:'do roku', format:'tr', css:'bila' }
  check prc [-178,4,30,] { title:'%', format:'t', css:'bila' }
  select org [-35,5,120,] { title:'pro', format:'t', css:'bila' }
  button [-7,5,20,] {title:'[fa-refresh]' 
    func onclick() { 
      last_menu.click(last_menu);
//    m.s.onclick(0)
  }}
  // funkce
  func Init() {
    chart= `<div id="container" style="width:800px; height:400px;"></div>`;
  }
  func header(i:ezer) { var i_owner:ezer
    clear(); i_owner= i.owner();
    head= replace_fa(`<div class='karta'>${i_owner.title} - ${i.title}</div>`);
  }
  func fill(x,y) {
    if (x) head= `<div class='karta'>${replace_fa(x)}</div>`;
    if (y) note= y
  }
  func append(x) { note= conc(note,'<br>',x) }
  func shift(x) { note= conc(x,'<br>',note) }  
  func spinner(on) { js.waiting(on); }
}
