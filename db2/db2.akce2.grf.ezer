#pragma test
# ------------------------------------------------------------------------------------------------ #
# Karta pro grafické zobrazení různých statistik                                                   #
#                                                                                                  #
# Ans(w)er/Ezer                                          (c) 2021 Martin Šmídek <martin@smidek.eu> #
# ------------------------------------------------------------------------------------------------ #
# ----------------------------------------------------------------------------------- globální jména
# ----------------------------------------------------------------------------------==> inicializace
use info: form _right
func onfirstfocus() { 
  info.Init();
  // ys:1 fa:2 cr:4 ms:8
  switch (my_org) {
    case 1: case 2: info.org.selects("YMCA Setkání:1,YMCA Familia:2,Setkání + Familia:3"); break;
    case 4: info.org.selects("Centrum pro rodinu ...:4"); break;
    case 8: info.org.selects("Šance pro manželství:8"); break;
  }
  info.org.key(my_org);
  info.do= fdate('Y');
  info.od=  info.do - 5;
  m.s.prih.click(m.s.prih);
} 
# ------------------------------------------------------------------------------------------==> stav
# ----------------------------------------------------------------------------==> globální procedury
proc reaccess() { #==> reaccess - změna
}
# ------------------------------------------------------------------------------------------==> MENU
var last_par:object, last_menu:ezer
menu m {type:'left', format:'f+' 
  menu s {title:'Infografika LK MS',type:'group'
    item  {title:"[fa-bar-chart] Skladba účastníků kurzu", par:{type:'skladba',pro:'ms', tag:'o|d',
        chart:{chart:'column',plotOptions:{column:{stacking:'value'}}}}}
    item  {title:"[fa-bar-chart] ... v %", par:{type:'skladba',pro:'ms', tag:'o|d',
        chart:{chart:'column',plotOptions:{column:{stacking:'percent'}}}}}
    item  {title:"[fa-bar-chart] Délka manželství nováčků", par:{type:'novacci',pro:'ms', tag:'o|d',
        chart:{chart:'column',plotOptions:{column:{stacking:'value'}}}}}
    item skup {title:"[fa-line-chart] Velikost a počet skupinek", par:{type:'skupinky',pro:'ms', tag:'o|d'}}
    item prih {title:"[fa-line-chart] postupný součet přihlášek", par:{type:'prihlasky',ukaz:'celkem',pro:'ms', tag:'o|d'}}
    item  {title:"[fa-line-chart] ... přírůstky přihlášek", par:{type:'prihlasky',ukaz:'prubeh',pro:'ms', tag:'o|d'}}
  }
  // fa-?-chart: area|bar|line|pie
  // graf=line|bar|bar%|pie, x=od-do, y=vek|pocet [,z=typ-ucasti]
  menu s2 {title:'2. Infografika LK MS',type:'group'
    item  {title:"[fa-line-chart] Stáří účastníků kurzu", par:{
        graf:'spline/regression', yaxis:'věk účastníků,36,54,10', rok:'od-do',y:'vek',z:'typ-ucasti'}}
    item  {title:"[fa-line-chart] Nezletilé děti na kurzu/doma", par:{
        graf:'spline/regression', yaxis:'průměrný počet dětí,0,2.5,10', rok:'od-do',y:'pocet', z:'deti'}}
    item  {title:"[fa-bar-chart] Příslušnost k církvím", par:{
        graf:'column', rok:'od-do',y:'pocet', z:'cirkev'}}
    func onclick(i:ezer) {  var y:object
      if (i) { last_par= i.par; last_menu= i; }
      info.display(0,'.*');
      if (i.par.tag) info.display(1,i.par.tag) else info.display(1,'o|d');
      info.header(last_menu); info.spinner(1);
      last_par.od= info.od; last_par.do= info.do; last_par.prc= info.prc; 
      last_par.title= replace_fa(i.title,1);
      last_par.org= info.org.key();
      y= php.chart_akce2(last_par);
      if (y.err) alert(y.err)
      else { 
        js.highcharts_load();
        js.highcharts_show(y.chart); info.fill('',y.note); 
      }
      info.spinner(0);
    }
  }
  menu {title:'Infografika MROP',type:'group'                                            skill:'y;f'
    item  {title:"[fa-bar-chart] na našich akcích před iniciaci", par:{type:'pred_mrop', tag:'d',
        chart:{chart:'bar',plotOptions:{series:{stacking:'normal'}}}}}
    item  {title:"[fa-bar-chart] na našich akcích po iniciaci", par:{type:'po_mrop', tag:'d',
        chart:{chart:'bar',plotOptions:{series:{stacking:'normal'}}}}}
    item  {title:"[fa-bar-chart] aktivita YS/FA před iniciací", par:{type:'ys_fa', tag:'d',
        chart:{chart:'bar',plotOptions:{series:{stacking:'normal'}}}}}
    item  {title:"[fa-bar-chart] ... YMCA/CPR+ŠM před iniciací", par:{type:'y_x pred', tag:'d',
        chart:{chart:'bar',plotOptions:{series:{stacking:'normal'}}}}}
    item  {title:"[fa-bar-chart] ... YMCA/CPR+ŠM po iniciací", par:{type:'y_x po', tag:'d',
        chart:{chart:'bar',plotOptions:{series:{stacking:'normal'}}}}}
    item  {title:"[fa-line-chart] postupný součet přihlášek", par:{type:'prihlasky',ukaz:'celkem',pro:'mrop'}, tag:'d'}
    item  {title:"[fa-line-chart] ... přírůstky přihlášek",   par:{type:'prihlasky',ukaz:'prubeh',pro:'mrop'}, tag:'d'}
    item  {title:"[fa-bar-chart] Aktuální věk iniciovaných",  par:{type:'vek_abs',tag:'i|d',
        chart:{chart:'column',plotOptions:{column:{stacking:'value'}}}}}
    item  {title:"[fa-bar-chart] Věk na jejich iniciaci",     par:{type:'vek_rel',tag:'i|d',
        chart:{chart:'column',plotOptions:{column:{stacking:'value'}}}}}
    item  {title:"[fa-bar-chart] ... model",                  par:{type:'vek_rnd',tag:'i|d',
        chart:{chart:'column',plotOptions:{column:{stacking:'value'}}}}}
  }
  menu {title:'Infografika FIRMING',type:'group'                                            skill:'y;f'
    item  {title:"[fa-line-chart] postupný součet přihlášek", par:{type:'prihlasky',ukaz:'celkem',pro:'firm'}}
    item  {title:"[fa-line-chart] ... přírůstky přihlášek",   par:{type:'prihlasky',ukaz:'prubeh',pro:'firm'}}
  }
  func onclick(i:ezer) {  var y:object, par:object
    if (i) { last_par= i.par; last_menu= i; }
    info.display(0,'.*');
    if (i.par.tag) info.display(1,i.par.tag) else info.display(1,'d');
    info.header(last_menu);
    info.spinner(last_menu);
    last_par.po= info.po;
    last_par.od= info.od;
    last_par.do= info.do;
    last_par.prc= info.prc;
    if (last_par.pro!='ms') 
      info.org.key(3);
    last_par.org= info.org.key();
    y= php.chart_akce(last_par);
    if (y.err) 
      alert(y.err)
    else {
      js.highcharts_load();
      js.highcharts_show(y.chart);
      info.fill('',y.note);
    }
    info.spinner(0);
  }
}
# ------------------------------------------------==> .. _right
# univerzální formulář pro levostranné menu
form _right [,,*,50] {
  label head  [10,0,*,50]  { title:'' }
  label chart [10,50,800,400] { title:'' }
  label note  [50,450,700,500] { title:'' }
  // podrobnost
  field po [-35,5,20,] { tag:'i', title:'interval věku: ', format:'tr', css:'bila', value:'10' }
  // období a organizace
  field od [-305,5,40,] { tag:'d', title:'graf od roku', format:'tr', css:'bila' }
  field do [-215,5,40,] { tag:'d', title:'do roku', format:'tr', css:'bila' }
  select org [-35,5,120,] { tag:'o', title:'pro', format:'t', css:'bila' }
  // procenta ?
  check prc [-178,4,30,] { title:'%', format:'t', css:'bila' }
  button [-7,5,20,] {title:'[fa-refresh]' 
    func onclick() { 
      last_menu.click(last_menu);
//    m.s.onclick(0)
  }}
  // funkce
  func Init() {
    chart= `<div id="container" style="width:800px; height:400px;"></div>`;
    js.highcharts_load();
  }
  func header(i:ezer) { var i_owner:ezer
    clear(); i_owner= i.owner();
    head= replace_fa(`<div class='karta'>${i_owner.title} - ${i.title}</div>`);
  }
  func fill(x,y) {
    if (x) head= `<div class='karta'>${replace_fa(x)}</div>`;
    if (y) note= y
  }
  func append(x) { note= conc(note,'<br>',x) }
  func shift(x) { note= conc(x,'<br>',note) }  
  func spinner(on) { js.waiting(on); }
}
