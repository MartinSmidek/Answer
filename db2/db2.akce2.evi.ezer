#pragma test
# ------------------------------------------------------------------------------------------------ #
# Karta pro evidenci členů (verze 2)                                                               #
#                                                                                                  #
# Ans(w)er/Ezer                                     (c) 2007-2015 Martin Šmídek <martin@smidek.eu> #
# ------------------------------------------------------------------------------------------------ #

  var virgin=1
  var idr= 0                                    // naposledy vybraná rodina
  var ido= 0                                    // naposledy vybraná osoba
# --------------------------------------------------------------------------------==> globální jména
# načtení rodiny a osoby
# stačí udat jedno z nich - osoba musí být udané členem rodiny

func curr() {
  return virgin
    ? {rodina:0, osoba:0}
    : {rodina:page.rod.key_rodina, osoba:page.oso.key_osoba}
}
          
proc evid_load(ido,idr) { page.evid_load(ido,idr) }

func evid_osoba(_ido)  { 
  echo(`--- evid_osoba virgin=${virgin}`);
  firstfocus(0);
  panel.focus(); 
  panel.display(1,'e');
  if (menu_item!=m.o.a) {
    m.o.a.click(1,1); 
    m.menu_click(m.o.a,_ido,0)
  }
  else {
    lst.load_osoba(_ido) 
  }
}
func evid_rodina(_idr) { 
  echo(`--- evid_rodina virgin=${virgin}`);
  firstfocus(0);
  panel.focus(); 
  if (menu_item!=m.r.a) {
    m.r.a.click(1,1); 
    m.menu_click(m.r.a,0,_idr)
  }
  else lst.load_rodina(_idr) 
//  if (menu_item!=m.r.a) 
//    m.r.a.click_wait(1); 
//  lst.seek_rodina(_idr,m.r.a.par) 
}

proc osoby_show()      { page.oso.show_osoba(2); }     // obnova údajů o osobě  (z konfigurace)
proc rodina_show()     { page.rod.show_rodina; }       // obnova údajů o rodině (z konfigurace)
proc ulozen_gmail()    { cmd.set_state('show'); page.page_plain; warning;  }
# ----------------------------------------------------------------------------------==> inicializace
func onfirstfocus() { 
  curr_h= sys('screen','height') - min_h; // zamezení úvodního onresize
  firstfocus(0); 
}
proc firstfocus(_click) {
  echo('--- onfirstfocus(',_click,') virgin=',virgin.get);
  virgin.get;
  has_skill('mrop');
  // přístup jen k seznamu iniciovaných se zvláštním tlačítkem na opravu gmail
  page.oso.call('gmail_save.visible');
  osoba_config.start;
  osoba_config.ucast.set(panel); osoba_config.cnfg.g.set(1); osoba_config.cnfg.g.change;
  rodina_config.start;
  page.start;
  virgin.set(0);
  [ _click; m.o.i.click_wait ];
  echo('--- onfirstfocus(',_click,') end');
| // plný přístup
  virgin.get;
  osoba_config.start; rodina_config.start;
  lst.start; page.start; cmd.start;
  virgin.set(0);
  [ _click; m.r.a.click_wait ];
  echo('--- onfirstfocus(',_click,') end');
}
proc onblur() { [ track.display; track.hide ] }
proc up(x)   { x.get.set_css('shift_up','shift_down'); x.set_css('shift_up','shift_down','.*') }
proc down(x) { x.get.set_css('shift_down','shift_up'); x.set_css('shift_down','shift_up','.*') }
# ------------------------------------------------------------------------------------------==> MENU
var menu_item=0,                // běžný item
    menu_par= {},               // ... jeho parametry
    menu_title=''               // ... a browse rodin: r/f a osob: o/h a mail-listů: m
menu m {type:'left', format:'f-'
  menu r {title:'rodiny',type:'group', skill:'yae;fae;cae'
    item a {title:'[fa-group]všechny rodiny'    par:°{lst:'r',tit:'všechny rodiny, co ',cnd:"1"}}
    item m {title:'[fa-send-o] podle mail-listu'par:°{lst:'m',tit:'MAILIST',cnd:'1',komu:'r'}  skill:'#crz' }
    item t {title:'[fa-wrench fa-red](test)'    par:°{lst:'r',tit:'testovací rodiny, co ',
                                                cnd:"r.id_rodina IN (383,2287,14,3329)"},skill:'m'}
  }
  menu o {title:'osoby',type:'group', skill:'yae;fae;cae;mrop'
    item a {title:'[fa-user]všechny kontakty'   par:{lst:'o',tit:'všechny osoby, co ',cnd:'1'}, skill:'yae;fae;cae'}
    item m {title:'[fa-send-o] podle mail-listu'par:{lst:'m',tit:'MAILIST',cnd:'1',komu:'o'}, skill:'yae;fae;cae#crz' }
    item i {title:'[fa-send-o] podle mail-listu'par:°{lst:'m',tit:'MAILIST',cnd:'1',komu:'o',ucel:'%MROP%'}, skill:'mrop'}
    item   {title:'[fa-male]muži'               par:°{lst:'o',tit:'všichni muži, co ',
                                                cnd:"o.sex=1 AND TIMESTAMPDIFF(YEAR,narozeni,NOW())>=18"}, skill:'yae;fae;cae'}
    item   {title:'[fa-female]ženy'             par:°{lst:'o',tit:'všechny ženy, co ',
                                                cnd:"o.sex=2 AND TIMESTAMPDIFF(YEAR,narozeni,NOW())>=18"}, skill:'yae;fae;cae'}
    item   {title:'[fa-child]děti'              par:°{lst:'o',tit:'všechny děti, co ',
                                                cnd:"TIMESTAMPDIFF(YEAR,narozeni,NOW())<18"}, skill:'yae;fae;cae'}
    item   {title:'[fa-male fa-red]iniciovaní'  par:°{lst:'o',tit:'iniciovaní muži, co ',
                                                cnd:"o.iniciace>0"}, skill:'mrop;yaa:c;faa:c'}
    item   {title:'[fa-male fa-green]brána dospělosti'  par:{lst:'o',tit:'muži za branou, co ',
                                                cnd:"o.brana>0"}, skill:'mrop;yaa:c;faa:c'}
    item w {title:'[fa-beer fa-red]s webovými právy'   par:°{lst:'o',tit:'práva pro úpravu webů',
                                                cnd:"o.web_level!=''"},skill:'a'}
    item t {title:'[fa-wrench fa-red](test)'    par:°{lst:'o',tit:'testovací osoby, co ',
                                                cnd:"o.id_osoba IN (679,4031,4504,5877,9437)"},skill:'m'}
  }
  menu c {title:'členové YMCA (lonští)',type:'group', skill:'dec'
    item {title:'[fa-file-text] přehled před závěrkou'
          par:{op:'loni',exp:'Excel',cnd:1,hav:1,rok:1,
                tit:'příjmení:15,jméno:10,datum nar.:10:t,obec:20,člen od:5,činný od:5,činný ČR od:5,letos přísp.:6,letos dary:6',
                fld:'prijmeni,jmeno,_naroz,obec,_clen_od,_cinny_od,_cinny_cr_od,_prisp,_dary'}}
    item {title:'[fa-retweet] recyklace pečounů (LK byl letos)', par:{op:'zmeny',rok:0} }
    item {title:'[fa-retweet] recyklace pečounů (LK byl loni)',  par:{op:'zmeny',rok:1} }
    item {title:'[fa-upload] statistický list YMCA v ČR'
          par:{op:'hlaseni',exp:'Excel',cnd:1,hav:1,rok:1,del:0,
                tit:'?:2,příjmení:15,jméno:10,ulice:20,město:20,PSČ:7,datum nar.:10:t,členství:5,čl. přísp.:6,KČ:12,e-mail:40,činný od letos:6,dobrovolník:6',
                fld:'1,prijmeni,jmeno,ulice,obec,psc,_naroz_ymd,_b_c,_prisp,_YS,email,_cinny_letos,_dobro'}}
    item {title:'[fa-database] přehled loňských dárců YS'
      func onclick(i:ezer) {
        info.header(i); clear();
        panel.display(1,'i'); panel.display(0,'e'); info.export= '';
        info.fill('',php.evid2_ymca_darci(1,'loni'));
    }}
  }
  menu {title:'členové YMCA (letošní - pro info)',type:'group', skill:'dec'
    item {title:'[fa-file-text-o] letošní přehled po závěrce'
          par:{op:'letos',exp:'Excel',cnd:1,hav:1,rok:0,del:0,
                tit:'příjmení:15,jméno:10,datum nar.:10:t,obec:20,člen od:5,činný od:5,činný ČR od:5,letos přísp.:6,letos dary:6',
                fld:'prijmeni,jmeno,_naroz,obec,_clen_od,_cinny_od,_cinny_cr_od,_prisp,_dary'}}
    item {title:'[fa-upload] statistický list YMCA v ČR'
          par:{op:'hlaseni',exp:'Excel',cnd:1,hav:1,rok:0,del:0,
                tit:'?:2,příjmení:15,jméno:10,ulice:20,město:20,PSČ:7,datum nar.:10:t,členství:5,čl. přísp.:6,KČ:12,e-mail:40,činný od letos:6,dobrovolník:6',
                fld:'1,prijmeni,jmeno,ulice,obec,psc,_naroz_ymd,_b_c,_prisp,_YS,email,_cinny_letos,_dobro'}}
  }
  menu f {title:'poslední úpravy rodin',type:'group', skill:'yad;fad'
    item   {title:'[fa-medkit]dnes'    par:°{lst:'f',tit:'úpravy za 24 hodin',days:'1',cnd:"1"}}
    item   {title:'[fa-medkit]včera'   par:°{lst:'f',tit:'úpravy za 48 hodin',days:'2',cnd:"1"}}
    item   {title:'[fa-medkit]týden'   par:°{lst:'f',tit:'úpravy za 7 dnů',days:'7',cnd:"1"}}
    item   {title:'[fa-medkit]měsíc'   par:°{lst:'f',tit:'úpravy za 30 dnů',days:'30',cnd:"1"}}
    item   {title:'[fa-medkit]rok'     par:°{lst:'f',tit:'úpravy za 365 dnů',days:'365',cnd:"1"}}
    item   {title:'[fa-medkit]všechny' par:°{lst:'f',tit:'všechny úpravy',days:'99999',cnd:"1"}}
  }
  menu h {title:'poslední úpravy osob',type:'group', skill:'yad;fad'
    item   {title:'[fa-medkit]dnes'    par:°{lst:'h',tit:'úpravy za 24 hodin',days:'1',cnd:"1"}}
    item   {title:'[fa-medkit]včera'   par:°{lst:'h',tit:'úpravy za 48 hodin',days:'2',cnd:"1"}}
    item   {title:'[fa-medkit]týden'   par:°{lst:'h',tit:'úpravy za 7 dnů',days:'7',cnd:"1"}}
    item   {title:'[fa-medkit]měsíc'   par:°{lst:'h',tit:'úpravy za 30 dnů',days:'30',cnd:"1"}}
    item   {title:'[fa-medkit]rok'     par:°{lst:'h',tit:'úpravy za 365 dnů',days:'365',cnd:"1"}}
    item   {title:'[fa-medkit]všechny' par:°{lst:'h',tit:'všechny úpravy',days:'99999',cnd:"1"}}
  }
  menu x {title:'tipy na duplicitní údaje',type:'group', skill:'yad;fad;cad'
    item   {title:'[fa-chain] stejné jméno a mail',par:{lst:'o',tip:'jmeno,email'}}
    item   {title:'[fa-chain] stejné jméno a telefon',par:{lst:'o',tip:'jmeno,telefon'}}
    item   {title:'[fa-chain] stejné jméno a narození',par:{lst:'o',tip:'jmeno,narozeni'}}
//    item   {title:'[fa-chain] stejné jméno a příjmení',par:{lst:'o',tip:'jmeno,prijmeni'}}
  }
  func onclick (i:ezer) { menu_click (i,0,0) }
  func menu_click(i:ezer,_ido,_idr) { var ret:object, cnd:text, sel: text
    menu_item= i; menu_par= i.par; menu_title= i.title;
    if (i.par.lst) { // podmenu s browse
      echo('--- menu_item virgin=',virgin);
      lst.brow_mapa_clear();
      lst.prijmeni_jmeno.display(eq(i.par.lst,'o')); // řazení na žádost podle příjmení+jméno 
      cnd= i.par.cnd;
      panel.display(0,'i'); panel.display(1,'e'); page.oso.chain.display(0);
      page.oso.set_mode(0,'E');
      if (i.par.tit=='MAILIST') {
        sel= php.mail2_mailist(has_access,menu_par);
        if (!sel) return;
        page.mapa.onclick();
      }
      if (i.par.tip) { 
        clear();
        page.oso.chain.display(1);
        ret= php.evid2_elim_fld_tips(i.par.tip);
        lst.bo.tips.set_attrib('expr',ret.tip);
        cnd= ret.ids;
      };
      lst.tit= i.par.tit; lst._reload(i.par.lst,cnd,_ido,_idr);
    }
    else { // podmenu pro členství - bez browse
      panel.display(1,'i'); panel.display(0,'e'); info.export= i.par.exp;
      info.fill(replace_fa(menu_title),' '); info.msg= '';
      menu_title= replace_fa(menu_title,1);
      ret= php.evid2_ymca_sprava(my_org,i.par,menu_title);
      if (ret.err) 
        alert(ret.err)
      else {
        info.fill('',ret.html);
        // pokračování podmenu pro recyklaci pečounů
        if (i.par.op=='zmeny') {
          confirm("Provést navržené změny?");
          ret= php.evid2_recyklace_pecounu(my_org,i.par,the_title,1);
          alert("Hotovo");
        }
      }
    }
  }
}
# proc reaccess() { #== > reaccess - změna
#   menu_item.get;
#   lst.init_mailist;
#   m.onclick(menu_item.get);
# }
# --------------------------------------------------------------------------------------------- stav
var evi_barvit_zmeny=0          // 1: v page.evid budou zvýrazněna pole se změnou
# --------------------------------------------------------------------------------------------- uses
# const min_h=540           // minimální výška (nelze jednoduše měnit - jsou k ní vztaženy souřadnice)
const min_h=395           // minimální výška (nelze jednoduše měnit - jsou k ní vztaženy souřadnice)
use lst:  form _lst  [0,0,,]            { tag:'e' }
use page: form _page [370,10,,]         { tag:'e' }
use cmd:  form _cmd  [370,min_h-120,,]  { tag:'e' }
use info: form right [12,4,,]           { tag:'i'
  button export [-20,4,,] { title:'Excel', style:'display:block;zIndex:2',
    func onclick() { var ret: object
      if (export) { // export.title vrací ten původní "Excel"
        ret= php.evid2_ymca_sprava(my_org,menu_par,menu_title,1);
        form.msg= ret.html;
      }
    }
  }
}
# ------------------------------------------------------------------------------------------==> test
use test: form _test [360,2,,] { skill:'m' }
form _test [0,0,12,25] { style:'z-index:3;cursor:help;text-align:center'
  label [0,0,12,12] {title:' r', style:'color:white;background:red'
    func onclick() { clear(); GEO.locate(0,idr); lst.brow.browse_row(); lst.load_rodina(idr); }}
  label [0,13,12,12] {title:'o', style:'color:white;background:blue'
    func onclick() { clear(); GEO.locate(ido,0); page.oso_load(ido); }}
}
# =========================================================================================> SEZNAMY
form _lst [,,370,*] {
  # ---------------------------------------------- interface 
  proc load_osoba(_ido) {                       // zobrazí rodinu v kontextu všech
    _reload('o',1,_ido,0)
  }
  proc load_rodina(_idr) {                      // zobrazí osobu v kontextu všech
    _reload('r',1,0,_idr)
  }
  proc reload(_mode,_cnd,_tit) {                // zobrazí rodiny či osoby podle daných podmínek
    tit.set(_tit); _reload(_mode,_cnd,0,0)
  }
  func _reload(_mode,_cnd,_ido,_idr) {          // zobrazí rodinu či osobu podle daných podmínek
    mode= _mode; cnd= _cnd;                     // - zapamatuje podmínky pro refresh
    form.display(2,_mode);
    switch(_mode){
      case 'o': ido= _ido; brow= bo; if (_ido) bo.init_queries(0); bo_reload(_cnd); break;
      case 'm': idm= _ido; brow= bm; if (_ido) bm.init_queries(0); bm_reload(_cnd); break;
      case 'h': idh= _ido; brow= bh; if (_ido) bh.init_queries(0); bh_reload(_cnd); break;
      case 'f': idf= _idr; brow= bf; if (_idr) bf.init_queries(0); bf_reload(_cnd); break;
      case 'r': idr= _idr; brow= br; if (_idr) br.init_queries(0); br_reload(_cnd); break;
    }
    if (has_skill('fad|yad|cad')) page.ctx_enable(_mode) // uprav kontextové menu 
  }
  proc refresh() {                              // zobrazí rodiny či osoby podle starých podmínek
    _reload(mode.get,cnd.get,ido.get,idr.get)   //   s případnou inicializací
  }
  proc refresh2() {                             // zobrazí rodiny či osoby podle starých podmínek
    eq(mode.get,'o'); [ bo_reload(cnd.get) ]
  | eq(mode.get,'m'); [ bm_reload(cnd.get) ]
  | eq(mode.get,'h'); [ bh_reload(cnd.get) ]
  | eq(mode.get,'f'); [ bf_reload(cnd.get) ]
  | eq(mode.get,'r'); [ br_reload(cnd.get) ]
  }
  proc seek_osoba(_ido,par) {           // zviditelní seznam nesmazaných osob a zobrazí osobu
    [ virgin.get; start ];
    tit.set(par.tit); [ eq(mode.get,'o') | mode.set('o'); form.display(2,'o') ];
    bo_cnd.set(1); bo_filtr.set(1); bo_having.set(1); o_vyber.key(1);
    bo.init_queries(0); ido.set(_ido);
    bo.browse_seek(conc("id_osoba=",_ido)); 
//    bo.browse_seek(conc("id_osoba=",_ido),"o.deleted=''",1); 
//    page.evid_load(_ido,0);
  }
  proc seek_rodina(_idr,par) {          // zviditelní seznam nesmazaných rodin a zobrazí rodinu
    [ virgin.get; start ];
    tit.set(par.tit); [ eq(mode.get,'r') | mode.set('r'); form.display(2,'r') ];
    br_cnd.set(1); br_having.set(1); r_vyber.key(1);
    br.init_queries(0); idr.set(_idr);
    br.browse_seek(conc("id_rodina=",_idr)); 
//    br.browse_seek(conc("id_rodina=",_idr),"r.deleted=''",1); page.evid_load(0,_idr);
  }
  # ---------------------------------------------- funkce společné všem seznamům
  func brow_mapa_clear()  { if (brow) {brow.selected('clear'); page.map.mapa_clear(0)}}
  func brow_selected(param:text) { brow.selected(param); brow.browse_refresh() }
  # ---------------------------------------------- stav a inicializace
//  var virgin=1
  var brow:ezer,                                // aktuální browse
      mode:text,                                // o|h|r|f|m
      cnd:text                                  // zapamatovaná podmínka
//  var idr= 0                                    // naposledy vybraná rodina
//  var ido= 0                                    // naposledy vybraná osoba
  var idm= 0                                    // naposledy vybraná osoba v mapě
  var idh= 0                                    // naposledy vybraná osoba  při prohlídce historie změn
  var idf= 0                                    // naposledy vybraná rodina při prohlídce historie změn
  var idx= 0                                    // naposledy vybraná osoba pro opravy kontaktu
  var idz= 0                                    // naposledy vybraná osoba pro tip na eliminaci
  var idy= 0                                    // naposledy vybraná rodina pro tip na eliminaci
  proc start() { [                              // při startu vynecháme prázdná příjmení
//    virgin.set(0);
    o_vyber.key( 1); bo_filtr.set(o_vyber.get); o_vyber.refresh;
    r_vyber.key( 1); br_filtr.set(r_vyber.get); r_vyber.refresh;
//    echo("start: br_filtr=",br_filtr.get);
    //init_mailist;
  ] }
  // zobraz mailist podle access
  func init_mailist() { var sel:text
    page.map.mapa_clear(1);
    sel= php.mail2_mailist(has_access,menu_par);
    if (sel) {
      bm_vyber.selects(sel);
      bm_vyber.key(function('select',"for (var key in select.Items) return key; return 0;",&bm_vyber));
      bm_vyber.change();
    }
    else 
     alert("Mailist pro ",menu_par.komu=='r' ? 'rodiny' : 'osoby'," nebyl ještě vytvořen");
  }
  # ---------------------------------------------- společné prvky
  label [10,2,347,25] { css:'ae_work' }
  label tit [16,8,137,14] { format:'r', css:'frame_label_big' }
  label prijmeni_jmeno [17,10,,] { title:'<i class="fa fa-chevron-circle-up"></i>' 
    func onclick() {
      bo.browse_load(cnd,'prijmeni,jmeno',bo_having)    
  }}
  # ==============================================> H - HISTORIE ZMĚN OSOB
  var track_past=''
  var bh_cnd=1, bh_filtr=1
  proc bh_reload(cnd) { //echo("bh_reload ",virgin.get);
    [ virgin.get; start ];
    evi_barvit_zmeny.set(1); bh_cnd.set(cnd);
    idh.get; [ bh.browse_seek(conc("id_osoba=",idh.get)); bh.onrowclick ]
  | track_past.set(conc("kdy BETWEEN ADDDATE(NOW(),-",menu_par.get('days'),") AND NOW()"));
    bh.browse_load(conc(
      'h.access & ',has_access.get,' AND ',cnd," AND ",bh_filtr.get," AND ",track_past.get));
  }
  view h: table osoba
  view ht: table tvori  { join_type:"LEFT", join:"USING(id_osoba)" }
  view hh: table _track { join:"ON kde='osoba' AND klic=id_osoba" }
  browse bh [10,30,,] {tag:'h', group_by:'h.id_osoba'
      rows:27, buf_rows:60, qry_rows:1, css_rows:'barva,0:neucast'
    show id_o { data:h.id_osoba }
    show id_r { data:ht.id_rodina }
    show id_h { data:hh.id_track }
    show barva { expr:"h.deleted=''" }
    show access { data:h.access }
    show prijmeni [,,70,] { data:h.prijmeni, title:'příjmení', format:'q*s+'
      css_cell:'access,0:,1:ezer_ys,2:ezer_fa,3:ezer_db' }
    show jmeno [,,70,] { data:h.jmeno, title:'jméno', format:'q*s' }
    show [,,54,] { title:'obec', data:h.obec, format:'q*s' }
    show kdo [,,30,] { title:'kdo', expr:"SUBSTR(MAX(CONCAT(kdy,kdo)),20)", format:'q*s'
      help:'kdo naposledy provedl změnu'}
    show kdy [,,65,] { title:"kdy", expr:"SUBSTR(MAX(CONCAT(kdy,kdo)),1,10)", sql_pipe:'sql_date1',
      format:'q*s', help:'kdy byla naposledy provedana změna'}
    show [,,20,] { title:"změn", expr:"COUNT(*)", help:'celkem změn v daném období', format:'rq*s' }
    proc onrowclick() { idh.set(id_o.get); page.evid_load(idh.get,0); }
  }
  # ==============================================> F - HISTORIE ZMĚN RODIN
  var bf_cnd=1, bf_filtr=1
  proc bf_reload(cnd) { //echo("bf_reload ",virgin.get);
    [ virgin.get; start ];
    evi_barvit_zmeny.set(1); bf_cnd.set(cnd);
    idf.get; [ bf.browse_seek(conc("id_osoba=",idf.get)); bf.onrowclick ]
  | track_past.set(conc("kdy BETWEEN ADDDATE(NOW(),-",menu_par.get('days'),") AND NOW()"));
    bf.browse_load(conc(
      'f.access & ',has_access.get,' AND ',cnd," AND ",bf_filtr.get," AND ",track_past.get));
  }
  view f: table rodina
  view fh: table _track { join:"ON kde='rodina' AND klic=id_rodina" }
  browse bf [10,30,,] {tag:'f', group_by:'f.id_rodina'
      rows:27, buf_rows:60, qry_rows:1, css_rows:'barva,0:neucast'
    show id_r { data:f.id_rodina }
    show id_f { data:fh.id_track }
    show barva { expr:"f.deleted=''" }
    show access { data:f.access }
    show nazev [,,70,] { data:f.nazev, title:'příjmení', format:'q*s+'
      css_cell:'access,0:,1:ezer_ys,2:ezer_fa,3:ezer_db' }
    show [,,54+70,] { title:'obec', data:f.obec, format:'q*s' }
    show kdo [,,30,] { title:'kdo', expr:"SUBSTR(MAX(CONCAT(kdy,kdo)),20)", format:'q*s'
      help:'kdo naposledy provedl změnu'}
    show kdy [,,65,] { title:"kdy", expr:"SUBSTR(MAX(CONCAT(kdy,kdo)),1,10)", sql_pipe:'sql_date1',
      format:'q*s', help:'kdy byla naposledy provedana změna'}
    show [,,20,] { title:"změn", expr:"COUNT(*)", help:'celkem změn v daném období', format:'rq*s' }
    proc onrowclick() { idf.set(id_r.get); page.evid_load(0,idf.get); }
  }
  # ==============================================> O - OSOBY
  var bo_cnd=1, bo_filtr= 1, bo_having= 1
  func bo_reload(cnd) { var cond:text
    if (virgin) start();
    bo_cnd= cnd;
    cond= `o.access & ${has_access} AND ${cnd} AND ${bo_filtr}`;
    if (ido) bo.browse_seek(`id_osoba=${ido}`,cond);
    elseif (!bo.browse_load(cond,'',bo_having))
      page.page_init()
  }
//  proc bo_reload(cnd) {
//    [ virgin.get; start ];
//    bo_cnd.set(cnd);
//    ido.get; [ bo.browse_seek(conc("id_osoba=",ido.get)); /*bo.onrowclick*/ ]
//  | bo.browse_load(conc('o.access & ',has_access.get,' AND ',cnd," AND ",bo_filtr.get),'',bo_having.get);
//  | page.page_init
//  }
  select o_vyber [163,5,154,] {tag:'o', type:'map'
    map_pipe:map_evi_vyber_o.hodnota, options:map_evi_vyber_o.zkratka, format:'t'
    help:'výběrová podmínka, doplňující případně zadaná kritéria'
    proc onchanged() { refresh; ido.set(0); bo_reload(bo_cnd.get) }
    proc refresh() {
      bo_filtr.set(this.get);
      bo_having.set(ask('select','ikona','_cis',conc("druh='evi_vyber_o' AND data=",this.key)));
      [ bo_having.get | bo_having.set(1) ] }
  }
  check o_filtr [320,5,40,] {tag:'o', title:"filtr", format:'t', value:'0'
    func onchange () {
      bo_filtr= o_vyber;
      if (o_filtr) {
        filtr_osob.modal(282,258); 
        bo.filtr.expr= filtr_osob.filtr;
        bo_filtr= conc(bo_filtr,filtr_osob.sql);
        bo_having= conc(bo_having,filtr_osob.having);
      }
      else {
//        bo_filtr= 0; 
//        filtr_osob.filtr_clear();
        o_vyber.refresh(); // obnov having
      }
      ido= 0; bo_reload(bo_cnd);
  }}
//    proc onchange () {
//      bo_filtr.set(o_vyber.get);
//      { this.get; filtr_osob.modal(282,258); bo_filtr.set(conc(bo_filtr.get,filtr_osob.sql.get));
//      | this.set(0); filtr_osob.filtr_clear };
//      ido.set(0); bo_reload(bo_cnd.get);
//  }}
  view o:  table osoba
//  view od: table dar    { join_type:"LEFT", join:"USING(id_osoba)" }
  view ot: table tvori  { join_type:"LEFT", join:"USING (id_osoba)" }
  view of: table rodina { join_type:"LEFT", join:"ON of.id_rodina=ot.id_rodina" }
  view og: table osoba_geo { join_type:"LEFT", join:"USING (id_osoba)" }
//  view os: table spolu  { join_type:"LEFT", join:"USING(id_osoba)" }
//  view op: table pobyt  { join_type:"LEFT", join:"USING(id_pobyt)" }
//  view u: table psc_axy { join_type:"LEFT", join:"ON u.psc=IF(o.adresa,o.psc,of.psc)" }
  browse bo [10,30,,] {tag:'o', group_by:'o.id_osoba', rows:27, buf_rows:60, qry_rows:1, 
      optimize:{fetch:'all'}, css_rows:'barva,0:neucast'
    show id_o { data:o.id_osoba }
//    show id_d { data:od.id_dar }
    show id_t { data:ot.id_tvori }
    show id_r { data:of.id_rodina }
//    show id_p { data:os.id_pobyt }
    show lat  { expr:"IFNULL(og.lat,0)" }
    show lng  { expr:"IFNULL(og.lng,0)" }
    show filtr { expr:"1" } // dynamicky definováno ve filtr_osob funkcí o_filtr.onchange
    show _rodin { expr:"COUNT(*)" }
    show barva { expr:"o.deleted=''" }
//    show _funkce { expr:"GROUP_CONCAT(DISTINCT op.funkce)" }
    show access { data:o.access }
    show prijmeni [,,70,] { data:o.prijmeni, title:'příjmení', format:'q*s+'
//      css_cell:'access,0:,1:ezer_ys,2:ezer_fa,3:ezer_db' }
      css_cell:'access,0:,1:ezer_ys,2:ezer_fa,3:ezer_db,64:ezer_ds,65:ezer_ys ezer_ds,66:ezer_fa ezer_ds,67:ezer_db ezer_ds,77:ezer_ys ezer_web,78:ezer_fa ezer_web,79:ezer_db ezer_web,80:ezer_ds ezer_web,81:ezer_ys ezer_ds ezer_web,82:ezer_fa ezer_ds ezer_web,83:ezer_db ezer_ds ezer_web' }
    show jmeno [,,70,] { data:o.jmeno, title:'jméno', format:'q*s' }
    show ulice {expr:"IF(o.adresa,o.ulice,of.ulice)"}
    show psc { expr:"IF(o.adresa,o.psc,of.psc)" }
    show obec [,,60,] { title:'obec', expr:"IF(o.adresa,o.obec,of.obec)", format:'q*s' }
    show nar [,,50,] { title:'rrmmdd', data:o.narozeni, sql_pipe:'sql_yymmdd', format:'q@s'
      help:'datum narození ve tvaru rrmmdd'}
    show _vek [,,20,] { title:"věk", expr:"TIMESTAMPDIFF(YEAR,narozeni,NOW())", format:'rsq*' }
    // volitelná položka
    show telefon [,,0,] { data:o.telefon, title:'telefony',help:'osobní telefony', format:'tq%s' }
    show mail   [,,44,] { data:o.email, title:'emaily',help:'osobní emaily', format:'tq%s' }
    show iniciace [,,0,] { data:o.iniciace, title:'MROP',help:'rok iniciace', format:'tq%s' }
    show _id_o   [,,0,] { data:o.id_osoba, title:'ID osoby',help:'osobní číslo', format:'rq=s' }
    show tips [,,0,] { title:'dup?', expr:'', format:'trsq*' }
    // počítané položky
//    var lat:number, lng:number
    proc onrowclick() { bo_show }
    func bo_show() { 
      ido= id_o; page.evid_load(ido,0);
      page.map.mapa_mark_curr(lat,lng,psc);
    }
    func onchoice(on) { page.map.mapa_mark_add(ido,lat,lng,psc,on) }
    proc onclick() {
      mail.width;     mail.width(0);    telefon.width(44)
    | telefon.width;  telefon.width(0); _id_o.width(44)
    | _id_o.width;   _id_o.width(0);    iniciace.width(44);   
    | iniciace.width; iniciace.width(0); tips.width(44);
    | tips.width;     tips.width(0);    mail.width(44);
    }
    menu { type:'context'
      item { title:'export seznamu do Excelu',
        func onclick () { var name:text
          name= "seznam_osob";
          bo.browse_export({file:name,type:'xlsx',title:'Výběr evidovaných osob',
              show:'prijmeni,jmeno,ulice,psc,obec,_vek,telefon,mail,iniciace'});
          alert(`soubor pro Excel lze stáhnout <a href='docs/${name}.xlsx'>zde</a>`);
      }}
      item { title:'-zrušit výběr a zobrazení'    func onclick() { brow_mapa_clear() }}
      item { title:'vybrat vše, co lze zobrazit'  func onclick() { page.map.select_in_map() }}
      item { title:'-v seznamu jen vybraní'       func onclick() { brow_selected('use') }}
      item { title:'v seznamu i nevybraní'        func onclick() { brow_selected('ignore') }}
      item { title:'-vybraná ID dát do schránky'  func onclick() { clipboard(brow_selected('get')) }}
      item { title:'všechna ID dát do schránky'   func onclick() { clipboard(brow.browse_keys()) }}
      item { title:'-pozice na mapě'              func onclick() { geo_latltd(); }}
    }
//    proc re_geocode() {   // podle maps.google.com
//      C_clen.goo_ok.enable(0);
//      C_clen.goo_pat.set(clen_adresa);
//      geo.set(object('id',C_clen.id_clen.get,'address',C_clen.goo_pat.get));
//      geo.set(m.geocode(geo.get));            // přepočítáme polohu
//      [ geo.get('mark');                      // pokud je známa poloha zobrazíme výrazně
//        klub_google(m,'curr',geo.get('lat'),geo.get('lng'),'red',12);
//        C_clen.goo_ok.enable(1) ];
//      C_clen.goo_adr.set(geo.get('found.addr')); // včetně adresy vrácené geocode
//    }
    func geo_latltd() { var adresa:text, geo:object // {mark:'lat,lng',ezer:{c,k,a,s}}
      adresa= geo_adresa();
      geo= {id:id_o,address:adresa};
      geo= page.map.cr.geocode(geo);
      echo(debug(geo,adresa));
    }
    func geo_adresa() {
      return `${ulice}, ${psc} ${obec}, CZ`;
    }

  }
  # ==============================================> M - MAILIST
  var bm_having= 1, bm_komu= -1
  func bm_reload(cnd) {
    if (virgin) start();
    if (bm_komu!=menu_par.komu) {
      bm_komu= menu_par.komu; 
      init_mailist(); 
    }
    page.map.mapa_clear(1);
    if (!bm.browse_load(bm_vyber.key())) page.page_init();
  }
  label [15,9,,] { tag:'m', title:'mail-list:' }
  select bm_vyber [62,5,289,] { tag:'m', format:'t', help:'komu|výběr mail-listu'
    proc onchanged() { bm_reload(1) } }
  browse bm [10,30,,] {tag:'m', rows:27, qry_rows:1, buf_rows:6000,
      optimize:{ask:'evid2_browse_mailist'}, key_id:'id_o'
    show id_o
    show lat
    show lng
    show ulice
    show telefon
    show access
    show prijmeni [,,70,] { title:'příjmení', format:'sq*'
      css_cell:'access,0:,1:ezer_ys,2:ezer_fa,3:ezer_db' }
    show jmeno    [,,70,] { title:'jméno', format:'sq*' }
    show obec     [,,70,] { title:'obec', format:'tsq*' }
    show psc      [,,40,] { title:'psč', format:'s+q*' }
    show _vek     [,,20,] { title:"věk", format:'rsq=' }
    // volitelná položka
    show mail     [,,44,] { title:'emaily', format:'tsq*', help:'osobní emaily' }
    show _id_o    [,,0,]  { title:'ID osoby', format:'rsq=', help:'osobní číslo' }
    // events
    func onrowclick() {
      page.map.mapa_mark_curr(lat,lng,psc);
      if (bm_komu=='o') { // seznam osob
        ido= id_o; page.evid_load(ido,0);
      }
      else if (bm_komu=='r') { // seznam rodin
        idr= id_o; page.evid_load(0,idr);
      }
    }
    func onchoice(on) { page.map.mapa_mark_add(ido,lat,lng,psc,on) }
    proc onclick() {
      mail.width; mail.width(0);  _id_o.width(44)
    |             mail.width(44); _id_o.width(0)
    }
    #==> . kontextové menu
    proc bm_keys() { return(function('b',"return b.keys.toString();",bm)) }
    menu { type:'context'
      item { title:'export vybraných do Excelu',
        proc onclick () { var ret:object
          ret.set(bm.browse_export(°{type:'xlsx',selected:1}));
          alert(ret.html);
      }}
      item { title:'-zrušit výběr a zobrazení'    func onclick() { brow_mapa_clear() }}
      item { title:'vybrat vše co lze zobrazit'   func onclick() { page.map.select_in_map() }}
      item { title:'-v seznamu jen vybraní'       func onclick() { brow_selected('use') }}
      item { title:'v seznamu i nevybraní'        func onclick() { brow_selected('ignore') }}
      item { title:'-vybraná ID dát do schránky'  func onclick() { clipboard(bm.selected('get')) }}
    }
  }
  # ==============================================> R - RODINY
  var br_cnd=1, br_filtr= 1, br_having= 1
  proc br_reload(cnd) {
    [ virgin.get; start ];
    br_cnd.set(cnd);
    idr.get; [ br.browse_seek(conc("id_rodina=",idr.get)); br.onrowclick ]
  |    // echo("load: br_filtr=",br_filtr.get);
    br.browse_load(conc('r.access & ',has_access.get,' AND ',cnd," AND ",br_filtr.get),'',br_having.get);
  }
  select r_vyber [163,5,154,] {tag:'r', type:'map'
    map_pipe:map_evi_vyber_r.hodnota, options:map_evi_vyber_r.zkratka, format:'t'
    help:'výběrová podmínka, doplňující případně zadaná kritéria'
    proc onchanged() { refresh; idr.set(0); br_reload(br_cnd.get) }
    proc refresh() {
      br_filtr.set(this.get);
      br_having.set(ask('select','ikona','_cis',conc("druh='evi_vyber_r' AND data=",this.key)));
      [ br_having.get | br_having.set(1) ] }
  }
  check r_filtr [320,5,50,] {tag:'r', title:"filtr", format:'t', value:'0'
    proc onchange () {
      br_filtr.set(r_vyber.get);
      { this.get; filtr_rodin.modal(282,258); br_filtr.set(conc(br_filtr.get,filtr_rodin.sql.get));
      | this.set(0); filtr_rodin.filtr_clear };
      idr.set(0); br_reload(br_cnd.get);
  }}
  view r: table rodina
  view rt: table tvori { join_type:'LEFT', join:"USING (id_rodina)" }
  view rto: table osoba { join_type:'LEFT', join:"USING (id_osoba)" }
  view rg: table rodina_geo { join_type:"LEFT", join:"USING (id_rodina)" }
  browse br [10,30,,] {tag:'r', rows:27, buf_rows:60, qry_rows:1, css_rows:'barva,0:neucast'
      group_by:'r.id_rodina', optimize:{fetch:'all'}
    show id_r { data:r.id_rodina }
    show id_t { data:rt.id_tvori }
    show barva { expr:"r.deleted=''" }
    show _clenu { expr:"COUNT(*)" }
    show access { data:r.access }
    show lat  { expr:"IFNULL(rg.lat,0)" }
    show lng  { expr:"IFNULL(rg.lng,0)" }
    show nazev [,,70,] { data:r.nazev, title:'rodina', format:'q*s+',
      css_cell:'access,0:,1:ezer_ys,2:ezer_fa,3:ezer_db' }
    show jmena [,,130,] { expr:"GROUP_CONCAT(rto.jmeno ORDER BY rt.role)", title:'jména', format:'tq%s' }
    show ulice { data:r.ulice }
    show psc { data:r.psc }
    show obec [,,50,] { title:'obec', data:r.obec, format:'q*s' }
    // volitelná položka
    show maily [,,66,] { data:r.emaily, title:'emaily', help:'rodinné emaily', format:'tq%s' }
    show _id_r [,,0,] { data:r.id_rodina, title:'ID rodiny',help:'číslo rodiny', format:'rq=s' }
    func onrowclick() {  var ll:array, lat:number, lng:number
      idr= id_r; 
      page.evid_load(0,idr); 
      if (psc && page.map.on) {
//        ll= php.select('lat,lng','psc_axy',`psc='${psc}'`); lat= ll[0]; lng= ll[1];
        page.map.mapa_mark_curr(0,0,psc)
      }
    }
    func onchoice(on) { page.map.mapa_mark_add(ido,lat,lng,psc,on) }
    proc onclick() {
      maily.width; maily.width(0);  _id_r.width(66)
    |              maily.width(66); _id_r.width(0)
    }
    menu { type:'context'
      item { title:'export seznamu do Excelu',
        func onclick () { var name:text
          name= "seznam_rodin";
          br.browse_export({file:name,type:'xlsx',title:'Výběr evidovaných rodin',
              show:'nazev,jmena,ulice,psc,obec,_vek,telefon,maily'});
          alert(`soubor pro Excel lze stáhnout <a href='docs/${name}.xlsx'>zde</a>`);
      }}
      item { title:'-zrušit výběr a zobrazení'    func onclick() { brow_mapa_clear() }}
      item { title:'vybrat vše co lze zobrazit'   func onclick() { page.map.select_in_map() }}
      item { title:'-v seznamu jen vybraní'       func onclick() { brow_selected('use') }}
      item { title:'v seznamu i nevybraní'        func onclick() { brow_selected('ignore') }}
      item { title:'-vybraná ID dát do schránky'  func onclick() { clipboard(bm.selected('get')) }}
    }
  }
}
# ============================================================================================> PAGE
# formulář se záložkami
form _page {
  # ---------------------------------------------- interface
  # 0,r = zobrazení rodiny a náhodné osoby (prioritně s role='a')
  # o,0 = zobrazení osoby a náhodně rodiny (prioritně tu, kde má roli 'a' nebo 'b')
  # o,r = zobrazení rodiny a její osoby (nebo warning, že to nesedí a pokračování jako v o,0)
  proc evid_load(_ido,_idr) { var x:object        // zobrazí a zapamatuje podmínky
    idr0.set(_idr); ido0.set(_ido);
    idr.set(_idr); ido.set(_ido);
    page_load;
    [ track.display;
      { ido; track.back_show('osoba',ido.get)
      | idr; track.back_show('rodina',idr.get)
      }
    ]
  }
  proc refresh() {                              // zobrazí rodiny či osoby podle starých podmínek
    evid_load(ido0.get,idr0.get) }
  # ---------------------------------------------- stav a inicializace
  var idr0=0, //idr=0,    // původní a zobrazená rodina
      ido0=0, //ido=0,    // původní a zobrazený člen (jehož rodina, role, rodiny jsou zobrazeny)
      last_upd=''       // r|o
  proc start() {
    cvak(evid); oso.show_osoba(2); rod.show_rodina; up(oso); up(rod); down(dar)
  }
  # ---------------------------------------------- pomocné procedury
  # načtení osoby a jeho rodin
  proc page_load() { var x:object
    page_init;
    x.set(ask('evid2_cleni',ido.get,idr.get,conc(cconc(ido.get,lst.bo_filtr.get,lst.br_filtr.get))));
    x.msg; [ x.rodina; rod_load(x.rodina) ]; warning(x.msg)
  | idr.set(x.rodina); x.rodina; rod.enable(1,'.*');
    cleni.browse_fill(x.cleni);
    //cleni.cleni_click(ido.get);
    cleni.raise('onrowclick',ido.get);
    rod_load(x.rodina);
  | rod.enable(0,'.*'); osoba_load
  }
  # vyčištění stránky
  proc page_init() {
    oso.init; rod.init; cleni.browse_init; act.akc.browse_init;
    last_upd.set(''); cmd.set_state('show'); 
    oso.set_helps(0,'zluty','oranzovy'); rod.set_helps(0,'zluty','oranzovy') }
  # změna v některém z formulářů
  proc d_change(x) { last_upd.set(x); cmd.is_chng }
  # žádná změna
  proc page_same() { return(and(form.same,rod.same,oso.same)) }
  # zruš příznaky změn
  proc page_plain() { form.plain; rod.plain; oso.plain; }
  # načíst aktivní osobu do aktivní záložky
  proc osoba_load() {
    switch(curr.get,
    'evid',{ oso_load(ido.get) },
    'akce',{ oso_load(ido.get); act.ucasti(ido.get,conc(oso.prijmeni.get,' ',oso.jmeno.get)) },
    'albu',{ alb.galery('rodina',idr.get) },
    'dary',{ oso_load(ido.get); 
             dar.form_init; dar.dary(ido.get,conc(oso.prijmeni.get,' ',oso.jmeno.get)) },
    'clen',{ oso_load(ido.get);
             cle.form_init; [ cle.dary(ido.get,conc(oso.prijmeni.get,' ',oso.jmeno.get)) ];
             pri.form_init; pri.dary(ido.get,conc(oso.prijmeni.get,' ',oso.jmeno.get)) },
    'mapa',{ oso_load(ido.get); act.ucasti(ido.get,conc(oso.prijmeni.get,' ',oso.jmeno.get)) }
    );
    [ cmd.part('o_smaz');
      cmd.o_smaz.display(not(page.oso.deleted.get)); cmd.o_obnov.display(page.oso.deleted.get) ];
  }
  # načíst osobu do záložky evid (případně s doplněním informací z _track) ==> . oso_load
  func oso_load(ido) { var ret: object, geo:object
    if (ido && evi_barvit_zmeny) {
      oso.set_helps(0,'zluty','oranzovy');
      ret= php.elim2_data_osoba(ido,eq(lst.mode,'f','h') ? lst.track_past : '');
      oso.load(ido);
      oso.show_osoba(2);
      oso.set_helps(ret.chng,'zluty','oranzovy');
    }
    elseif (ido) {
      oso.load(ido);
      oso.geo_ok= php.select('stav=1','osoba_geo',`id_osoba=${ido}`) ? 'ok' : '';
//      geo= php.geo_get_osoba(ido);
//      oso.geo_ok= geo.ok ? 'OK' : '';
      oso.show_osoba(2);
    }
  }
//  proc oso_load(ido) { var ret: object
//    ido;
//    evi_barvit_zmeny.get;
//    oso.set_helps(0,'zluty','oranzovy');
//    ret.set(ask('elim2_data_osoba',ido,cconc(eq(lst.mode.get,'f','h'),lst.track_past.get,'')));
//    oso.load(ido);
////    oso.rc.set(make_rc(oso.narozeni.get,oso.sex.get));
//    oso.show_osoba(2);
//    oso.set_helps(ret.chng,'zluty','oranzovy');
//  | ido; oso.load(ido);
////    oso.rc.set(make_rc(oso.narozeni.get,oso.sex.get));
//    oso.show_osoba(2);
//  }
  # načíst rodinu (případně s doplněním informací z _track) ==> . rod_load
  func rod_load(idr) { var ret: object, mazat:number
    if (evi_barvit_zmeny) 
      ret= php.elim2_data_rodina(idr,eq(lst.mode,'f','h') ? lst.track_past : '');
    rod.r.load(idr); rod.od_svatby();
    if (evi_barvit_zmeny) 
      rod.set_helps(ret.chng,'zluty','oranzovy');
    // zobrazení značky ověřené adresy
    rod.r_geo_ok= lst.mode=='r' && lst.brow.lat ? 'ok' : '';
    // přepínání mezi mazat/obnovit
    mazat= page.rod.r_deleted ? 0 : 1; 
    if (has_skill('yaa+|faa+|caa+')) {
      cmd.r_smaz.display(mazat);
      cmd.r_obnov.display(!mazat);
    }
  }
//  func rod_load(idr) { var ret: object, mazat:number
//    if (evi_barvit_zmeny) {
//      ret= php.elim2_data_rodina(idr,eq(lst.mode,'f','h') ? lst.track_past : '');
//      rod.r.load(idr); rod.od_svatby();
//      rod.set_helps(ret.chng,'zluty','oranzovy');
//    }
//    else {
//      rod.r.load(idr); rod.od_svatby();
//    }
//    mazat= page.rod.r_deleted ? 0 : 1; 
//    cmd.r_smaz.display(mazat);
//    cmd.r_obnov.display(!mazat);
//  }
//  proc rod_load(idr) { var ret: object
//    [ evi_barvit_zmeny.get;
//      ret.set(ask('elim2_data_rodina',idr,cconc(eq(lst.mode.get,'f','h'),lst.track_past.get,'')));
//      rod.r.load(idr); [ rod.call('od_svatby') ];
//      rod.set_helps(ret.chng,'zluty','oranzovy');
//    | rod.r.load(idr); [ rod.call('od_svatby') ]
//    ];
//    [ cmd.part('r_smaz');
//      cmd.r_smaz.display(not(page.rod.r_deleted.get));
//      cmd.r_obnov.display(page.rod.r_deleted.get) ];
//  }
  # ----------------------------------------------==> záložky
  var curr:object // aktuální záložka
  proc cvak(i) {
    form.set_css('ae_butt_off','ae_butt_on','_'); i.set_css('ae_butt_on','ae_butt_off');
    curr.set(i._id); form.display(0,i.par.off); form.display(1,i.par.on)  }
  label evid [12,0,90,20] {par:°{on:'C|O',off:'B|F|D|Y|M'}, tag:'_', title:'<b>Osobní údaje</b>',
    proc onclick() { cvak(this); osoba_load; oso.on_click }}
  label akce [129,0,90,20] {par:°{on:'C|O|B',off:'F|D|Y|M'}, tag:'_', title:'<b>Účast na akcích</b>'
    proc onclick() { cvak(this); osoba_load }}
  label albu [247,0,90,20] {par:°{on:'C|F',off:'O|B|D|Y|M'}, tag:'_', title:'<b>Fotografie</b>',
    proc onclick() { cvak(this); osoba_load }}
  label mapa [365,0,90,20] {par:°{on:'C|O|M',off:'F|D|Y'},  tag:'_', title:'<b>Mapa</b>',
    proc onclick() { cvak(this); osoba_load; map.mapa_show('smap') }}
  label dary [483,0,90,20] {par:°{on:'C|O|D',off:'F|Y|M'},   tag:'_', title:'<b>Dary</b>', skill:'yae;fae;cae'
    proc onclick() { cvak(this); osoba_load; dar.onclick }}
  label clen [601,0,90,20] {par:°{on:'C|O|Y',off:'F|D|M'},  tag:'_', title:'<b>Členství YMCA</b>', skill:'yae;fae;cae'
    proc onclick() { cvak(this); osoba_load; cle.onclick }}
  label frame [0,16,706,min_h-150] { css:'ae_work'
    proc onclick() { cmd.get.set_css('shift_down','shift_up') }}
  # ==============================================> C: EVIDENCE RODINY
  view r: table rodina
  use rod: form _rodina2p [5,20,,] { tag:'Cf'   // view r:rodina
    func on_click() {
      up(page.oso); up(page.rod); down(page.dar); down(page.cle); down(page.pri); down(page.map) }
    func onchanged() { page.d_change('r') }     // reakce na změnu ve formuláři => změna tlačítek
    func rodina_changed() {                // reakce na změnu select:rodiny => výměna členů
      if (idr!=rod.rodiny.key()) {
        idr= rod.rodiny.key();             // - změna lokálního idr
        page.page_load()
      }
    }
    func rodina_edit() { page.d_change('r') }
  }
  # ----------------------------------------------==> ... členové rodiny
  label [10,40,200,] {tag:'C', title:"členi zobrazené rodiny" }
  browse cleni [10,54,107,191] {tag:'Cb', rows:8, buf_rows:50, css_rows:'barva,0:neucast,2:seda'
    var last=1
    show id_osoba
    show id_tvori
    show barva
    show rodiny
    show org
    show jmeno [,,110,] {title:'jména'
//      css_cell:'org,0:,1:ezer_ys,2:ezer_fa,3:ezer_db' }
      css_cell:'org,0:,1:ezer_ys,2:ezer_fa,3:ezer_db,64:ezer_ds,65:ezer_ys ezer_ds,66:ezer_fa ezer_ds,67:ezer_db ezer_ds,77:ezer_ys ezer_web,78:ezer_fa ezer_web,79:ezer_db ezer_web,80:ezer_ds ezer_web,81:ezer_ys ezer_ds ezer_web,82:ezer_fa ezer_ds ezer_web,83:ezer_db ezer_ds ezer_web' }
    show vek   [,,30,] {title:'věk', format:'r' }
    show role  [,,10,] {title:'role'}
    func onrowclick() {
      if (has_skill('mrop')) {
        this.browse_active(last); cleni_click(ido0);
        this.blur(1);
      }
      else {
        cleni_click(id_osoba);
        if (track.display()) track.back_show('osoba',ido);
      }
    }
//    proc onrowclick() {
//      has_skill('mrop'); this.browse_active(last.get); cleni_click(ido0.get);
//      this.blur(1);
//    | cleni_click(id_osoba.get);
//      [ track.display; track.back_show('osoba',ido.get) ];
//    }
    proc cleni_click(id) {
      accessed(org.get);
      // zobrazení člena rodiny, případná úprava focus
      last.set(this.browse_active);
      ido.set(id);                              // změna lokálního ido
      rod.t.key(id_tvori.get); rod.t_role.set(role.get);
      rod.rodiny.selects(rodiny.get); rod.rodiny.key(idr.get);
      [ eq(lst.mode.get,'o');                   // pokud jde o jinou osobu než je v lst
        { eq(ido.get,ido0.get); lst.bo.browse_focus
        | lst.bo.blur(1) }
      ];
      osoba_load
    | not(accessed(org.get));
      warning("nemáte oprávnění vidět údaje o osobě z jiné organizace");
      this.browse_active(last.get)
    }
  }
  # ==============================================> O: EVIDENCE OSOBY
  use oso: form _osoba2x  [200,20,,] { tag:'O'  // view o:osoba
    button chain [102,1,,] {tag:'f', title:'[fa-chain]', css:'db_chain',
      proc onclick() { var zmena:text, ido:number, tips:text
        ido.set(lst.bo.id_o.get); tips.set(lst.bo.tips.get);
        spinner(1); zmena.set(elim.osoby_lst(ido,tips)); spinner(0);
//        echo("elim.osoby_lst(",ido,",",tips,")=",zmena," ido=",elim.osoby_lst_id.get);
        lst.bo.browse_seek(conc("o.id_osoba=",elim.osoby_lst_id.get));
        lst.bo.raise('onrowclick',elim.osoby_lst_id.get);
#         [ lst.bo.browse_row;
#           lst.bo.raise('onrowclick',elim.osoby_lst_id.get);
#           lst.bo.browse_row
#         ];
    }}
    proc on_click() {
      up(page.oso); up(page.rod); down(page.dar); down(page.cle); down(page.pri); down(page.map) }
    proc onchanged() { page.d_change('o') }
    menu detail { type:'context', skill:'yae;fae;cae'
      item { title:'EVID konfigurace formuláře', proc onclick () { osoba_config.tune(panel) } }
    }
  }
  # ==============================================> . kontextové menu
  func ctx_enable(_mode) {
    switch(_mode){
      case 'o': ctx.r.enable(0); ctx.o.enable(1); ctx.c.enable(0); break;
      case 'r': ctx.r.enable(1); ctx.o.enable(0); ctx.c.enable(1); break;
      case 'h': case 'f': case 'm': 
                ctx.r.enable(0); ctx.o.enable(0); ctx.c.enable(0); break;
    };
  }
  menu ctx { type:'context', join:frame, skill:'yae;fae;cae'
    func vyber(kolik,ceho,kde:ezer) {
      if (kolik<2) 
        alert(`v levém seznamu je třeba pomocí Insert vybrat 2 ${ceho}`)
      elseif ( confirm(`je vybráno více jak 2 ${ceho}, mám starý výběr zrušit?`) )
        kde.selected('clear');
    }
//    item { title:'sjednotit kopie osob', skill:'fad|fad;yad|yad;cad|cad'
//      proc onclick () { elim.osoby(ido.get,oso.jmeno.get,oso.prijmeni.get); }} 
//    item { title:'sjednotit kopie rodin', skill:'fad|fad;yad|yad;cad|cad'
//      proc onclick () { elim.rodiny(idr.get,rod.rodiny.get); }} 
    item r { title:'sjednotit 2 označené rodiny', skill:'fad|fad;yad|yad;cad|cad'
      func onclick () { var sel:array
        sel= lst.br.selected('get');
        sel= split(sel,',');
        if ( array_length(sel)==2 ) {
          idr= elim.rodiny_12(sel[0],sel[1]); 
          if ( idr ) {
            lst.br.selected('clear');
            lst.br.browse_refresh(idr);
          }
        }
        else vyber(array_length(sel),'rodiny',lst.br)
//          warning(`Vyber právě 2 rodiny ne ${array_length(sel)}`)
    }}
    item o { title:'sjednotit 2 označené osoby', skill:'fad|fad;yad|yad;cad|cad'
      func onclick () { var sel:array
        sel= lst.bo.selected('get');
        sel= split(sel,',');
        if ( array_length(sel)==2 ) {
          ido= elim.osoby_12(sel[0],sel[1]); 
          if ( ido ) {
            lst.bo.selected('clear');
            lst.bo.browse_refresh(ido);
          }
        }
        else vyber(array_length(sel),'osoby',lst.bo)
//          confirm(`nejsou vybrány právě 2 osoby (ale ${array_length(sel)})`)
    }}
    item c { title:'sjednotit kopie členů rodiny', skill:'fad|fad;yad|yad;cad|cad'
      func onclick () { 
        if ( idr ) {
          idr= elim.cleni(idr); 
          if ( idr ) lst.br.browse_refresh(idr);
        }
    }} 
    item { title:'-zvýrazňovat změněné údaje'  proc onclick () { evi_barvit_zmeny.set(1)} }
    item { title:'nezvýrazňovat změněné údaje' proc onclick () { evi_barvit_zmeny.set(0)} }
  }
  use act: form _aktivity  [450, 20,,]  { tag:'B', format:'n' }
  use alb: form foto._alb  [200, 20,,]  { tag:'F', format:'n' }
  use dar: form _dary      [  5,-57,,]  { tag:'D', format:'n' }
  use cle: form _clenstvi  [  5,-57,,]  { tag:'Y', format:'n' }
  use pri: form _prispevky [326,-57,,]  { tag:'Y', format:'n' }
#   use map: form _mapa      [200,-57,,]  { tag:'M', format:'n' }
  use map: form _mapa      [ 50,-57,,]  { tag:'M', format:'n' }
}
# ------------------------------------------------------------------------------------------==> DARY
form _dary [,,440,min_h-160] {css:'ae_frame shift_down'
  proc onclick() {
    up(page.dar); down(page.pri); down(page.cle); down(page.oso); down(page.rod); down(page.map) }
  label frame [2,3,78,] { css:'frame_label_up', title:'přehled darů - '}
  label jmeno [79,3,172,] { css:'frame_label_up'}
  label ido   [378,3,50,] { format:'r' }
  label idd   [378,18,50,] { format:'r' }
  proc dary(_ido,_jmeno) {
    form.init; ido.set(_ido); jmeno.set(_jmeno);
    b.browse_load(conc("da.deleted='' AND ukon='d' AND id_osoba=",_ido))
  }
  proc refresh() { dary(id_o.get,jmeno.get) }
  # seznam darů
  view da: table dar
  view do: table osoba { join:"USING(id_osoba)" }
  browse b [5,20,,] { rows:9, qry_rows:1
    show id_d { data:da.id_dar }
    show id_o { data:do.id_osoba }
    show access { data:da.access }
    show [,,13,] { data:da.ukon, title:'způsob', format:'q*s'
//      css_cell:'access,0:,1:ezer_ys,2:ezer_fa,3:ezer_db' }
      css_cell:'access,0:,1:ezer_ys,2:ezer_fa,3:ezer_db,64:ezer_ds,65:ezer_ys ezer_ds,66:ezer_fa ezer_ds,67:ezer_db ezer_ds,77:ezer_ys ezer_web,78:ezer_fa ezer_web,79:ezer_db ezer_web,80:ezer_ds ezer_web,81:ezer_ys ezer_ds ezer_web,82:ezer_fa ezer_ds ezer_web,83:ezer_db ezer_ds ezer_web' }
    show castka [,,70,] { data:da.castka, title:'částka', format:'rq*s' }
    show [,,80,] { data:da.dat_od, title:'přijetí', format:'rq*s' }
    show [,,80,] { data:da.dat_do, title:'potvrzení', format:'rq*s' }
    proc onrowclick() { form.load(id_d.get); idd.set(id_d.get); form_state('n|s','u|z'); }
  }
  # formulář pro úpravu darů
  # skryté položky
  field id_o { data:da.id_osoba }
  field access { data:da.access }
  field ukon { data:da.ukon }
  field del  { data:da.deleted }
  # viditelné položky
  const Ld=293
  field castka [Ld,40,65,] {data:da.castka, help:'darovaná částka', format:'xr', title:'^částka'}
  field datum  [Ld,75,85,] {type:'date', data:da.dat_od, help:'datum přijetí daru',
    format:'xRUr', title:'^dar přijat dne'}
  field potvrz [Ld,110,85,] {type:'date', data:da.dat_do, help:'datum odeslání potvrzení',
    format:'RUr', title:'^potvrzeno dne'}
  field pozn   [Ld,145,137,] {data:da.note, help:'poznámka', format:'x', title:'^poznámka'}
  # ==> . tlačítka a obslužné procedury
  proc form_init() {
    b.browse_init; form.init; form_state('n','u|z|s'); }
  proc form_state(on,off) {
    form.enable(1,on); form.enable(0,off); }
  proc onchanged () {
    form_state('u|z','n|s') }
  label cmd [Ld,175,137,50] { css:'ae_parm', style:'z-index:1' }
  # -------------------------- Nový
  button [cmd.l+6,cmd.t+5,,] { tag:'n', title:'[fa-database] Nový',
    help:'Vložit nový dar', style:'z-index:2'
    proc onclick() {
      form.init(2); form_state('u|z','n|s');
      id_o.set(ido.get); id_o.change(1); del.set(''); del.change(1);
      access.set(has_access.get); access.change(1);
      ukon.set('d'); ukon.change(1); castka.change(1);
      datum.set(now); datum.change(1);
  }}
  # -------------------------- Uložit
  button [cmd.l+63,cmd.t+5,,] {tag:'u', title:'[fa-save] Uložit',
    help:'Uložit změněná data', style:'z-index:2'
    proc onclick() {
      form.same
    | // oprava
      form.key; form.save; form.load;
      [ b.browse_seek; form_state('n|s','u|z') ]
    | // přidání
      id_o.get;
      form.insert; form.load;
      b.raise('onrowclick',b.browse_seek(conc('id_dar=',form.key))); form_state('n|s','u|z');
#       [ osoby.b.browse_row | osoby.b.browse_init ]
    | error('chyba při vkládání daru - nenastavený člen')
  }}
  # -------------------------- Zpět
  button [cmd.l+6,cmd.t+27,,] {tag:'z', title:'[fa-undo] Zpět',
    help:'Neměnit změněná data', style:'z-index:2'
    proc onclick() {
      b.browse_count; form.load(b.id_d.get); form_state('n|s','u|z')
    | form.init; form_state('n','u|z|s')
  }}
  # -------------------------- Smazat
  button [cmd.l+63,cmd.t+27,,] {tag:'s', title:'[fa-times fa-red] Vymazat',
    help:'Smazat tento dar', style:'z-index:2'
    proc onclick() {
      confirm("opravdu smazat dar ",b.castka.get," od ",jmeno.get,"?");
      del.set(conc('D ',sys('user','abbr'),' ',now)); del.change; form.save;
      refresh
  }}
}
# -------------------------------------------------------------------------------------==> PŘÍSPĚVKY
form _prispevky [,,375,min_h+-180] {css:'ae_frame shift_down'
  proc onclick() {
    up(page.pri); down(page.dar); down(page.oso); down(page.rod); down(page.cle); down(page.map) }
  label frame [2,3,163,] { css:'frame_label_up', title:'přehled členských příspěvků - '}
  label jmeno [163,3,147,] { css:'frame_label_up'}
  label ido   [318,3,50,] { format:'r' }
  label idd   [318,18,50,] { format:'r' }
  proc dary(_ido,_jmeno) {
    form.init; ido.set(_ido); jmeno.set(_jmeno);
    b.browse_load(conc("da.deleted='' AND ukon='p' AND id_osoba=",_ido))
  }
  proc refresh() { dary(id_o.get,jmeno.get) }
  # seznam darů
  view da: table dar
  view do: table osoba { join:"USING(id_osoba)" }
  browse b [5,20,,] { rows:8, qry_rows:1
    show id_d { data:da.id_dar }
    show id_o { data:do.id_osoba }
    show access { data:da.access }
    show [,,13,] { data:da.ukon, title:'způsob', format:'q*s'
//      css_cell:'access,0:,1:ezer_ys,2:ezer_fa,3:ezer_db' }
      css_cell:'access,0:,1:ezer_ys,2:ezer_fa,3:ezer_db,64:ezer_ds,65:ezer_ys ezer_ds,66:ezer_fa ezer_ds,67:ezer_db ezer_ds,77:ezer_ys ezer_web,78:ezer_fa ezer_web,79:ezer_db ezer_web,80:ezer_ds ezer_web,81:ezer_ys ezer_ds ezer_web,82:ezer_fa ezer_ds ezer_web,83:ezer_db ezer_ds ezer_web' }
    show castka [,,40,] { data:da.castka, title:'výše', format:'rq*s' }
    show [,,70,] { data:da.dat_od, title:'přijetí', format:'rq*s' }
    show [,,70,] { data:da.dat_do, title:'potvrzení', format:'rq*s' }
    proc onrowclick() { form.load(id_d.get); idd.set(id_d.get); form_state('n|s','u|z'); }
  }
  # formulář pro úpravu darů
  # skryté položky
  field id_o { data:da.id_osoba }
  field access { data:da.access }
  field ukon { data:da.ukon }
  field del  { data:da.deleted }
  # viditelné položky
  const Lp=235
  field castka [Lp,29,65,] {data:da.castka, help:'výše členského příspěvku',
    format:'xr', title:'^částka'}
  field datum  [Lp,63,85,] {type:'date', data:da.dat_od, help:'datum přijetí příspěvku',
    format:'xRUr', title:'^dar přijat dne'}
  field platnost[Lp,98,85,] {type:'date', data:da.dat_do, help:'zaplaceno do',
    format:'xRUr', title:'^zaplaceno do ...'}
  field pozn   [Lp,134,130,] {data:da.note, help:'poznámka', format:'x', title:'^poznámka'}
  # ==> . tlačítka a obslužné procedury
  proc form_init() {
    b.browse_init; form.init; form_state('n','u|z|s'); }
  proc form_state(on,off) {
    form.enable(1,on); form.enable(0,off); }
  proc onchanged () {
    form_state('u|z','n|s') }
  label cmd [Lp,159,132,50] { css:'ae_parm', style:'z-index:1' }
  # -------------------------- Nový
  button [cmd.l+6,cmd.t+5,,] { tag:'n', title:'[fa-database] Nový',  help:'Vložit nový dar',
    style:'z-index:2'
    proc onclick() {
      form.init(2); form_state('u|z','n|s');
      id_o.set(ido.get); id_o.change(1); del.set(''); del.change(1);
      access.set(has_access.get); access.change(1);
      ukon.set('p'); ukon.change(1); castka.change(1);
      datum.set(now); datum.change(1);
  }}
  # -------------------------- Uložit
  button [cmd.l+63,cmd.t+5,,] {tag:'u', title:'[fa-save] Uložit',  help:'Uložit změněná data',
    style:'z-index:2'
    proc onclick() {
      form.same
    | // oprava
      form.key; form.save; form.load;
      [ b.browse_seek; form_state('n|s','u|z') ]
    | // přidání
      id_o.get;
      form.insert; form.load;
      b.raise('onrowclick',b.browse_seek(conc('id_dar=',form.key))); form_state('n|s','u|z');
#       [ osoby.b.browse_row | osoby.b.browse_init ]
    | error('chyba při vkládání příspěvku - nenastavený člen')
  }}
  # -------------------------- Zpět
  button [cmd.l+6,cmd.t+27,,] {tag:'z', title:'[fa-undo] Zpět',
    help:'Neměnit změněná data', style:'z-index:2'
    proc onclick() {
      b.browse_count; form.load(b.id_d.get); form_state('n|s','u|z')
    | form.init; form_state('n','u|z|s')
  }}
  # -------------------------- Smazat
  button [cmd.l+58,cmd.t+27,,] {tag:'s', title:'[fa-times fa-red] Vymazat',
    help:'Smazat tento příspěvek', style:'z-index:2'
    proc onclick() {
      confirm("opravdu smazat příspěvek ",b.castka.get," od ",jmeno.get,"?");
      del.set(conc('D ',sys('user','abbr'),' ',now)); del.change; form.save;
      refresh
  }}
}
# --------------------------------------------------------------------------------------==> ČLENSTVÍ
form _clenstvi [,,320,min_h+-180] {css:'ae_frame shift_down'
  proc onclick() {
    down(page.pri); down(page.dar); down(page.oso); down(page.rod); up(page.cle); down(page.map) }
  label frame [2,3,98,] { css:'frame_label_up', title:'přehled členství - '}
  label jmeno [96,3,160,] { css:'frame_label_up'}
  label ido   [264,3,50,] { format:'r' }
  label idd   [264,18,50,] { format:'r' }
  func dary(_ido,_jmeno) {
    form.init(); ido= _ido; jmeno= _jmeno;
    b.browse_load(`da.deleted='' AND ukon IN ('b','c','k','H','Y') AND id_osoba=${_ido}`);
  }
  proc refresh() { dary(id_o.get,jmeno.get) }
  # seznam darů
  view da: table dar
  view do: table osoba { join:"USING(id_osoba)" }
  browse b [5,17,,] { rows:8, qry_rows:1
    show id_d { data:da.id_dar }
    show id_o { data:do.id_osoba }
    show access { data:da.access }
    show ukon [,,10,] { data:da.ukon, title:'způsob', format:'q*s'
//      css_cell:'access,0:,1:ezer_ys,2:ezer_fa,3:ezer_db' }
      css_cell:'access,0:,1:ezer_ys,2:ezer_fa,3:ezer_db,64:ezer_ds,65:ezer_ys ezer_ds,66:ezer_fa ezer_ds,67:ezer_db ezer_ds,77:ezer_ys ezer_web,78:ezer_fa ezer_web,79:ezer_db ezer_web,80:ezer_ds ezer_web,81:ezer_ys ezer_ds ezer_web,82:ezer_fa ezer_ds ezer_web,83:ezer_db ezer_ds ezer_web' }
    show [,,65,] { data:da.dat_od, title:'vznik', format:'rq*s' }
    show [,,65,] { data:da.dat_do, title:'zánik', format:'rq*s' }
    func onrowclick() { form.load(id_d); idd= id_d; form_state('n|s','u|z'); }
  }
  # formulář pro úpravu členství
  # skryté položky
  field id_o { data:da.id_osoba }
  field access  { data:da.access }
  field ukon { data:da.ukon }
  field del  { data:da.deleted }
  # viditelné položky
  const Lc=180
  select typ   [Lc,27,90,] {type:'map', help:'typ členství', options:ms_akce_clen.zkratka
    title:'^typ členství'
    func onchanged() {
      switch(typ.key()){
        case '0': ukon= 'b'; break;
        case '1': ukon= 'c'; break;
        case '2': alert("účastníci nejsou evidováni jako členové YMCA"); break;
        case '3': ukon= 'k'; break;
        case '4': ukon= 'H'; break;
        case '5': ukon= 'Y'; break;
      }
      ukon.change(1) 
    }
  }
  field datum  [Lc,62,90,] {type:'date', data:da.dat_od, help:'datum přijetí',
    format:'RUr', title:'^člen ode dne' }
  field konec  [Lc,97,90,] {type:'date', data:da.dat_do, help:'datum zrušení',
    format:'RUr', title:'^platí do dne' }
  field pozn   [Lc,132,134,] {data:da.note, help:'poznámka', title:'^poznámka'}

  # ==> . tlačítka a obslužné procedury
  func form_init() {
    b.browse_init(); typ.plain(); form.init(); form_state('n','u|z|s'); }
  func form_state(on,off) {
    form.enable(1,on); form.enable(0,off); }
  func onchanged () {
    form_state('u|z','n|s') }
  func onload () { 
    typ.key(ukon=='b' ? 0 : (ukon=='c' ? 1 : (ukon=='H' ? 4 : (ukon=='Y' ? 5 : 3)))); 
    typ.plain() }
  label cmd [Lc,158,132,50] { css:'ae_parm' }
  # -------------------------- Nový
  button [cmd.l+6,cmd.t+5,,] { tag:'n', title:'[fa-database] Nový',  help:'Vložit nový dar',
    func onclick() {
      form.init(); form_state('u|z','n|s');
      id_o= ido; id_o.change(1); del= ''; del.change(1);
      access= has_access; access.change(1);
      typ.key(0); typ.change(); datum= now(); datum.change(1);
  }}
  # -------------------------- Uložit
  button [cmd.l+64,cmd.t+5,,] {tag:'u', title:'[fa-save] Uložit',  help:'Uložit změněná data',
    proc onclick() {
      form.same
    | // oprava
      form.key; form.save; form.load;
      [ b.browse_seek; form_state('n|s','u|z') ];
      [ b.browse_row | b.browse_init ]
    | // přidání
      id_o.get;
      form.insert; form.load;
      b.raise('onrowclick',b.browse_seek(conc('id_dar=',form.key))); form_state('n|s','u|z');
#       [ osoby.b.browse_row | osoby.b.browse_init ]
    | error('chyba při vkládání členství - nenastavený člen')
  }}
  # -------------------------- Zpět
  button [cmd.l+6,cmd.t+27,,] {tag:'z', title:'[fa-undo] Zpět',  help:'Neměnit změněná data',
    func onclick() {
      if (b.browse_count()) { form.load(b.id_d); form_state('n|s','u|z') }
      else { form.init(); form_state('n','u|z|s') }
  }}
  # -------------------------- Vymazat
  button [cmd.l+58,cmd.t+27,,] {tag:'s', title:'[fa-times fa-red] Vymazat',
    help:'Zcela zrušit (ne ukončit) členství',
    func onclick() {
      if (confirm(`opravdu zrušit (ne ukončit) členství ${jmeno}?`)) {
        del= conc('D ',sys('user','abbr'),' ',now()); del.change(); form.save();
        refresh()
      }
  }}
}
# --------------------------------------------------------------------------------------==> AKTIVITY
# jsou vidět všechny aktivity dané osoby ve všech organizacích,
# k přepnutí na akci musí být dostatečná funkční i datová oprávnění
form _aktivity [,,248,min_h-160] {css:'ae_frame shift_up'
  view a: table akce
  view p: table pobyt { join:'ON a.id_duakce=p.id_akce' }
  view s: table spolu { join:'USING(id_pobyt)' }
  label frame [2,3,96,] { css:'frame_label_up', title:'účasti na akcích - '}
  label jmeno [96,3,130,] { css:'frame_label_up' }
  field ido [186,0,50,] { format:'or', css:'Label' }
  browse akc [4,20,,] { rows:21, qry_rows:1, buf_rows:50, optimize:°{ask:'evid2_browse_act_ask'}
    show ida { data:a.id_duakce }
    show idp { data:p.id_pobyt }
    show ids { data:s.id_spolu }
    show fce { data:p.funkce }
    show org { data:a.access }
    show rok  [,, 30,] { title:'rok', format:'q*s-', help:"rok konání akce"
      css_cell:'org,0:,1:ezer_ys,2:ezer_fa,3:ezer_db,64:ezer_ds' }
    show akce [,,128,] { title:'název', format:'tq*s', help:"název akce" }
    show _vek [,, 20,] { title:'věk', help:"stáří v době konání akce" }
    show _fce [,, 30,] { title:'fce', help:"funkce na akci"
      data:p.funkce, map_pipe:ms_akce_funkce.zkratka, format:'q*s' }
    proc onsubmit() { na_akci }
    proc na_akci() {
      has_skill('yaa;faa;caa'); accessed(org.get);
      [ eq(fce.get,99);
        pece.akce_show(ida.get,ids.get,conc(akce.get,' (',rok.get,')'),'EVIDENCE')
      | ucast.akce_show(ida.get,idp.get,conc(akce.get,' (',rok.get,')'),'EVIDENCE')
    ] }
  }
  button [-40,2,,] { title:'^akce', css:'db_evidence', help:'též lze skočit dvojklikem na řádku s akcí', skill:'yaa;faa;caa'
    proc onclick() { akc.na_akci } }
  proc ucasti(_ido,_jmeno) {
    form.display(_ido); _ido; ido.set(_ido); jmeno.set(_jmeno);
    akc.browse_load(conc('a.spec=0 AND a.zruseno=0 AND s.id_osoba=',_ido),"datum_od DESC")
  }
}
# ------------------------------------------------------------------------------------------==> MAPA
//form _mapa [,,500,500] {css:'ae_frame shift_down'
form _mapa [,,650,500] {css:'ae_frame shift_down'
  var on=0,
      mode='evidence',  // evidence|skupiny|psc
      skup:object,
      padding= 10       // % okraje mezi zoom a polygonem
  func mapa_show(map_type) {
    if (!on) {
      cr.init('ROADMAP',{mapTypeControl:0,streetViewControl:0},map_type);
      on= 1; 
//      lst.bm_selected();
    }
  }
  proc onclick() {
    up(page.map); down(page.pri); down(page.dar); down(page.oso); down(page.rod); down(page.cle) }
  label cr [2,2,646,496] { type:'map'
    proc onmarkclick(mark) {
      { eq(mark.ezer,'skupiny'); //echo('skupiny');
        alert(skup.get(mark.id));
      | eq(mark.ezer,'psc'); //echo('psc')
      | lst.bm.raise('onrowclick',mark.id)
      };
    }
  }
  label [6,6,28,26] { css:'ae_parm', style:"z-index:1" }
  button op [10,10,,] { title:'[fa-navicon]'
    proc onclick() { mode.set('evidence'); }
    menu cm { type:'context', par:{trigger:'click'} # ==> . kontextové menu
      item { title:'vyber bydlící ve výřezu'
        func onclick() { select_in_map() }}
//        proc onclick() { lst.bm_select_in(cr.get_bounds) }}
      item { title:'vyber bydlící v ohraničení'
        func onclick() { var poly:text
          poly= cr.get('poly');
          if (poly) select_in_polygon(poly) 
          else alert("napřed v menu vytvoř ohraničující obdélník")
      }}
      item { title:'vypiš PSČ ležící v ohraničení'
        func onclick() { var poly:text, ret:object
          poly= cr.get('poly');
          if ( poly ) {
            ret= php.mapa2_psc_v_polygonu(poly);
            clipboard(ret.pscs); 
            warning(`vybraných ${ret.pocet} PSČ je ve schránce`)
          }
          else alert("napřed volbou 'zoom na ... okolí' vytvoř červený ohraničující čtverec")
      }}
      item { title:'-zruš pomocné obrazce'
        func onclick() { cr.set({rect:'',poly:''}) }}
      item { title:'zruš všechny body'
        func onclick() { cr.set({mark:''});}}
//      item { title:'zruš body ležící mimo výřez'
//        func onclick() { unselect_out_map(cr.get_bounds()) }}
//          lst.bm_unselect_out(cr.get_bounds());
//          function('m',"return evid_mapa_focus(m);",cr); }}
      item { title:'-zoom na malé okolí'
        func onclick() { zoom(eq(lst.mode,'r','f')?idr:ido,1) }}
      item { title:'zoom na na velké okolí', 
        func onclick() { zoom(eq(lst.mode,'r','f')?idr:ido,5) }}
      item { title:'zoom na všechny vybrané'
        proc onclick() { cr.fit_bounds }}
      item { title:'zoom na celou republiku'
        func onclick() { cr.set({zoom:"49.0,14.6;50.5,16.4"}) }}
      item { title:'vytvoř ohraničující obdélník (klik dovnitř zjemní ohraničení)'
        func onclick() { ohraniceni() }}
      item { title:'- aktuální chlapské skupiny'
        proc onclick() { var ret:object
          mode.set('skupiny');
          ret.set(ask('mapa2_skupiny'));
          [ ret.err; warning(ret.err) ];
          cr.set(object('mark',ret.mark,'ezer','skupiny'));
          skup.set(ret.clmns);
          [ ret.msg; alert(ret.msg) ];
      }}
      item { title:'zobrazení seznamu PSČ'
        proc onclick() { var lst:text, ret:object
          mode.set('psc');
          lst.set(prompt("napiš seznam PSČ"));
          ret.set(ask('mapa2_psc_list',lst));
          cr.set(object('mark',ret.mark,'ezer','psc'));
      }}
      item { title:'-použít mapu ČR od Mapy.cz'
        func onclick() { on= 0; mapa_show('smap') }}
      item { title:'... nebo mapu ČR od Google'
        func onclick() { on= 0; mapa_show('gmap') }}
    }
  }
  # ==> . označ adresu kroužkem
  func mapa_mark_curr(lat,ltg,psc) { var ll:array
    if (on) { 
      if (!lat && psc) {
        ll= php.select('lat,lng','psc_axy',`psc='${psc}'`); 
        if (ll && ll[0]) {
          lat= ll[0]; ltg= ll[1];
        }
        else warning(`PSČ ${psc} nemá definované souřadnice`)
      }
      cr= {clear:0,mark:`0,${lat},${ltg},,./ezer3.1/client/img/circle_blue_25x25.png,12`}; 
  }}
  # ==> . přidej nebo zruš značku adresy pro id
  func mapa_mark_add(id,lat,ltg,psc,add) { var ll:array, color:text
    if (on) { 
      if (!add) {
        cr.set_mark(id,'delete')
      }
      else {
        color= 'green';
        if (!lat && psc) {
          ll= php.select('lat,lng','psc_axy',`psc='${psc}'`); 
          if (ll && ll[0]) {lat= ll[0]; ltg= ll[1]; color= 'gold';}
          else warning(`PSČ ${psc} nemá definované souřadnice`)
        }
        cr= {clear:0,mark:`${id},${lat},${ltg},${id},./ezer3.1/client/img/circle_${color}_15x15.png,7`}; 
      }
  }}
//  proc mapa_curr(lat,lng) {
//    on.get; function('m','lat','lng',"evid_mapa_curr(m,lat,lng,'blue',12); return 1;",cr,lat,lng) }
//  # ==> . přidej osobu na mapu
//  proc mapa_add(geo) { on.get; cr.set(geo); }
//  # ==> . uber osobu z mapy
//  proc mapa_del(ido) { var mark:object
//    on.get;
//    mark.set(cr.get('id',ido));
//    mark; cr.set_mark(mark,'delete');
//  }
  # ==> . vyber v browse adresy v polygonu nebo ve výřezu či mimo něj
  // vybere v aktuálním browse osoby resp. rodiny s adresou ve výřezu
  func select_in_map() { var ret:object, keys:array
    if (on) {
      keys= lst.mode=='m' // maillist je v browse načtený vždy celý
          ? function('browse',"return browse.keys.toString();",lst.brow)
          : lst.brow.browse_keys();
      ret= php.mapa2_ve_ctverci(lst.mode,cr.get_bounds(),keys);
      if (ret.msg) { alert(ret.msg); return; }
      lst.brow.selected('clear');
      page.map.mapa_clear(0); // polyline nechat
      lst.brow.selected('set',ret.ids);
      page.map.cr.set({mark:ret.marks});
    }
  }
//  // zrušit výběr osob mimo výřez
//  func unselect_out_map(rect) { 
//    select_in_map(rect);
//  }
  // vybere v aktuálním browse osoby resp. rodiny s adresou v polygonu
  func select_in_polygon(poly) { var ret:object, keys:array
    if (on) {
      keys= lst.mode=='m' // maillist je v browse načtený vždy celý
          ? function('browse',"return browse.keys.toString();",lst.brow)
          : lst.brow.browse_keys();
      ret= php.mapa2_v_polygonu(lst.mode,poly,keys);
      if (ret.msg) { alert(ret.msg); return; }
      lst.brow.selected('clear');
      page.map.mapa_clear(0); // polyline nechat
      lst.brow.selected('set',ret.ids);
      page.map.cr.set({mark:ret.marks});
    }
  }
  # ==> . vymazání mapy
  proc mapa_clear(even_poly) { 
    on.get; 
    { even_poly; cr.set(°{zoom:'',rect:'',mark:'',poly:''}) 
    | cr.set(°{zoom:'',rect:'',mark:''}) 
    } 
  }
  func zoom (id,km) { var ret: object
    if (on) {
      ret= php.mapa2_ctverec(lst.mode,id,km,5);
      cr.set({zoom:'',rect:'',poly:''});
      cr.set({zoom:ret.rect});
//      cr.set({poly:ret.poly});
//      cr.option({poly_edit:1});
    }
  }
  func ohraniceni () { var rect:text, zoom:number
    if (on) { 
      rect= cr.get_bounds(1);
      cr.set({poly:rect});
      cr.option({poly_edit:1});
    }
  }
//  proc zoom (id,km) { var ret: object
//    on.get;
//    ret.set(ask('mapa2_ctverec',cconc(eq(lst.bm_komu.get,'o'),'osoba','rodina'),id,km,5));
//    cr.set(°{zoom:'',rect:'',poly:''});
//    cr.set(object('zoom',ret.rect));
//    cr.set(object('poly',ret.poly));
//    cr.option(object('poly_edit',1));
//  }
}
# =============================================================================================> GEO
panel GEO [0,0,400,110] {type:'popup', title:'hledání adresy v mapy.cz' 
  var ido: number,
      idr:number,
      geo:object
  // poznamená navrženou polohu jako správnou se stav=2
  func locate(_ido,_idr) { 
    ido= _ido;
    idr= _idr;
    fork.panel.mapy_cz();
    panel.modal();
  }
  func mapy_cz() {
    f.pat= ''; f.adr= ''; f.err= '';
    geo= ido ? php.geo_get_osoba(ido) : (idr ? php.geo_get_rodina(idr) : {error:'?'});
    f.pat= geo.address; f.adr= geo.full; f.err= geo.error;
  }
  use f: form {
    label [11,0,56,15] {title:'hledáme:' }
    label pat [75,0,312,25] {style:"border:1px solid #777" }
    label [11,32,55,16] {title:'nalezeno:' }
    label adr [74,32,312,25] {style:"border:1px solid #777" }
    label [11,70,55,16] {title:'problém:' }
    label err [74,70,312,25] {style:"color:red" }
    button [72,92,,] {title:'Ok - zapamatovat', 
      func onclick() {
        if (ido) php.geo_set_osoba(ido,geo); 
        if (idr) php.geo_set_rodina(idr,geo); 
        panel.hide(1);
    }}
  }
}
# ============================================================================================> _CMD
# změny v evidenci
form _cmd [,,706,32] { css:'ae_frame'
  # ---------------------------------------------- stav a inicializace
  var mode:text  // 'novy_clen' nebo 'nova_rodina' nebo ''
  proc start() { set_state('show'); }
  proc onclick() { form.set_css('shift_up','shift_down') }
  # ---------------------------------------------- zámky
  func evid_lock(on) { var lock:object, msg:text
    msg= '';
    if (ido) {
      lock= php.table_lock(on,'osoba',ido); 
      if ((on=='on') && (!lock.ok)) {
        if (page.last_upd=='o') alert(lock.info);
        msg= lock.info;
      }
    }
    if (idr) {
      lock= php.table_lock(on,'rodina',idr); 
      if ((on=='on') && (!lock.ok)) {
        if (page.last_upd=='r') alert(lock.info);
        msg= `${msg} ${lock.info}`;
      }
    }
    if (msg) {
      page.refresh();
      echo(`${msg} upd=${page.last_upd} oso=${page.oso.same()} rod=${page.rod.same()}`);
    }
  }
  # ---------------------------------------------- pomocné procedury
  proc is_chng() { eq(mode.get,'show'); set_state('edit'); evid_lock('on') }
  proc set_state(_mode) {
    mode.set(_mode);
    switch(mode.get,
    'newx',{ # nová samostatná osoba
             m.enable(0); form.enable(1,'s'); form.enable(0,'x'); lst.bo.enable(0); lst.br.enable(0);
             page.oso.init; page.rod.t.init;
             page.cleni.enable(0); page.rod.enable(0,'r|R'); },
    'newo',{ # nový člen rodiny
             m.enable(0); form.enable(1,'s'); form.enable(0,'x'); lst.bo.enable(0); lst.br.enable(0);
             page.oso.init; page.rod.t.init;
             page.cleni.enable(0); page.rod.enable(0,'r|R'); },
    'newr',{ # nová rodina s nastaveným členem
             m.enable(0); form.enable(1,'s'); form.enable(0,'x'); lst.bo.enable(0); lst.br.enable(0);
             page.oso.enable(0,'.*'); page.rod.t.init; page.rod.init;
             page.display(0,'Cb'); page.cleni.enable(0);
             page.rod.display(0,'R'); page.rod.display(1,'r|n'); page.rod.enable(1,'.*'); },
    'newro',{ # nová rodina s novým členem
             m.enable(0); form.enable(1,'s'); form.enable(0,'x'); lst.bo.enable(0); lst.br.enable(0);
             page.oso.init; page.rod.t.init; page.rod.init;
             page.display(0,'Cb'); page.cleni.enable(0);
             page.rod.display(0,'R'); page.rod.display(1,'r|n'); },
    'edit',{ # editace údajů rodiny nebo/i člena
             m.enable(0); form.enable(1,'s'); form.enable(0,'x'); lst.bo.enable(0); lst.br.enable(0);
             page.cleni.enable(0); page.rod.rodiny.enable(0); },
    'show',{ # jen zabrazení
             m.enable(1); form.enable(0,'s'); form.enable(1,'x'); page.oso.enable(1,'.*');
             lst.bo.enable(1); lst.br.enable(1);
             page.display(1,'C'); page.cleni.enable(1); page.rod.enable(1,'r|R'); }
    );
  }
  proc reload(depth) { // depth ... hloubka znovunačtení
    set_state('show'); page.page_plain; warning;
    page.rod.display(1,'a'); page.rod.display(0,'m|n');
    switch(depth,
    'curr',{ [ page.oso.key_tvori.get;
               page.cleni.cleni_click(page.oso.key); page.rod.r.load(idr.get)
             | page.osoba_load
             ] },
    'page',{ page.refresh },
    'lst', { lst.refresh });
  }
  // ==> ... zpět
  label  [0,0,136,30] { css:'ae_parm', skill:'yaa+;faa+;caa+' }
  button [7,6,,] {tag:'s', type:'html', title:'[fa-undo] Zpět',  help:'Neměnit změněná data'// , skill:'yaa+|yaa+;faa+|faa+;fae+|fae+;caa+|caa+'
    proc onclick() { reload('curr'); evid_lock('off'); } }
  // ==> ... uložit změny
  button [64,6,66,] {tag:'s', type:'html', title:'[fa-save] Uložit',  help:'Uložit změněná data', skill:'yaa+|yaa+;faa+|faa+;fae+|fae+;caa+|caa+'
    func onclick() { var depth:text
      switch (mode) {
      case 'newx':  # -------------- vytvoření nové samostatné rodiny
        page.oso.insert();
        reload('page');
        break;
      case 'newo':  # -------------- vytvoření nového člena rodiny: osoba+tvori
        page.oso.insert();
        tvori.insert_record({id_osoba:page.oso.key(),id_rodina:idr,role:page.rod.t_role});
        reload('page');
        break;
      case 'newro': # -------------- vytvoření rodiny a prvního člena
        page.rod.r.insert(); page.oso.insert();
        tvori.insert_record({id_osoba:page.oso.key(),id_rodina:page.rod.r.key(),role:page.rod.t_role});
        reload('lst');
        break;
      case 'newr': # --------------- vytvoření rodiny a připojení nastaveného člena
        page.rod.r.insert();
        tvori.insert_record({id_osoba:page.oso.key(),id_rodina:page.rod.r.key(),role:page.rod.t_role});
        reload('lst');
        break;
      case 'edit': # --------------- oprava
        depth= 'curr';
        if (!page.oso.same()) {
          if (page.oso.jmeno.changed() || page.oso.prijmeni.changed() || page.oso.narozeni.changed())
            depth= 'page';
//          page.oso.save()
          page.oso.save_osoba();
        }
        if (!page.rod.same()) {
          page.rod.r.save();
          if (page.rod.t_role.changed()) {
            php.akce2_save_role(page.rod.t.key(),page.rod.t_role);
            depth= 'page';
          }
        };
        evid_lock('off');
        reload(depth);
        break;
      default: error("Uložit co?")
      }
    }
  }
  # RODINA
  label [144,9,80,] { title:"<b>Rodina:</b>", skill:'yaa;faa;caa' }
  // ==> ... rodina: nová, vymazat
  button [188,6,,] {tag:'x', type:'html', title:'[fa-heart] Nová rodina',
    skill:'yaa+|yaa+;faa+|faa+;caa+|caa+', help:'Vytvoření nové rodiny s prvním členem'
    proc onclick() { var jmeno:text
      // ochrana před vytvářením s access=3
      not(eq(has_access.get,3));
      // buďto úplně nová s novým členem, nebo nová s nastaveným členem
      jmeno.set(conc(page.oso.prijmeni.get," ",page.oso.jmeno.get));
      switch(choose(conc("Mám vytvořit novou rodinu s novým členem, <br>nebo s ",jmeno,"?"),
          conc("nový člen:2,",jmeno,":1,Zpět:0")),
      0,{ stop },
      1,{ set_state('newr');
          page.rod.access.set(has_access.get); foreach(page.rod.tagged('N'),change_x); },
      2,{ set_state('newro');
          page.rod.access.set(has_access.get); foreach(page.rod.tagged('N'),change_x);
          page.oso.access.set(has_access.get); foreach(page.oso.tagged('I'),change_x); }
      )
    | eq(has_access.get,3);
      alert("Ve společném Answeru prosím vkládej rodiny a osoby na kartě Účastníci");
  }}
  // vymazat
  button r_smaz [287,6,,] {tag:'x', title:'[fa-times fa-red] Vymazat', skill:'yaa+|yaa+;faa+|faa+;caa+|caa+',
    help:'Vymazání informací o rodině z evidence', format:'d'
    proc onclick() { var ret:object
      ret.set(ask('evid2_delete',0,idr.get,'conf_rod'));
      { eq(ret.ok,1);                           // rodinu lze smazat
        [ confirm(ret.html);
          ret.set(ask('evid2_delete',0,idr.get,'del_rod')); warning(ret.html);
          reload('page');
        ]
      | alert(ret.html) }                       // nastal problém
  } }
  // obnovit
  button r_obnov [287,6,,] {tag:'x', title:'[fa-plus fa-red] Obnovit', skill:'yaa+|yaa+;faa+|faa+;caa+|caa+',
    help:'Obnovení informací o smazané rodině', format:'n'
    proc onclick() { var ret:object
      ret.set(ask('evid2_delete',0,idr.get,'undel_rod'));
      warning(ret.html); reload('lst')
  }}
  # ČLENI
  label [364,9,80,] { title:"<b>Osoba:</b>", skill:'yaa;faa;caa' }
  // ==> ... člen: nový, přidat, vyjmout
  proc change_x(elem) { elem.change(1) }
  button novy_clen [404,6,,] {tag:'x', type:'html', title:'[fa-heart-o] Nový člen',
    skill:'yaa+|yaa+;faa+|faa+;caa+|caa+', help:'Vytvoření nové osoby a její zapojení do rodiny'
    func onclick() { var _sex:number, _prijmeni:text, elem:ezer
      if (has_access==3) { // ochrana před vytvářením s access=3
        alert("Ve společném Answeru prosím vkládej rodiny a osoby na kartě Účastníci");
        return
      }
      switch (choose("Má se vytvořit nový člen zobrazené rodiny nebo samostatná osoba",
            "nový člen:clen,samostatná osoba:osoba,nic z toho:zpet")) {
      case 'zpet':
        break;
      case 'osoba':
        set_state('newx');
        page.oso.access= has_access;
        for (elem of page.oso.tagged('I')) {
          elem.change(1); 
        }
        break;
      case 'clen':
        _sex= page.oso.sex; _prijmeni= page.oso.prijmeni;
        set_state('newo');
        page.oso.sex= _sex; page.oso.prijmeni= _prijmeni; page.oso.narozeni= now();
        page.oso.access= has_access;
        page.rod.t_role= 'd';
        for (elem of page.oso.tagged('I')) {
          elem.change(1); 
        }
        page.rod.t_role.change(1);
        break;
      }
    } 
  }
  // PŘIDAT
  button [490,6,,] {tag:'x', title:'[fa-plug] Přidat', skill:'yaa+|yaa+;faa+|faa+;caa+|caa+',
    help:'Výběr osoby z evidovaných a jeho připojení k rodině', format:'d'
    proc onclick() { var ret:object, bo: text, bh:text, br:text, bf:text
      bo.set(lst.bo.selected('get')); bh.set(lst.bh.selected('get'));
      br.set(lst.br.selected('get')); bf.set(lst.bf.selected('get'));
      ret.set(ask('evid2_spoj_osoba_rodina',bo,bh,br,bf,0));
      ret.ok; confirm(ret.msg);
      ask('evid2_spoj_osoba_rodina',bo,bh,br,bf,1);
      lst.bo.selected('clear'); lst.bh.selected('clear');
      lst.br.selected('clear'); lst.bf.selected('clear')
    | PridaniJmenem1.modal(240,50)
  } }
  // VYJMOUT
  button [554,6,,] {tag:'x', title:'[fa-plug fa-rotate-180 fa-red] Vyjmout',
    skill:'yaa+|yaa+;faa+|faa+;caa+|caa+', help:'Vyjmutí osoby z rodiny', format:'d'
    proc onclick() { var ret:object
      ret.set(ask('evid2_delete',ido.get,idr.get,'conf_mem'));
      { eq(ret.ok,1);                           // osobu lze vyjmout
        [ confirm(ret.html);
          ret.set(ask('evid2_delete',ido.get,idr.get,'del_mem')); warning(ret.html);
          // obnovíme page nebo lst (když byl smazán reprezentant rodiny)
          { eq(lst.mode.get,'r'); ido.set(0); reload('page')
          | eq(ido.get,page.ido0.get); ido.set(0); reload('lst')
          | reload('page')
          } ]
      | eq(ret.ok,2);                           // nutno (a lze) smazat i s rodinou
        [ confirm(ret.html);
          ret.set(ask('evid2_delete',ido.get,idr.get,'del_mem')); warning(ret.html);
          ret.set(ask('evid2_delete',ido.get,idr.get,'del_rod')); warning(ret.html);
          ido.set(0); idr.set(0); reload('lst') ]
      | alert(ret.html) }                       // nastal problém
  } }
  // ==> ... osoba: vymazat, obnovit
  button o_smaz [630,6,,] {tag:'x', title:'[fa-times fa-red] Vymazat', skill:'yaa+|yaa+;faa+|faa+;caa+|caa+',
    help:'Vymazání informací o osobě nebo jednočlenné rodině z evidence', format:'d'
    proc onclick() { var ret:object
      ret.set(ask('evid2_delete',ido.get,idr.get,'conf_oso'));
      { eq(ret.ok,1);                           // osobu lze smazat
        [ confirm(ret.html);
          ret.set(ask('evid2_delete',ido.get,idr.get,'del_oso')); warning(ret.html);
          // obnovíme page nebo lst (když byl smazán reprezentant rodiny)
          { eq(lst.mode.get,'r'); ido.set(0); reload('page')
          | eq(ido.get,page.ido0.get); ido.set(0); reload('lst')
          | reload('page')
          } ]
      | eq(ret.ok,2);                           // nutno (a lze) smazat i s rodinou
        [ confirm(ret.html);
          ret.set(ask('evid2_delete',ido.get,idr.get,'del_rod')); warning(ret.html);
          ido.set(0); idr.set(0); reload('lst') ]
      | alert(ret.html) }                       // nastal problém
  } }
  // OBNOVIT
  button o_obnov [630,6,,] {tag:'x', title:'[fa-plus fa-red] Obnovit', skill:'yaa+|yaa+;faa+|faa+;caa+|caa+',
    help:'Obnovení informací o smazané osobě', format:'n'
    proc onclick() { var ret:object
      ret.set(ask('evid2_delete',ido.get,idr.get,'undel_oso'));
      warning(ret.html); reload('lst')
  }}
}
# ================================================================================> Přidání jménem 1
var pall: object
proc rodina_add(p,i) {  // používá panel PridaniJmenem1
  var ret: object
  p.chck.get;
  pall.set(p.all.get);
  ret.set(ask('evid2_pridej_k_rodine',idr.get,pall.get));
  { ret.ok; page.evid_load(pall.get('id'),idr.get);
  | warning(ret.msg) }
}
panel PridaniJmenem1 [,,320,280] {type:'popup', title:'Přidání osoby do rodiny'
  use fl: form _fl [12,10,,]
  proc onfocus() { fl.jmena.focus }
  form _fl [,,300,200] {
    var last: number
    var rod: object
    var popis: text
    button [0,250,,] { title:'přidat k nastavené rodině', proc onclick() {
      each(L,rodina_add);
      panel.hide(0)
    } }
    button [160,250,,] { title:'jiná jména', proc onclick() {
      jmena.init; vse.init; L.init } }
    button [240,250,,] { title:'konec', proc onclick() {
      jmena.init; vse.init; L.init; panel.hide(0) } }
    // výběr z databáze
    select jmena [73,1,180,17] { type:'auto', format:'t'
        par:°{fce:'akce2_auto_jmena1',patt:'whole',deti:0,save:'key_only'}
      proc onchanged() {
        rod.set(ask('akce2_auto_jmena1L',this.key));
        each(rod.get,jmenovci); // nesmí volat asynchronní operace
      }
    }
    check deti [258,0,46,] { title:"i děti", value:'0', format:'t'
      proc onchange() { jmena.add_attrib('par.deti',this.get)
    }}
    proc jmenovci(p,i) {
      last.set(L.add);
      popis.set(p.nazev);
      L.part(last.get).part('info').set(popis.get);
      L.part(last.get).part('all').set(p);
    }
    // nastavení výběru
    check vse [4,0,80,16] { title:'všichni', format:'t'
      proc onchange() { each(L,nastav) }
      proc nastav (x,i) { x.chck.set(form.vse.get) }
    }
    list L [0,23,300,200] {rows:22      // anonymní group
      check chck [4,0,50,16] { title:' ', format:'t', value:'1' /*, proc onchange() { echo('jo/ne') }*/ }
      label info [23,4,300,16] {style:'white-space:nowrap', format:'t'}
      var all: object
    }
  }
}
# ======================================================================================> filtr_osob
panel filtr_osob [,,250,200] { type:'popup', title:"doplňující podmínky výběru osob"
  var sql='', having='', filtr='' // filtr je hodnota pro bo.filtr.expr
  func filtr_set() { var vycet:text, IN:text
    filtr= '';
    having= '';
    sql= cconc(1,''
      ,f.mrop,    ` AND o.iniciace!=0`
      ,f.firm,    ` AND o.firming!=0`
      ,f.aktiv,   ` AND o.aktivita RLIKE '.*${f.aktiv}.*' `
      ,f.jazyk,   ` AND o.jazyk RLIKE '.*${f.jazyk}.*' `
      ,f.zajmy,   ` AND o.zajmy RLIKE '.*${f.zajmy}.*' `
      ,f.pozn,    ` AND o.note RLIKE '.*${f.pozn}.*' `
      ,f.zamest,  ` AND o.zamest RLIKE '.*${f.zamest}.*' `
      ,f.slovaci, ` AND (o.stat='SK' OR IF(ISNULL(of.id_rodina),0,of.stat='SK')) `
      ,f.cizinci, ` AND (o.stat!='' AND o.stat!='SK' AND o.stat!='CZ' 
           OR IF(ISNULL(of.id_rodina),0,of.stat!='' AND of.stat!='SK' AND of.stat!='CZ')) `
      // členství a dárcovství
      ,f.bcleni,  ` AND od.ukon IN ('b') AND dat_do='0000-00-00' `
      ,f.ccleni,  ` AND od.ukon IN ('c') AND dat_do='0000-00-00' `
      ,f.darci,   ` AND od.ukon IN ('d') AND YEAR(dat_od)>=YEAR(NOW())-1 `
      ,f.klub,    ` AND od.ukon IN ('k') AND dat_do='0000-00-00' `
      // podle věku
      ,f.vek_od+f.vek_do, ` AND TIMESTAMPDIFF(YEAR,o.narozeni,NOW()) BETWEEN ${f.vek_od} AND ${f.vek_do}`
      );
    lst.o_filtr.set_attrib('help',cconc(1,"... "
    ,f.mrop,        " a iniciovaní"
    ,f.firm,        " a firming"
    ,f.elder,       " a starší"
    ,f.neelder,     " a nebyl starší"
    ,f.team,        " a v týmu"
    ,f.neteam,      " a nebyl v týmu"
    ,f.zajmy,       " a jsou aktivní"
    ,f.jazyk,       " a znají jazyk"
    ,f.zajmy,       " a mají zájmy"
    ,f.zamest,      " a zaměstnání"
    ,f.pozn,        " s poznámkou"
    ,f.slovaci,     " a Slováci"
    ,f.cizinci,     " a cizinci"
    ,f.umi,         f.ne_umi ? " a neumí" : " a umí"
//    ,f.slouzi,      " a slouží"
    ,f.bcleni,      " a jsou běžnými členy"
    ,f.ccleni,      " a jsou činnými členy"
    ,f.darci,       " a dali vloni či letos dar"
    ,f.klub,        " a jsou v klubu přátel"
    ,f.vek_od+f.vek_do, " a jsou určitého věku"
    ));
    // přidáme podmínky vkládané do HAVING
    if (f.umi.key()) {
      // osobní případně pro manžele i rodinné schopnosti
      filtr= ` MAX((FIND_IN_SET('${f.umi.key()}',o.o_umi)
           OR ot.role IN ('a','b') AND IF(ISNULL(of.id_rodina),0,FIND_IN_SET('${f.umi.key()}',of.r_umi))))`;
      having= f.ne_umi ? " AND filtr=0" : " AND filtr>0";
    }
    // přidáme speciální filtry, řešené výčtem
    if (f.elder || f.neelder) {
      IN= f.elder ? 'IN' : 'NOT IN';
      vycet= php.select('GROUP_CONCAT(DISTINCT id_osoba)',
            `akce JOIN pobyt ON id_akce=id_duakce JOIN spolu AS s USING (id_pobyt)`,
            `mrop=1 AND funkce IN (1,2)`);
      sql= `${sql} AND o.id_osoba ${IN} (${vycet}) `;
    }
    if (f.team || f.neteam) {
      IN= f.team ? 'IN' : 'NOT IN';
      vycet= php.select('GROUP_CONCAT(DISTINCT id_osoba)',
            `akce JOIN pobyt ON id_akce=id_duakce JOIN spolu AS s USING (id_pobyt)`,
            `(mrop=1 AND funkce IN (3,4,5,12) OR nazev='MROP tým') `);
      sql= `${sql} AND o.id_osoba ${IN} (${vycet}) `;
    }
  }
  func filtr_clear() { sql= ''; lst.o_filtr.set_attrib('help',"bez filtru") }
  use f: form _f
  form _f {
    // vlevo
    field aktiv   [53,0,79,]   { title:'aktivita',    format:'t' }
    field jazyk   [53,25,79,]  { title:'jazyky',      format:'t' }
    field zajmy   [53,50,79,]  { title:'zájmy',       format:'t' }
    field zamest  [53,75,79,]  { title:'zaměst.',     format:'t' }
    field pozn    [53,100,79,] { title:'poznámka',    format:'t' }
    check ne_umi  [50,123,20,17]  { title:' ', format:'t', help:'negace schopnosti', style:'z-index:3'}
    select umi    [68,125,74,] { type:'map0', options:ms_umi.hodnota, title:'schopnosti     ',
                                 value:'0', format:'t', help:"zohledňuje i rodiný um" }
    field vek_od  [53,150,25,]  { title:'věk od',      format:'tr' }
    field vek_do  [108,150,25,] { title:'do',          format:'tr' }
//    select slouzi [53,150,99,] { type:'map0', options:ms_umi.hodnota, title:'služby',
//                                 value:'0', format:'t', help:"zohledňuje i rodinou službu" }
    // vpravo
    check mrop    [160,1,95,17]  { title:'iniciovaní', format:'t', skill:'f|f;y|f;c|f' }
    check firm    [160,16,95,17] { title:'firming', format:'t', skill:'f|f;y|f;c|f' }
    check team    [160,31,95,17] { title:'v týmu', format:'t', skill:'f|f;y|f;c|f' }
    check neteam  [206,31,95,17] { title:'nebyl', format:'t', skill:'f|f;y|f;c|f' }
    check elder   [160,46,95,17] { title:'elder', format:'t', skill:'f|f;y|f;c|f' }
    check neelder [206,46,95,17] { title:'nebyl', format:'t', skill:'f|f;y|f;c|f' }
    check slovaci [160,70,95,17] { title:'Slováci', format:'t', help:"zohledňuje i rodinnou adresu" }
    check cizinci [160,87,95,17] { title:'jiní cizinci', format:'t', help:"zohledňuje i rodinnou adresu" }
    check bcleni  [160,115,95,17] { title:'běžní členové', format:'t', help:"nezrušení běžní členové" }
    check ccleni  [160,132,95,17] { title:'činní členové', format:'t', help:"nezrušení činní členové" }
    check darci   [160,149,47,17] { title:'dárci', format:'t', help:"letošní as loňští dárci" }
    check klub    [206,149,47,17] { title:'klub', format:'t', help:"jsou v klubu přátel" }
    // ukončení
    button [57,181,,] { title:'[fa-filter] filtruj', proc onclick() { filtr_set; panel.hide(1) }}
    button [137,181,,] { title:'[fa-undo] zruš filtr', proc onclick() { filtr_clear; panel.hide(0) }}
  }
}
# =====================================================================================> filtr_rodin
panel filtr_rodin [,,250,175] { type:'popup', title:"doplňující podmínky výběru rodin"
  var sql=''
  proc filtr_set() { sql.set(cconc(1,''
    ,f.pozn.get, conc(" AND r.note RLIKE '.*",  f.pozn.get,".*'")
    ,f.slovaci.get," AND r.stat='SK'"
    ,f.cizinci.get," AND r.stat!='' AND r.stat!='SK' AND r.stat!='CZ'"
    ,f.umi.key,conc(" AND FIND_IN_SET('",f.umi.key,"',r.r_umi)")
    ));
    lst.r_filtr.set_attrib('help',cconc(1,"... "
    ,f.pozn.get,        " s poznámkou"
    ,f.slovaci.get,     " a Slováci"
    ,f.cizinci.get,     " a cizinci"
    ,f.umi.get,         " a umí"
    ));
  }
  proc filtr_clear() { sql.set(''); lst.r_filtr.set_attrib('help',"bez filtru") }
  use f: form _f
  form _f {
    // vlevo
    field pozn    [53,0,79,17] { title:'poznámka',    format:'t' }
    select umi    [53,25,99,]  { type:'map0', options:ms_umi.hodnota,
                                   title:'schopnosti:', value:'0', format:'t' }
    // vpravo
    check slovaci [171,1,76,17] { title:'Slováci', format:'t'}
    check cizinci [171,26,76,17] { title:'jiní cizinci', format:'t' }
    // ukončení
    button [57,156,,] { title:'filtruj', proc onclick() { filtr_set; panel.hide(1) }}
    button [137,156,,] { title:'zruš filtr', proc onclick() { filtr_clear; panel.hide(0) }}
  }
}
# -------------------------------------------------------==> reakce na změny rozměrů okna prohlížeče
var  curr_h: number     // výška nad základní výšku
proc onresize(w,h) { // echo('evid2 onresize(',w,',',h,')');
  eq(curr_h.get,minus(h,min_h.get))                     // změnu šířky ignorujeme
| [ and(lt(h,min_h.get),not(eq(curr_h.get,min_h.get)));
    curr_h.set(0); h_change_all();
    menu_item.get;
#     m.onclick(menu_item.get);
    m.menu_click(menu_item.get,ido.get,idr.get);
  | and(gt(h,min_h.get),not(eq(h,sum(curr_h.get,min_h.get))));
    curr_h.set(minus(h,min_h.get)); h_change_all();
    menu_item.get;
#     m.onclick(menu_item.get);
    m.menu_click(menu_item.get,ido.get,idr.get);
  ];
}
# změna výšky oproti základní min_h
proc h_change_all() {
  cmd.property(object('down',curr_h.get));              // posunutí proti původnímu - omezeno na min_h
  page.frame.property(object('stretch',curr_h.get));    // protažení proti původnímu
  h_change_b;
}
# změna počtu řádků browse oproti aktuálním řádkům
proc h_change_b() { var r1:number, r2:number, h:number
  h.set(sys('screen','height'));
  r1.set(divide(sum(h.get,-147),18));
  r2.set(divide(sum(h.get,-230),18));
  not(eq(r1,lst.br.rows));
  r1.set(max(5,r1)); r2.set(max(3,r2));
  lst.br.set_attrib('rows',r1);
  lst.bo.set_attrib('rows',r1);
  lst.bm.set_attrib('rows',r1);
  lst.bh.set_attrib('rows',r1);
  page.act.akc.set_attrib('rows',r2);
  page.act.property(object('height',sum(72,multiply(r2,18))));
}