# ------------------------------------------------------------------------------------------------ #
# Modul pro prohlížení a upload fotografií                                                         #
#                                                                                                  #
# Ans(w)er/Ezer                                          (c) 2014 Martin Šmídek <martin@smidek.eu> #
# ------------------------------------------------------------------------------------------------ #

form _alb [,,600,357] {
  var n: number, tab:text, rec:number   // pořadí aktuální fotky (z tabulky tab a záznamu rec)

  # zobrazí fotky z daného záznamu dané tabulky
  proc galery(_tab,_rec) {
    tab.set(_tab); rec.set(_rec); n.set(-1);
    rec.get; has_skill("yaa+;faa+;caa+");
    // lze prohlížet, mazat, přidávat
    drop_galery;
    drop_init
  // bez oprávnění lze jen prohlížet
  | rec.get; not(has_skill("yaa+;faa+;caa+"));
    drop_galery
  }
  # posunování a mazání
  button go_left [0,4,,] { type:'html', title:'[fa-chevron-left fa-2x]'
    proc onclick() { n.set(sum(n.get,-1)); drop_galery }}
  button go_right [35,4,,] { type:'html', title:'[fa-chevron-right fa-2x]'
    proc onclick() { n.set(sum(n.get,1)); drop_galery }}
  button smazat [469,4,,] { type:'html', title:'[fa-times fa-2x fa-red]', format:'d'
    proc onclick() { var ret:object
      confirm("Opravdu odstranit tuto fotografii?");
      { ret.set(ask('foto2_delete',tab.get,rec.get,n.get));
        ret.ok;
        n.set(ret.n);
        drop_galery;
      | warning("fotografii nebylo možné smazat");
      }
  }}
  # layout
  label drop  [70,6,393,18] { format:'c' style:'padding-top:8px' }
  label jmeno [0,41,500,400] { format:'c' }
  label foto  [0,61,500,400] { format:'c' }
  proc ondrop(fileinfo) { var name: text
    drop.set_css('','drop_area drop_area_hover');
    name.set(ask('foto2_add',tab.get,rec.get,fileinfo));
    n.set(-1); drop_galery;
    drop.set_css('','drop_area drop_area_hover drop_area_run');
    drop_init;
  }
  proc drop_init() {
    drop.set_css('drop_area','');
    drop.set("Sem můžeš přidat fotografii přetažením myší ze složky svého počítače");
    form.file_drop(drop,°{css_hover:'drop_area_hover',css_run:'drop_area_run',handler:'ondrop',
      max_width:1200,max_height:960});
  }
  # zobrazí n-tou fotku
  proc drop_galery() { var ret:object
    ret.set(ask('foto2_get',tab.get,rec.get,n.get,foto._w,foto._h));
    go_left.enable(ret.left);
    n.set(sum(ret.left,1));
    go_right.enable(ret.right);
    foto.set(ret.html);
    jmeno.set(ret.jmeno);
    smazat.enable(jmeno.get);
  }
}
