# ------------------------------------------------------------------------------------------------ #
# Modul pro prohlížení a upload fotografií                                                         #
#                                                                                                  #
# Ans(w)er/Ezer                                          (c) 2014 Martin Šmídek <martin@smidek.eu> #
# ------------------------------------------------------------------------------------------------ #

form _alb [,,600,357] {
  var n: number, tab:text, rec:number, nazev:text  // pořadí aktuální fotky (z tabulky tab a záznamu rec)

  # zobrazí fotky z daného záznamu dané tabulky
  proc galery(_tab,_rec) {
    tab.set(_tab); rec.set(_rec); n.set(-1);
    rec.get; has_skill("yaa+;faa+;caa+");
    // lze prohlížet, mazat, přidávat
    drop_galery;
  // bez oprávnění lze jen prohlížet
  | rec.get; not(has_skill("yaa+;faa+;caa+"));
    drop_galery
  }
  # posunování a mazání
  button go_left [0,4,,] { type:'html', title:'[fa-chevron-left fa-2x]'
    proc onclick() { n.set(sum(n.get,-1)); drop_galery }}
  button go_right [35,4,,] { type:'html', title:'[fa-chevron-right fa-2x]'
    proc onclick() { n.set(sum(n.get,1)); drop_galery }}
  button smazat [469,4,,] { type:'html', title:'[fa-times fa-2x fa-red]', format:'d'
    func onclick() { var ret:object
      ret= php.foto2_get(tab,rec,n,foto._w,foto._h);
      if (!ret.nazev) { alert(`problém s fotografí - nelze odstranit`); return; }
      if (confirm(`Opravdu odstranit fotografii '${ret.nazev}'?`)) {
        ret= php.foto2_delete(tab,rec,n);
        if (ret.ok) {
          n= ret.n;
          drop_galery();
        }
        else
          warning("fotografii nebylo možné smazat");
      }
  }}
  button otocit [430,4,,] { type:'html', title:'[fa-rotate-left fa-2x]', help:'otoč 90° doleva'
    proc onclick() { var ret:object
      ask('foto2_rotate',nazev.get,90);
      drop_galery;
    | warning("fotografii nebylo možné otočit");
  }}
  # zobrazení a vkládání fotek
  label jmeno [0,41,500,400] { format:'c' }
  label foto  [0,61,500,400] { format:'c' }
  label drop  [70,6,351,18] { type:'drop' format:'c' style:'padding-top:8px' 
      title:"Sem můžeš přidat fotografii přetažením myší ze složky počítače"
    proc onstart() { drop.init('','U:','',°{utf8:0,list:0,mime:'img'}) } // převod na ASCII, bez listingu
    func ondrop(f:object) { var name: text, fsize:number
      function("Ezer.options.path_files_u=Ezer.options.path_foto;return 1;"); 
      name= php.utf2ascii(f.name,'.');
      name= `${sys('root')}_${tab}_${rec}.${name}`;
      n= php.foto2_add(tab,rec,name);
      if (!n) { alert("duplicitní název fotky, změň jej prosím"); return 0; }
      echo('DROP ',f.name,' aka ',name); 
      fsize= this.resample(f,1920,1920);
      echo('RESAMPLED ',f.size,' -> ',fsize); 
      return(name); 
    }
    proc onload(f) { 
      echo('LOAD ',f.name); 
      drop_galery; 
    } 
    proc onerror(e) { // funkce onerror nesmí obsahovat asynchronní kód a lokální proměnné, ale přes call lze zavolat už cokoliv
      warning(e.msg); return(1) // vrácení 0 způsobí standardní hlášení chyby
    }
  }
  # zobrazí n-tou fotku
  proc drop_galery() { var ret:object
    ret.set(ask('foto2_get',tab.get,rec.get,n.get,foto._w,foto._h));
    go_left.enable(ret.left);
    n.set(sum(ret.left,1));
    go_right.enable(ret.right);
    foto.set(ret.html);
    jmeno.set(ret.jmeno);
    nazev.set(ret.nazev);
    smazat.enable(jmeno.get);
  }
}
