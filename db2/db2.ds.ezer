#pragma library
# Systém Ans(w)er - modul Dům
# (c) 2008-2010 Martin Šmídek <martin@smidek.eu>

  proc the_formsave (f,b) {
    f.same
  | f.key; f.save; f.load; b.browse_seek
  | f.insert; f.load;
    b.raise('onrowclick',b.browse_seek(conc(f.key_id,'=',f.key)))
  }

# ======================================================================================> OBJEDNÁVKY
panel dum {type:'right', title:"Objednávky", _sys:'obj',include:'onload,db2.ds.dum' } // musí být onload aby se natáhla knihovna
panel dum2 {type:'right', title:"Objednávky2", _sys:'dum2',include:'onload,db2.ds.dum2' skill:'a;yd2'} 
panel obs2 {type:'right', title:"Obsazenost2", _sys:'obs2',include:'onload,db2.ds.obs2' skill:'a;yd2'} 
# ===========================================================================================> HOSTÉ
panel kl {type:'right', title:"Hosté", _sys:'kli', include:'onload,db2.ds.kl' }
# ============================================================================================> CENY
var rok: number
panel cen {type:'right', title:'Ceny'
  func onfirstfocus() { var letos:ezer
    // vytvoř itemy pro 7 posledních let a klikni na letošní
    create_menu();
    letos= m.roky.part[`part.rok_${fdate('Y')}`];
    letos.click();
  }
  func create_menu() { 
      m.roky.attach_code(php.ds_ceny_group()); 
  }
  menu m {type:'left', active:*
    menu roky {title:'Ceny posledních 7 let ',type:'group'
      // itemy budou doplněny metodou attach_code jako
      //   item {title:'2020',par:{rok:'2020'} }
      func onclick (i:ezer) {
        rok= i.par.rok;
        info.header(i);
        info.ceny.display(1);
        if (!info.ceny.browse_load(`rok=${rok}`))
          info.fill('',`Ceník pro rok ${rok} není definován, ve fakturách použijeme ten nejstarší.<br>`);
        info.fill('',`Úpravy lze provádět po dvojkliku na měněný řádek ceníku.`);
      }
    }
    menu {title:'Ceník dalšího roku ',type:'group', active:no
      item {title:'kopie letos => příští rok',  par:{op:'kopie',z:'0',na:'1'} }
      item {title:'kopie loni => letos',        par:{op:'kopie',z:'-1',na:'0'} }
      item {title:'kopie předloni => letos',    par:{op:'kopie',z:'-2',na:'0'} }
      proc onclick (i) { var na:text var z:text
        info.ceny.display(1);
        na.set(sum(fdate('Y'),i.par.na));
        z.set(sum(fdate('Y'),i.par.z));
        confirm("Ceník roku ",na," bude nahrazen kopií ceníku roku ",z,". Pokračovat?");
        info.fill(conc(i.owner.title,i.title),ask('ds_ceny_uprava',i.par));
        info.ceny.browse_load(conc("rok=",na));
        alert("nový ceník se v levém menu objeví po restartu Answeru")
      }
    }
    menu {title:'Fakturace',type:'group', skill:'a;yd2'
      item {title:'ukázka',       par:{idos:'18280',show:1,save:0} }
      item {title:'PDF',          par:{idos:'18280',show:0,save:1} }
      item {title:'ukázka + PDF', par:{idos:'18280',show:1,save:1} }
      func onclick(i:ezer) { var f:object
        info.ceny.display(0);
        info.header(i);
        f= php.ds2_faktura(i.par);
        info.fill('',f.html);
      }
//      item {title:'test PDF'
//        func onclick(i:ezer) { var f:object
//          info.ceny.display(0);
//          info.header(i);
//          f= php.ds2_pdf();
//          info.fill('',f.html);
//        }
//      }
    }
  }
  use info: form right [12,4,,],
  # ---------------------------------------------------------------------------------------==> right
  # formulář pro levostranné menu s permanentním nadpisem s ceníkem
  form right [,,640,50] {
  #   button export [620,18,100,16] { title:'Export', style:'display:none;zIndex:2'
  #     proc onclick() {
  #       note.set(ask('lide_cleni_kurs',rok.get,1));
  #     }
  #   }
    label info [0,0,680,*] { title:'' }
    label head [0,0,680,50]  { title:'' }
    label note [0,50,680,*] { title:'' }
    func header(i:ezer) { var i_owner:ezer
      clear(); i_owner= i.owner();
      head= replace_fa(`<div class='karta'>${i_owner.title} - ${i.title}</div>`);
      note= '';
    }
    func fill(x,y) {
      if (x) head= `<div class='karta'>${replace_fa(x)}</div>`;
      if (y) note= y
    }
    func append(y) {
      if (y) note= conc(note,y)
    }
    browse ceny [10,76,,] { rows:30
      show id [,,0,] { data:ds_cena.id_cena }
      show rok [,,0,] { data:ds_cena.rok }
      show [,,240,] {title:'položka ceníku',  data:ds_cena.polozka }
      show [,,100,] {title:'druh',  data:ds_cena.druh format:'s+'}
      show [,,60,] {title:'typ',  data:ds_cena.typ format:'s+'}
      show [,,50,] {title:'od vč.',  data:ds_cena.od, format:'r' }
      show [,,50,] {title:'do nevč.',  data:ds_cena.do, format:'r' }
      show [,,80,] {title:'cena s DPH',  data:ds_cena.cena, format:'r' }
      show [,,40,] {title:'DPH',  data:ds_cena.dph, format:'r' }
      proc onsubmit (id) {
        oprava.modal(300,100)
      }
    }
  }
  # ======================================================================================> z_oprava
  panel oprava [0,0,300,135] { title:'oprava položky ceníku', type:'popup', css:'dialog'
    use f: form formular [1,9,,],
    proc onfocus() { f.load(info.ceny.id.get); }
  }
  # ========================================================================================> z_novy
  panel novy [0,0,300,125] { title:'nová položka ceníku', type:'popup', css:'dialog'
    use z: form formular [1,9,,],
  }
  # ------------------------------------------------------------------------------------==> formular
  form formular [,,300,125] {
    label [  0,10, 80,17] { title:'položka', format:'r' },
    field [ 90,10,200,17] { data:ds_cena.polozka },
    label [  0,35, 80,17] { title:'věkové pásmo', format:'r' },
    field [ 90,35, 40,17] { data:ds_cena.od, format:'r'  },
    field [140,35, 40,17] { data:ds_cena.do, format:'r'  },
    label [  0,60, 80,17] { title:'druh', format:'r' },
    field [ 90,60, 90,17] { data:ds_cena.druh },
    label [190,60, 20,17] { title:'typ', format:'r' },
    field [220,60, 70,17] { data:ds_cena.typ },
    label [  0,85, 80,17] { title:'cena', format:'r' },
    field [ 90,85, 40,17] { data:ds_cena.cena, format:'r'  },
    label [190,85, 20,17] { title:'DPH', format:'r' },
    field [220,85, 40,17] { data:ds_cena.dph, format:'r'  },
    button save [90,115,45,20] { type:'submit', title:'Uložit', help:'uložit změny',
      proc onclick () { the_formsave(form,info.ceny); panel.hide(form.key) }
    }
    button cancel [150,115,45,20] { title:'Zpět', help:'zadané údaje neukládat, vrátit se zpět'
      proc onclick () { panel.hide(0) }
    }
  }
}
# ---------------------------------------------------------------------------------------==> tabulky
table ds_cena {  db:'setkani', key_id:'id_cena'
  number id_cena
  number rok
  text polozka         { sql_pipe:'wu' }
  text druh            { sql_pipe:'wu' }
  text typ             { sql_pipe:'wu' }
  number od
  number do
  number cena
  number dph
}
table ds_osoba { db:'setkani', key_id:'id_osoba'
  number id_osoba
  number id_order       // tx_gnalberice_order.uid
  number ys_osoba       // ezer_ys.id_osoba
  text   rodina         { sql_pipe:'wu' }
  text   prijmeni       { sql_pipe:'wu' }
  text   jmeno          { sql_pipe:'wu' }
  date   narozeni       { sql_pipe:'sql_date1' }
  text   psc
  text   obec           { sql_pipe:'wu' }
  text   ulice          { sql_pipe:'wu' }
  text   email          { sql_pipe:'wu' }
  text   telefon
  // ubytování
  number pokoj
  number luzko          // L|P|B|DP
  number postylka       // 0|1
  number zvire          // n
  number strava         // L|P|B|DP
  number dar
  text   oprava
  date   fromday        { sql_pipe:'sql_date1' }
  date   untilday       { sql_pipe:'sql_date1' }
  text   pozn           { sql_pipe:'wu' }
}
table tx_gnalberice_room { db:'setkani', key_id:'uid'
  number uid
  number deleted
  number hidden
  number number
  text   category
  number bads
  number addbeds
  number etage
  text   note         { sql_pipe:'wu' }
}
table tx_gnalberice_order { db:'setkani', key_id:'uid'
  number uid
  number crdate       { sql_pipe:'stamp_date' }
  number deleted
  number hidden
  text   name         { sql_pipe:'wu' }
  number room
  number fromday      { sql_pipe:'stamp_date' }
  number untilday     { sql_pipe:'stamp_date' }
  number confirmed
  number state
  number akce         { help:"akce|číslo akce z číselníku akcí" }
  text   note         { sql_pipe:'wu' }
  text   rooms        { sql_pipe:'wu' }
  text   rooms1       { sql_pipe:'wu' }
  number adults       { help:'?|počet dospělých podle objednávky' }
  number kids_10_15   { help:'?|počet dětí ve věku 10-15 let podle objednávky' }
  number kids_3_9     { help:'?|počet dětí ve věku 3-9 let podle objednávky' }
  number kids_3       { help:'?|počet dětí do 3 let podle objednávky' }
  number board
  number prog_cely
  number prog_polo
  text   address      { sql_pipe:'wu' }
  text   zip
  text   city         { sql_pipe:'wu' }
  text   telephone
  text   email
  number fe_user_id
  // přidané polozky
  text   firstname    { sql_pipe:'wu' }
  text   org          { sql_pipe:'wu' }
  number ic
  text   dic
  number ucast        // 1 => účastní se pobytu
  number sleva        { help:'%|případná sleva v procentech' }
  number skoleni      // 1 => školení, neplatí se rekreační poplatek
}
table _cis { key_id:'id_cis'
# --------------------------------------------------------------------------------==> číselníky,mapy
  number id_cis, text druh, text data, text hodnota, text zkratka, number poradi, text barva, text ikona
}

map ds_stav:   table _cis {where:"druh='ds_stav'", order:'poradi', key_id:'data'}
map ds_strava: table _cis {where:"druh='ds_strava'", order:'poradi', key_id:'data'}
map ds_luzko:  table _cis {where:"druh='ds_luzko'", order:'poradi', key_id:'data'}
# map ds_pokoj:  table tx_gnalberice_room {key_id:'uid'}

