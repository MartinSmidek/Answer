#pragma test
# ------------------------------------------------------------------------------------------------ #
# Karta pro zobrazení a export statistik                                                           #
#                                                                                                  #
# Ans(w)er/Ezer                                     (c) 2007-2014 Martin Šmídek <martin@smidek.eu> #
# ------------------------------------------------------------------------------------------------ #
# ----------------------------------------------------------------------------------- globální jména
# ----------------------------------------------------------------------------------==> inicializace
proc onfirstfocus() { dotaz.Init; } // has_skill('m'); has_skill('yaet'); m.d.d.click }
# ------------------------------------------------------------------------------------------==> stav
var the_vypis: text
# ----------------------------------------------------------------------------==> globální procedury
proc reaccess() { #==> reaccess - změna
  menu_item.get;
  m.onclick(menu_item.get);
}
# ------------------------------------------------------------------------------------------==> MENU
var menu_item=0,
    menu_par=0
menu m {type:'left', format:'f+', active:*
  menu s {title:'STATISTIKA',type:'group'
    item {title:'Struktura letních kurzů MS',                                        skill:'yae;fae'
      ,par:°{typ:'ms_struktura',xls:'Excel',pdf:0,graf:1
        ,dsc:'Sestava ukazuje počty rodin a jejich dětí s rozlišením na nové, dvouročáky a VPS.<br>'}}
    item {title:'Struktura LK MS od roku 2005',                                      skill:'yae;fae'
      ,par:°{typ:'ms_struktura',xls:'Excel',pdf:0,od:2005,graf:1
        ,dsc:'Sestava ukazuje počty rodin a jejich dětí s rozlišením na nové, dvouročáky a VPS.<br>'}}
    item {title:' ... jen tabulka',                                                  skill:'yae;fae'
      ,par:°{typ:'ms_struktura',xls:'Excel',pdf:0,od:2005,graf:0
        ,dsc:'Sestava ukazuje počty rodin a jejich dětí s rozlišením na nové, dvouročáky a VPS.<br>'}}
    item {title:'Cesty na LK MS od roku 2005',                                       skill:'yae;fae'
      ,par:°{typ:'ms_cesty',xls:'Excel',pdf:0,od:2005
        ,dsc:'Sestava ukazuje cesty, kterými procházeli účastníci LK MS předchozími akcemi.<br>'}}
    item {title:'Letní kurzy MS a pečovatelé',                                       skill:'#crz'
      ,par:°{typ:'ms-pecouni',rok:0,xls:'Excel',pdf:0
      ,dsc:'Sestava ukazuje přehled letních kurzů s počtem rodin, jejich dětí a pečovatelů.
        <br>Je uveden počet pečovatelů, kteří se před službou účastnili školení a kolik je z toho nových pečovatelů.
        <br>V poznámce je počet pořádaných školení a poměr dětí/pečovatele.
        <br>V počtu školení se objeví jen ta, která jsou evidovaná v databázi<br>'
      ,tit:'rok:5,rodin:5,dětí:5,pečounů:5,+pp:3,+po:3,+pg:3,=celkem,školených:5,%:5,nových:5,školených:5,%:5,poznámka:20'
      ,fld:'_rok,_rodi,_deti,_pec,_pp,_po,_pg,_celk,_sko,_proc,_pecN,_skoN,_procN,_note,chyba',ord:'_rok DESC'}}
    item {title:'Letošní účastníci podle věku'  ,par:°{typ:'ucast-vek',rok:0,xls:'Excel',pdf:0
      ,dsc:'Sestava ukazuje celkový počet účastníků resp. pečovatelů na akcích letošního roku, rozdělený podle věku.
        <br>Účastník resp. pečovatel je započítán jen jednou, bez ohledu na počet akcí, jichž se zúčastnil.<br>'
      ,tit:'věk:4,účastníků:10,pečovatelů:10',fld:'_vek,_uca,_pec',ord:'_vek'}}
    item {title:'Přehled větších akcí'  ,par:°{typ:'4roky',rok:0,xls:'Excel',pdf:0
      ,dsc:'Sestava ukazuje údaje o účastnících na vybraných akcích za uplynulé 4 roky.<br>'
      ,tit:'věk:4,účastníků:10,pečovatelů:10',fld:'_vek,_uca,_pec',ord:'_vek'}}
  }
  menu ms /* ms */ {title:'STATISTIKA MS',type:'group'                             skill:'faa:m'
    item {title:'Vytvoř #stat_ms'                                 par:°{op:'gen'}  skill:'r|m'}
    item {title:'[fa-birthday-cake] ... jak jsou staří'           par:°{op:'see',typ:'v',org:'3'} }
    item {title:'... ... YS/FA'                                   par:°{op:'see-o',typ:'v'} }
    item {title:'[fa-map-o] ... v kterém místě ČR žijí'           par:°{op:'see',typ:'g',org:'3'} }
    item {title:'... ... YS/FA'                                   par:°{op:'see-o',typ:'g'} }
    item {title:'[fa-home] ... v jak velké obci žijí'             par:°{op:'see',typ:'o',org:'3'} }
    item {title:'... ... YS/FA'                                   par:°{op:'see-o',typ:'o'} }
    item {title:'[fa-bar-chart] ... info z akcí YS+FA'            par:°{op:'see',typ:'s',org:'3'} }
    proc onclick (i) {
      mapa.close(1);
      panel.display(2,'i');
      menu_item.set(i); menu_par.set(i.par); clear;
      the_vypis.set(replace_fa(conc(i.owner.title,' - ',i.title)));
      info.fill(the_vypis.get,' '); info.msg.set('');
      info.append(ask('sta2_ms_stat',i.par));
    }
  }
  menu mg {title:'MAPY MS' type:'group'                                          skill:'faa:m'
    item {title:'[fa-globe] mapa - všichni v ČR'        par:°{mapa:'malé obce',od:0,do:9999999} }
    item {title:'[fa-globe] mapa - obce ..580'          par:°{mapa:'malé obce',od:0,do:580} }
    item {title:'[fa-globe] mapa - obce 580..1152'      par:°{mapa:'malé obce',od:580,do:1152} }
    item {title:'[fa-globe] mapa - obce 1152..2168'     par:°{mapa:'malé obce',od:1152,do:2168} }
    item {title:'[fa-globe] mapa - obce 2168..4460'     par:°{mapa:'malé obce',od:2168,do:4460} }
    item {title:'[fa-globe] mapa - obce 4460..9300'     par:°{mapa:'malé obce',od:4460,do:9300} }
    item {title:'[fa-globe] mapa - obce 9300..19400'    par:°{mapa:'malé obce',od:9300,do:19400} }
    item {title:'[fa-globe] mapa - obce 19400..42400'   par:°{mapa:'malé obce',od:19400,do:42400} }
    item {title:'[fa-globe] mapa - obce 42400..93200'   par:°{mapa:'malé obce',od:42400,do:93200} }
    item {title:'[fa-globe] mapa - obce 93200..600000'  par:°{mapa:'malé obce',od:93200,do:600000} }
    item {title:'[fa-globe] mapa - obce 600000..'       par:°{mapa:'malé obce',od:600000,do:9999999} }
    item {title:'[fa-globe] smazat mapu'                par:°{mapa:'clear'} }
    proc onclick (i) {
      menu_item.set(i); menu_par.set(i.par); clear;
      the_vypis.set(replace_fa(conc(i.owner.title,' - ',i.title)));
      info.fill(the_vypis.get,''); 
      info.msg.set('xxx');
      mapa.metoda.set('ms');
      mapa.mrop_par.set(i.par);
      [ mapa.modal(490,110,i.par.mapa,0,1) ];
    }
  }
  menu mr /* mrop */ {title:'STATISTIKA MROP' type:'group'                                    skill:'faa:m'
    item {title:'Vytvoř #psc,#obec,#okres,#kraj'                  par:°{op:'psc'}  skill:'r|m'}   
    item {title:'Vytvoř #stat (dlouhý výpočet)'                   par:°{op:'gen'}  skill:'r|m'}
    item {title:'Přepočítej v #stat položku akce'                 par:°{op:'gen',x:'a'}  skill:'r|m'}
    item {title:'[fa-birthday-cake] ... jak jsou staří'           par:°{op:'see',  typ:'v'} }
    item {title:' ... ... vývoj v čase'                           par:°{op:'see-t',typ:'v'} }
    item {title:'[fa-map-o] ... v kterém místě ČR žijí'           par:°{op:'see',  typ:'g'} }
    item {title:' ... ... vývoj v čase'                           par:°{op:'see-t',typ:'g'} }
    item {title:'[fa-home] ... v jak velké obci žijí'             par:°{op:'see',  typ:'o'} }
    item {title:' ... ... vývoj v čase'                           par:°{op:'see-t',typ:'o'} }
    item {title:'[fa-bar-chart] ... info z akcí YS+FA+(CPR/MS)'   par:°{op:'see',  typ:'s'} }
    item {title:' ... ... vývoj v čase'                           par:°{op:'see-t',typ:'s'} }
    item {title:'[fa-recycle] souvislosti mezi akcemi'            par:°{op:'see',  typ:'x'} }
    item {title:' ... ... vývoj v čase (nejasný smysl)'           par:°{op:'see-t',typ:'x'} }
//    item {title:'... ročníky, cesty (bez #stat)', par:°{mrop:2002}
//      proc onclick (i) {
//        mapa.close(1);
//        panel.display(2,'i');
//        menu_item.set(i); menu_par.set(i.par); clear;
//        the_vypis.set(conc(i.owner.title,' - ',i.title));
//        info.fill(the_vypis.get,' '); info.msg.set('');
//        info.append(ask('sta2_mrop',i.par));
//      }
//    }
    proc onclick (i) {
      mapa.close(1);
      panel.display(2,'i');
      menu_item.set(i); menu_par.set(i.par); clear;
      the_vypis.set(replace_fa(conc(i.owner.title,' - ',i.title)));
      info.fill(the_vypis.get,' '); info.msg.set('');
      info.append(ask('sta2_mrop_stat',i.par));
    }
  }
  menu g {title:'MAPY MROP' type:'group'                                          skill:'faa:m'
    var men: number
    item {title:'[fa-globe] mapa - skupiny v ČR'        par:°{mapa:'skupiny'} }
    item {title:'[fa-globe] mapa - všichni v ČR'        par:°{mapa:'malé obce',od:0,do:9999999} }
//    item {title:'[fa-globe] mapa - obce 0..300'         par:°{mapa:'malé obce',od:0,do:300} }
//    item {title:'[fa-globe] mapa - obce 300..1000'      par:°{mapa:'malé obce',od:300,do:1000} }
//    item {title:'[fa-globe] mapa - obce 1000..3000'     par:°{mapa:'malé obce',od:1000,do:3000} }
//    item {title:'[fa-globe] mapa - obce 3000..10000'    par:°{mapa:'malé obce',od:3000,do:10000} }
//    item {title:'[fa-globe] mapa - obce 10000..30000'   par:°{mapa:'malé obce',od:10000,do:30000} }
//    item {title:'[fa-globe] mapa - obce 30000..100000'  par:°{mapa:'malé obce',od:30000,do:100000} }
//    item {title:'[fa-globe] mapa - obce 100000..300000' par:°{mapa:'malé obce',od:100000,do:300000} }
//    item {title:'[fa-globe] mapa - obce 3000000..'      par:°{mapa:'malé obce',od:300000,do:9999999} }
    item {title:'[fa-globe] mapa - obce 0..290'           par:°{mapa:'malé obce',od:0,do:290} }
    item {title:'[fa-globe] mapa - obce 290..576'         par:°{mapa:'malé obce',od:290,do:576} }
    item {title:'[fa-globe] mapa - obce 576..1084'        par:°{mapa:'malé obce',od:576,do:1084} }
    item {title:'[fa-globe] mapa - obce 1084..2230'       par:°{mapa:'malé obce',od:1084,do:2230} }
    item {title:'[fa-globe] mapa - obce 2230..4650'       par:°{mapa:'malé obce',od:2230,do:4650} }
    item {title:'[fa-globe] mapa - obce 4650..9700'       par:°{mapa:'malé obce',od:4650,do:9700} }
    item {title:'[fa-globe] mapa - obce 9700..21200'      par:°{mapa:'malé obce',od:9700,do:21200} }
    item {title:'[fa-globe] mapa - obce 21200..46600'     par:°{mapa:'malé obce',od:21200,do:46600} }
    item {title:'[fa-globe] mapa - obce 46600..300000'    par:°{mapa:'malé obce',od:46600,do:300000} }
    item {title:'[fa-globe] mapa - obce 300000..'         par:°{mapa:'malé obce',od:300000,do:9999999} }
//    item {title:'[fa-globe] mapa - Brno'                par:°{mapa:'Brno'} }
    item {title:'[fa-globe] smazat mapu'                  par:°{mapa:'clear'} }
    item {title:'[fa-globe] ukázat mapu a počty'            
      proc onclick (i) { var msg:text
        msg.set(function('mapa','celkem',"return mrop_map_count(mapa,celkem);",mapa.f.cr,1374));
        info.fill(msg,''); 
      }
    }
    proc onclick (i) {
      menu_item.set(i); menu_par.set(i.par); clear;
      the_vypis.set(replace_fa(conc(i.owner.title,' - ',i.title)));
      info.fill(the_vypis.get,''); 
      info.msg.set('xxx');
      mapa.metoda.set('mrop');
      mapa.mrop_par.set(i.par);
      [ mapa.modal(490,110,i.par.mapa,0,1) ];
    }
  }
  menu l {title:'SEZNAMY',type:'group',                                              skill:'yae;fae;cae'
    // přednášející
    item {title:'přednášející LK MS',                                                skill:'yae;fae;cae#crz'
         ,par:°{typ:'prednasejici',rok:5,xls:'Excel',parm_tit:'roky zpět:'
      ,dsc:"Tabulka obsahuje historii přednášejících párů na letních kurzech MS. <br>Hloubku historie je třeba nastavit parametrem 'roky zpět'.<br>"
      ,tit:'',fld:'',ord:''}}
    // VPS-PPS
    item {title:'sloužící VPS na LK MS (páry)'                                       skill:'yae;fae;cae#crz'
         ,par:°{typ:'slouzici',podtyp:'pary',xls:'Excel',parm_tit:'roky zpět:'
          ,dsc:"Tabulka obsahuje historii sloužících párů na letních kurzech MS. <br>Poslední rok služby VPS je třeba nastavit parametrem 'roky zpět'.<br>"
      ,tit:'',fld:'',ord:''}}
    item {title:'... přehled historie skupinek'                                      skill:'yaet'
         ,par:°{typ:'slouzici2',xls:'Excel',parm_tit:'roky zpět:'
          ,dsc:"Tabulka ukazuje pro jednotlivé VPS účast jejich skupinek na obnovách. <br><br>Parametr 'roky zpět' je třeba nastavit jako dvojici čísel X,Y <br>kde X určuje, které VPS nás zajímají (0=jen letošní, 1=i loňské) a Y určuje na kolik let jejich služby se díváme.<br>Zkus třeba 0,2 a pak opět klikni na ... přehled historie skupinek<br>"
      ,tit:'',fld:'',ord:''}}
    item {title:' ... sloužící VPS/PPS (jednotlivci)'                                skill:'yae;fae;cae#crz'
         ,par:°{typ:'slouzici',podtyp:'osoby',xls:'Excel',parm_tit:'roky zpět:'
          ,dsc:"Tabulka obsahuje historii sloužících párů na letních kurzech MS. <br>Poslední rok služby VPS je třeba nastavit parametrem 'roky zpět'.<br>"
      ,tit:'',fld:'',ord:''}}
    // pečouni
    item {title:'pečovatelé na LK MS'                                                skill:'yae;fae;cae#crz'
         ,par:°{typ:'pecujici',rok:5,xls:'Excel',parm_tit:'roky zpět:'
          ,dsc:"Tabulka obsahuje historii pečovatelů na letních kurzech MS. <br>Poslední rok pečovatele je třeba nastavit parametrem 'roky zpět'.<br>1.školení je rok první účasti na akci typu 'pečovatelé'.<br>"
      ,tit:'',fld:'',ord:''}}
    // účastníci akcí
    item {title:'adresy účastníků'  ,par:°{typ:'adresy',rok:0,xls:'Excel',/*pdf:'Štítky',*/parm_tit:'roky zpět:'
      ,dsc:'Seznam obsahuje účastníky akcí v posledních letech (parametr určuje počet let zpět, default=0 tj. jen letošní).<br>Kliknutí na číslo ve sloupci <b>ID osoby</b> zobrazí první osobu na kartě EVIDENCE.<br>'
      ,tit:'jména:30,ulice:20,psč:6,obec:20,stát:6,(příjmení):15,(aktivní členi):20,(poslední akce):80,(mrop):7,(PPS):7,(akt.členů):11,(ID osoby):10'
      ,fld:'jmena,ulice,psc,obec,stat,prijmeni,_cleni,akce,_mrop,_pps,_clenu,id_osoba',ord:'prijmeni'}}
    item {title:'... bez PSČ'  ,par:°{typ:'adresy',rok:0,xls:'Excel',/*pdf:'Štítky',*/parm_tit:'roky zpět:'
      ,dsc:'Seznam obsahuje účastníky akcí v posledních letech (parametr určuje počet let zpět, default=0 tj. jen letošní).<br>Kliknutí na číslo ve sloupci <b>ID osoby</b> zobrazí první osobu na kartě EVIDENCE.<br>'
      ,tit:'jména:30,ulice:20,psč:6,obec:20,stát:6,(příjmení):15,(aktivní členi):20,(poslední akce):80,(mrop):7,(PPS):7,(akt.členů):11,(ID osoby):10'
      ,fld:'jmena,ulice,psc,obec,stat,prijmeni,_cleni,akce,_mrop,_pps,_clenu,id_osoba',ord:'prijmeni',cnd:"_psc=''"}}
    item {title:'... s PSČ ale bez státu'  ,par:°{typ:'adresy',rok:0,xls:'Excel',/*pdf:'Štítky',*/parm_tit:'roky zpět:'
      ,dsc:'Seznam obsahuje účastníky akcí v posledních letech (parametr určuje počet let zpět, default=0 tj. jen letošní).<br>Kliknutí na číslo ve sloupci <b>ID osoby</b> zobrazí první osobu na kartě EVIDENCE.<br>'
      ,tit:'jména:30,ulice:20,psč:6,obec:20,stát:6,(příjmení):15,(aktivní členi):20,(poslední akce):80,(mrop):7,(PPS):7,(akt.členů):11,(ID osoby):10'
      ,fld:'jmena,ulice,psc,obec,stat,prijmeni,_cleni,akce,_mrop,_pps,_clenu,id_osoba',ord:'prijmeni',cnd:"_psc!='' AND _stat IN ('','?')"}}
  }
  menu d {title:'Dotazníky LK YMCA Setkání',type:'group', _sys:'*' skill:'yaet'
    item {title:'[fa-calendar] volba roku dotazníku'            par:°{typ:'r'} }
    item i {title:'[fa-ban] (re)import dotazníku'               format:'d'
      proc onclick (i) { var y:object
        mapa.close(1);
        panel.display(2,'i');
        show_dotazy;
        info.fill(conc(i.owner.title,' - ',i.title),' ');
        confirm(conc("Mám přepočítat dotazník roku ",dotaz.rok.get,"?"));
        clear;
        y.set(ask('dot_import',dotaz.rok.get));
        [ y.war; warning(y.war) ]; 
        [ y.err; alert(y.err) ]; 
        info.fill('',y.html) 
    }}
    item {title:'[fa-line-chart] statistika dotazník & Answer'  par:°{typ:'2'} }
    item {title:'[fa-balance-scale] různé výběry a srovnání'    par:°{typ:'v'} }
    item d {title:'[fa-forward] procházení dotazníku'           par:°{typ:'d',id:'1'} 
      proc onclick (i) { 
        mapa.close(1);
        show_dotazy;
        panel.display(2,'dp|do');
        obraz.fill(conc(i.owner.title,' - ',i.title),' ',' ');
        obraz.display(0,'x'); obraz.display(1,i.par.typ);
        clear;
        dotaz.index.set('id'); dotaz.id.set(i.par.id); 
        dotaz.Show(0,0) 
      }
    }
    proc onclick (i) { var x: object, y: object
      mapa.close(1);
      panel.display(2,'do');
      obraz.fill(conc(i.owner.title,' - ',i.title),' ','');
      obraz.display(0,'x'); obraz.display(1,i.par.typ);
      clear;
      switch(i.par.typ,
      'r',{ x.set(ask('dot_roky')); obraz.roky.selects(x.roky); obraz.roky.set(dotaz.rok.get) },
      '2',{ x.set(ask('dot_prehled',dotaz.rok.get,°{zdroj:'akce',par1:'rok'})); 
            y.set(ask('dot_prehled',dotaz.rok.get,°{zdroj:'dotaz',par1:'rok'})); 
            obraz.fill('',conc("<table><tr><td>",y.html,"</td><td>",x.html,"</tr></table>"),' ') },
      'v',{ obraz.recond; obraz.rechoice; }
//            x.set(ask('dot_vyber',cconc(obraz.vsechno.get,0,dotaz.rok.get),i.par)); 
//            obraz.fill2('',x.html,' ') },
      );
    }
  }
  proc onclick (i) { var ret: object
    mapa.close(1);
    panel.display(2,'i');
    menu_item.set(i); menu_par.set(i.par); clear;
    the_vypis.set(conc(i.owner.title,' - ',i.title));
    [ i.par.xls; info.ex_xls.display(1); info.ex_xls.set(i.par.xls) | info.ex_xls.display(0) ];
    [ i.par.pdf; info.ex_pdf.display(1); info.ex_pdf.set(i.par.pdf) | info.ex_pdf.display(0) ];
    [ //echo(i.par.parm_tit,info.parm.get); 
      i.par.parm_tit; info.display(1,'p');
      info.parm_tit.set(i.par.parm_tit); menu_par.set(info.parm.get,'parm') | info.display(0,'p') ];
    info.fill(the_vypis.get,i.par.dsc); info.msg.set('');
    { eq(i.par.typ,'ms_struktura'); ret.set(ask('sta2_struktura',my_org.get,i.par,the_title.get));
    | eq(i.par.typ,'ms_cesty'); ret.set(ask('sta2_cesty',my_org.get,i.par,the_title.get));
    | ret.set(ask('sta2_sestava',my_org.get,the_vypis.get,menu_par.get));
    };
    info.append(ret.html);
  }
}
proc show_obraz(i) { // přes odkaz z PHP vygenerovaný voláním Spy
  { eq(dotaz.index.get,'pg'); dotaz.pg.set(i) | dotaz.id.set(i) };
  dotaz.Show(0,0); 
  tipy.call('Spy'); 
}
proc show_dotazy() {
  function('group',"group._fold(); return 1;",m.s);
  function('group',"group._fold(); return 1;",m.l);
}
# ------------------------------------------------------------------------------------------==> uses
use dotaz: form _dotaz [24,300,,] { tag:'dp' format:'n' }
use obraz: form _obraz [12,4,,]   { tag:'do' format:'n' }
use tipy:  form _tipy  [24,370,,] { tag:'dp' format:'n' skill:'mš'}
use info: form right   [12,4,,]   { tag:'i'
  label parm_tit [-200,9,100,] { tag:'p', style:'zIndex:2;color:white', format:'rn' }
  field parm [-150,5,40,] { tag:'p', style:'zIndex:2', format:'rtn' }
  button ex_pdf [-80,5,,] { title:'PDF...', style:'zIndex:2', format:'n',
    proc onclick() {
    }
  }
  button ex_xls [-20,5,,] { title:'Excel', style:'zIndex:2', format:'n',
    proc onclick() { var res: object
      switch(this.get,
      'Excel',{
        res.set(ask('sta2_excel',my_org.get,the_vypis.get,menu_par.get));
        form.msg.set(res.html);
      })
    }
  }
}
form _obraz [,,700,50] {
  const D=320
  label text [0,28,700,700] 
  label text2 [0,60,320,700] { style:"overflow:auto"  }
  label img [D,28,480,700] { title:'' }
  label head [0,0,800,30]  { title:'' }
  label note [0,508,320,200] { style:"overflow:auto" }
  // volba roku dotazníku
  select roky [100,60,100,] { tag:'x,r' title:'LK roku' value:'2019' format:'t'
    proc onchanged() { 
      dotaz.rok.set(this.get); 
      dotaz.Init
  }}
  // výběrové podmínky
  label [0,40,80,] { tag:'x,v' title:'volba roků:' }
  check r2018  [80,40,50,] { tag:'x,v' title:'2018' format:'t' proc onchange() { recond }}
  check r2019 [140,40,50,] { tag:'x,v' title:'2019' format:'t' proc onchange() { recond }}
//  check vsechno [0,40,200,] { tag:'x,v' title:'vzít údaje ze všech roků dohromady' format:'t' 
//    proc onchange() { recond }
//  }
  label [D+20,35,100,] { tag:'x,v' title:'<b>Statistika</b>' }
  check x_sex [D+20,50,90,] { tag:'x,v' title:'sex' format:'t'
    proc onchange() { recond }
  }
  radio r_sex [D+100,52,200,20] { tag:'x,v' value:'0' format:'t'
    case [0,0,50,18] { title:'muž' value:'0' }
    case [50,0,50,18] { title:'žena' value:'1' }
    proc onchange() { recond }
  }
  check x_vek [D+20,70,90,] { tag:'x,v' title:'věk' format:'t'
    proc onchange() { recond }
  }
  radio r_vek [D+100,72,200,20] { tag:'x,v' value:' 1 AND 30' format:'t'
    case [  0,0,50,18] { title:'...30' value:' 1 AND 30' }
    case [ 50,0,50,18] { title:'31-40' value:'31 AND 40' }
    case [100,0,50,18] { title:'41-50' value:'41 AND 50' }
    case [150,0,50,18] { title:'51-60' value:'51 AND 60' }
    case [200,0,50,18] { title:'61...' value:'61 AND 99' }
    proc onchange() { recond }
  }
  check x_det [D+20,90,90,] { tag:'x,v' title:'dětí/LK' format:'t'
    proc onchange() { recond }
  }
  radio r_det [D+100,92,200,20] { tag:'x,v' value:'0 AND 0' format:'t'
    case [  0,0,50,18] { title:'0' value:'0 AND 0' }
    case [ 50,0,50,18] { title:'1' value:'1 AND 1' }
    case [100,0,50,18] { title:'2' value:'2 AND 2' }
    case [150,0,50,18] { title:'3...' value:'3 AND 99' }
    case [200,0,50,18] { title:'1...' value:'1 AND 99' }
    proc onchange() { recond }
  }
  check x_man [D+20,110,90,] { tag:'x,v' title:'manželství' format:'t'
    proc onchange() { recond }
  }
  radio r_man [D+100,112,200,20] { tag:'x,v' value:'0 AND 5' format:'t'
    case [  0,0,50,18] { title:' ...5' value:'0 AND 5' }
    case [ 50,0,50,18] { title:' 6-10' value:'6 AND 10' }
    case [100,0,50,18] { title:'11-20' value:'11 AND 20' }
    case [150,0,50,18] { title:'21-30' value:'21 AND 30' }
    case [200,0,50,18] { title:'31...' value:'31 AND 99' }
    proc onchange() { recond }
  }
  check x_nov [D+20,130,90,] { tag:'x,v' title:'účast' format:'t'
    proc onchange() { recond }
  }
  radio r_nov [D+100,132,200,20] { tag:'x,v' value:'1' format:'t'
    case [0,0,50,18] { title:'noví' value:'1' }
    case [50,0,80,18] { title:'víckrát' value:'0' }
    proc onchange() { recond }
  }
  label [D+20,165,100,] { tag:'x,v' title:'<b>Přínos</b>' }
  check x_pri [D+20,180,90,] { tag:'x,v' title:'přínos' format:'t'
    proc onchange() { recond }
  }
  radio r_pri [D+100,182,200,20] { tag:'x,v' value:'1' format:'t'
    case [0,0,50,18] { title:'význ.' value:'1' }
    case [50,0,80,18] { title:'část.' value:'2' }
    case [100,0,80,18] { title:'nevím' value:'3' }
    case [150,0,80,18] { title:'není' value:'4' }
    case [200,0,80,18] { title:'horší' value:'5' }
    proc onchange() { recond }
  }
  edit cond [D+20,235,260,100] { tag:'x,v' title:'^výběr jako SQL' value:'1' format:'t'
    proc onchanged() { rechoice }}
  // funkce
  proc recond() { var roky:text
    echo(x_sex.get,',',r_sex.get);
    roky.set('0');
    [ r2018.get; roky.set(conc(roky,',2018')) ]; 
    [ r2019.get; roky.set(conc(roky,',2019')) ]; 
    cond.set(conc('dotaznik IN (',roky,')',
      cconc(x_sex.get,conc(' AND sex=',r_sex.get),''),
      cconc(x_vek.get,conc(' AND vek BETWEEN ',r_vek.get),''),
      cconc(x_det.get,conc(' AND deti BETWEEN ',r_det.get),''),
      cconc(x_man.get,conc(' AND manzel BETWEEN ',r_man.get),''),
      cconc(x_nov.get,conc(' AND novic=',r_nov.get),''),
      cconc(x_pri.get,conc(' AND prinos=',r_pri.get),''),
    ));
    rechoice;
    dotaz.dirty.set(1);
  }
  proc rechoice() { var x:object
//    x.set(ask('dot_vyber',cconc(vsechno.get,0,dotaz.rok.get),object('cond',cond.get))); 
    x.set(ask('dot_vyber',object('cond',cond.get))); 
    dotaz.celkem.set(x.celkem);
    obraz.fill2('',x.html,'')
  }
  proc fill(x,y,z) {
    note.set(''); text2.set(''); img.set('');
    [ x; head.set(conc("<div class='karta'>",x,"</div>")) ];
    [ y; text.set(y) ];
    [ z; img.set(z) ];
  }
  proc fill2(x,y,z) {
    note.set(''); text.set('');
    [ x; head.set(conc("<div class='karta'>",x,"</div>")) ];
    [ y; text2.set(y) ];
    [ z; img.set(z) ];
  }
  proc append(x) { note.set(x) }
}
form _dotaz [,,163,60] { style:'position:fixed;z-index:2' css:'parm shadow'
  var rok=2019,   // rok dotazníku ... 2019
      kurz=0,
      back=0,     // návrat na poslední kliknuté (ne odkazem)
      dirty=1,    // po změně dotazu, čistí se po prvním Show
      virgin=1
  proc Init() { 
    has_skill('yaet'); 
    [ has_skill('a'); m.d.i.enable(1) ];
    m.d.set_attrib('title',conc("Dotazník LK YS roku ",rok.get)); 
    kurz.set(0);
    back.set(0);
    [ virgin.get; obraz.init; obraz.cond.set(1); back.set(0); dirty.set(1); virgin.set(0) ];
  }
  proc Show(offset,go_back) { var y:object
    clear;
    y.set(ask('dot_show',
        cconc(vyber.get,0,rok.get),
        index.get,
        cconc(eq(index.get,'page'),pg.get,id.get),offset,
        cconc(vyber.get,obraz.cond.get,1),dotaz.dirty.get,rok.get));
    not(y.none);
    pg.set(y.page); id.set(y.id); rok.set(y.rok);
    [ go_back; back.set(cconc(eq(index.get,'page'),pg.get,id.get)) ]; 
    obraz.fill2('',y.html,y.jpg);
    dotaz.dirty.set(0); 
  }
  radio index [50,2,60,50] { format:'t'
    case  [0,0,10,10] { title:' ' value:'page' }
    case [30,0,10,10] { title:' ' value:'id' }
  }
  label celkem [10,4,49,] { style:'color:white' }
  check vyber [112,1,49,] { title:'výběr' value:'0' 
    proc onchange() { dotaz.dirty.set(1); back.set(0); Show(0,0) }
  }
  button [9,32,,] { title:"[fa-step-backward]" help:'první'     proc onclick() { Show(-2,1); }}
  button [29,32,,] { title:"[fa-caret-left]" help:'předchozí'   proc onclick() { Show(-1,1); }}
  field pg [50,32,25,] { title:'^pdf' format:'rt'  proc onchanged() { index.set('page'); Show(0,1); }} 
  field id [80,32,25,] { title:'^xls' format:'rt'  proc onchanged() { index.set('id');   Show(0,1); }} 
  button [112,32,,] { title:"[fa-caret-right]" help:'další'     proc onclick() { Show(1,1); }}
  button [132,32,,] { title:"[fa-step-forward]" help:'poslední' proc onclick() { Show(2,1); }}
}
form _tipy [,,163,46] { style:'position:fixed;z-index:2' css:'parm shadow'
  proc Spy() { var y:object
    clear;
    [ dotaz.kurz.get | dotaz.kurz.set(object('rok',dotaz.rok.get)) ];
    dotaz.kurz.set(object('sex',sex.get,'vek',vek.get,'det',det.get,'man',man.get,'nov',nov.get),'filtr');
    dotaz.kurz.set(ask('dot_spy',dotaz.kurz.get,dotaz.rok.get,dotaz.index.get,
        cconc(eq(dotaz.index.get,'page'),dotaz.pg.get,dotaz.id.get),dotaz.back.get));
    shody.set(dotaz.kurz.get('html'));
  }
  check sex  [8,19,50,] { value:'1' format:'t' skill:'m|m' } 
  label  [8,9,50,] { title:'sex' skill:'m|m' }
  check vek  [33,19,50,] { value:'1' format:'t' skill:'m|m' } 
  label  [33,9,50,] { title:'věk' skill:'m|m' }
  check det  [58,19,50,] { value:'1' format:'t' skill:'m|m' } 
  label  [58,9,50,] { title:'děti' skill:'m|m' }
  check man  [83,19,50,] { value:'1' format:'t' skill:'m|m' } 
  label  [83,9,50,] { title:'man' skill:'m|m' }
  check nov [108,19,50,] { value:'1' format:'t' skill:'m|m' } 
  label [108,9,50,] { title:'nový' skill:'m|m' }
  button [133,19,22,] { title:"[fa-eye]" help:'špión' skill:'m|m' proc onclick() { Spy }}
  label shody [0,50,163,280] { style:'background:silver' css:'parm shadow' }
}
# ============================================================================================= _lst
# ---------------------------------------------------------- reakce na změny rozměrů okna prohlížeče
# const min_h=395           // minimální výška (nelze jednoduše měnit - jsou k ní vztaženy souřadnice)
# var  curr_h: number     // výška nad základní výšku
# proc onresize(w,h) {
# }
# změna výšky oproti základní min_h
# proc h_change_all() {
# }
# změna počtu řádků browse oproti aktuálním řádkům
# proc h_change_b() { var r1:number, r2:number, h:number
# }
