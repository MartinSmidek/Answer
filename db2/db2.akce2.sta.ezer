#pragma test
# ------------------------------------------------------------------------------------------------ #
# Karta pro zobrazení a export statistik                                                           #
#                                                                                                  #
# Ans(w)er/Ezer                                     (c) 2007-2014 Martin Šmídek <martin@smidek.eu> #
# ------------------------------------------------------------------------------------------------ #
# ----------------------------------------------------------------------------------- globální jména
# ----------------------------------------------------------------------------------==> inicializace
func onfirstfocus() { dot.Init(); } // has_skill('m'); has_skill('yaet'); m.d.d.click }
# ------------------------------------------------------------------------------------------==> stav
var the_vypis: text
# ----------------------------------------------------------------------------==> globální procedury
proc reaccess() { #==> reaccess - změna
  menu_item.get;
  m.onclick(menu_item.get);
}
# ------------------------------------------------------------------------------------------==> MENU
var menu_item=0,
    menu_par=0
menu m {type:'left', format:'f+', active:*
  menu s {title:'STATISTIKA',type:'group'
    item {title:'Struktura letních kurzů MS',                                        skill:'yae;fae'
      ,par:°{typ:'ms_struktura',xls:'Excel',pdf:0,graf:1
        ,dsc:'Sestava ukazuje počty rodin a jejich dětí s rozlišením na nové, dvouročáky a VPS.<br>'}}
    item {title:'Struktura LK MS od roku 2005',                                      skill:'yae;fae'
      ,par:°{typ:'ms_struktura',xls:'Excel',pdf:0,od:2005,graf:1
        ,dsc:'Sestava ukazuje počty rodin a jejich dětí s rozlišením na nové, dvouročáky a VPS.<br>'}}
    item {title:' ... jen tabulka',                                                  skill:'yae;fae'
      ,par:°{typ:'ms_struktura',xls:'Excel',pdf:0,od:2005,graf:0
        ,dsc:'Sestava ukazuje počty rodin a jejich dětí s rozlišením na nové, dvouročáky a VPS.<br>'}}
    item {title:'Cesty na LK MS od roku 2005',                                       skill:'yae;fae'
      ,par:°{typ:'ms_cesty',xls:'Excel',pdf:0,od:2005
        ,dsc:'Sestava ukazuje cesty, kterými procházeli účastníci LK MS předchozími akcemi.<br>'}}
    item {title:'Letní kurzy MS a pečovatelé',                                       skill:'#crz'
      ,par:°{typ:'ms-pecouni',rok:0,xls:'Excel',pdf:0
      ,dsc:'Sestava ukazuje přehled letních kurzů s počtem rodin, jejich dětí a pečovatelů.
        <br>Je uveden počet pečovatelů, kteří se před službou účastnili školení a kolik je z toho nových pečovatelů.
        <br>V poznámce je počet pořádaných školení a poměr dětí/pečovatele.
        <br>V počtu školení se objeví jen ta, která jsou evidovaná v databázi<br>'
      ,tit:'rok:5,rodin:5,dětí:5,pečounů:5,+pp:3,+po:3,+pg:3,=celkem,školených:5,%:5,nových:5,školených:5,%:5,poznámka:20'
      ,fld:'_rok,_rodi,_deti,_pec,_pp,_po,_pg,_celk,_sko,_proc,_pecN,_skoN,_procN,_note,chyba',ord:'_rok DESC'}}
    item {title:'Letošní účastníci podle věku'  ,par:°{typ:'ucast-vek',rok:0,xls:'Excel',pdf:0
      ,dsc:'Sestava ukazuje celkový počet účastníků resp. pečovatelů na akcích letošního roku, rozdělený podle věku.
        <br>Účastník resp. pečovatel je započítán jen jednou, bez ohledu na počet akcí, jichž se zúčastnil.<br>'
      ,tit:'věk:4,účastníků:10,pečovatelů:10',fld:'_vek,_uca,_pec',ord:'_vek'}}
    item {title:'Přehled větších akcí'  ,par:°{typ:'4roky',rok:0,xls:'Excel',pdf:0
      ,dsc:'Sestava ukazuje údaje o účastnících na vybraných akcích za uplynulé 4 roky.<br>'
      ,tit:'věk:4,účastníků:10,pečovatelů:10',fld:'_vek,_uca,_pec',ord:'_vek'}}
  }
//  menu ms /* ms */ {title:'STATISTIKA MS',type:'group'                             skill:'faa:m'
//    item {title:'Vytvoř #stat_ms'                                 par:°{op:'gen'}  skill:'r|m'}
//    item {title:'[fa-birthday-cake] ... jak jsou staří'           par:°{op:'see',typ:'v',org:'3'} }
//    item {title:'... ... YS/FA'                                   par:°{op:'see-o',typ:'v'} }
//    item {title:'[fa-map-o] ... v kterém místě ČR žijí'           par:°{op:'see',typ:'g',org:'3'} }
//    item {title:'... ... YS/FA'                                   par:°{op:'see-o',typ:'g'} }
//    item {title:'[fa-home] ... v jak velké obci žijí'             par:°{op:'see',typ:'o',org:'3'} }
//    item {title:'... ... YS/FA'                                   par:°{op:'see-o',typ:'o'} }
//    item {title:'[fa-bar-chart] ... info z akcí YS+FA'            par:°{op:'see',typ:'s',org:'3'} }
//    proc onclick (i) {
//      mapa.close(1);
//      panel.display(2,'i');
//      menu_item.set(i); menu_par.set(i.par); clear;
//      the_vypis.set(replace_fa(conc(i.owner.title,' - ',i.title)));
//      info.fill(the_vypis.get,' '); info.msg.set('');
//      info.append(ask('sta2_ms_stat',i.par));
//    }
//  }
//  menu mg {title:'MAPY MS' type:'group'                                          skill:'faa:m'
//    item {title:'[fa-globe] mapa - všichni v ČR'        par:°{mapa:'malé obce',od:0,do:9999999} }
//    item {title:'[fa-globe] mapa - obce ..580'          par:°{mapa:'malé obce',od:0,do:580} }
//    item {title:'[fa-globe] mapa - obce 580..1152'      par:°{mapa:'malé obce',od:580,do:1152} }
//    item {title:'[fa-globe] mapa - obce 1152..2168'     par:°{mapa:'malé obce',od:1152,do:2168} }
//    item {title:'[fa-globe] mapa - obce 2168..4460'     par:°{mapa:'malé obce',od:2168,do:4460} }
//    item {title:'[fa-globe] mapa - obce 4460..9300'     par:°{mapa:'malé obce',od:4460,do:9300} }
//    item {title:'[fa-globe] mapa - obce 9300..19400'    par:°{mapa:'malé obce',od:9300,do:19400} }
//    item {title:'[fa-globe] mapa - obce 19400..42400'   par:°{mapa:'malé obce',od:19400,do:42400} }
//    item {title:'[fa-globe] mapa - obce 42400..93200'   par:°{mapa:'malé obce',od:42400,do:93200} }
//    item {title:'[fa-globe] mapa - obce 93200..600000'  par:°{mapa:'malé obce',od:93200,do:600000} }
//    item {title:'[fa-globe] mapa - obce 600000..'       par:°{mapa:'malé obce',od:600000,do:9999999} }
//    item {title:'[fa-globe] smazat mapu'                par:°{mapa:'clear'} }
//    proc onclick (i) {
//      menu_item.set(i); menu_par.set(i.par); clear;
//      the_vypis.set(replace_fa(conc(i.owner.title,' - ',i.title)));
//      info.fill(the_vypis.get,''); 
//      info.msg.set('xxx');
//      mapa.metoda.set('ms');
//      mapa.mrop_par.set(i.par);
//      [ mapa.modal(490,110,i.par.mapa,0,1) ];
//    }
//  }
//  menu mr /* mrop */ {title:'STATISTIKA MROP' type:'group'                                    skill:'faa:m'
//    item {title:'Vytvoř #psc,#obec,#okres,#kraj'                  par:°{op:'psc'}  skill:'r|m'}   
//    item {title:'Vytvoř #stat (dlouhý výpočet)'                   par:°{op:'gen'}  skill:'r|m'}
//    item {title:'Přepočítej v #stat položku akce'                 par:°{op:'gen',x:'a'}  skill:'r|m'}
//    item {title:'[fa-birthday-cake] ... jak jsou staří'           par:°{op:'see',  typ:'v'} }
//    item {title:' ... ... vývoj v čase'                           par:°{op:'see-t',typ:'v'} }
//    item {title:'[fa-map-o] ... v kterém místě ČR žijí'           par:°{op:'see',  typ:'g'} }
//    item {title:' ... ... vývoj v čase'                           par:°{op:'see-t',typ:'g'} }
//    item {title:'[fa-home] ... v jak velké obci žijí'             par:°{op:'see',  typ:'o'} }
//    item {title:' ... ... verze 2.0'                              par:°{op:'see2', typ:'o'} }
//    item {title:' ... ... vývoj v čase'                           par:°{op:'see-t',typ:'o'} }
//    item {title:'[fa-bar-chart] ... info z akcí YS+FA+(CPR/MS)'   par:°{op:'see',  typ:'s'} }
//    item {title:' ... ... vývoj v čase'                           par:°{op:'see-t',typ:'s'} }
//    item {title:'[fa-recycle] souvislosti mezi akcemi'            par:°{op:'see',  typ:'x'} }
//    item {title:' ... ... vývoj v čase (nejasný smysl)'           par:°{op:'see-t',typ:'x'} }
////    item {title:'... ročníky, cesty (bez #stat)', par:°{mrop:2002}
////      proc onclick (i) {
////        mapa.close(1);
////        panel.display(2,'i');
////        menu_item.set(i); menu_par.set(i.par); clear;
////        the_vypis.set(conc(i.owner.title,' - ',i.title));
////        info.fill(the_vypis.get,' '); info.msg.set('');
////        info.append(ask('sta2_mrop',i.par));
////      }
////    }
//    proc onclick (i) {
//      mapa.close(1);
//      panel.display(2,'i');
//      menu_item.set(i); menu_par.set(i.par); clear;
//      the_vypis.set(replace_fa(conc(i.owner.title,' - ',i.title)));
//      info.fill(the_vypis.get,' '); info.msg.set('');
//      info.append(ask('sta2_mrop_stat',i.par));
//    }
//  }
//  menu g {title:'MAPY MROP' type:'group'                                          skill:'faa:m'
//    var men: number
//    item {title:'[fa-globe] mapa - skupiny v ČR'        par:°{mapa:'skupiny'} }
//    item {title:'[fa-globe] mapa - všichni v ČR'        par:°{mapa:'malé obce',od:0,do:9999999} }
////    item {title:'[fa-globe] mapa - obce 0..300'         par:°{mapa:'malé obce',od:0,do:300} }
////    item {title:'[fa-globe] mapa - obce 300..1000'      par:°{mapa:'malé obce',od:300,do:1000} }
////    item {title:'[fa-globe] mapa - obce 1000..3000'     par:°{mapa:'malé obce',od:1000,do:3000} }
////    item {title:'[fa-globe] mapa - obce 3000..10000'    par:°{mapa:'malé obce',od:3000,do:10000} }
////    item {title:'[fa-globe] mapa - obce 10000..30000'   par:°{mapa:'malé obce',od:10000,do:30000} }
////    item {title:'[fa-globe] mapa - obce 30000..100000'  par:°{mapa:'malé obce',od:30000,do:100000} }
////    item {title:'[fa-globe] mapa - obce 100000..300000' par:°{mapa:'malé obce',od:100000,do:300000} }
////    item {title:'[fa-globe] mapa - obce 3000000..'      par:°{mapa:'malé obce',od:300000,do:9999999} }
//    item {title:'[fa-globe] mapa - obce 0..290'           par:°{mapa:'malé obce',od:0,do:290} }
//    item {title:'[fa-globe] mapa - obce 290..576'         par:°{mapa:'malé obce',od:290,do:576} }
//    item {title:'[fa-globe] mapa - obce 576..1084'        par:°{mapa:'malé obce',od:576,do:1084} }
//    item {title:'[fa-globe] mapa - obce 1084..2230'       par:°{mapa:'malé obce',od:1084,do:2230} }
//    item {title:'[fa-globe] mapa - obce 2230..4650'       par:°{mapa:'malé obce',od:2230,do:4650} }
//    item {title:'[fa-globe] mapa - obce 4650..9700'       par:°{mapa:'malé obce',od:4650,do:9700} }
//    item {title:'[fa-globe] mapa - obce 9700..21200'      par:°{mapa:'malé obce',od:9700,do:21200} }
//    item {title:'[fa-globe] mapa - obce 21200..46600'     par:°{mapa:'malé obce',od:21200,do:46600} }
//    item {title:'[fa-globe] mapa - obce 46600..300000'    par:°{mapa:'malé obce',od:46600,do:300000} }
//    item {title:'[fa-globe] mapa - obce 300000..'         par:°{mapa:'malé obce',od:300000,do:9999999} }
////    item {title:'[fa-globe] mapa - Brno'                par:°{mapa:'Brno'} }
//    item {title:'[fa-globe] smazat mapu'                  par:°{mapa:'clear'} }
//    item {title:'[fa-globe] ukázat mapu a počty'            
//      proc onclick (i) { var msg:text
//        msg.set(function('mapa','celkem',"return mrop_map_count(mapa,celkem);",mapa.f.cr,1374));
//        info.fill(msg,''); 
//      }
//    }
//    proc onclick (i) {
//      menu_item.set(i); menu_par.set(i.par); clear;
//      the_vypis.set(replace_fa(conc(i.owner.title,' - ',i.title)));
//      info.fill(the_vypis.get,''); 
//      info.msg.set('xxx');
//      mapa.metoda.set('mrop');
//      mapa.mrop_par.set(i.par);
//      [ mapa.modal(490,110,i.par.mapa,0,1) ];
//    }
//  }
  menu l {title:'SEZNAMY',type:'group',                                              skill:'yae;fae;cae'
    // přednášející
    item {title:'přednášející LK MS',                                                skill:'yae;fae;cae#crz'
         ,par:°{typ:'prednasejici',rok:5,xls:'Excel',parm_tit:'roky zpět:'
      ,dsc:"Tabulka obsahuje historii přednášejících párů na letních kurzech MS. <br>Hloubku historie je třeba nastavit parametrem 'roky zpět'.<br>"
      ,tit:'',fld:'',ord:''}}
    // VPS-PPS
    item {title:'sloužící VPS na LK MS (páry)'                                       skill:'yae;fae;cae#crz'
         ,par:°{typ:'slouzici',podtyp:'pary',xls:'Excel',parm_tit:'roky zpět:'
          ,dsc:"Tabulka obsahuje historii sloužících párů na letních kurzech MS. <br>Poslední rok služby VPS je třeba nastavit parametrem 'roky zpět'.<br>"
      ,tit:'',fld:'',ord:''}}
    item {title:'... přehled historie skupinek'                                      skill:'yaet'
         ,par:°{typ:'slouzici2',xls:'Excel',parm_tit:'roky zpět:'
          ,dsc:"Tabulka ukazuje pro jednotlivé VPS účast jejich skupinek na obnovách. <br><br>Parametr 'roky zpět' je třeba nastavit jako dvojici čísel X,Y <br>kde X určuje, které VPS nás zajímají (0=jen letošní, 1=i loňské) a Y určuje na kolik let jejich služby se díváme.<br>Zkus třeba 0,2 a pak opět klikni na ... přehled historie skupinek<br>"
      ,tit:'',fld:'',ord:''}}
    item {title:' ... sloužící VPS/PPS (jednotlivci)'                                skill:'yae;fae;cae#crz'
         ,par:°{typ:'slouzici',podtyp:'osoby',xls:'Excel',parm_tit:'roky zpět:'
          ,dsc:"Tabulka obsahuje historii sloužících párů na letních kurzech MS. <br>Poslední rok služby VPS je třeba nastavit parametrem 'roky zpět'.<br>"
      ,tit:'',fld:'',ord:''}}
    // pečouni
    item {title:'pečovatelé na LK MS'                                                skill:'yae;fae;cae#crz'
         ,par:°{typ:'pecujici',rok:5,xls:'Excel',parm_tit:'roky zpět:'
          ,dsc:"Tabulka obsahuje historii pečovatelů na letních kurzech MS. <br>Poslední rok pečovatele je třeba nastavit parametrem 'roky zpět'.<br>1.školení je rok první účasti na akci typu 'pečovatelé'.<br>"
      ,tit:'',fld:'',ord:''}}
    // účastníci akcí
    item {title:'adresy účastníků'  ,par:°{typ:'adresy',rok:0,xls:'Excel',/*pdf:'Štítky',*/parm_tit:'roky zpět:'
      ,dsc:'Seznam obsahuje účastníky akcí v posledních letech (parametr určuje počet let zpět, default=0 tj. jen letošní).<br>Kliknutí na číslo ve sloupci <b>ID osoby</b> zobrazí první osobu na kartě EVIDENCE.<br>'
      ,tit:'jména:30,ulice:20,psč:6,obec:20,stát:6,(příjmení):15,(aktivní členi):20,(poslední akce):80,(mrop):7,(PPS):7,(akt.členů):11,(ID osoby):10'
      ,fld:'jmena,ulice,psc,obec,stat,prijmeni,_cleni,akce,_mrop,_pps,_clenu,id_osoba',ord:'prijmeni'}}
    item {title:'... bez PSČ'  ,par:°{typ:'adresy',rok:0,xls:'Excel',/*pdf:'Štítky',*/parm_tit:'roky zpět:'
      ,dsc:'Seznam obsahuje účastníky akcí v posledních letech (parametr určuje počet let zpět, default=0 tj. jen letošní).<br>Kliknutí na číslo ve sloupci <b>ID osoby</b> zobrazí první osobu na kartě EVIDENCE.<br>'
      ,tit:'jména:30,ulice:20,psč:6,obec:20,stát:6,(příjmení):15,(aktivní členi):20,(poslední akce):80,(mrop):7,(PPS):7,(akt.členů):11,(ID osoby):10'
      ,fld:'jmena,ulice,psc,obec,stat,prijmeni,_cleni,akce,_mrop,_pps,_clenu,id_osoba',ord:'prijmeni',cnd:"_psc=''"}}
    item {title:'... s PSČ ale bez státu'  ,par:°{typ:'adresy',rok:0,xls:'Excel',/*pdf:'Štítky',*/parm_tit:'roky zpět:'
      ,dsc:'Seznam obsahuje účastníky akcí v posledních letech (parametr určuje počet let zpět, default=0 tj. jen letošní).<br>Kliknutí na číslo ve sloupci <b>ID osoby</b> zobrazí první osobu na kartě EVIDENCE.<br>'
      ,tit:'jména:30,ulice:20,psč:6,obec:20,stát:6,(příjmení):15,(aktivní členi):20,(poslední akce):80,(mrop):7,(PPS):7,(akt.členů):11,(ID osoby):10'
      ,fld:'jmena,ulice,psc,obec,stat,prijmeni,_cleni,akce,_mrop,_pps,_clenu,id_osoba',ord:'prijmeni',cnd:"_psc!='' AND _stat IN ('','?')"}}
    item {title:'všichni evidovaní s mailem'                                          skill:'cae#crz'
       ,par:°{typ:'maily',podtyp:'osoby',xls:'Excel',parm_tit:''
       ,dsc:"Tabulka pro export jmen a emailových adres"
       ,tit:'',fld:'',ord:''}}
  }
  menu d {title:'Dotazníky LK YMCA Setkání',type:'group', _sys:'*' skill:'yaet', active:no
    item {title:'[fa-calendar] volba roku dotazníku'            par:{typ:'r'} }
    item i {title:'[fa-ban] (re)import dotazníku'               format:'d'                 skill:'m'
      func onclick (i:ezer) { var y:object
        mapa.close(1);
        panel.display(2,'i');
        show_dotazy();
        info.header(i);
        if (!confirm(`Mám přepočítat dotazník roku ${dot.rok}?`)) return;
        clear();
        // na NTB umožni smazat dotazník
        if (!sys('ezer','server') && dot.rok==2022 
            && confirm(`Mám před importem vymazat všechny hodnoty roku ${dot.rok}?`)) {
          php.query(`DELETE FROM dotaz WHERE dotaznik=${dot.rok}`);
        }
        y= php.dot_import(dot.rok);
        if (y.war) warning(y.war); 
        if (y.err) alert(y.err); 
        info.fill('',y.html) 
    }}
    item {title:'[fa-balance-scale] skutečnost & dotazníky' par:{typ:'2'} }
    item q {title:'[fa-cogs] různé výběry a srovnání'       par:{typ:'v'} }
    item d {title:'[fa-forward] procházení dotazníku'           par:{typ:'d',id:'1'} 
      func onclick (i:ezer) { 
        clear(); mapa.close(1); show_dotazy(); panel.display(2,'dp|do'); obraz.header(i);
        if (has_skill('mš')) panel.display(1,'dt')
        obraz.display(0,'x'); obraz.display(1,i.par.typ);
        obraz.display(obraz.r2021,'r21'); obraz.enable(0,'r21'); obraz.display(0,'r21u');
        dot.index= 'id'; 
        if (dot.id) {
          dot.Show(0,1,1) 
        }
        else {
          dot.id= i.par.id; 
          dot.Show(-2,0,0) 
        }
      }
    }
    item {title:'[fa-forward] procházení ... s doplněním'       par:{typ:'d',id:'1'}       skill:'a'
      func onclick (i:ezer) { 
        clear(); mapa.close(1); show_dotazy(); panel.display(2,'dp|do'); obraz.header(i);
        if (has_skill('mš')) panel.display(1,'dt')
        obraz.display(0,'x'); obraz.display(1,i.par.typ);
        obraz.display(obraz.r2021,'r21'); obraz.enable(1,'r21');
        dot.index= 'id'; dot.id= i.par.id; 
        dot.Show(-2,0,0) 
      }
    }
//    item {title:'[fa-forward] procházení duplicit roku 2021'    par:{typ:'d',id:'1'} 
//      func onclick (i:ezer) { 
//        clear(); mapa.close(1); show_dotazy(); panel.display(2,'dp|do'); obraz.header(i);
//        if (has_skill('mš')) panel.display(1,'dt')
//        obraz.display(0,'x'); obraz.display(1,i.par.typ);
//        obraz.display(obraz.r2021,'r21'); obraz.enable(0,'r21'); obraz.display(0,'r21u');
//        dot.index= 'id'; dot.id= i.par.id; 
//        obraz.cond= `dotaznik IN (0,2021) AND FIND_IN_SET(id,(
//  SELECT GROUP_CONCAT(duplicita)
//  FROM dotaz
//  WHERE dotaznik=2021 AND duplicita!=''
//))`;
//        dot.Show(-2,0,0) 
//      }
//    }
    item {title:'[fa-edit] úpravy odpovědí zvoleného roku'       par:{typ:'c'}             skill:'a'} 
    func onclick (i:ezer) { var x: object, y: object
      clear(); mapa.close(1); 
      panel.display(2,'do'); 
      obraz.display(0,'x'); obraz.display(1,i.par.typ); obraz.display(0,'r21|r_21'); obraz.header(i);
      switch(i.par.typ) {
        case 'c': // korekce odpovědí 
          if (!obraz.rok) { alert('musí být vybrán jediný rok'); return; }
          panel.display(1,'dc'); corr.Init(); 
          break;
        case 'r': // volba roku
          x= php.dot_roky(); obraz.zvoleny_rok.selects(x.roky); obraz.zvoleny_rok= dot.rok; break;
        case '2': // srovnání s answerem
          x= php.dot_prehled(dot.rok,{zdroj:'akce',par1:'rok'}); 
          y= php.dot_prehled(dot.rok,{zdroj:'dotaz',par1:'rok',know:x.know}); 
          obraz.text= `<table style='z-index:2;position:absolute'><tr>
                  <td>${x.html}</td><td>${y.html}</tr></table>`; 
          break;
        case 'v': // výběry
//          chart.Init(); // jen pro ladění
          obraz.recond(); //obraz.rechoice(); 
          break;
        case 'd': // procházení
//          if (obraz.rok && corr.id) dot.id= corr.id;
          break;
      };
    }
  }
  proc onclick (i) { var ret: object
    mapa.close(1);
    panel.display(2,'i');
    menu_item.set(i); menu_par.set(i.par); clear;
    the_vypis.set(conc(i.owner.title,' - ',i.title));
    [ i.par.xls; info.ex_xls.display(1); info.ex_xls.set(i.par.xls) | info.ex_xls.display(0) ];
    [ i.par.pdf; info.ex_pdf.display(1); info.ex_pdf.set(i.par.pdf) | info.ex_pdf.display(0) ];
    [ //echo(i.par.parm_tit,info.parm.get); 
      i.par.parm_tit; info.display(1,'p');
      info.parm_tit.set(i.par.parm_tit); menu_par.set(info.parm.get,'parm') | info.display(0,'p') ];
    info.fill(the_vypis.get,i.par.dsc); info.msg.set('');
    { eq(i.par.typ,'ms_struktura'); ret.set(ask('sta2_struktura',my_org.get,i.par,the_title.get));
    | eq(i.par.typ,'ms_cesty'); ret.set(ask('sta2_cesty',my_org.get,i.par,the_title.get));
    | ret.set(ask('sta2_sestava',my_org.get,the_vypis.get,menu_par.get));
    };
    info.append(ret.html);
  }
}
func show_obraz(i) { // přes odkaz z PHP vygenerovaný voláním Spy
  if (dot.index=='pg') dot.pg= i; else dot.id= i;
  dot.Show(0,0,1); 
}
proc show_dotazy() {
  function('group',"group._fold(); return 1;",m.s);
  function('group',"group._fold(); return 1;",m.l);
}
use dot: form _dot [10,36,,] { tag:'dp' format:'n' }
use obraz: form _obraz [12,4,,]   { tag:'do' format:'n' }
use chart: form _chart [702,33,,] { tag:'do' format:'n' }
use tipy:  form _tipy  [340,120,,] { tag:'dt' format:'n' }
use corr:  form _corr  [12,4,,] { tag:'dc' format:'n' }
use info: form right   [12,4,,]   { tag:'i'
  label parm_tit [-200,9,100,] { tag:'p', style:'zIndex:2;color:white', format:'rn' }
  field parm [-150,5,40,] { tag:'p', style:'zIndex:2', format:'rtn' }
  button ex_pdf [-80,5,,] { title:'PDF...', style:'zIndex:2', format:'n',
    proc onclick() {
    }
  }
  button ex_xls [-20,5,,] { title:'Excel', style:'zIndex:2', format:'n',
    proc onclick() { var res: object
      switch(this.get,
      'Excel',{
        res.set(ask('sta2_excel',my_org.get,the_vypis.get,menu_par.get));
        form.msg.set(res.html);
      })
    }
  }
}
# -----------------------------------------------------------------------------------------==> chart
# grafické zobrazení hodnot dotazníku 
form _chart { //   název:y:graf:z
  var grafy= "-:-;
          Přispěl kurz ... tvému manželství?:prinos:pie:prinos;
          Jaká témata bys chtěl slyšet:temata:pie:temata;
          Hodnocení přednášek:prednasky:pie:0-5;
          Hodnocení skupinek:skupinky:pie:0-5;
          byly skupinky lepší než přednášky?:
            CASE WHEN skupinky<prednasky THEN 1 WHEN skupinky=prednasky THEN 2 ELSE 3 END:pie:1-3;
          Hodnocení duchovna:duchovno:pie:0-5;
          Hodnocení ubytování:ubytovani:pie:0-5;
          Hodnocení stravy:strava:pie:0-5;
          Hodnocení péče o děti:pecedeti:pie:0-5;
          Hodnocení motta:motto:pie:0-5;
          Hodnocení maturity:maturita:pie:0-5;
          Hodnocení hudby:hudba:pie:0-5;
          ",
      graf: array, graf_tit: array, graf_y: array, graf_z: array
  func Init() { var g:object
    container= `<div id="chart-dotazniky" style="width:400px; height:300px;"></div>`;
    g= php.chart_typs(grafy);
    vyber.selects(g.sel);
    graf= g.graf; graf_tit= g.tit; graf_y= g.y; graf_z= g.z; 
  }
  func Show() { var y:object, par:object //typ,ceho
    if (!vyber.key()) return;
    if (!dot.celkem) { js.highcharts_clear('chart-dotazniky'); return; }
    par= {graf:graf[vyber.key()], y:graf_y[vyber.key()], z:graf_z[vyber.key()], 
        dotaznik:obraz.cond, title:graf_tit[vyber.key()]};
    y= php.chart_akce2(par);
    if (y.err) alert(y.err)
    else { js.highcharts_show(y.chart,'chart-dotazniky'); warning(y.note); }
  }
  select vyber [50,6,300,] { title:'graf: ', format:'t' 
    func onchanged() { Show(); }}
  label container [0,25,400,400]
  
}
# -----------------------------------------------------------------------------------------==> obraz
# zobrazení dotazníku zvoleného ve form dot
form _obraz [,,700,50] { 
  var rok=0   // pokud je zvolen právě jeden rok tak ten, jinak 0
  const D=320
  label text2 [0,85,320,800] { style:"overflow:auto;z-index:4"  }
  label img [D+2,28,485,700] { title:'' }
  label head [0,0,1089,30]  { title:'' }
  label note [0,508,320,200] { style:"overflow:auto" }
  label text [0,28,700,700] 
  // extrakce informace spec. pro hnízda
  field nazor_kurz   [350,260,20,] {tag:'r21', format:'r:e'
      title:'-  preference velikosti kurzu: - komornější, + velký'}
  field nazor_online [350,290,20,] {tag:'r21', format:'r:e', 
      title:'-  online přednášky: - raději ne, + nevadily'}
  field nazor_cas    [350,320,20,] {tag:'r21', format:'r:e', 
      title:'-  času bylo: - málo, + dostatek'}
  check nazor_ok     [350,350,200,] {tag:'r21', title:' nic mi nevadilo'}
  label nazor_zapsal [416,384,200,] {tag:'r21u'}
  button [350,380,,] {tag:'r21u', title:'[fa-save] Ulož]'
    func onclick() {
      php.dot_nazory(2021,dot.id,
          {nazor_kurz:nazor_kurz,nazor_online:nazor_online,nazor_cas:nazor_cas,nazor_ok:nazor_ok});
      form.plain();
  }}
  // volba roku dotazníku
  select zvoleny_rok [100,60,100,] { tag:'x,r' title:'LK roku' value:'2021' format:'t'
    func onchanged() { 
      dot.rok= this; 
      dot.Init();
  }}
  // výběrové podmínky
  label [0,40,80,] { tag:'x,v' title:'volba roků:' }
  check r2017  [80,35,50,] { tag:'x,v' title:'2017' format:'t' func onchange() { recond() }}
  check r2018  [131,35,50,] { tag:'x,v' title:'2018' format:'t' func onchange() { recond() }}
  check r2019 [182,35,50,] { tag:'x,v' title:'2019' format:'t' func onchange() { recond() }}
  check r2021 [233,35,50,] { tag:'x,v', title:'2021', format:'t' func onchange() { recond() }}
  check r2022 [284,35,50,] { tag:'x,v', title:'2022', value:'1', format:'t' func onchange() { recond() }}
  // výběr hnízd
  label [0,65,80,] { tag:'x,v,r_21' title:'výběr hnízda:' }
  check hnizdo_1 [80,60,70,] { tag:'x,v,r_21' title:'Kroměříž' format:'t' func onchange() { recond() }}
  label hnizdo_1p [100,80,20,14] { tag:'x,v,r_21' }
  check hnizdo_2 [151,60,70,] { tag:'x,v,r_21' title:'Olomouc' format:'t' func onchange() { recond() }}
  label hnizdo_2p [171,80,20,14] { tag:'x,v,r_21' }
  check hnizdo_3 [222,60,70,] { tag:'x,v,r_21' title:'Albeřice' format:'t' func onchange() { recond() }}
  label hnizdo_3p [242,80,20,14] { tag:'x,v,r_21' }

  label [D+20,35,100,] { tag:'x,v' title:'<b>Statistika</b>' }
  check x_sex [D+20,50,90,] { tag:'x,v' title:'sex' format:'t' func onchange() { recond() }}
  radio r_sex [D+100,52,200,20] { tag:'x,v' value:'0' format:'t'
    case [0,0,50,18] { title:'muž' value:'0' }
    case [50,0,50,18] { title:'žena' value:'1' }
    func onchange() { recond() }
  }
  check x_vek [D+20,70,90,] { tag:'x,v' title:'věk' format:'t' func onchange() { recond() }}
  radio r_vek [D+100,72,200,20] { tag:'x,v' value:' 1 AND 30' format:'t'
    case [  0,0,50,18] { title:'...30' value:' 1 AND 30' }
    case [ 50,0,50,18] { title:'31-40' value:'31 AND 40' }
    case [100,0,50,18] { title:'41-50' value:'41 AND 50' }
    case [150,0,50,18] { title:'51-60' value:'51 AND 60' }
    case [200,0,50,18] { title:'61...' value:'61 AND 99' }
    func onchange() { recond() }
  }
  check x_det [D+20,90,90,] { tag:'x,v' title:'dětí/LK' format:'t' func onchange() { recond() }}
  radio r_det [D+100,92,200,20] { tag:'x,v' value:'0 AND 0' format:'t'
    case [  0,0,50,18] { title:'0' value:'0 AND 0' }
    case [ 50,0,50,18] { title:'1' value:'1 AND 1' }
    case [100,0,50,18] { title:'2' value:'2 AND 2' }
    case [150,0,50,18] { title:'3...' value:'3 AND 99' }
    case [200,0,50,18] { title:'1...' value:'1 AND 99' }
    func onchange() { recond() }
  }
  check x_man [D+20,110,90,] { tag:'x,v' title:'manželství' format:'t' func onchange() { recond() }}
  radio r_man [D+100,112,200,20] { tag:'x,v' value:'0 AND 5' format:'t'
    case [  0,0,50,18] { title:' ...5' value:'0 AND 5' }
    case [ 50,0,50,18] { title:' 6-10' value:'6 AND 10' }
    case [100,0,50,18] { title:'11-20' value:'11 AND 20' }
    case [150,0,50,18] { title:'21-30' value:'21 AND 30' }
    case [200,0,50,18] { title:'31...' value:'31 AND 99' }
    func onchange() { recond() }
  }
  check x_nov [D+20,130,90,] { tag:'x,v' title:'účast' format:'t' func onchange() { recond() }}
  radio r_nov [D+100,132,200,20] { tag:'x,v' value:'1' format:'t'
    case [0,0,50,18] { title:'noví' value:'1' }
    case [50,0,80,18] { title:'víckrát' value:'0' }
    func onchange() { recond() }
  }
  label [D+20,165,100,] { tag:'x,v' title:'<b>Přínos</b>' }
  check x_pri [D+20,180,90,] { tag:'x,v' title:'přínos' format:'t' func onchange() { recond() }}
  radio r_pri [D+100,182,200,20] { tag:'x,v' value:'1' format:'t'
    case [0,0,50,18] { title:'význ.' value:'1' }
    case [50,0,80,18] { title:'část.' value:'2' }
    case [100,0,80,18] { title:'nevím' value:'3' }
    case [150,0,80,18] { title:'není' value:'4' }
    case [200,0,80,18] { title:'horší' value:'5' }
    func onchange() { recond() }
  }
  label [D+20,220,100,] { tag:'x,v,r_21' title:'<b>LK 2021</b>' }
  check x_r21 [D+20,235,61,] { tag:'x,v,r_21' title:'z textů' format:'t' func onchange() { recond() }}
  radio r_r21 [D+100,237,200,40] { tag:'x,v,r_21' value:'1' format:'t'
    case [0,0,74,18]   { title:'komorní+' value:'k+' }
    case [75,0,80,18]  { title:'komorní-' value:'k-' }
    case [147,0,80,18] { title:'přenosy+' value:'p+' }
    case [215,0,80,18] { title:'přenosy-' value:'p-' }
    case [0,20,80,18] { title:'čas+'     value:'c+' }
    case [75,20,80,18] { title:'čas-'     value:'c-' }
    case [147,20,80,18] { title:'nic nevadilo' value:'ok' }
    func onchange() { recond() }
  }
  check x_patt [D+20,300,50,] { tag:'x,v' title:'' format:'t' func onchange() { recond() }}
  select patt [D+50,300,120,] { tag:'x,v' title:'^výběr vzorů textů' format:'t'
    func Init() {
      patt.selects('-:0,přednášky:1,VPS:2,čas:3,kohout;-):4,opravené dotazníky:5,vzor =>:9');
      patt.key(0);
    }
    func onchanged() { recond() }
  }
  field patt2 [D+180,300,157,] { tag:'x,v' title:'^napiš vzor textu' format:'t' 
    func onchanged() { recond() }
  }
  check x_odkud [D+20,350,50,] { tag:'x,v' title:'znám' format:'t' func onchange() { recond() }}
  check x_proc  [D+20,370,50,] { tag:'x,v' title:'proč' format:'t' func onchange() { recond() }}
  check x_libi  [D+20,390,50,] { tag:'x,v' title:'líbí' format:'t' func onchange() { recond() }}
  check x_vadi  [D+20,410,50,] { tag:'x,v' title:'vadí' format:'t' func onchange() { recond() }}
  check x_vzkaz [D+20,430,50,] { tag:'x,v' title:'vzkaz' format:'t' func onchange() { recond() }}
  check x_tema  [D+20,450,50,] { tag:'x,v' title:'téma' format:'t' func onchange() { recond() }}
  check x_prinos [D+20,470,57,] { tag:'x,v' title:'přínos' format:'t' func onchange() { recond() }}
  edit cond [D+80,350,260,136] { tag:'x,v' title:'^výběr jako SQL' value:'1' format:'t'
    func onchanged() { rechoice(); chart.Show() }}
  // =====================> funkce
  func recond() { var roky:text, hnizda:text, sub:text, carek:number
    // výběr roků
    roky= `0${r2017?',2017':''}${r2018?',2018':''}${r2019?',2019':''}${r2021?',2021':''}${r2022?',2022':''}`;
    // je vybrán jen jeden rok?
    carek= function('str','return (str.match(/,/g) || []).length;',roky);
    rok= carek==1 ? substr(roky,2) : 0;
//    echo(`carek=${carek}, rok=${rok}`);
    // výběr hnízda
    hnizda= '';
    if (obraz.r2021) { 
      hnizda= `${hnizdo_1?',1':''}${hnizdo_2?',2':''}${hnizdo_3?',3':''}`;
    }
    else {
      hnizdo_1= 0; hnizdo_2= 0; hnizdo_3= 0; 
    }
    // sestavení dotazu
    cond= conc(`dotaznik IN (${roky}) AND duplicita='' `,
      hnizda ? ` AND IF(dotaznik=2021,hnizdo IN (99${hnizda}),1)` :'',
      x_sex  ? ` AND sex=${r_sex}` : '',
      x_vek  ? ` AND vek BETWEEN ${r_vek}` : '',
      x_det  ? ` AND deti BETWEEN ${r_det}` : '',
      x_man  ? ` AND manzel BETWEEN ${r_man}` : '',
      x_nov  ? ` AND novic=${r_nov}` : '',
      x_pri  ? ` AND prinos=${r_pri}` : ''
    );
    // doplnění roku 2021
    form.display(r2021,'r_21');
    if (x_r21) {
      cond= conc(cond,
        r_r21=='ok' ? ' AND nazor_ok=1' : '',
        r_r21=='k+' ? ' AND nazor_kurz<0' : '',
        r_r21=='k-' ? ' AND nazor_kurz>0' : '',
        r_r21=='p+' ? ' AND nazor_online>0' : '',
        r_r21=='p-' ? ' AND nazor_online<0' : '',
        r_r21=='c+' ? ' AND nazor_cas>0' : '',
        r_r21=='c-' ? ' AND nazor_cas<0' : ''
      )
    }
    // doplnění textových vzorů
    if (x_patt && patt.key()) {
      if (patt.key()==5) {
        cond= conc(cond,` AND warning!=''`); 
      }
      else {
        switch (patt.key()) {
          case 1: sub= "'predn|předn'"; break;
          case 2: sub= "'vps|pps'"; break;
          case 3: sub= "'čas[^tn]'"; break;
          case 4: sub= "'kohout'"; break;
          case 9: sub= patt2 ? `'${patt2}'` : "'.'"; break;
        }
        cond= conc(cond,' AND (0',
          x_odkud ? ` OR od_jine_text RLIKE ${sub}` : '',
          x_proc  ? ` OR proc_jine_text RLIKE ${sub}` : '',
          x_libi  ? ` OR libilo RLIKE ${sub}` : '',
          x_vadi  ? ` OR vadilo RLIKE ${sub}` : '',
          x_vzkaz ? ` OR vzkaz RLIKE ${sub}` : '',
          x_tema  ? ` OR tema_jine_text RLIKE ${sub}` : '',
          x_prinos? ` OR prinos_text RLIKE ${sub}` : '',
        ')');
      }
    }
    rechoice();
    chart.Show();
    dot.dirty= 1;
  }
  func rechoice() { var x:object
    x= php.dot_vyber({cond:cond}); 
    dot.celkem= x.celkem;
    copy_by_name(x.r21,obraz);
    obraz.fill2('',x.html,'')
  }
  func header(i:ezer) { var own:ezer
    own= i.owner();
    head= replace_fa(`<div class='karta'>${own.title} - ${i.title}</div>`);
    text= '';
    text2= '';
    img= '';
  }
  proc fill(x,y,z) {
    note.set(''); text2.set(''); img.set('');
    [ x; head.set(conc("<div class='karta'>",x,"</div>")) ];
    [ y; text.set(y) ];
    [ z; img.set(z) ];
  }
  proc fill2(x,y,z) {
    note.set(''); text.set('');
    [ x; head.set(conc("<div class='karta'>",x,"</div>")) ];
    [ y; text2.set(y) ];
    [ z; img.set(z) ];
  }
  proc append(x) { note.set(x) }
}
# -------------------------------------------------------------------------------------------==> dot
# posuny v dotaznících
form _dot [,,163,60] { css:'parm shadow' style:'z-index:2'  //style:'position:fixed;z-index:2' 
  var rok=2022,   // rok dotazníku ... 2022
      kurz:object,
      back=0,     // návrat na poslední kliknuté (ne odkazem)
      dirty=1,    // po změně dotazu, čistí se po prvním Show
      virgin=1
  func Init() { 
    if (!has_skill('yaet')) return;
    js.highcharts_load();
    chart.Init();
    if (has_skill('a')) m.d.i.enable(1);
    m.d.title= `Dotazník LK YS roku ${rok}`; 
    kurz= {rok:rok};
    back= 0;
    if (virgin) { 
      obraz.patt.Init(); obraz.init(); obraz.cond= 1; dirty= 1; obraz.recond(); obraz.rechoice(); 
      virgin= 0; 
    }
  }
  // ==> . Show
  func Show(offset,go_back,even) { var y:object
    clear(); tipy.Display(0);
    y= php.dot_show(vyber ? 0 : rok, index, index=='page' ? pg : id, offset,
        vyber ? obraz.cond : 1, dot.dirty, rok);
    copy_by_name(y.nazory,obraz);
    if (!y.none || even) {
      pg= y.page; id= y.id; 
      if (y.rok) rok= y.rok;
      if (go_back) back= index=='page' ? pg : id; 
      obraz.fill2('',' ',' ');
      obraz.fill2('',y.html,y.jpg);
      dot.dirty= 0;
    }
  }
  radio index [50,2,60,50] { format:'t', value:'id'
    case  [10,0,10,10] { title:' ' value:'page' }
    case [30,0,10,10] { title:' ' value:'id' }
  }
  label celkem [10,4,49,] { style:'color:white' }
  button [171,6,,] { title:"[fa-question]" help:'ukázat tipy' skill:'mš|mš'  
    func onclick() { Show(0,1,1); tipy.Spy(); }}
  button [191,6,,] { title:"[fa-repeat]" help:'přepočítat tabulku' skill:'mš|mš'  
    func onclick() { php.dot_spy_data(rok); Show(0,1,1); tipy.Spy(); }}
  check vyber [112,1,49,] { title:'výběr' value:'1' 
    proc onchange() { dot.dirty.set(1); back.set(0); Show(0,0,0) }
  }
  button [9,32,,] { title:"[fa-step-backward]" help:'první'     func onclick() { Show(-2,1,0); }}
  button [29,32,,] { title:"[fa-caret-left]" help:'předchozí'   func onclick() { Show(-1,1,0); }}
  field pg [50,32,25,] { title:'^pdf' format:'rt', value:'0', func onchanged() { index.set('page'); Show(0,1,1); }} 
  field id [80,32,25,] { title:'^xls' format:'rt', value:'0', func onchanged() { index.set('id');   Show(0,1,1); }} 
  button [112,32,,] { title:"[fa-caret-right]" help:'další'     func onclick() { Show(1,1,0); }}
  button [132,32,,] { title:"[fa-step-forward]" help:'poslední' func onclick() { Show(2,1,0); }}
}
# ------------------------------------------------------------------------------------------==> tipy
# tipy identifikace respondenta
form _tipy [,,163,46] { 
  style:'z-index:7' //css:'parm shadow'
//  style:'position:fixed;z-index:2' //css:'parm shadow'
//  proc Spy() { var y:object
//    clear;
//    [ dot.kurz.get | dot.kurz.set(object('rok',dot.rok.get)) ];
//    dot.kurz.set(object('sex',sex.get,'vek',vek.get,'det',det.get,'man',man.get,'nov',nov.get),'filtr');
//    dot.kurz.set(ask('dot_spy',dot.kurz.get,dot.rok.get,dot.index.get,
//        cconc(eq(dot.index.get,'page'),dot.pg.get,dot.id.get),dot.back.get));
//    shody.set(dot.kurz.get('html'));
//  }
  func Display(on) { 
    if (!has_skill('mš')) return;
    shody.display(on);
  }
  func Spy() { 
    if (!has_skill('mš')) return;
    clear();
    shody= php.dot_spy(dot.rok,dot.id);
    Display(1);
    //    if (!dot.kurz) 
//      dot.kurz= {rok:dot.rok};
//    dot.kurz.filtr= {sex:sex,vek:vek,det:det,man:man,nov:nov};
//    dot.kurz= php.dot_spy(dot.kurz,dot.rok,dot.index,
//        dot.index=='page'?dot.pg:dot.id,dot.back);
//    shody= dot.kurz.html;
  }
//  check sex  [8,19,50,] { value:'1' format:'t' skill:'m|m' } 
//  label  [8,9,50,] { title:'sex' skill:'m|m' }
//  check vek  [33,19,50,] { value:'1' format:'t' skill:'m|m' } 
//  label  [33,9,50,] { title:'věk' skill:'m|m' }
//  check det  [58,19,50,] { value:'1' format:'t' skill:'m|m' } 
//  label  [58,9,50,] { title:'děti' skill:'m|m' }
//  check man  [83,19,50,] { value:'1' format:'t' skill:'m|m' } 
//  label  [83,9,50,] { title:'man' skill:'m|m' }
//  check nov [108,19,50,] { value:'1' format:'t' skill:'m|m' } 
//  label [108,9,50,] { title:'nový' skill:'m|m' }
//  button [133,19,22,] { title:"[fa-eye]" help:'špión' skill:'m|m' proc onclick() { Spy }}
  label shody [0,0,163,280] { /*style:'background:silver', css:'parm shadow',*/ format:'n' }
}
# ------------------------------------------------------------------------------------------==> corr
# opravy hodnot v dotazníku - např. obrácená hodnocení
form _corr [,,163,400] { 
  const w=750, a= 100, o= 126, j= 160, h= 230, s=270, t=446, p= 555
  var id_min:number, id_max:number
  view d: table dotaz
  func Init() { 
    id_min= php.select('id','dotaz',`dotaznik=${dot.rok} AND duplicita='' ORDER BY id LIMIT 1`);
    id_max= php.select('id','dotaz',`dotaznik=${dot.rok} AND duplicita='' ORDER BY id DESC LIMIT 1`);
    id= dot.id ? dot.id : id_min;
    Show(0);
  }
  func Show(ofset) { var y:object
    editujeme= `upravujeme rok <big><b>${dot.rok}</b></big>`;
    y= php.dot_show(dot.rok,'id',id,ofset,1,0,dot.rok);
    id= y.id;
    obraz.fill2('',y.html,y.jpg);
    form.load(0,`dotaznik=${dot.rok} AND id=${id}`);
  }
  // pohyb
  label [w+67,1,229,48] { css:'parm' }
  label editujeme [w+76,6,200,16] { style:'color:white'}
  button [w+76,23,,] { title:"[fa-step-backward]" help:'první'    proc onclick() { Show(-2); }}
  button [w+96,23,,] { title:"[fa-caret-left]" help:'předchozí'   proc onclick() { Show(-1); }}
  field id [w+114,23,25,] { format:'rt', value:'0'                proc onchanged() { Show(0); }} 
  button [w+146,23,,] { title:"[fa-caret-right]" help:'další'     proc onclick() { Show(1); }}
  button [w+166,23,,] { title:"[fa-step-forward]" help:'poslední' proc onclick() { Show(2); }}
  // zápis
  button [w+200,23,,] { title:"[fa-save] Ulož změnu" 
    proc onclick() { form.save(); Show(0); }
  }
  // poznámka - warning
  field [w+90,63,220,15] {title:"<span style='color:red'>Důvod úpravy </span>", data:d.warning}
  // statistika
  label [w+40,a+4,47,15] {title:'Statistika'}
  field [w+90,a,20,] {title:'^žena', format:'cf', data:d.sex}
  field [w+148,a,20,] {title:'^věk', format:'cf', data:d.vek}
  field [w+185,a,20,] {title:'^děti', format:'cf', data:d.deti}
  field [w+220,a,20,] {title:'^manžel.', format:'cf', data:d.manzel}
  field [w+281,a,20,] {title:'^poprvé', format:'cf', data:d.novic}
  // odkud
  label [w+38,o+8,49,15] {title:'Odkud ...'}
  check [w+90,o,20,]  {title:'přa', data:d.od_pratele}
  check [w+115,o,20,] {title:'par', data:d.od_partner} 
  check [w+140,o,20,] {title:'pří', data:d.od_pribuzni} 
  check [w+165,o,20,] {title:'peč', data:d.od_pecoun} 
  check [w+190,o,20,] {title:'inz', data:d.od_inzerce} 
  check [w+215,o,20,] {title:'chl', data:d.od_chlapi}
  check [w+240,o,20,] {title:'ymc', data:d.od_ymca}
  check oj [w+265,o,20,] {data:d.od_jine, format:'n'}
  field [w+265,o+4,46,] {data:d.od_jine_text
    func onchange() { oj= this ? 1 : 0; oj.change(); }} 
  // protože
  label [w+27,j+11,61,15] {title:'Přihláška ...'}
  check [w+90,j+5,27,] {title:'lep', data:d.proc_zlepsit} 
  check [w+115,j+5,27,] {title:'kri', data:d.proc_krize}
  check [w+140,j+5,27,] {title:'opa', data:d.proc_opak}
  check jj [w+165,j,27,] {data:d.proc_jine, format:'n'}
  field [w+165,j+7,147,] {data:d.proc_jine_text
    func onchange() { jj= this ? 1 : 0; jj.change(); }} 
  // hodnocení
  label [w+32,h+4,55,15] {title:'Hodnocení'}
  field [w+ 90,h,20,] {title:'^pře', format:'cf', data:d.prednasky}
  field [w+115,h,20,] {title:'^sku', format:'cf', data:d.skupinky}
  field [w+140,h,20,] {title:'^duc', format:'cf', data:d.duchovno}
  field [w+165,h,20,] {title:'^uby', format:'cf', data:d.ubytovani}
  field [w+190,h,20,] {title:'^str', format:'cf', data:d.strava}
  field [w+215,h,20,] {title:'^peč', format:'cf', data:d.pecedeti}
  field [w+240,h,20,] {title:'^mot', format:'cf', data:d.motto}
  field [w+265,h,20,] {title:'^mat', format:'cf', data:d.maturita}
  field [w+290,h,20,] {title:'^hud', format:'cf', data:d.hudba}
  // slovně
  edit [w+90,s,220,40] {title:'Líbilo se mi', data:d.libilo}
  edit [w+90,s+45,220,40] {title:'Vadilo mi', data:d.vadilo}
  edit [w+90,s+90,220,40] {title:'Vzkaz týmu', data:d.vzkaz}
  // témata
  label [w+47,t+8,39,15] {title:'Témata'}
  check [w+90,t,20,]  {title:'mal', data:d.tema_male}
  check [w+115,t,20,] {title:'dos', data:d.tema_dosp} 
  check [w+140,t,20,] {title:'mat', data:d.tema_matka} 
  check [w+165,t,20,] {title:'ote', data:d.tema_otec} 
  check [w+190,t,20,] {title:'mez', data:d.tema_mezigen} 
  check [w+215,t,20,] {title:'duc', data:d.tema_duchovni}
  check tj [w+240,t,20,] {data:d.tema_jine, format:'n'}
  field [w+240,t+4,67,] {data:d.tema_jine_text
    func onchange() { tj= this ? 1 : 0; tj.change(); }} 
  // přínos
  field [w+90,p,20,] {title:'Přínos ', format:'cf', data:d.prinos}
  edit [w+90,p+22,220,40] {title:'... slovně', data:d.prinos_text}
  // struktura tabulky DOTAZ
  table dotaz { key_id:'id_dotaz'
    number id
    text duplicita // seznam duplicitních klíčů které budou vynechány ze statistik
    number sex
    number vek
    number deti
    number manzel
    number novic
    number od_pratele
    number od_partner
    number od_pribuzni
    number od_pecoun
    number od_inzerce
    number od_chlapi
    number od_ymca
    number od_jine
    text od_jine_text
    number proc_zlepsit
    number proc_krize
    number proc_opak
    number proc_jine
    text proc_jine_text  
    number prednasky
    number skupinky
    number duchovno
    number ubytovani
    number strava
    number pecedeti
    number motto
    number maturita
    number hudba
    text libilo
    text vadilo
    text vzkaz
    number tema_male
    number tema_dosp
    number tema_matka
    number tema_otec
    number tema_mezigen
    number tema_duchovni
    number tema_jine
    text tema_jine_text
    number prinos
    text prinos_text
    text warning
  }
}
