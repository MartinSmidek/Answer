# ------------------------------------------------------------------------------------------------ #
# Karta pro evidenci členů                                                                         #
#                                                                                                  #
# Ans(w)er/Ezer                                     (c) 2007-2011 Martin Šmídek <martin@smidek.eu> #
# ------------------------------------------------------------------------------------------------ #

var osoby_vyber: text         // výběrová podmínka

use info: form right [5,5,,]              { tag:'inf' }
/*
use prihlasky: form _prihlasky [10,44,,]  { tag:'app' }
use prihlaska: form _prihlaska [390,44,,] { tag:'app' }
use osoba:  form _osoba [390,36,,]        { tag:'pers' }
use rodina: form _rodina [390,36,,]       { tag:'pers' }
use platby: form _platby [10,64,,]        { tag:'bank' }
use platba: form _platba [10,300,,]       { tag:'bank' }
*/
use osoby:  form _osoby  [10,10,,]        { tag:'list' }
use evid:   form _evid   [355,10,,]       { tag:'evid' }
use dary:   form _dary   [355,300,,]      { tag:'dary', format:'n' }

# záložky evidence
use p_oso:  form _osoba  [360,10,,]       { tag:'p_oso' }
use p_rod:  form _rodina [360,10,,]       { tag:'p_rod' }
use p_cle:  form _rcleni [360,10,,]       { tag:'p_cle' }
use p_fir:  form _firma  [360,10,,]       { tag:'p_fir' }
use p_akc:  form _oakce  [360,10,,]       { tag:'p_akc' }

# záložky členství a darů
use d_cle:  form _dcleni [360,300,,]      { tag:'d_cle', format:'n' }
use d_pri:  form _dpris  [360,300,,]      { tag:'d_pri', format:'n' }
use d_dar:  form _ddary  [360,300,,]      { tag:'d_dar', format:'n' }

proc onstart () {
  osoby_vyber.set(1);
}

proc onfirstfocus() {
  osoby.b.browse_load;
  evid.oso.onclick;
  m.g.i.click;
}
# proc evid_show() {  }


menu m {type:'left'
  menu g {title:'Evidence YMCA Setkání',type:'group', skill:'yae'
   item i{title:'všechny kontakty'
          par:°{cond:"1"} }
    item {title:'pouze členové'
          par:°{cond:"od.ukon IN ('b','c')
                      AND NOW()>=od.dat_od AND (NOW()<=od.dat_do OR od.dat_do='0000-00-00') "} }
    item {title:'pouze činní členové'
          par:°{cond:"od.ukon='c'
                      AND NOW()>=od.dat_od AND (NOW()<=od.dat_do OR od.dat_do='0000-00-00') "} }
    item {title:'pouze členové - dlužníci', format:'d'
          par:°{cond:"od.ukon=IN ('b','c')
                      AND NOW()>=od.dat_od AND (NOW()<=od.dat_do OR od.dat_do='0000-00-00') "} }
    item {title:'pouze dárci'
          par:°{cond:"od.ukon='d'"} }
    item {title:'pouze dárci bez potvrzení'
          par:°{cond:"od.ukon='d' AND od.dat_do='0000-00-00'"} }
    proc onclick (i) {
      panel.display(0,'inf'); panel.display(1,'evid|list|dary');
      evid.page_init; dary.page_init;
      osoby.b.browse_load(conc("os.deleted='' AND ",i.par.cond));
      evid.page_show; dary.page_show;
      osoby.b.raise('onrowclick');
    }
  }
  menu {title:'Administrace evidence',type:'group', skill:'m'
    item   {title:'zrušení členství, příspěvků a darů'
      proc onclick (i) {
        confirm('opravdu?');
        panel.display(0,'list|evid|p_oso|p_rod|p_cle|p_fir|p_akc|dary|d_cle|d_dar|d_pri');
        panel.display(1,'inf');
        info.fill(conc(i.owner.title,' - ',i.title),ask('query',"TRUNCATE dar "));
      }
    }
    item   {title:'vymazání všech členství a příspěvků'
      proc onclick (i) {
        confirm('opravdu?');
        panel.display(0,'list|evid|p_oso|p_rod|p_cle|p_fir|p_akc|dary|d_cle|d_dar|d_pri');
        panel.display(1,'inf');
        info.fill(conc(i.owner.title,' - ',i.title),ask('query',"DELETE FROM dar WHERE ukon IN ('a','c','p')"));
      }
    }
    item   {title:'import členů z Googlu'
      proc onclick (i) {
        panel.display(0,'list|evid|p_oso|p_rod|p_cle|p_fir|p_akc|dary|d_cle|d_dar|d_pri');
        panel.display(1,'inf');
        info.fill(conc(i.owner.title,' - ',i.title),ask('akce_google_cleni'));
      }
    }
  }
/*
  menu {title:'Klub přátel YMCA Setkání',type:'group'
    item {title:'přihlášky z www.setkani.org'
      proc onclick (i) {
        panel.display(0,'bank|pers'); panel.display(1,'app');
        info.fill(conc(i.owner.title,' - ',i.title),' ');
      }
    }
    item {title:'import plateb z RB'
      proc onclick (i) {
        panel.display(0,'pers|app'); panel.display(1,'bank');
        info.fill(conc(i.owner.title,' - ',i.title),' ');
        info.msg.set(ask('akce_rb_platby'));
#         info.fill(conc(i.owner.title,' - ',i.title),'staré info');
        platby.b.browse_load
      }
    }
*/
}
# -------------------------------------------------------------------------------------------------- _osoby
form _osoby {
  view os: table osoba
  view ot: table tvori { join:"ON os.id_osoba=ot.id_osoba" }
  view od: table dar { join_type:'LEFT', join:"ON os.id_osoba=od.id_osoba AND od.deleted=''" }
  browse b [0,0,,] { rows:28, qry_rows:1, group_by:'os.id_osoba'
    show id_o { data:os.id_osoba }
    show id_r { data:ot.id_rodina }
    show id_d { data:od.id_dar }
    show prijmeni [,,80,] { data:os.prijmeni, title:'příjmení', format:'q*s' }
    show jmeno [,,70,] { data:os.jmeno, title:'jméno', format:'q*s' }
    show [,,95,] { title:'obec', data:os.obec, format:'q*s' }
    show nar [,,65,] { title:'rrmmdd', data:os.narozeni, sql_pipe:'sql_yymmdd', format:'q@s'
      help:'datum narození ve tvaru rrmmdd'}
    show rel [,,0,] { expr:"GROUP_CONCAT(DISTINCT od.ukon ORDER BY od.ukon SEPARATOR '')", title:'YS',
      help:'vztah k YMCA Setkání', format:'s' }
    proc onrowclick() { evid.reload; dary.reload }
    proc onclick() {
      rel.width; rel.width(0);  nar.width(65);
    |            rel.width(65); nar.width(0);
    }
  }
}
# -------------------------------------------------------------------------------------------------- _evid
form _evid { style:'z-index:1'
  var zalozka: text           // oso | rod | cle | fir | akc
  proc page_show() { tags(oso,'p_oso','p_rod|p_cle|p_fir|p_akc') }
  # --------------------------- záložky
  proc tags(a,on,off) {
    zalozka.set(on);
    form.set_css('ae_butt_off','ae_butt_on','p');
    a.set_css('ae_butt_on','ae_butt_off');
    panel.display(1,on); panel.display(0,off);
  }
  proc reload() {
    switch(zalozka.get,
    'p_oso',{ p_oso.load(osoby.b.id_o.get) },
    'p_rod',{ p_rod.load(osoby.b.id_r.get) },
    'p_cle',{ p_cle.b.browse_load(conc('ox.id_rodina=',osoby.b.id_r.get)) },
    'p_akc',{ p_akc.ucasti(osoby.b.id_o.get) },
    { stop });
  }
  proc page_init() { zalozka.set('oso');
    p_oso.init; p_rod.init; p_cle.b.browse_init; p_akc.akc.browse_init; }
  label oso [ 10,0,60,20] {tag:'p', title:'<b>Osoba</b>', format:'c', css:'ae_butt_on'
    proc onclick() { tags(this,'p_oso','p_rod|p_cle|p_fir|p_akc'); reload } }
  label rod [ 90,0,60,20] {tag:'p', title:'<b>Rodina</b>', format:'c',
    proc onclick() { tags(this,'p_rod','p_oso|p_cle|p_fir|p_akc'); reload } }
  label cle [170,0,60,20] {tag:'p', title:'<b>Členové</b>', format:'c',
    proc onclick() { tags(this,'p_cle','p_oso|p_rod|p_fir|p_akc'); reload } }
  label fir [250,0,60,20] {tag:'p', title:'<b>Firma</b>', format:'c',
    proc onclick() { tags(this,'p_fir','p_oso|p_rod|p_cle|p_akc'); reload } }
  label akc [330,0,60,20] {tag:'p', title:'<b>Akce</b>', format:'c',
    proc onclick() { tags(this,'p_akc','p_oso|p_rod|p_cle|p_fir'); reload } }
  label [0,16,400,260] { css:'ae_work'}
}
# ---------------------------------------------------------------------- _osoba
form _osoba { tag:'a'
  view o: table osoba
  view t: table tvori { join:"ON o.id_osoba=t.id_osoba" }
  view r: table rodina { join:"ON r.id_rodina=t.id_rodina" }
  # --------------------------------------------------  OSOBA
  const L=0; T=10
  field  { data:o.id_osoba }
  field  { data:t.id_tvori }
  field  { data:r.id_rodina }
#   label  [L+0,T+9,367,249] { css:'ae_part' }
#   label  [L+8,T+0,74,] { title:'osobní údaje', css:'ae_part_label' }
  label          [L+5,T+15,89,] {title:'příjmení'}
  field prijmeni [L+5,T+30,89,] {data:o.prijmeni, help:'příjmení'}
  label          [L+109,T+15,78,] {title:'jméno'}
  field jmeno    [L+109,T+30,79,] {data:o.jmeno}
  radio sex [L+200,T+15,50,35] { data:o.sex, value:'1'
    case [0,2,50,] { title:'muž', expr:'1' }
    case [0,17,50,] { title:'žena', expr:'2' }
    proc onchange() { echo('sex=',this.get) }
  }
  label          [L+256,T+16,33,] {title:'narození'}
  field narozeni [L+255,T+32,85,] {type:'date', data:o.narozeni, format:'rR:e'}
  label          [L+346,T+16,41,] {title:'nnnn rč'}
  field rodne    [L+345,T+32,37,] {data:o.rc_xxxx, format:'r:e'}
  label          [L+5,T+54,100,] {title:'ulice'}
  field ulice    [L+5,T+69,162,] {data:o.ulice}
  label          [L+179,T+54,39,] {title:'psč'}
  field psc      [L+179,T+69,40,] {data:o.psc}
  label          [L+230,T+54,100,] {title:'obec'}
  field obec     [L+229,T+69,131,] {data:o.obec}
  label          [L+13,T+101,33,] {title:'telefon'}
  field telefon  [L+51,T+97,212,] {data:o.telefon}
  label          [L+16,T+128,30,] {title:'e-mail'}
  field email    [L+52,T+124,330,] {data:o.email}
  label          [L+0,T+152,48,] {title:'poznámka'}
  edit  pozn     [L+52,T+151,329,60] {data:o.note}
}
# ---------------------------------------------------------------------- _rodina
form _rodina {
  view r: table rodina
  # --------------------------------------------------  RODINA
  const Lr= 0; Tr= 10
#   label  [Lr+0,Tr+9,367,249] { css:'ae_part' }
#   label  [Lr+8,Tr+0,74,] { title:'rodinné údaje', css:'ae_part_label' }
  label          [Lr+5,Tr+15,100,] {title:'společné příjmení'}
  label          [Lr+181,Tr+15,75,] {title:'svatba'}
  label          [Lr+297,Tr+15,43,] {title:'SPZ'}
  label          [Lr+5,Tr+54,100,] {title:'ulice'}
  label          [Lr+179,Tr+54,39,] {title:'psč'}
  label          [Lr+230,Tr+54,100,] {title:'obec'}
  label          [Lr+5,Tr+94,37,] {title:'telefon'}
  label          [Lr+5,Tr+138,33,] {title:'e-mail'}
  label          [Lr+5,Tr+179,33,] {title:'poznámka'}
  field nazev     [Lr+4,Tr+30,100,] {data:r.nazev, help:'příjmení'}
  field _spolu    [Lr+149,Tr+32,23,] {format:'o', value:'?'}
  field svatba    [Lr+178,Tr+31,36,] {data:r.svatba}
  field ulice_r   [Lr+4,Tr+69,162,] {data:r.ulice}
  field psc_r     [Lr+179,Tr+69,39,] {data:r.psc}
  field obec_r    [Lr+228,Tr+69,129,] {data:r.obec}
  field telefony  [Lr+5,Tr+109,353,] {data:r.telefony}
  field emaily    [Lr+5,Tr+153,354,] {data:r.emaily}
  edit  note      [Lr+5,Tr+192,355,60] {data:r.note}
  field datsvatba [Lr+220,Tr+31,63,] {data:r.datsvatba}
  field spz       [Lr+297,Tr+31,62,] {data:r.spz}
}
# ---------------------------------------------------------------------- _rcleni
form _rcleni {
  view ox: table rodina
  view ot: table tvori { join:"ON ox.id_rodina=ot.id_rodina AND ot.id_rodina!=1" }
  view os: table osoba { join:"USING(id_osoba)" }
  browse b [10,30,,] { rows:10, buf_rows:20, qry_rows:1
    show id_r { data:ox.id_rodina }
    show id_t { data:ot.id_tvori }
    show id_o { data:os.id_osoba }
    show [,,80,] { data:os.prijmeni, title:'příjmení', format:'q*s' }
    show [,,70,] { data:os.jmeno, title:'jméno', format:'q*s' }
    show [,,30,] { data:ot.role, title:'vztah', help:'vztah k rodině', format:'q*s' }
    proc onrowclick() { echo('onrowclick: osoba=',id_o.get,', rodina=',id_r.get) }
    proc onsubmit() { echo('onsubmit: osoba=',id_o.get,', rodina=',id_r.get);
      osoby.b.raise('onrowclick',id_o.get);
    | osoby.b.browse_seek(conc('os.id_osoba=',id_o.get));
    }
  }
}
# ---------------------------------------------------------------------- _oakce
form _oakce {
  view a: table akce
  view p: table pobyt { join:'ON a.id_duakce=p.id_akce' }
  view s: table spolu { join:'USING(id_pobyt)' }
  browse akc [10,30,,] { rows:10, qry_rows:1
    show ida { data:a.id_duakce }
    show idp { data:p.id_pobyt }
    show rok  [,, 30,] { title:'rok', expr:"YEAR(a.datum_od)", format:'q*s-' }
    show akce [,,150,] { title:'název', data:a.nazev, format:'q*s' }
    show fce  [,, 30,] { title:'fce', data:p.funkce, map_pipe:ms_akce_funkce.zkratka, format:'q*s' }
    proc onsubmit() {
      ducast.akce_show(ida.get,idp.get,conc(akce.get,' ',rok.get),'EVIDENCE') }
  }
  proc ucasti(id_osoba) {
    id_osoba; akc.browse_load(conc('a.spec=0 AND s.id_osoba=',id_osoba),"datum_od DESC")
  | akc.browse_init
  }
}
# ---------------------------------------------------------------------- _firma
form _firma {
  label [10,50,,] { title:'tady bude informace o firmě, pokud se mají dary psát na ni' }
}
# -------------------------------------------------------------------------------------------------- _dary
form _dary { style:'z-index:1'
  var zalozka: text           // cle | dar
  proc page_show() { tags(cle,'d_cle','d_pri|d_dar'); }
  # --------------------------- záložky
  proc tags(a,on,off) {
    zalozka.set(on);
    form.set_css('ae_butt_off','ae_butt_on','d');
    a.set_css('ae_butt_on','ae_butt_off');
    panel.display(1,on); panel.display(0,off);
  }
  proc reload() {
    switch(zalozka.get,
    'd_cle',{
      d_cle.form_init;
      d_cle.b.browse_load(conc("da.deleted='' AND ukon IN ('b','c') AND id_osoba=",osoby.b.id_o.get)) },
    'd_pri',{
      d_pri.form_init;
      d_pri.b.browse_load(conc("da.deleted='' AND ukon='p' AND id_osoba=",osoby.b.id_o.get)) },
    'd_dar',{
      d_dar.form_init;
      d_dar.b.browse_load(conc("da.deleted='' AND ukon='d' AND id_osoba=",osoby.b.id_o.get)) },
    { stop });
  }
  proc page_init() { zalozka.set('oso');
    d_cle.form_init; d_pri.b.browse_init; d_dar.form_init; }
  label cle [10,0,70,20] {tag:'d', title:'<b>Členství</b>', format:'c',
    proc onclick() { tags(this,'d_cle','d_pri|d_dar'); reload }}
  label pri [90,0,70,20] {tag:'d', title:'<b>Příspěvky</b>', format:'c',
    proc onclick() { tags(this,'d_pri','d_cle|d_dar'); reload }}
  label dar [170,0,70,20] {tag:'d', title:'<b>Dary</b>', format:'c',
    proc onclick() { tags(this,'d_dar','d_cle|d_pri'); reload }}
  label [0,16,400,225] { css:'ae_work'}
}
# ---------------------------------------------------------------------- _dcleni
form _dcleni {
  view da: table dar
  view do: table osoba { join:"USING(id_osoba)" }
  browse b [10,30,,] { rows:8, qry_rows:1
    show id_d { data:da.id_dar }
    show id_o { data:do.id_osoba }
    show ukon [,,10,] { data:da.ukon, title:'způsob', format:'q*s' }
    show [,,70,] { data:da.dat_od, title:'vznik', format:'rq*s' }
    show [,,70,] { data:da.dat_do, title:'ukončení', format:'rq*s' }
    proc onrowclick() { form.load(id_d.get); form_state('n|s','u|z'); }
  }
  # formulář pro úpravu členství
  # skryté položky
  field id_o { data:da.id_osoba }
  field ukon { data:da.ukon }
  field del  { data:da.deleted }
  # viditelné položky
  const Lc=200
  select typ   [Lc,40,65,] {type:'map', help:'typ členství', options:ms_akce_clen.zkratka
    proc onchange() { ukon.set(cconc(typ.key,'c','b')); ukon.change(1) }
  }
  label        [Lc,typ.t+-14,,] {title:'typ členství'}
  field datum  [Lc,75,85,] {type:'date', data:da.dat_od, help:'datum přijetí', format:'RUr'}
  label        [Lc,datum.t+-14,,] {title:'člen ode dne'}
  field konec  [Lc,110,85,] {type:'date', data:da.dat_do, help:'datum zrušení', format:'RUr'}
  label        [Lc,konec.t+-14,,] {title:'členství ukončeno dne'}
  field pozn   [Lc,145,170,] {data:da.note, help:'poznámka'}
  label        [Lc,pozn.t+-14,,] {title:'poznámka'}

  # tlačítka a obslužné procedury
  proc form_init() {
    b.browse_init; typ.plain; form.init; form_state('n','u|z|s'); }
  proc form_state(on,off) {
    form.enable(1,on); form.enable(0,off); }
  proc onchanged () {
    form_state('u|z','n|s') }
  proc onload () { typ.key(cconc(eq(ukon.get,'b'),0,1)); typ.plain }
  label cmd [Lc,175,110,50] { css:'ae_parm', style:'z-index:1' }
  # Nový
  button [cmd.l+6,cmd.t+3,,] { tag:'n', title:'Nový',  help:'Vložit nový dar',
    proc onclick() {
      form.init; form_state('u|z','n|s');
      id_o.set(osoby.b.id_o.get); id_o.change(1); del.set(''); del.change(1);
      typ.key(0); typ.change; datum.set(now); datum.change(1);
  }}
  # Uložit
  button [cmd.l+50,cmd.t+3,,] {tag:'u', title:'Uložit',  help:'Uložit změněná data',
    proc onclick() {
      form.same
    | // oprava
      form.key; form.save; form.load;
      b.browse_seek; form_state('n|s','u|z'); osoby.b.browse_row
    | // přidání
      id_o.get;
      form.insert; form.load;
      b.raise('onrowclick',b.browse_seek(conc('id_dar=',form.key))); form_state('n|s','u|z');
      osoby.b.browse_row
    | error('chyba při vkládání členství - nenastavený člen')
  }}
  # Zpět
  button [cmd.l+6,cmd.t+25,,] {tag:'z', title:'Zpět',  help:'Neměnit změněná data',
    proc onclick() {
      b.browse_count; form.load(b.id_d.get); form_state('n|s','u|z')
    | form.init; form_state('n','u|z|s')
  }}
  # Smazat
  button [cmd.l+50,cmd.t+25,,] {tag:'s', title:'Smazat',  help:'Zcela zrušit (ne ukončit) členství',
    proc onclick() {
      confirm("opravdu zrušit členství ",osoby.b.jmeno.get,' ',osoby.b.prijmeni.get,"?");
      del.set(conc('D ',sys('user','abbr'),' ',now)); del.change; form.save;
      dary.reload; osoby.b.browse_row
  }}
}
# ---------------------------------------------------------------------- _dpris
form _dpris {
  view da: table dar
  view do: table osoba { join:"USING(id_osoba)" }
  browse b [10,30,,] { rows:8, qry_rows:1
    show id_d { data:da.id_dar }
    show id_o { data:do.id_osoba }
    show [,,10,] { data:da.ukon, title:'způsob', format:'q*s' }
    show [,,70,] { data:da.castka, title:'částka', format:'rq*s' }
    show [,,70,] { data:da.dat_od, title:'zaplaceno', format:'rq*s' }
    show [,,70,] { expr:"YEAR(da.dat_do)", title:'zapl.do', format:'rq*s' }
    proc onrowclick() { form.load(id_d.get); form_state('n|s','u|z'); }
  }
  # formulář pro úpravu členských příspěvků
  # skryté položky
  field id_o { data:da.id_osoba }
  field ukon { data:da.ukon }
  field del  { data:da.deleted }
  # viditelné položky
  const Lp=270
  field castka [Lp,40,65,] {data:da.castka, help:'výše členského příspěvku', value:'100', format:'r'}
  label        [Lp,castka.t+-14,,] {title:'částka'}
  field datum  [Lp,75,85,] {type:'date', data:da.dat_od, help:'datum přijetí příspěvku', format:'RUr'}
  label        [Lp,datum.t+-14,,] {title:'příspěvek přijat dne'}
  field platnost[Lp,110,85,] {type:'date', data:da.dat_do, help:'zaplaceno do', format:'RUr'}
  label        [Lp,platnost.t+-14,,] {title:'zaplaceno do ...'}
  field pozn   [Lp,145,100,] {data:da.note, help:'poznámka'}
  label        [Lp,pozn.t+-14,,] {title:'poznámka'}

  # tlačítka a obslužné procedury
  proc form_init() {
    b.browse_init; form.init; form_state('n','u|z|s'); }
  proc form_state(on,off) {
    form.enable(1,on); form.enable(0,off); }
  proc onchanged () {
    form_state('u|z','n|s') }
  label cmd [Lp,175,110,50] { css:'ae_parm', style:'z-index:1' }
  # Nový
  button [cmd.l+6,cmd.t+3,,] { tag:'n', title:'Nový',  help:'Vložit nový příspěvek',
    proc onclick() {
      form.init; form_state('u|z','n|s');
      id_o.set(osoby.b.id_o.get); id_o.change(1); del.set(''); del.change(1);
      ukon.set('d'); ukon.change(1); datum.set(now); datum.change(1);
  }}
  # Uložit
  button [cmd.l+50,cmd.t+3,,] {tag:'u', title:'Uložit',  help:'Uložit změněná data',
    proc onclick() {
      form.same
    | // oprava
      form.key; form.save; form.load; b.browse_seek; form_state('n|s','u|z');
    | // přidání
      id_o.get;
      form.insert; form.load;
      b.raise('onrowclick',b.browse_seek(conc('id_dar=',form.key))); form_state('n|s','u|z');
    | error('chyba při vkládání příspěvku - nenastavený člen')
  }}
  # Zpět
  button [cmd.l+6,cmd.t+25,,] {tag:'z', title:'Zpět',  help:'Neměnit změněná data',
    proc onclick() {
      b.browse_count; form.load(b.id_d.get); form_state('n|s','u|z')
    | form.init; form_state('n','u|z|s')
  }}
  # Smazat
  button [cmd.l+50,cmd.t+25,,] {tag:'s', title:'Smazat',  help:'Smazat tento příspěvek',
    proc onclick() {
      confirm("opravdu smazat příspěvek ",b.castka.get," od ",osoby.b.jmeno.get,' ',osoby.b.prijmeni.get,"?");
      del.set(conc('D ',sys('user','abbr'),' ',now)); del.change; form.save;
      dary.reload
  }}
}
# ---------------------------------------------------------------------- _ddary
form _ddary {
  # seznam darů
  view da: table dar
  view do: table osoba { join:"USING(id_osoba)" }
  browse b [10,30,,] { rows:8, qry_rows:1
    show id_d { data:da.id_dar }
    show id_o { data:do.id_osoba }
    show [,,10,] { data:da.ukon, title:'způsob', format:'q*s' }
    show castka [,,70,] { data:da.castka, title:'částka', format:'rq*s' }
    show [,,70,] { data:da.dat_od, title:'přijetí', format:'rq*s' }
    show [,,70,] { data:da.dat_do, title:'potvrzení', format:'rq*s' }
    proc onrowclick() { form.load(id_d.get); form_state('n|s','u|z'); }
  }
  # formulář pro úpravu darů
  # skryté položky
  field id_o { data:da.id_osoba }
  field ukon { data:da.ukon }
  field del  { data:da.deleted }
  # viditelné položky
  const Ld=270
  field castka [Ld,40,65,] {data:da.castka, help:'darovaná částka', format:'r'}
  label        [Ld,castka.t+-14,,] {title:'částka'}
  field datum  [Ld,75,85,] {type:'date', data:da.dat_od, help:'datum přijetí daru', format:'RUr'}
  label        [Ld,datum.t+-14,,] {title:'dar přijat dne'}
  field potvrz [Ld,110,85,] {type:'date', data:da.dat_do, help:'datum zaslání potvrzení', format:'RUr'}
  label        [Ld,potvrz.t+-14,,] {title:'potvrzení zasláno dne'}
  field pozn   [Ld,145,100,] {data:da.note, help:'poznámka'}
  label        [Ld,pozn.t+-14,,] {title:'poznámka'}

  # tlačítka a obslužné procedury
  proc form_init() {
    b.browse_init; form.init; form_state('n','u|z|s'); }
  proc form_state(on,off) {
    form.enable(1,on); form.enable(0,off); }
  proc onchanged () {
    form_state('u|z','n|s') }
  label cmd [Ld,175,110,50] { css:'ae_parm', style:'z-index:1' }
  # Nový
  button [cmd.l+6,cmd.t+3,,] { tag:'n', title:'Nový',  help:'Vložit nový dar',
    proc onclick() {
      form.init; form_state('u|z','n|s');
      id_o.set(osoby.b.id_o.get); id_o.change(1); del.set(''); del.change(1);
      ukon.set('d'); ukon.change(1); datum.set(now); datum.change(1);
  }}
  # Uložit
  button [cmd.l+50,cmd.t+3,,] {tag:'u', title:'Uložit',  help:'Uložit změněná data',
    proc onclick() {
      form.same
    | // oprava
      form.key; form.save; form.load; b.browse_seek; form_state('n|s','u|z');
    | // přidání
      id_o.get;
      form.insert; form.load;
      b.raise('onrowclick',b.browse_seek(conc('id_dar=',form.key))); form_state('n|s','u|z');
    | error('chyba při vkládání daru - nenastavený člen')
  }}
  # Zpět
  button [cmd.l+6,cmd.t+25,,] {tag:'z', title:'Zpět',  help:'Neměnit změněná data',
    proc onclick() {
      b.browse_count; form.load(b.id_d.get); form_state('n|s','u|z')
    | form.init; form_state('n','u|z|s')
  }}
  # Smazat
  button [cmd.l+50,cmd.t+25,,] {tag:'s', title:'Smazat',  help:'Smazat tento dar',
    proc onclick() {
      confirm("opravdu smazat dar ",b.castka.get," od ",osoby.b.jmeno.get,' ',osoby.b.prijmeni.get,"?");
      del.set(conc('D ',sys('user','abbr'),' ',now)); del.change; form.save;
      dary.reload
  }}
}

/*
# ---------------------------------------------------------------------- _platby
form _platby {
  view p: table platba
  browse b [0,0,,] { rows:10, qry_rows:1
    show id_platba { data:p.id_platba }
#     number id_osoba
#     number id_rodina
#     number id_duakce
#     number id_pokl
#     number ucel
#     number zpusob
    show datum  [,,65,] { title:'datum', data:p.datum, format:'s-r' }
    show castka [,,60,] { title:'částka', data:p.castka, format:'sq*r' }
    show poznamka [,,180,] { title:'poznámka', data:p.poznamka, format:'tsq*' }
    show ucet_nazev [,,160,] { title:'název účtu', data:p.ucet_nazev, format:'tsq*' }
    show vs [,,75,] { title:'VS', data:p.vs, format:'sq*r' }
    show ks [,,30,] { title:'KS', data:p.ks, format:'sq*r' }
    show ss [,,30,] { title:'SS', data:p.ss, format:'sq*r' }
    proc onrowclick() { copy_by_name(b,platba.get); }
  }
}
# ---------------------------------------------------------------------- _platba
form _platba {
  view p: table platba
  label  [0,9,367,249] { css:'ae_part' }
  label  [8,0,87,] { title:'zpracování platby', css:'ae_part_label' }
  field vs [9,36,75,] { format:'ro' }
  button [80,36,75,] { title:'urči platbu'
    proc onclick() { var res:object
      res.set(ask('akce_urci',vs.get,ss.get));
      info.set(res.html);
    }
  }
  label info [8,60,200,100]
  field ss [9,70,75,] { format:'ro' }
}
# ---------------------------------------------------------------------- _prihlasky
form _prihlasky {
  view ws: table webform
  browse b [0,0,,] { rows:28
    show id { data:ws.id_webform }
    show [,,70,] { data:ws.prijmeni, title:'příjmení', format:'q*s' }
    show [,,60,] { data:ws.jmeno, title:'jméno', format:'q*s' }
    show [,,100,] { data:ws.email, title:'email', format:'q*s' }
    show [,,120,] { data:ws.potvrzeno, title:'potvrzeno', format:'q*s' }
    proc onrowclick() {
      prihlaska.load(id.get);
    }
  }
}
# ---------------------------------------------------------------------- _prihlaska
form _prihlaska {
  var url: text
  view w: table webform
  label [10,14,,] { title:"Přihlašuji se jako *" }
  radio typ_osoby [100,10,201,20] { data:w.typ_osoby, value:'1'
    case [0,0,80,] { title:'jednotlivec', expr:'1' }
    case [80,0,61,] { title:'rodina', expr:'2' }
    case [144,0,48,] { title:'firma', expr:'3' }
  }
  label x [10,40,,] { title:"Osobní údaje" }
  // osobní
  const Tp=60; Tpf=74
  label [9,Tp+0,,] { title:"titul" }
  field [9,Tpf+0,34,]
  label [59,Tp+0,,]    { title:"* jméno" }
  field jmeno [57,Tpf+0,80,] { data:w.jmeno, value:'M' }
  label [147,Tp+0,,] { title:"* příjmení" }
  field prijmeni [148,Tpf+0,115,] { data:w.prijmeni, value:'Š' }
  label [277,Tp+0,,] { title:"narození" }
  field [274,Tpf+0,80,] { data:w.narozeni, type:'date' }
  // adresa
  const Ta=100; Taf=114
  label [9,Ta+0,,] { title:"ulice" }
  field [9,Taf+0,126,] { data:w.ulice }
  label [148,Ta+0,,] { title:"PSČ" }
  field [146,Taf+0,44,] { data:w.psc }
  label [205,Ta+0,,] { title:"obec" }
  field [206,Taf+0,149,] { data:w.obec }
  // kontakt
  const Tk=140; Tkf=154
  label [9,Tk+0,,] { title:"* email" }
  field email [9,Tkf+0,126,] { data:w.email, value:'martin@smidek.eu' }
  label [148,Tk+0,,] { title:"telefon" }
  field [146,Tkf+0,44,]
  // potvrzení
  const Tc=450
  label [9,Tc+0,,] { title:'potvrzeno zpětným mailem' }
  edit msg [9,Tc+14,300,] { data:w.potvrzeno }
}
*/
