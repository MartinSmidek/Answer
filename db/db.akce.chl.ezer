# Systém Ans(w)er - modul Akce.ch - jednoduchá správa databáze chlapů
# (c) 2011 Martin Šmídek <martin@smidek.eu>

# ================================================================================================== CHLAPI
use info: form _info [10,10,,]
use chs: form _chlapi [10,40,,]
use ch: form _chlap [10,40,,]

var the_akce: number
var the_akce_nazev: text
var the_akce_cena: number

proc onstart() {
  chs.reset('mrop'); ch.reset('mrop');
  chs.reload(0);
}
proc onfirstfocus() {
#   m.g.i.click;
  m.os.inic.click;
}
menu m {type:'left'
  menu g {title:'rok 2012',type:'group'
    item {title:'MROP - přihlášky', par:°{id_akce:6} }
    item {title:'MROP - závěr', par:°{id_akce:7} }
    item i {title:'Křižanov - přihlášky', par:°{id_akce:5} }
    proc onclick(i) {
      the_akce.set(i.par.id_akce); info.get.display(0,'ex');
      ch.get.display(0); chs.get.display(0);
      info.fill(conc(i.owner.title,' ',i.title),ask('chlapi_akce_prehled',the_akce.get));
      the_akce_nazev.set(i.title);
      [ the_akce.get; the_akce_cena.set(ask('select','cena','ch_akce',conc('id_akce=',the_akce.get))) ]
    }
  }
  menu {title:'rok 2011',type:'group'
    item {title:'MROP', par:°{id_akce:2} }
    item {title:'MROP - závěr', par:°{id_akce:4} }
    item i {title:'GloTraCh', par:°{id_akce:1} }
    item {title:'Jordán', par:°{id_akce:3} }
#     item {title:'všechny akce - minulé', format:'d' }
#     item {title:'všechny akce - přihlášky', format:'d' }
    proc onclick(i) {
      the_akce.set(i.par.id_akce); info.get.display(0,'ex');
      ch.get.display(0); chs.get.display(0);
      info.fill(conc(i.owner.title,' ',i.title),ask('chlapi_akce_prehled',the_akce.get));
      the_akce_nazev.set(i.title);
      [ the_akce.get; the_akce_cena.set(ask('select','cena','ch_akce',conc('id_akce=',the_akce.get))) ]
    }
  }
  menu os {title:'Osobní údaje',type:'group'
    item a {title:'účastníků zvolené akce'
      proc onclick(i) {
        chs.reset('akce'); ch.reset('akce');
        info.get.display(1,'ex'); info.export_type.set('akce');  // umožníme export
        the_akce.get;
        info.fill(conc('Seznam pro ',the_akce_nazev.get),' ');
        ch.get.display(1); chs.get.display(1);
        [ chs.reload(0) ]
      | ch.get.display(0); chs.get.display(0);
        info.fill('Účastníci akce','Nebyla zvolena akce');
      }
    }
    item inic {title:'všech iniciovaných chlapů' }
#     item {title:'všech známých chlapů', format:'d'  }
    proc onclick(i) {
      chs.reset('mrop'); ch.reset('mrop');
      info.get.display(1,'ex'); info.export_type.set('mrop');  // umožníme export
      info.fill(conc(i.owner.title,' ',i.title),' ');
      ch.get.display(1); chs.get.display(1);
      chs.reload(0);
    }
  }
}
# -------------------------------------------------------------------------------------------------- _info
# univerzální formulář pro levostranné menu
form _info [,,*,50] { css:'ds_form'
  var export_type: text
  label head [0,0,*,50]  { title:'' }
  button [-22,3,50,] { tag:'ex', title:"Export"
    proc onclick() {
      switch(export_type.get,
      'akce',{ ref.set(ask('chlapi_akce_export',the_akce.get,the_akce_nazev.get))},
      'mrop',{ ref.set(ask('chlapi_mrop_export'))}
      )
    }
  }
  label ref [-80,7,200,]  { tag:'ex', format:'r', css:'title_ref' }
  label note [0,35,,550] { title:'' }
  proc fill(x,y) {
    [ x; head.set(conc("<div class='karta'>",x,"</div>")) ];
    [ y; note.set(y) ]
  }
  proc append(x) { note.get; note.set(conc(note.get,'<br>',x)) | note.set(x) }
}
# -------------------------------------------------------------------------------------------------- _chlapi
form _chlapi [,,*,50] {
  var mode: text        // mrop|akce
  // funkce
  proc reset(m) {
    mode.set(m);
    eq(mode.get,'mrop');
    mrop.display(1); all.display(0);
  | mrop.display(0); all.display(1);
  }
  proc reload(id) {
    ch.init;
    eq(mode.get,'mrop');
    [ mrop.browse_load("deleted='' && cc.iniciace!=0",'iniciace,id_chlapi');
      mrop.browse_focus;
      { id; mrop.raise('onrowclick',id)
      | mrop.raise('onrowclick') } ]
  | [ all.browse_load(conc("us.id_akce=",the_akce.get));
      all.browse_focus;
      { id; all.raise('onrowclick',id)
      | all.raise('onrowclick') } ]
  }
  proc reload_row() {
    eq(mode.get,'mrop'); chs.mrop.browse_seek | chs.all.browse_seek
  }
  # --------------------------------------------------  OSOBY
  // osoby
  view cc: table chlapi
  browse mrop [8,10,200,447] { rows:23, qry_rows:1, optimize:°{qry:'noseek'}
    show id_chlapi {data:cc.id_chlapi }
    show [,,136,] {title:'jméno', expr:"CONCAT(cc.prijmeni,' ',cc.jmeno)", format:'q*s+',
                  help:"příjmení a jméno" }
    show mr [,,45,] {title:'mrop', data:cc.iniciace, format:'rsq', help:"rok iniciace"}
    show vs [,,0,] {title:'VS', format:'rsq:e', help:"VS utvořený z data narození",
      expr:"DATE_FORMAT(cc.narozeni,'%y%m%d')" }
    proc onrowclick() {
      ch.c.load(id_chlapi.get);
      ch.c.key(id_chlapi.get);
    }
    proc onclick() {
      mr.width; mr.width(0); vs.width(45)
    | mr.width(45); vs.width(0)
    }
    proc onchange () { mrop.browse_count | ch.init }
  }
  # --------------------------------------------------  OSOBY NA AKCI
  // osoby
  view us: table ch_ucast
  view cs: table chlapi { join:'USING(id_chlapi)' }
  browse all [8,10,200,447] { rows:23, qry_rows:1 /*optimize:°{qry:'noseek'}*/
    show id_ucast {data:us.id_ucast }
    show id_akce {data:us.id_akce }
    show id_chlapi {data:cs.id_chlapi }
    show stupen {data:us.stupen }
    show [,,15,] {title:'?', data:us.stupen, format:'sq*', map_pipe:akce_ucast.ikona }
    show [,,120,] {title:'jméno', expr:"CONCAT(cs.prijmeni,' ',cs.jmeno)", format:'q*s+',
                  help:"příjmení a jméno" }
    show mr [,,45,] {title:'mrop', data:cs.iniciace, format:'rsq', help:"rok iniciace"}
    show vs [,,0,] {title:'VS', format:'rsq:e', help:"VS utvořený z data narození",
      expr:"DATE_FORMAT(cs.narozeni,'%y%m%d')" }
    show dl [,,0,] {title:'dluh', format:'rsq:e', help:"dlužná částka",
      expr:"us.cena-us.uctem-us.pokladnou" }
    show ob [,,0,] {title:'obec', format:'sq', help:"obec bydliště",
      data:cs.obec }
    proc onrowclick() {
      ch.load(id_ucast.get);
      ch.c.key(id_chlapi.get);
      ch.u.key(id_ucast.get);
    }
    proc onclick() {
      mr.width; mr.width(0); vs.width(45);
    | vs.width; vs.width(0); dl.width(45);
    | dl.width; dl.width(0); ob.width(45);
    | ob.width(0); mr.width(45);
    }
    proc onchange () { all.browse_count | ch.init }
  }
}
# -------------------------------------------------------------------------------------------------- _chlap
form _chlap [,,*,466] {
  var mode: text        // mrop|akce
  // funkce
  proc reset(m) {
    mode.set(m);
    eq(mode.get,'mrop');
    form.display(0,'akce');
  | form.display(1,'akce');
  }
  # --------------------------------------------------  data
  view u: table ch_ucast
  view a: table ch_akce { join:'USING(id_akce)' }
  view c: table chlapi { join:'USING(id_chlapi)' }
  field id_ucast { data:u.id_ucast }
  field id_akce { data:a.id_akce }
  field nazev { data:a.nazev }
  field id_chlapi { data:c.id_chlapi }
  # --------------------------------------------------  VZTAH k AKCI pro mode='akce'
  const Lu=240
  const Tu=280
  label  [Lu+2,Tu+9,454,101] { tag:'akce', css:'ae_part' }
  label  [Lu+9,Tu+0,90,] { tag:'akce', title:'údaje k této akci', css:'ae_part_label' }
  select stupen [Lu+7,Tu+17,106,] { tag:'akce', type:'map', data:u.stupen, options:akce_ucast.zkratka }
  label  [Lu+255,Tu+20,56,]   { tag:'akce', title:'cena akce' }
  field cena [Lu+315,Tu+17,44,]  { tag:'akce', format:'r', data:u.cena }
  check avizo [Lu+361,Tu+17,50,] { tag:'akce', title:'avízo', format:'r', data:u.avizo }
  label  [Lu+306,Tu+42,33,16] { tag:'akce', title:'zaplaceno'}
  label  [Lu+278,Tu+64,33,]   { tag:'akce', title:'účtem' }
  field  [Lu+315,Tu+60,44,]   { tag:'akce', format:'r', data:u.uctem }
  field  [Lu+368,Tu+60,85,]   { type:'date', tag:'akce', format:'rUR', data:u.uctem_dne }
  label  [Lu+285,Tu+88,26,]   { tag:'akce', title:'pokl.' }
  field  [Lu+315,Tu+85,44,]   { tag:'akce', format:'r', data:u.pokladnou }
  label  [Lu+7,Tu+42,98,16]   { tag:'akce', title:'poznámka k akci'}
  edit   [Lu+7,Tu+58,258,48]  { tag:'akce', data:u.pozn }
  button [Lu+118,Tu+15,,] { tag:'akce', title:'Vyřadit z této akce',
      help:'Vyřazení účastníka z účasti na této akci'
    proc onclick() {
      confirm('Opravdu vyřadit ',prijmeni.get,' ',jmeno.get,' z akce ',nazev.get,'?');
      ch_ucast.delete_record(conc('id_ucast=',id_ucast.get));
      chs.reload(0);
  } }
  # --------------------------------------------------  PŘIDÁVÁNÍ Z JINÝCH ZDROJŮ pro mode='akce'
  label  [Lu+256,Tu+125,109,51] { tag:'akce', css:'ae_parm' }
  button [Lu+262,Tu+128,,29] { tag:'akce', title:'Přidat z MROP',
      help:'Nalezení účastníka podle jména a přidání na akci'
    proc onclick() {
      PridaniJmenem.modal(240,50)
  } }
  button [Lu+262,Tu+152,,] { tag:'akce', title:'Přidat z MS', format:'d',
      help:'Nalezení účastníka podle jména a přidání na akci' }
  # --------------------------------------------------  OSOBA
  const L=240
  const T=10
  label  [L+0,T+9,367,249] { css:'ae_part' }
  label  [L+8,T+0,74,] { title:'osobní údaje', css:'ae_part_label' }
  label          [L+5,T+15,100,] {title:'příjmení'}
  field prijmeni [L+5,T+30,100,] {data:c.prijmeni, help:'příjmení'}
  label          [L+117,T+15,100,] {title:'jméno'}
  field jmeno    [L+117,T+30,101,] {data:c.jmeno}

  radio sex [L+225,T+15,50,35] { data:c.sex, value:'1'
    case [0,2,50,] { title:'muž', expr:'1' }
    case [0,17,50,] { title:'žena', expr:'2' }
    proc onchange() { echo('sex=',this.get) }
  }

  label          [L+323,T+15,35,] {title:'MROP'}
  field iniciace [L+323,T+30,36,] {data:c.iniciace, format:'r:e'}
  label          [L+5,T+54,100,] {title:'ulice'}
  field ulice    [L+5,T+69,162,] {data:c.ulice}
  label          [L+179,T+54,39,] {title:'psč'}
  field psc      [L+179,T+69,40,] {data:c.psc}
  label          [L+230,T+54,100,] {title:'obec'}
  field obec     [L+229,T+69,131,] {data:c.obec}
  label          [L+5,T+94,37,] {title:'telefon'}
  field telefon  [L+5,T+110,212,] {data:c.telefon}
  label          [L+230,T+94,33,] {title:'narození'}
  field narozeni [L+229,T+110,85,] {type:'date', data:c.narozeni, format:'rR:e'}
  label          [L+324,T+94,41,] {title:'nnnn rč'}
  field rodne    [L+323,T+110,37,] {data:c.rc_xxxx, format:'r:e'}
  label          [L+5,T+138,33,] {title:'e-mail'}
  field email    [L+5,T+154,355,] {data:c.email}
  label          [L+5,T+179,33,] {title:'poznámka'}
  edit  pozn     [L+5,T+194,355,60] {data:c.pozn}
  # --------------------------------------------------  TLAČÍTKA
  const Lt=240
  const Tt=340
  // - opravy
  label  [Lt+0,Tt+86,98,30] { css:'ae_parm' }
  button [Lt+6,Tt+91,,] {title:'Uložit', help:'Uložení změněných osobních údajů'
    proc onclick() {
      var zmena_browse: number
      c.key;
      // oprava
      [ c.save; c.load ];
      [ eq(mode.get,'akce'); u.save; u.load ];
      chs.reload_row()
    | // přidání
      c.insert;
      [ eq(mode.get,'akce'); ch_ucast.insert_record(
        object('id_akce',the_akce.get,'id_chlapi',c.key,'stupen',stupen.key,'cena',cena.get)) ];
      chs.reload(c.key);
  }}
  button [Lt+55,Tt+91,,] {title:'Zpět', help:'Změny nebudou uloženy'
    proc onclick() {
      chs.reload(0);
  }}
  # --------------------------------------------------  VYTVÁŘENÍ A MAZÁNÍ
  label  [Lt+122,Tt+86,109,30] { css:'ae_parm' }
  button [Lt+127,Tt+91,,] {title:'Nový', help:'Založení nového účastníka'
    proc onclick() {
      form.init(1); chs.all.blur(1);
      prijmeni.change; jmeno.change; ulice.change; psc.change; obec.change; sex.change;
      stupen.key(1); stupen.change; id_akce.set(the_akce.get); id_akce.change;
      cena.set(the_akce_cena.get); cena.change;
      c.key(0); u.key(0);
  } }
  button [Lt+170,Tt+91,,] {title:'Vymazat', help:'Vymazání osobních informací z evidence'
    proc onclick() {
      var ans: text
      confirm('Opravdu vymazat všechny údaje o ',prijmeni.get,' ',jmeno.get,' z databáze?');
      ans.set(ask('chlapi_delete',c.key));
      ans.get; alert(ans.get)
    | chs.reload(0)
  } }
  # --------------------------------------------------  KONTEXTOVÉ MENU
  menu { type:'context'
    item back { title:'ukázat všechny změny'
      proc onclick () { Ctrack.CT_tab.set('chlapi'); Ctrack.CT_load(c.key) }
    }
  }
}
# ================================================================================================== Přidání jménem
var pall: object
proc ucast_add(p,i) {  // používají panely PridatJmenem a PridatAkce
  var rec: number
  p.chck.get;
  pall.set(p.all.get);
  { ask('chlapi_auto_not_yet',the_akce.get,pall.get('id_chlapi'));
    rec.set(ch_ucast.insert_record(
      object('id_akce',the_akce.get,'id_chlapi',pall.get('id_chlapi'),'stupen',1,'cena',the_akce_cena.get)));
    [ chs.all.browse_seek(conc('id_ucast=',rec)) ]
  | warning(pall.get('nazev'),' už na akci je') }
}
panel PridaniJmenem [,,320,280] {type:'popup', title:'Přidání známých osob jménem'
  use fl: form _fl [12,10,,]
  form _fl [,,300,200] {
    var last: number
    var rod: object
    var popis: text
    button [0,250,50,16] { title:'přidat do účastníků', proc onclick() {
      each(L,ucast_add); chs.reload(0);
    } }
    button [130,250,50,16] { title:'jiná jména', proc onclick() {
      jmena.init; vse.init; L.init } }
    button [220,250,50,16] { title:'konec', proc onclick() {
      jmena.init; vse.init; L.init; panel.hide(0) } }
    // výběr z databáze chlapů
    select jmena [100,0,200,17] { type:'auto', par:°{fce:'chlapi_auto_jmena'}, format:'t'
      proc onchanged() {
        rod.set(ask('chlapi_auto_jmenovci',this.key));
        each(rod.get,jmenovci); // nesmí volat asynchronní operace
      }
    }
    proc jmenovci(p,i) {
      last.set(L.add);
      popis.set(p.nazev);
      L.part(last.get).part('info').set(popis.get);
      L.part(last.get).part('all').set(p);
    }
    // nastavení výběru
    check vse [4,0,80,16] { title:'všichni', format:'t'
      proc onchange() { each(L,nastav) }
      proc nastav (x,i) { x.chck.set(form.vse.get) }
    }
    list L [0,23,300,200] {rows:22
      check chck [4,0,50,16] { title:' ', format:'t' }
      label info [23,4,300,16] {style:'white-space:nowrap'}
      var all: object
    }
  }
}
# ================================================================================================== BackTrace
# --- back_track
panel Ctrack [,,572,564] { title:'Korekce oprav databáze', type:'popup', css:'dialog'
  use CT_track: form _back [0,16,,] { tabindex:20 },
  proc CT_load (id) {
    Ctrack.popup(0,1);
    CT_track.list.browse_load(conc("kde='",CT_tab.get,"' AND klic=",id),"kdy DESC");
    CT_track.list.raise('onrowclick')
  }
  var CT_tab:text
  var CT_fld_id:text
  var CT_fld:object
  proc CT_track.list.onsubmit () {
    eq(CT_track.list.op.get,'c','d','p','a');
    alert('změny v historii nelze vracet - pouze ručně opravit')
  | eq(CT_tab.get,'chlapi');
    CT_fld_id.set(CT_track.list.fld.get); CT_fld_id.get;
    CT_fld.set(ch.part(CT_fld_id.get)); CT_fld.get;
    ch.part(CT_track.list.fld.get).set(CT_track.list.old.get);
    CT_fld.change
  | alert('Položka ',CT_fld_id.get,' není dostupná, vraťte prosím změnu ručně')
  }
  # ------------------------------------------------------------------------------------------------- BACK
  # id_track,kdy,kdo,kde,klic,zmena:fld,op,val,old
  form _back [,,570,570] {
    browse list [8,8,150,100] { rows:27,
      show kdy [,,100,] { title:'kdy', data:_track.kdy },
      show kdn [,,30,] { title:'kdn', data:_track.kdo },
      show op [,,10,] { title:'?', data:_track.op },
      show fld [,,60,] { title:'položka', data:_track.fld },
      show old [,,200,] { title:'původní hodnota', data:_track.old, format:'t' },
      show val [,,110,] { title:'změněná hodnota', data:_track.val, format:'t' },
      show id_track [,,0,] { data:_track.id_track },
    },
    button zpet [500,515,80,20] { title:'Zpět'
      proc onclick() { panel.close(0) }
    }
  }
}
