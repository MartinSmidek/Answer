# ------------------------------------------------------------------------------------------------ #
# Karta pro výběr akce                                                                             #
#                                                                                                  #
# Ans(w)er/Ezer                                     (c) 2007-2011 Martin Šmídek <martin@smidek.eu> #
# ------------------------------------------------------------------------------------------------ #

var zalozka: text           // inf | new

use info: form _akce [12,4,,]
use page: form _page [450,40,,]

proc onstart() { echo('db.akce.lst.onstart');
  info.fill(the_title.get);
  info.rok.selects(javascript(conc(
    "var x='';for(i=",sum(fdate('Y'),1),";i>=1990;i--){x+=i+',';};x+='0'")));
  { the_duakce.get;
    info.rok.set(the_rok.get)
  | info.rok.set(fdate('Y')) };
}
proc onfirstfocus() {
  zalozka.set('inf');
  m.g.sey.click;
  page.inf.onclick
}

# Výběr akce má dvě možnosti, uchované v proměnné the_source:
#   'google' používá jako primárná data spreadsheet v intranetu YS a z něj upravuje tabulku AKCE
#   'sey' používá pouze tabulku AKCE

menu m {type:'left'
  menu g {title:'Výběr akce zadaného roku',type:'group'
    item {title:'podle číselníku akcí na intranetu', skill:'y'          // jen pro YS
      proc onclick (i) {
        info.rok.display(1); info.google.display(1); info.sey.display(0);
        the_source.set('google'); akce_roku
    } }
    item sey {title:'ze seznamu všech akcí'
      proc onclick (i) {
        info.rok.display(1); info.sey.display(1); info.google.display(0);
        the_source.set('sey'); akce_roku
  } } }
  menu {title:'Výběr ze všech roků',type:'group'
    item sey {title:'ze seznamu všech akcí',    par:°{cond:'1'} }
    item  {title:'... opravdové akce',          par:°{cond:'da2.spec=0'} }
    item  {title:'... jen různé seznamy',       par:°{cond:'da2.spec=1'} }
    proc onclick (i) {
      info.rok.display(0); info.sey.display(1); info.google.display(0);
      the_source.set('sey'); akce_vsechny(i.par.cond)
} } }
proc akce_roku() {
  the_rok.set(info.rok.get);
  [ gt(the_rok.get,2010);                       // intranet obsahuje akce od roku 2010
    info.google.browse_load(conc("g_kat='a' AND g_rok=",the_rok.get))
  | info.google.browse_init];
  [ info.sey.browse_load(conc("YEAR(da2.datum_od)='",the_rok.get,"' AND spec=0")) ];
  set_cookie(conc(sys('root'),'_akce_rok'),the_rok.get);
}
proc akce_vsechny(cond) {
  the_rok.set(0);
  [ info.sey.browse_load(cond) ];
  set_cookie(conc(sys('root'),'_akce_rok'),the_rok.get);
}
# -------------------------------------------------------------------------------------------------- _page
form _page [,,285,500] {
  var a_akce: number
  # --------------------------- záložky
  proc tags(a,on,off) {
    form.set_css('ae_butt_off','ae_butt_on','p');
    a.set_css('ae_butt_on','ae_butt_off');
    form.display(1,on); form.display(0,off);
  }
  label inf [ 12,0,80,20] {tag:'p', title:'<b>Info</b>', format:'c', css:'ae_butt_on'
    proc onclick() { tags(this,'inf','cor|cen'); zalozka.set('inf'); show_akce(a_akce.get)
  } }
  label cor [100,0,80,20] {tag:'p', title:'<b>Úprava</b>', format:'c'
    proc onclick() { tags(this,'cor','inf|cen'); zalozka.set('cor'); show_akce(a_akce.get)
  } }
  // jen pro YF,CR - přidání akce
  label new [188,0,80,20] {tag:'p', title:'<b>Nová akce</b>', format:'c', skill:'faan;caan'
    proc onclick() { tags(this,'cor','inf|cen'); zalozka.set('new'); show_akce(a_akce.get)
  } }
  // je pro YS - ceník akce
  label cen [188,0,80,20] {tag:'p', title:'<b>Ceník</b>', format:'c', skill:'yaan'
    proc onclick() { tags(this,'cen','inf|cor'); zalozka.set('cen'); show_akce(a_akce.get)
  } }
  // je pro CPR - ceník akce
  label  [280,0,30,20] {tag:'p', title:'<b>C..</b>', format:'c', skill:'caan'
    proc onclick() { tags(this,'cen','inf|cor'); zalozka.set('cen'); show_akce(a_akce.get)
  } }
  proc show_akce(id_akce) {
    page.a_akce.set(id_akce);
    switch(zalozka.get,
    'inf',{
      note.set(ask('akce_info',id_akce));
    },
    'cor',{
      form.load(id_akce); info.sey.browse_focus
    },
    'new',{
      form.init; info.sey.blur(1)
    },
    'cen',{
      form.init; c.browse_load(conc("id_akce=",id_akce));
    },
    { stop}
  )}
  # --------------------------- ceník
  browse c [10,25,,] { tag:'cen', rows:14, qry_rows:1, css_rows:"barva,1:grey"
    show id_cenik { data:cenik.id_cenik }
    show id_akce  { data:cenik.id_akce }
    show poradi [,,25,] { title:'N', help:'pořadí ve faktuře', data:cenik.poradi, format:'ruq*s+'
      proc onsubmit() { this.save } }
    show barva { expr:"IF(LEFT(polozka,1)='-',1,0)" }
    show polozka [,,125,] { title:'položka', help:'... do faktury', data:cenik.polozka, format:'uq*s'
      proc onsubmit() { this.save } }
    show za [,,27,] { title:'za', data:cenik.za, format:'urq=s',
      help:'noc:N, strava:(s=snídaně/o=oběd/v=večeře)(c=celá/p=poloviční), pobyt:P, slevy:S'
      proc onsubmit() { this.save } }
#     show od [,,29,] { title:'od', help:'od věku - včetně', data:cenik.od, format:'urq=s'
#       proc onsubmit() { this.save } }
#     show do [,,29,] { title:'do', help:'do věku - nevčetně', data:cenik.do, format:'urq=s'
#       proc onsubmit() { this.save } }
    show cena [,,42,] { title:'cena', help:'cena v Kč', data:cenik.cena, format:'urq=s'
      proc onsubmit() { this.save } }
    show dph [,,35,] { title:'dph', help:'DPH v Kč', data:cenik.dph, format:'urq=s'
      proc onsubmit() { this.save } }
    menu { type:'context'
      item { title:'přidat nový řádek', skill:'a|a'
        proc onclick() {
          c.browse_seek(conc('id_cenik=',cenik.insert_record(
            object('id_akce',a_akce.get,'polozka','?'))));
      } }
      item { title:'odebrat běžný řádek', skill:'a|a'
        proc onclick() { confirm('opravdu odebrat položku ',polozka.get,'?');
          cenik.delete_record(conc('id_cenik=',id_cenik.get));
          c.browse_load
      } }
    }
  }
  # --------------------------- informace
  label note [10,25,270,460] { tag:'inf', title:'', style:'color:black;font-size:12px' }
  # --------------------------- položky akce
  label [17,32,104,] { tag:'cor', title:'název akce *' }
  field [17,49,218,] { tag:'cor', data:akce.nazev }
  label [17,72,104,] { tag:'cor', title:'místo' }
  field [17,89,218,] { tag:'cor', data:akce.misto }
  label [18,116,65,] { tag:'cor', title:'začátek *' }
  field [18,133,83,] { tag:'cor', type:'date', data:akce.datum_od }
  label [118,116,64,] { tag:'cor', title:'ukončení' }
  field [118,133,83,] { tag:'cor', type:'date', data:akce.datum_do }
  check [205,166,83,] { tag:'cor', title:'jen seznam', data:akce.spec, format:'r' }
  check [206,189,69,] { tag:'cor', title:'má ceník', data:akce.ma_cenik, format:'r', skill:'yaa|yaa;caa|caa' }
  label [18,173,,]     { tag:'cor', title:'typ akce *' }
  select [71,169,139,] { tag:'cor', type:'map', data:akce.druh, options:ms_akce_typ.zkratka }
  radio [213,117,86,35] { tag:'cor', data:akce.strava_oddo, value:'vo'
    case [0,2,83,] { title:'večeře-oběd', expr:'vo' }
    case [0,17,76,] { title:'oběd-oběd', expr:'oo' }
    proc onchange() { echo('strava=',this.get) }
  }
  label [14,202,,] { tag:'cor', title:'účetní symbol:', skill:'faa;caa|faa;caa' }
  field [96,202,47,] { tag:'cor', data:akce.ciselnik_akce, skill:'faa|faa;caa|caa' }
  // Zpět a Uložit
  label  [144,236,98,30] { tag:'cor', css:'ae_parm', style:'z-index:1', skill:'yaa+;faa+;caa+' }
  button [150,241,,] { tag:'cor', title:'Zpět', skill:'yaa+|yaa+;faa+|faa+;caa+|caa+'
    help:'Neměnit změněná data'
    proc onclick() { form.load(page.a_akce.get);
  }}
  button [196,241,,] { tag:'cor', title:'Uložit', skill:'yaa+|yaa+;faa+|faa+;caa+|caa+'
    help:'Uložit změněná data',
    proc onclick() {
      form.same
    | // oprava
      form.key; form.save; form.load; info.sey.browse_seek
    | // přidání
      form.insert; form.load;
      info.sey.raise('onrowclick',info.sey.browse_seek(conc('id_duakce=',form.key)))
    }
  }
  # --------------------------- pozadí stránky nakonec
  label [2,16,283,476] { title:'', css:'ae_work page' }
}
# -------------------------------------------------------------------------------------------------- _akce
# formulář pro výběr akce pro další práci
form _akce [,,*,50] {
  select rok [4,5,52,] { format:'t'
    proc onchange() { akce_roku }
  }
  # je voláno z kontextem správného the_source
  proc nazev_akce(id_akce) {
    var nazev: text
    { and(id_akce,not(eq(id_akce,'0')));
      { eq(the_source.get,'google');
        nazev.set(conc(' Akce ',google.kod.get,'/',google.rok.get,' - ',google.nazev.get));
      | eq(the_source.get,'sey');
        nazev.set(conc(' ',conc(sey.nazev.get,', ',fdate('Y',sey.od.get))));
      | error('X1')
      };
    };
    return (nazev);
  }
  proc volba_akce(id_akce,naz,ma_cenik) {
    the_duakce.set(id_akce); the_cenik.set(ma_cenik);
    set_cookie(conc(sys('root'),'_akce_id'),the_duakce.get);
    set_cookie(conc(sys('root'),'_akce_cenik'),the_cenik.get);
    [ eq(the_source.get,'sey');
      the_rok.set(fdate('Y',sey.od.get));
      set_cookie(conc(sys('root'),'_akce_rok'),the_rok.get) ];
    the_title.set(nazev_akce(id_akce));
#     { and(the_duakce.get,not(eq(the_duakce.get,'0')));
#       { eq(the_source.get,'google');
#         the_title.set(conc(' Akce ',google.kod.get,'/',google.rok.get,' - ',google.nazev.get));
#       | eq(the_source.get,'sey');
#         the_rok.set(fdate('Y',sey.od.get));
#         set_cookie(conc(sys('root'),'_akce_rok'),the_rok.get);
#         the_title.set(conc('&nbsp;',conc(sey.nazev.get,', ',the_rok.get)));
#       | error('X1')
#       };
    lst.info.fill(the_title.get);
    set_cookie(conc(sys('root'),'_akce_title'),the_title.get);
    [ ucast.call('init_ucast2') ];
    [ has_skill('yam;fam;cam'); mai.call('init_ucast') ];
    [ pece.call('init_pece') ];
#     };
    // vložení informací o akci do proměnné obj_akce
    obj_akce.set(object('nazev',naz));
  }

#       [ the_title.set(if(eq(the_source.get,'google'),
#           conc(' Akce ',google.kod.get,'/',google.rok.get,' - ',google.nazev.get),
#           conc('&nbsp;',conc(sey.nazev.get,', ',the_rok.get))
#         ));
#       [ the_title.set(if(eq(the_source.get,'google'),
#           conc(' Akce ',google.kod.get,'/',google.rok.get,' - ',google.nazev.get),
#           conc('&nbsp;',conc(sey.nazev.get,', ',the_rok.get))
#         ));
#         lst.info.fill(the_title.get);
#         set_cookie(conc(sys('root'),'_akce_title'),the_title.get);
#         [ ucast.call('init_ucast2') ];
#         [ mai.call('init_ucast') ];
#         [ pece.call('init_pece') ]
#       ]
#
#
#     };
#     // vložení informací o akci do proměnné obj_akce
#     obj_akce.set(object('nazev',naz));
#   }
  // -------------------------------------------  g_akce (kopie číselníku z intranetu) pro YS
  view ga: table g_akce
  view ja: table join_akce {join_type:'LEFT',join:'USING(g_rok,g_kod)'}
  view da: table akce      {join_type:'LEFT',join:'ON ja.id_akce=da.id_duakce'}
  browse google [0,36,200,500] { type:'smart', rows:27, qry_rows:1, buf_rows:120,
    show id_gakce {data:ga.id_gakce}
    show id_akce [,,30,] {title:'akce', data:ja.id_akce, format:'rsqu', css_cell:'id_akce,0:yellow'}
    show naz_akce {data:da.nazev}
    show ma_cenik {data:da.ma_cenik}
    show rok {data:ga.g_rok}
    show kod   [,,30,] {title:'kód',    data:ga.g_kod, format:'rs+q*'}
    show nazev [,,210,] {title:'název', data:ga.g_nazev, format:'sq*'}
    show od    [,,65,] {title:'od',     data:ga.g_od, format:'rsq*'}
    show do    [,,65,] {title:'do',     data:ga.g_do, format:'rsq*'}
#     show strava[,,20,] {title:'str',    data:da.strava_oddo, format:'rsq*'}
#     show typ   [,,30,] {title:'typ',    data:ga.g_typ, format:'sq*'}
#     show kap   [,,30,] {title:'kap',    data:ga.g_kap, format:'sq*'}
    proc onsubmit() {
      id_akce.get; volba_akce(id_akce.get,naz_akce.get,ma_cenik.get)
    | not(eq(fdate('Y',od.get),rok.get));
      alert('Akce má na intranetu uvedeno chybné datum zahájení (např. chybějící nebo z jiného roku)');
    | confirm('akce ',nazev.get,' zatím není v databázi, mám ji založit?');
      the_duakce.set(akce.insert_record(
        object('nazev',nazev.get,'datum_od',date2sql(od.get),'datum_do',date2sql(do.get))));
      join_akce.insert_record(object('id_akce',the_duakce.get,'g_rok',rok.get,'g_kod',kod.get));
      google.browse_row; volba_akce(id_akce.get,nazev.get,ma_cenik.get)
    }
    proc onrowclick() { page.show_akce(id_akce.get); }
  }
  // ------------------------------------------- akce (všechny akce) pro YS, FA, CR
  view da2: table akce
#   view ma2: table ms_akce   {join_type:'LEFT',join:'USING(id_duakce)'}
  view ja2: table join_akce {join_type:'LEFT',join:'ON ja2.id_akce=da2.id_duakce'}
  view ga2: table g_akce    {join_type:'LEFT',join:'USING(g_rok,g_kod)'}
  browse sey [0,36,200,500] { type:'smart', rows:27, qry_rows:1, buf_rows:120,
    show id_akce  {data:da2.id_duakce}
    show          {data:ja2.id_akce}
    show ma_cenik {data:da2.ma_cenik}
#     show          {data:ma2.id_akce}
    show id_gakce {data:ga2.id_gakce}
#     show kod   [,,30,] {title:'kód', data:ga2.g_kod, format:'rs+q*u', css_cell:'kod,0:yellow'
#       proc onsubmit() { echo(ask('akce_roku_id',id_akce.get,kod.get,the_rok.get)) }
#     }
/**/show druh  [,,60,] {title:'druh',   data:da2.druh, map_pipe:ms_akce_typ.zkratka, format:'sq#'}
    show kod   [,,30,] {title:'účet',   expr:"IF(ga2.g_kod,ga2.g_kod,da2.ciselnik_akce)", format:'rs+q*' }
    show nazev [,,120,] {title:'název', expr:"IF(da2.nazev='',ga2.g_nazev,da2.nazev)", format:'sq*'}
    show misto [,,60,] {title:'místo',  data:da2.misto, format:'sq*'}
    show od    [,,65,] {title:'od',     data:da2.datum_od, format:'rsq*'}
    show do    [,,65,] {title:'do',     data:da2.datum_do, format:'rsq*'}
#     show strava[,,20,] {title:'str',    data:da2.strava_oddo, format:'rsq*'}
#     show       [,,30,] {title:'klíč',   data:da2.id_duakce, format:'rsq'}
#     show typ   [,,30,] {title:'typ',    data:ga2.g_typ, format:'sq*'}
#     show kap   [,,30,] {title:'kap',    data:ga2.g_kap, format:'sq*'}
    proc onsubmit() { volba_akce(id_akce.get,nazev.get,ma_cenik.get) }
    proc onrowclick() { page.show_akce(id_akce.get); }
  }
  label head [0,0,*,30] { title:'' }
  button [-160,3,100,] { title:'Obnovit rok z intranetu', skill:'y|yaa+'
    help:"obnoví akce roku z http://intranet.setkani.org"
    # získání seznamu akcí z docs.google.com pomocí API spredsheet
    proc onclick() {
      alert('bylo obnoveno ',ask('akce_roku_update',the_rok.get),' záznamů o akcích');
      google.browse_seek;
    }
  }
  button [-30,3,100,] { title:'Mapa', //skill:'a|a'
    help:"mapa s bydlišti účastníků akcí"
    proc onclick() {
      [ eq(the_source.get,'google'); mapa.id_akce.set(google.id_akce.get)
      | eq(the_source.get,'sey'); mapa.id_akce.set(sey.id_akce.get) ];
      mapa.id_akce.get;
      mapa.modal(20,100,nazev_akce(mapa.id_akce.get))
    }
  }
  proc fill(x) {
    [ x; head.set(conc("<div class='karta' style='padding-left:60px'>",x,"</div>")) ];
  }
}
// ------------------------------------------- mapa
panel mapa [,,640,400] { type:'popup', title:'mapa účastníků'
  var id_akce: number
  proc onfirstfocus() {
    google_maps('mapa_cr',object('label',f.cr));
    ukaz_psc;
  }
  proc onfocus() {
    ukaz_psc;
  }
  proc ukaz_psc() {
    var ret: object
    google_maps('mark_new');
    ret.set(ask('akce_mapa',id_akce.get));
    google_maps('mark_put',object('mark',ret.mark));
  }
  use f: form _f [0,10,,]
  form _f [,,700,400] {
    label cr [0,0,640,400]
  }
}
