# ------------------------------------------------------------------------------------------------ #
# Karta pro výběr akce                                                                             #
#                                                                                                  #
# Ans(w)er/Ezer                                     (c) 2007-2011 Martin Šmídek <martin@smidek.eu> #
# ------------------------------------------------------------------------------------------------ #

var zalozka: text           // inf | cen

use info: form _akce [12,4,,]
use page: form _page [510,40,,]

proc onstart() { echo('db.akce.lst.onstart');
  info.fill(the_title.get);
  info.rok.selects(javascript(conc(
    "var x='';for(i=",sum(fdate('Y'),1),";i>=1990;i--){x+=i+',';};x+='0'")));
  { the_duakce.get;
    info.rok.set(the_rok.get)
  | info.rok.set(fdate('Y')) };
}
proc onfirstfocus() {
  zalozka.set('inf');
  m.g.sey.click;
  page.inf.onclick
}

menu m {type:'left'
  menu g {title:'Výběr akce zadaného roku',type:'group'
    item {title:'podle číselníku akcí na intranetu'
      proc onclick (i) {
        info.rok.display(1); info.google.display(1); info.sey.display(0); the_source.set('google'); akce_roku
    } }
    item sey {title:'ze seznamu všech akcí'
      proc onclick (i) {
        info.rok.display(1); info.sey.display(1); info.google.display(0); the_source.set('sey'); akce_roku
  } } }
  menu {title:'Výběr ze všech roků',type:'group'
    item sey {title:'ze seznamu všech akcí'
      proc onclick (i) {
        info.rok.display(0); info.sey.display(1); info.google.display(0);
        the_source.set('sey'); akce_vsechny
} } } }
proc akce_roku() {
  the_rok.set(info.rok.get);
  [ gt(the_rok.get,2010); info.google.browse_load(conc('g_rok=',the_rok.get))
  | info.google.browse_init];
  [ info.sey.browse_load(conc('YEAR(da2.datum_od)=',the_rok.get," AND spec=0")) ];
  set_cookie('ys_akce_rok',the_rok.get);
}
proc akce_vsechny() {
  the_rok.set(0);
  [ info.sey.browse_load(1) ];
  set_cookie('ys_akce_rok',the_rok.get);
}
# -------------------------------------------------------------------------------------------------- _page
form _page {
  var a_akce: number
  # --------------------------- záložky
  proc tags(a,on,off) {
    form.set_css('ae_butt_off','ae_butt_on','p');
    a.set_css('ae_butt_on','ae_butt_off');
  }
  label inf [ 12,0,80,20] {tag:'p', title:'<b>Informace</b>', format:'c', css:'ae_butt_on'
    proc onclick() { tags(this,'z','h|o');
      zalozka.set('inf'); show_akce(a_akce.get)
  } }
  label cen [100,0,80,20] {tag:'p', title:'<b>Ceny</b>', format:'c',
    proc onclick() { tags(this,'h','z|o');
      zalozka.set('cen'); show_akce(a_akce.get)
  } }
  proc show_akce(id_akce) {
    page.a_akce.set(id_akce);
    switch(zalozka.get,
    'inf',{
      note.set(ask('akce_info',id_akce));
    },
    'cen',{
      note.set('na tomto místě bude časem :-)<br>možno definovat ceník této akce');
    }
  )}
  label note [2,16,223,476] { title:'', css:'ae_work page' }
}
# -------------------------------------------------------------------------------------------------- _akce
# formulář pro výběr akce pro další práci
form _akce [,,*,50] {
  select rok [4,5,52,] { format:'t'
    proc onchange() { akce_roku }
  }
  proc volba_akce(id_akce) {
    the_duakce.set(id_akce);
    set_cookie("ys_akce_id",the_duakce.get);
    { and(the_duakce.get,not(eq(the_duakce.get,'0')));
      [ the_title.set(if(eq(the_source.get,'google'),
          conc(' Akce ',google.kod.get,'/',google.rok.get,' - ',google.nazev.get),
          conc('&nbsp;',conc(sey.nazev.get,', ',the_rok.get))
        ));
        lst.info.fill(the_title.get);
        set_cookie("ys_akce_title",the_title.get);
        ducast.init_ucast;
      ]
    }
  }
  // -------------------------------------------  g_akce (kopie číselníku z intranetu)
  view ga: table g_akce
  view ja: table join_akce {join_type:'LEFT',join:'USING(g_rok,g_kod)'}
  view da: table akce      {join_type:'LEFT',join:'USING(id_akce)'}
  browse google [0,36,200,500] { type:'smart', rows:27, qry_rows:1, buf_rows:120,
    show id_gakce {data:ga.id_gakce}
    show rok {data:ga.g_rok}
    show kod   [,,30,] {title:'kód',    data:ga.g_kod, format:'rs+q*'}
    show nazev [,,200,] {title:'název', data:ga.g_nazev, format:'sq*'}
    show od    [,,70,] {title:'od',     data:ga.g_od, format:'rsq*'}
    show do    [,,70,] {title:'do',     data:ga.g_do, format:'rsq*'}
    show id_akce [,,30,] {title:'akce', data:ja.id_akce, format:'rsqu', css_cell:'id_akce,0:yellow'}
    show       [,,30,] {title:'typ',    data:ga.g_typ, format:'sq*'}
    show       [,,30,] {title:'kap',    data:ga.g_kap, format:'sq*'}
    proc onsubmit() {
      id_akce.get; volba_akce(id_akce.get)
    | not(eq(fdate('Y',od.get),rok.get));
      alert('Akce má na intranetu uvedeno chybné datum zahájení (např. chybějící nebo z jiného roku)');
    | confirm('akce ',nazev.get,' zatím není v databázi, mám ji založit?');
      the_duakce.set(akce.insert_record(
        object('nazev',nazev.get,'datum_od',date2sql(od.get),'datum_do',date2sql(do.get))));
      join_akce.insert_record(object('id_akce',the_duakce.get,'g_rok',rok.get,'g_kod',kod.get));
      google.browse_row; volba_akce(id_akce.get)
    }
    proc onrowclick() { page.show_akce(id_akce.get) }
  }
  // ------------------------------------------- akce (všechny akce)
  view da2: table akce
  view ma2: table ms_akce   {join_type:'LEFT',join:'USING(id_duakce)'}
  view ja2: table join_akce {join_type:'LEFT',join:'ON ja2.id_akce=da2.id_duakce'}
  view ga2: table g_akce    {join_type:'LEFT',join:'USING(g_rok,g_kod)'}
  browse sey [0,36,200,500] { type:'smart', rows:27, qry_rows:1, buf_rows:120,
    show id_akce  {data:da2.id_duakce}
    show          {data:ja2.id_akce}
    show          {data:ma2.id_akce}
    show id_gakce {data:ga2.id_gakce}
    show kod   [,,30,] {title:'kód', data:ga2.g_kod, format:'rs+q*u', css_cell:'kod,0:yellow'
      proc onsubmit() { echo(ask('akce_roku_id',id_akce.get,kod.get,the_rok.get)) }
    }
    show nazev [,,200,] {title:'název', expr:"CONCAT(da2.nazev,' ',IFNULL(ma2.misto,''))", format:'sq*'}
    show       [,,70,] {title:'od',     data:da2.datum_od, format:'rsq*'}
    show       [,,70,] {title:'do',     data:da2.datum_do, format:'rsq*'}
    show       [,,30,] {title:'klíč',   data:da2.id_duakce, format:'rsq'}
    show       [,,30,] {title:'typ',    data:ga2.g_typ, format:'sq*'}
    show       [,,30,] {title:'kap',    data:ga2.g_kap, format:'sq*'}
    proc onsubmit() { volba_akce(id_akce.get) }
    proc onrowclick() { page.show_akce(id_akce.get) }
  }
  label head [0,0,*,30] { title:'' }
  button [-20,3,100,] { title:'Obnovit rok z intranetu', help:"obnoví akce roku z http://intranet.setkani.org"
    # získání seznamu akcí z docs.google.com pomocí API spredsheet
    proc onclick() {
      alert('bylo obnoveno ',ask('akce_roku_update',the_rok.get),' záznamů o akcích');
      google.browse_seek;
    }
  }
  proc fill(x) {
    [ x; head.set(conc("<div class='karta' style='padding-left:60px'>",x,"</div>")) ];
  }
}
# ================================================================================================== definice akce
panel DefIdAkce [,,220,60] {type:'popup', title:'Zapsání id_akce'
  use f: form _id_akce [12,10,,]
  form _id_akce [,,100,50] {
    label [10,10,50,] { title:'L|M akce' }
    field lm [60,10,10,] { help:'L|M na rozlišení verze MS.EXE'}
    field id [80,10,40,] { help:'číslo akce z programu MS.EXE'}
    button [140,8,,] { type:'submit', title:'Zapiš', proc onclick() {
      panel.hide(id.get)
    } }
  }
}
