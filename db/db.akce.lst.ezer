# ------------------------------------------------------------------------------------------------ #
# Karta pro výběr akce                                                                             #
#                                                                                                  #
# Ans(w)er/Ezer                                     (c) 2007-2013 Martin Šmídek <martin@smidek.eu> #
# ------------------------------------------------------------------------------------------------ #

var zalozka: text           // inf | cor | cen

use info: form _akce [12,4,,]
use page: form _page [450,40,,]

# ------------------------------------------------------------------------------- globální procedury
# nastaví akci jako pracovní
proc akce_work(id_akce) {
  info.volba_akce(id_akce)
}
# ------------------------------------------------------------------------------- inicializace
# inicializace panelu proběhne po akce.onstart, zobrazí seznam akcí daného roku podle the_source
proc onfirstfocus() {
  [ dirty.get | akce_onstart ]; // zavolá akce_work
  echo('db.akce.lst.onfirstfocus');
  // zobrazení akce
  info.fill(the_title.get);
  // inicializace zobrazení
  info.rok.selects(javascript(conc(
    "var x='';for(i=",sum(fdate('Y'),1),";i>=1990;i--){x+=i+',';};x+='0'")));
  info.rok.set(the_rok.get);
  page.cen_copy.selects("neměnit:0");
  zalozka.set('inf');
  // zobrazení seznamu akcí
  akce_roku(the_rok.get);
  [ has_skill('y;f');  m.g.sey.click
  | has_skill('caam'); m.c.m.click
  | has_skill('caar'); m.c.r.click
  | has_skill('a');    m.c.a.click ];
  page.inf.onclick;
  echo('db.akce.lst.onfirstfocus end');
}
# ---------------------------------------------------------- reakce na změny rozměrů okna prohlížeče
const min_h=550           // minimální výška (nelze jednoduše měnit - jsou k ní vztaženy souřadnice)
var   curr_h: number      // výška nad základní výšku
var   curr_w: number      // 1024 nebo 1280

proc onresize(w,h) {
#   echo("resize(",w,",",h,") ... w=",sys('screen','width'));
  [ and(lt(h,min_h.get),not(eq(curr_h.get,min_h.get)));
    curr_h.set(0); h_change_all()
  | and(gt(h,min_h.get),not(eq(h,sum(curr_h.get,min_h.get))));
    curr_h.set(minus(h,min_h.get)); h_change_all() ];
  [ and(lt(w,1280),eq(curr_w.get,1280)); narrow
  | and(gt(w,1279),eq(curr_w.get,1024)); wide ];
}

# změna šířky 1024/1280
proc narrow() { curr_w.set(1024); w_change_all }
proc wide() { curr_w.set(1280); w_change_all }
proc w_change_all() {}

# změna výšky oproti základní min_h
proc h_change_all() {
  page.property(object('down',curr_h.get),'b');      // posunutí proti původnímu - omezeno na min_h
  page.frame.property(object('stretch',curr_h.get)); // protažení proti původnímu
  h_change_b;
}
# změna počtu řádků browse oproti aktuálním řádkům
proc h_change_b() { var r:number, h:number
  h.set(sys('screen','height'));
  r.set(divide(sum(h.get,-173),17));
  not(eq(r,info.sey.rows));
  info.google.set_attrib('rows',r);
  info.sey.set_attrib('rows',r);
  page.c.set_attrib('rows',sum(r,-5));
}

# --------------------------------------------------------------------------------------------- menu
# Výběr akce má dvě možnosti, uchované v proměnné the_source:
#   'google' používá jako primárná data spreadsheet v intranetu YS a z něj upravuje tabulku AKCE
#   'sey' používá pouze tabulku AKCE

menu m {type:'left'
  menu c {title:'Výběr akce',type:'group',   skill:'c'             // jen pro CR
    item m {title:'program Manželská setkání', skill:'caam', par:°{omez:'da2.druh IN (1,2,3,4)'} }
    item r {title:'programy Rodina mimo MS',   skill:'caar', par:°{omez:'da2.druh NOT IN (1,2,3,4)'} }
    item a {title:'... bez omezení',           skill:'a',    par:°{omez:'1'} }
      proc onclick (i) {
        info.rok.display(1); info.sey.display(1); info.google.display(0);
        the_omezeni.set(i.par.omez);
        the_source.set('sey'); akce_roku(info.rok.get);
  } }
  menu g {title:'Výběr akce zadaného roku',type:'group', skill:'y;f'
    item {title:'podle číselníku akcí z intranetu', skill:'y'          // jen pro YS
      proc onclick (i) {
        info.rok.display(1); info.google.display(1); info.sey.display(0);
        the_source.set('google'); akce_roku(info.rok.get);
    } }
    item sey {title:'ze seznamu všech akcí'
      proc onclick (i) {
        // skryjeme rok - po výběru zase zobrazíme - tento režim je lokální pro AKCE
        info.rok.display(1); info.sey.display(1); info.google.display(0);
        the_source.set('sey'); akce_roku(info.rok.get);
  } } }
  menu {title:'Výběr ze všech roků',type:'group', skill:'y;f'
    item sey {title:'ze seznamu všech akcí',    par:°{cond:'1'} }
    item  {title:'... opravdové akce',          par:°{cond:'da2.spec=0'} }
    item  {title:'... jen různé seznamy',       par:°{cond:'da2.spec=1'} }
    proc onclick (i) {
      info.rok.display(0); info.sey.display(1); info.google.display(0);
      the_source.set('sey'); akce_vsechny(i.par.cond)
} } }
# zobrazení akcí definovaného roku
proc akce_roku(rok) {
  page.init_page;
  [ eq(the_source.get,'google');
    { gt(rok,2010);  // intranet YS obsahuje akce od roku 2010 (ale 2010 jw nutné doplnit)
      info.google.browse_load(conc("g_kat='a' AND g_rok=",rok),
      '','','','','',
      conc('SET @curr:=',the_duakce.get));
    | alert("V intranetu jsou v přesném formátu popsány jen akce od roku 2010") }
  | info.google.browse_init];
  [ eq(the_source.get,'sey');
    info.sey.browse_load(conc("YEAR(da2.datum_od)='",rok,"' AND da2.spec=0 AND ",the_omezeni.get),
      '','','','','',
      conc('SET @curr:=',the_duakce.get));
}
proc akce_vsechny(cond) {
  [ info.sey.browse_load(cond) ];
}
# -------------------------------------------------------------------------------------------- _page
form _page [,,285,500] {
  var a_akce: number
  # --------------------------- záložky
  proc tags(a,on,off) {
    form.set_css('ae_butt_off','ae_butt_on','p');
    a.set_css('ae_butt_on','ae_butt_off');
    form.display(1,on); form.display(0,off);
  }
  label inf [ 12,0,80,20] {tag:'p', title:'<b>Info</b>', format:'c', css:'ae_butt_on'
    proc onclick() { tags(this,'inf','cor|cen'); zalozka.set('inf'); show_akce(a_akce.get)
  } }
  label cor [100,0,80,20] {tag:'p', title:'<b>Úprava</b>', format:'c'
    proc onclick() { tags(this,'cor','inf|cen'); zalozka.set('cor'); show_akce(a_akce.get)
  } }
  label cen [188,0,80,20] {tag:'p', title:'<b>Ceník</b>', format:'c',
    proc onclick() { tags(this,'cen','inf|cor'); zalozka.set('cen'); show_akce(a_akce.get)
  } }
  proc show_akce(id_akce) {
    page.a_akce.set(id_akce);
    eq(the_source.get,'sey');    [ info.sey.browse_count; show_page(id_akce) | init_page ]
  | eq(the_source.get,'google'); [ id_akce; show_page(id_akce) | init_page ]
  }
  proc show_page(id_akce) {
    cen_copy.enable(0); cen_copy.key(0);
    switch(zalozka.get,
    'inf',{ note.set(ask('akce_info',id_akce)) },
    'cor',{ form.load(id_akce);
            [ has_skill('f'); soubezna.set(cconc(id_hlavni.get,1,0)) ];
            info.sey.browse_focus },
    'cen',{ form.init; c.browse_load(conc("id_akce=",id_akce)) });
  }
  proc init_page() {
    page.a_akce.set(0);
    cen_copy.enable(0); cen_copy.key(0);
    form.init;
    note.set('');
  }
  # --------------------------- informace
  label note [10,25,283,193] { tag:'inf', title:'', style:'color:black;font-size:12px' }
  label web_href [30,265,264,50] { tag:'inf,web' }
  label [31,210,315,] {tag:'inf,web', title:'<h3>Propagace a dokumentace akce</h3>', skill:'demo|demo'}
  check web [10,240,114,] { tag:'inf,web', title:'svázat s webem', format:'t', skill:'demo|demo',
    proc onchange() {
      web_href.set(ask('web_akce',a_akce.get,this.get));
  }}
  check cal [10,310,114,] { tag:'inf,web', title:'vložit do kalendáře', format:'t', skill:'demo|demo',
    proc onchange() {
      web_href.set(ask('cal_akce',a_akce.get,this.get));
  }}
  check [10,337,21,] { tag:'inf,web', format:'t', skill:'demo|demo' }
  label [30,342,246,] { tag:'inf,web', skill:'demo|demo',
    title:"vložit na <a href='www.kalendar-nasich-akci.cz' target='web'>kalendar-nasich-akci.cz</a>"}
  check [196,337,100,] { tag:'inf,web', title:'s přihláškou', format:'t', skill:'demo|demo' }
  label [10,364,,] {title:'dokumentace k akci', skill:'demo|demo'} // nadpis na vnějším obalem
  label drop [10,384,283,113] { tag:'inf,web', type:'drop', skill:'demo|demo'  }
  # --------------------------- položky akce
  field id_hlavni { data:akce.id_hlavni }
  label [17,32,104,] { tag:'cor', title:'název akce *' }
  field [17,49,218,] { tag:'cor', data:akce.nazev }
  label [17,72,104,] { tag:'cor', title:'místo' }
  field [17,89,218,] { tag:'cor', data:akce.misto }
  label [18,116,65,] { tag:'cor', title:'začátek *' }
  field datum_od [18,133,83,] { tag:'cor', type:'date', data:akce.datum_od }
  label [118,116,64,] { tag:'cor', title:'ukončení' }
  field [118,133,83,] { tag:'cor', type:'date', data:akce.datum_do }
  check [205,166,83,] { tag:'cor', title:'jen seznam', data:akce.spec, format:'r' }
  label [18,173,,]     { tag:'cor', title:'typ akce *' }
  select [71,169,139,] { tag:'cor', type:'map', data:akce.druh, options:ms_akce_typ.zkratka, format:'n' }
  radio [213,117,86,35] { tag:'cor', data:akce.strava_oddo, value:'vo'
    case [0,2,83,] { title:'večeře-oběd', expr:'vo' }
    case [0,17,76,] { title:'oběd-oběd', expr:'oo' }
    proc onchange() { echo('strava=',this.get) }
  }
  label [18,204,,] { tag:'cor', title:'účetní symbol:', skill:'faa;caa' } //skill:'faa;caa|faa;caa;' }
  field [96,202,54,] { tag:'cor', data:akce.ciselnik_akce, skill:'faa|faa;caa|caa' }
  // zobrazení a nastavení akce jako souběžné (stejné datum a místo)
  check soubezna [157,200,120,] { tag:'cor', title:'akce je souběžná', skill:'faa|faa',
    proc onchange() {
      { info.sey.id_hlavni.get;
        [ confirm("opravdu zrušit souběh této akce?"); ask('akce_soubeh_nastav',form.key,0) ]
      | alert(ask('akce_soubeh_nastav',form.key)) };
      info.sey.browse_seek; show_akce(form.key);
  }}
  // Změna ceníku
  check [134,254,93,] { tag:'cor', title:'používat ceník', data:akce.ma_cenik,
                        format:'', skill:'yaa|yaa;caa|caa;faa|faa' }
  button [14,230,,] { tag:'cor', title:'Změnit ceník podle', skill:'yaa|yaa;caa|caa;faa|faa'
    proc onclick() {
      cen_copy.enable(1);
      cen_copy.selects(ask('akce_select_cenik',form.key))
  }}
  select cen_copy [138,232,158,] {tag:'cor', format:'ntd', help:'vzory ceníků'
    proc onchange() {
      cen_copy.key;
      confirm("opravdu zaměnit ceník za vybraný?");
      alert(ask('akce_change_cenik',form.key,cen_copy.key))
  }}
  // jednotná cena / dospělý
  check [134,284,93,] { tag:'cor', title:'používat cenu', data:akce.ma_cenu,
                        format:'', skill:'yaa|yaa;caa|caa;faa|faa' }
  field [230,286,43,] { tag:'cor', data:akce.cena, format:'r', skill:'yaa|yaa;caa|caa;faa|faa',
                        help:'pro 1 dospělého' }
  label [279,289,20,] { tag:'cor', title:'Kč' }
  // Zpět a Uložit
  label  [193,351,98,30] { tag:'cor', css:'ae_parm', style:'z-index:1', skill:'yaa+;faa+;caa+' }
  button [199,356,,] { tag:'cor', title:'Zpět', skill:'yaa+|yaa+;faa+|faa+;caa+|caa+'
    help:'Neměnit změněná data'
    proc onclick() { form.load(page.a_akce.get);
  }}
  button [245,356,,] { tag:'cor', title:'Uložit', skill:'yaa+|yaa+;faa+|faa+;caa+|caa+'
    help:'Uložit změněná data',
    proc onclick() {
      form.same
    | // oprava
      form.key; form.save; form.load; info.sey.browse_seek
    | // přidání
      or(has_skill("faa+;caa+"),lt(fdate("Y",datum_od.get),2011));
      form.insert; form.load;
      info.sey.raise('onrowclick',info.sey.browse_seek(conc('da2.id_duakce=',form.key)))
    | alert("Vkládat lze jen 'historické akce', po roce 2010 jen přes intranet")                    //ys
  } }
  // jen pro YF,CR - přidání a zrušení akce
  label  [10,351,164,30] { tag:'cor', css:'ae_parm', style:'z-index:1', skill:'faan;caan;yaan' }
  button [16,356,,] {tag:'cor', title:'Nová akce', format:'c', skill:'faan|faan;caan|caan;yaan|yaan'
    proc onclick() {
      has_skill('yaan'); {
        eq(the_source.get,'google'); alert("Vkládat lze jen do seznamu všech akcí")
      | gt(info.rok.get,2009); alert("Vkládat lze jen 'historické akce', po roce 2010 jen přes intranet")
      | form.init; info.sey.blur(1)
      }
    | form.init; info.sey.blur(1)
  } }
  button [91,356,,] {tag:'cor', title:'Smazat akci', format:'c', skill:'faan|faan;caan|caan;yaan|yaan'
    proc onclick() {
      var ret:object
      var msg:text
      ret.set(ask('akce_delete_confirm',form.key));
      ret.zrusit; confirm(ret.zrusit);                  // fakt zrušit + název
      { not(ret.ucastnici) | confirm(ret.ucastnici) };  // ale akce má účastníky
      { ret.platby; alert(ret.platby)                   // nelze smazat, jsou-li platby
      | msg.set(ask('akce_delete',form.key,ret));
        akce_roku(info.rok.get); alert(msg.get);
  } } }
  # --------------------------- ceník
  browse c [10,25,,] { tag:'cen', rows:22, qry_rows:1, css_rows:"barva,1:grey"
    show id_cenik { data:cenik.id_cenik }
    show id_akce  { data:cenik.id_akce }
    show polozka  { data:cenik.polozka }
    show poradi [,,25,] { title:'N', help:'pořadí ve faktuře', data:cenik.poradi, format:'ruq*s+'
      proc onsubmit() { this.save } }
    show barva { expr:"IF(LEFT(polozka,1)='-',1,0)" }
    show [,,155,] { title:'položka', help:'... do faktury', data:cenik.polozka, skill:'y|y;f|f',
      format:'tuq*s'
      proc onsubmit() { this.save } }
    show [,,105,] { title:'položka', help:'... do faktury', data:cenik.polozka,  skill:'c|c',
      format:'tuq*s'
      proc onsubmit() { this.save } }
    show za [,,27,] { title:'za', data:cenik.za, format:'urq=s',
      help:'noc:N, strava:(s=snídaně/o=oběd/v=večeře)(c=celá/p=poloviční), pobyt:P, slevy:S'
      proc onsubmit() { this.save } }
    show typ [,,50,] { title:'typ', data:cenik.typ, map_pipe:ms_akce_ubytovan.zkratka,
      format:'urq#s',                                                                    skill:'c|c'
      help:'typ ubutování: 1,2,3,...'
      proc onsubmit() { this.save } }
    show cena [,,32,] { title:'cena', help:'cena v Kč vč. DPH', data:cenik.cena, format:'urq=s'
      proc onsubmit() { this.save } }
    show dph [,,15,] { title:'dph', help:'DPH v Kč', data:cenik.dph, format:'urq=s'
      proc onsubmit() { this.save } }
    menu { type:'context', skill:'yaa+;faa+;caa+'
      item { title:'přidat nový řádek'
        proc onclick() {
          c.browse_seek(conc('id_cenik=',cenik.insert_record(
            object('id_akce',a_akce.get,'polozka','?'))));
      } }
      item { title:'odebrat běžný řádek'
        proc onclick() { confirm('opravdu odebrat položku ',polozka.get,'?');
          cenik.delete_record(conc('id_cenik=',id_cenik.get));
          c.browse_load
      } }
    }
  }
  // export ceníku
  const bc=370
  label  [10,bc,100,30] { tag:'b,cen', css:'ae_parm', style:'z-index:1' }
  button [18,bc+5,,] { tag:'b,cen', title:'Export ceníku', format:'c'
    proc onclick() {
      var ret: object
      ret.set(ask('akce_platby_xls',a_akce.get));
      ret.ok; xhref.set(ret.xhref);
    | warning(ret.msg);
  } }
  label  [129,bc+11,,] { tag:'b,cen', title:"test ceny:" }
  button [182,bc+5,,] { tag:'b,cen', title:'pár', proc onclick() { test_ceniku(2,0,0) }}
  button [217,bc+5,,] { tag:'b,cen', title:'pár+kojenec', proc onclick() { test_ceniku(2,0,1) }}
  proc test_ceniku(dosp,deti,koje) { var cena:object
    eq(the_duakce.get,a_akce.get);
    { eq(the_soubeh.get,1);
      cena.set(ask('akce_vzorec_soubeh',0,the_duakce.get,the_soubezna.get,dosp,deti,koje))
    | eq(the_soubeh.get,2);
      cena.set(ask('akce_vzorec_soubeh',0,the_hlavni.get,the_duakce.get,dosp,deti,koje))
    | cena.set(ask('akce_vzorec_test',a_akce.get,dosp,deti,koje)) };
    alert(cena.navrh)
  | alert("pro testování ceníku musí být akce nastavena jako pracovní")
  }
  label xhref [122,bc+9,173,]
  # --------------------------- pozadí stránky nakonec
  label frame [2,16,283,375] { title:'', css:'ae_work page' }
}
# -------------------------------------------------------------------------------------------- _akce
# formulář pro výběr akce pro další práci
form _akce [,,*,50] {
  select rok [4,5,52,] { format:'t', proc onchange() { akce_roku(this.get) }}
  # je voláno s kontextem správného the_source
  proc nazev_akce(id_akce) {
    var nazev: text
    { and(id_akce,not(eq(id_akce,'0')));
      { eq(the_source.get,'google');
        nazev.set(conc(' Akce ',google.kod.get,'/',google.rok.get,' - ',google.nazev.get));
      | eq(the_source.get,'sey');
        nazev.set(conc(' ',conc(sey.nazev.get,', ',fdate('Y',sey.od.get))));
      | alert('V levém menu je třeba vybrat typ akcí (na které máte oprávnění)')
      };
    };
    return (nazev);
  }
  proc volba_akce(id_akce) {
    akce_choose(id_akce);
    lst.info.fill(conc(the_title.get,' '));
    [ ucast.call('init_ucast2') ];
    [ has_skill('yam;fam;cam'); mai.call('init_ucast') ];
    [ pece.call('init_pece') ];
    // ošetření souběžných akcí - 0=normální akce, 1=hlavní akce, 2=souběžná akce
    [ has_skill('f'); page.soubezna.set(eq(the_soubeh.get,2)) ];
    // nastavení cookies
    set_cookie(conc(sys('root'),'_akce_id'),the_duakce.get);
  }
  // -------------------------------------------  g_akce (kopie číselníku z intranetu) pro YS
  view ga: table g_akce
  view ja: table join_akce {join_type:'LEFT',join:'USING(g_rok,g_kod)'}
  view da: table akce      {join_type:'LEFT',join:'ON ja.id_akce=da.id_duakce'}
  browse google [0,36,200,500] { type:'smart', rows:27, qry_rows:1, buf_rows:120,
    show id_gakce {data:ga.id_gakce}
    show id_akce [,,30,] {title:'akce', data:ja.id_akce, format:'rsqu', css_cell:'id_akce,0:yellow'}
    show naz_akce {data:da.nazev}
    show ma_cenik {data:da.ma_cenik}
    show ma_cenu {data:da.ma_cenu}
    show cena {data:da.cena}
    show rok {data:ga.g_rok}
    show curr { expr:"IF(da.id_duakce=@curr,1,0)" }
    show kod   [,,30,] {title:'kód',    data:ga.g_kod, format:'rs+q*'}
    show nazev [,,210,] {title:'název', data:ga.g_nazev, format:'tsq*'
      css_cell:"curr,1:curr_akce"}
    show od    [,,65,] {title:'od',     data:ga.g_od, format:'rsq*'}
    show do    [,,65,] {title:'do',     data:ga.g_do, format:'rsq*'}
    proc onsubmit() {
      id_akce.get; volba_akce(id_akce.get)
    | not(eq(fdate('Y',od.get),rok.get));
      alert('Akce má na intranetu uvedeno chybné datum zahájení (např. chybějící nebo z jiného roku)');
    | confirm('akce ',nazev.get,' zatím není v databázi, mám ji založit?');
      the_duakce.set(akce.insert_record(
        object('nazev',nazev.get,'datum_od',date2sql(od.get),'datum_do',date2sql(do.get))));
      join_akce.insert_record(object('id_akce',the_duakce.get,'g_rok',rok.get,'g_kod',kod.get));
      google.browse_row; volba_akce(id_akce.get) //,nazev.get,ma_cenik.get,ma_cenu.get,cena.get)
    }
    proc onrowclick() { page.show_akce(id_akce.get); }
  }
  // ------------------------------------------- akce (všechny akce) pro YS, FA, CR
  view da2: table akce
  view ja2: table join_akce {join_type:'LEFT',join:'ON ja2.id_akce=da2.id_duakce'}
  view ga2: table g_akce    {join_type:'LEFT',join:'USING(g_rok,g_kod)'}
  view sa2: table akce      {join_type:'LEFT',join:'ON sa2.id_hlavni=da2.id_duakce'}
  browse sey [0,36,200,500] { type:'smart', rows:27, qry_rows:1, buf_rows:120,
      css_rows:"barva,1:cerveny,2:zluty" // 1=hlavní akce, 2=souběžná akce
    show id_akce  {data:da2.id_duakce}
    show          {data:ja2.id_akce}
    show ma_cenik {data:da2.ma_cenik}
    show ma_cenu {data:da2.ma_cenu}
    show cena {data:da2.cena}
    show id_gakce {data:ga2.id_gakce}
    show id_hlavni {data:da2.id_hlavni}
    show id_soubeh {data:sa2.id_duakce}
    show curr { expr:"IF(da2.id_duakce=@curr,1,0)" }
    show barva { expr:"CASE WHEN da2.id_hlavni THEN 2 WHEN sa2.id_duakce THEN 1 ELSE 0 END" }
/**/show druh  [,,60,] {title:'typ',    data:da2.druh, map_pipe:ms_akce_typ.zkratka, format:'sq#'}
    show kod   [,,30,] {title:'účet',   expr:"IF(ga2.g_kod,ga2.g_kod,da2.ciselnik_akce)", format:'rsq*' }
    show nazev [,,120,] {title:'název', expr:"IF(da2.nazev='',ga2.g_nazev,da2.nazev)", format:'tsq*',
      css_cell:"curr,1:curr_akce"}
    show misto [,,60,] {title:'místo',  data:da2.misto, format:'tsq*'}
    show od    [,,65,] {title:'od',     data:da2.datum_od, format:'rs+q*'}
    show do    [,,65,] {title:'do',     data:da2.datum_do, format:'rsq*'}
    proc onsubmit() {
      volba_akce(id_akce.get);
      curr.set_attrib('expr',conc("IF(da2.id_duakce=",id_akce.get,",1,0)"));
      this.browse_seek;  }
    proc onrowclick() { page.show_akce(id_akce.get); }
  }
  label head [0,0,*,30] { title:'' }
  # volba pracovní akce
  button [376,3,,] { title:'Pracovní', proc onclick() {
    volba_akce(if(eq(the_source.get,'sey'),sey.id_akce.get,google.id_akce.get))
  }}
  # načtení akcí a intranetu
  button [-160,3,,] { title:'Obnovit rok z intranetu', skill:'y|yaa+'
    help:"obnoví akce roku z http://intranet.setkani.org"
    # získání seznamu akcí z docs.google.com pomocí API spredsheet
    proc onclick() {
      lt(rok.get,2010); alert("Z intranetu lze obnovovat jen akce roku 2010 a starší");
    | confirm("Mám pro rok ",rok.get," obnovit seznam akcí (musí mít značku 'a')?");
      alert('bylo obnoveno ',ask('akce_roku_update',rok.get),' záznamů o akcích');
      google.browse_seek;
    }
  }
  # export vlastností akcí nastaveného roku
  button [-88,3,,] { title:'Export', proc onclick() {
    sey.browse_count; sey.browse_export(°{file:'akce',type:'xls'});
    alert("vygenerovaný sešit lze stáhnout <a href='docs/akce.xls' target='xls'>zde</a>");
  }}
  # zobrazení mapy s pozicí míst účastníků dané akce
  button [-30,3,,] { title:'Mapa', //skill:'a|a'
    help:"mapa s bydlišti účastníků akcí"
    proc onclick() {
      [ eq(the_source.get,'google'); mapa.id_akce.set(google.id_akce.get)
      | eq(the_source.get,'sey'); mapa.id_akce.set(sey.id_akce.get) ];
      mapa.id_akce.get;
      mapa.modal(20,100,nazev_akce(mapa.id_akce.get))
    }
  }
  proc fill(x) {
    [ x; head.set(conc("<div class='karta' style='padding-left:60px'>",x,"</div>")) ];
  }
}
// ------------------------------------------- mapa
panel mapa [,,640,400] { type:'popup', title:'mapa účastníků'
  var id_akce: number
  proc onfirstfocus() {
    google_maps('mapa_cr',object('label',f.cr));
    ukaz_psc;
  }
  proc onfocus() {
    ukaz_psc;
  }
  proc ukaz_psc() {
    var ret: object
    google_maps('mark_new');
    ret.set(ask('akce_mapa',id_akce.get));
    google_maps('mark_put',object('mark',ret.mark));
  }
  use f: form _f [0,10,,]
  form _f [,,700,400] {
    label cr [0,0,640,400]
  }
}

map akce_cenik: table akce {where:"ma_cenik=1", order:'datum_od', key_id:'id_duakce'}
