# ------------------------------------------------------------------------------------------------ #
# Karta pro správu pečounů vybrané akce                                                            #
#                                                                                                  #
# Ans(w)er/Ezer                                     (c) 2007-2011 Martin Šmídek <martin@smidek.eu> #
# ------------------------------------------------------------------------------------------------ #

var the_duakce_local: number    // zobrazená akce - pokud se liší od the_duakce, je třeba překreslit
var zalozka: text               // evid | plat | akce
var the_pobyt: number           // klíč společného pobytu pečounů
var back: object

use header: form _header   [  4,0,,]
use up:     form _pece     [  6,40,,]
use page:   form _page     [400,10,,]
use p_oso:  form _osoba    [400,10,,]       { tag:'p_oso' }
use cmd:    form _cmd      [400, 0,,]       { tag:'p_oso' }
use p_act:  form _aktivity [400,10,,]       { tag:'p_act' }
use p_pla:  form _platba   [400,10,,]       { tag:'p_pla' }

proc onfocus() {
  [ the_duakce.get | akce_onstart ];
  eq(the_duakce_local.get,the_duakce.get)
| init_pece;
  the_duakce_local.set(the_duakce.get)
}
proc init_pece() { echo('init_pece ',the_title.get);
  page.page_init;
  header.fill(the_title.get);
  reload(0);
  page.evd.onclick
}
proc akce_show(xakce,xspolu,xnadpis,xback) {
  page.page_init;
#   set_trace('fm',1);
  back.set(object('ida',the_duakce.get,
    'idp',if(up.b.browse_count,up.b.key_pobyt.get,0),
    'tit',the_title.get,'bck',xback));
  header.zpet.display(1);
  panel.focus(1); // bez vyvolání onfocus
  the_duakce.set(xakce);
  header.fill(conc('<i>',xback,': ',xnadpis,'</i>'));
#   set_trace('fm',0);
  reload(xspolu);
}
proc akce_back() {
  the_duakce.set(back.get('ida'));
  the_title.set(back.get('tit')); header.fill(the_title.get);
  reload(back.get('idp'));
  switch(back.get('bck'),
  'AKTIVITA',{ page.jin.onclick },
  'EVIDENCE',{ evi.focus(1) });
}
proc reload(id_spolu) {
  the_duakce.get;
#   lst.the_akce.load(the_duakce.get);
  the_pobyt.set(ask('select','id_pobyt','pobyt',conc("funkce=99 AND id_akce=",the_duakce.get)));
  [ the_pobyt.get
  | the_pobyt.set(pobyt.insert_record(object('id_akce',the_duakce.get,'funkce',99))) ];
  up.b.browse_load(conc("p.funkce=99 AND p.id_akce=",the_duakce.get));
  up.b.browse_focus;
  { id_spolu; up.b.browse_seek(conc('s.id_spolu=',id_spolu)); up.b.reload_pecoun
  | up.b.raise('onrowclick') }
| p_oso.init; p_oso.d.browse_init;
  p_oso.o.key(0); p_oso.s.key(0);
}
var pall: object
proc pecouni_add(p,i) {  // používají panely PridaniJmenem a PridaniAkce
  var pk: number
  var id: number
  var pou: number
  p.chck.get;
  pall.set(p.all.get); id.set(pall.get('id'));
  // přidání pečouna na akci
  spolu.insert_record(object('id_osoba',id.get,'id_pobyt',the_pobyt.get));
  reload(0);
}
# -------------------------------------------------------------------------------------------------- _header
form _header [,,380,50] {
  proc fill(x) {
    head.set(conc("<div class='karta'>",x,"</div>"))
  }
  label head [0,2,*,50]  { title:'' }
  button zpet [-2,6,,] { title:'Zpět', help:'Navrátí zobrazení účastníků původní akce', format:'n'
    proc onclick() { this.display(0); akce_back }
  }
}
# -------------------------------------------------------------------------------------------------- _page
form _page {
  # --------------------------- záložky
  proc tags(a,on,off) {
    form.set_css('ae_butt_off','ae_butt_on','p');
    a.set_css('ae_butt_on','ae_butt_off');
    panel.display(1,on); panel.display(0,off);
  }
  proc page_init() {
    zalozka.set('evid'); tags(evd,'p_oso','p_act|p_pla');
  }
  label evd [ 12,0,120,20] {tag:'p', title:'<b>Evidenční karta</b>', format:'c', css:'ae_butt_on'
    proc onclick() { tags(this,'p_oso','p_act|p_pla');
      zalozka.set('evid'); up.b.reload_pecoun;
    }
  }
  label plt [152,0,120,20] {tag:'p', title:'<b>Platba za akci</b>', format:'c',
    proc onclick() { tags(this,'p_pla','p_oso|p_act');
      zalozka.set('plat'); up.b.reload_pecoun;
    }
  }
  label jin [292,0,120,20] {tag:'p', title:'<b>Přehled aktivit</b>', format:'c',
    proc onclick() { tags(this,'p_act','p_oso|p_pla');
      zalozka.set('akce'); up.b.reload_pecoun;
    }
  }
  label [0,16,582,530] { css:'ae_work'}
}
# ---------------------------------------------------------------------- _aktivity
form _aktivity {
  label [10,50,,] { title:'tady budou další akce tohoto pečouna' }
}
# ---------------------------------------------------------------------- _platba
form _platba {
  label [10,50,,] { title:'tady bude rozpis ceny akce podle cenového vzorce akce a přehled příchozích plateb' }
}
# ---------------------------------------------------------------------- _osoba
form _osoba { tag:'a',
  # ------------------------ osoba
  view o: table osoba,
  const L=0; T=5
  field  { data:o.id_osoba }
  label          [L+5,T+15,89,] {title:'příjmení'}
  field prijmeni [L+5,T+30,89,] {data:o.prijmeni, help:'příjmení'}
  label          [L+109,T+15,78,] {title:'jméno'}
  field jmeno    [L+109,T+30,79,] {data:o.jmeno}
  radio sex [L+200,T+15,50,35] { data:o.sex, value:'1'
    case [0,2,50,] { title:'muž', expr:'1' }
    case [0,17,50,] { title:'žena', expr:'2' }
    proc onchange() { echo('sex=',this.get) }
  }
  label          [L+256,T+16,33,] {title:'narození'}
  field narozeni [L+255,T+32,85,] {type:'date', data:o.narozeni, format:'rR:e'}
  label          [L+346,T+16,41,] {title:'nnnn rč'}
  field rodne    [L+345,T+32,37,] {data:o.rc_xxxx, format:'r:e'}
  label          [L+396,T+16,41,] {title:'církev'}
  select cirkev  [L+395,T+32,152,] {type:'map', data:o.cirkev, options:ms_akce_cirkev.zkratka}
  label          [L+5,T+54,100,] {title:'ulice'}
  field ulice    [L+5,T+69,162,] {data:o.ulice}
  label          [L+179,T+54,39,] {title:'psč'}
  field psc      [L+179,T+69,40,] {data:o.psc}
  label          [L+230,T+54,100,] {title:'obec'}
  field obec     [L+229,T+69,131,] {data:o.obec}
  label          [L+5,T+94,33,] {title:'telefon'}
  field telefon  [L+5,T+108,212,] {data:o.telefon}
  label          [L+229,T+94,30,] {title:'e-mail'}
  field email    [L+229,T+108,330,] {data:o.email}
  label          [L+5,T+132,48,] {title:'poznámka'}
  edit  pozn     [L+5,T+145,216,84] {data:o.note}
  # ------------------------ akce
  view s: table spolu
  label           [L+220,T+132,,] {title:'/ poznámka k pobytu na této akci'}
  edit  pozn_akce [L+229,T+145,329,84] {data:s.poznamka}
  // děti
  label           [L+5,T+240,,] {title:'skupinka č.'}
  field skupinka  [L+70,T+237,30,] {data:s.skupinka, format:'r:e'}
  check s_rodici  [L+226,T+232,210,] {data:s.s_rodici, title:'stravenky mají rodiče - účastníci akce' }
  check pulstrava [L+464,T+232,99,] {data:s.pulstrava, title:'poloviční strava' }
  browse d [L+5,T+260,,] {type:'smart', rows:12
    show id_osoba
    show id_spolu
    show prijmeni [,,80,] { title:'příjmení' }
    show jmeno    [,,70,] { title:'jméno' }
    show vek      [,,30,] { title:'věk', js_pipe:'roku', format:'r' }
    show narozeni
    show rc_xxxx
    show note     [,,350,] { title:'poznámka / poznámka k akci' }
#     proc onsubmit() {
#       Dite.key_dite.set(id_osoba.get); Dite.key_spolu.set(id_spolu.get);
#       Dite.modal(260,130)
#     }
  }
}
# -------------------------------------------------------------------------------------------------- _pece
form _pece [,,400,] { css:'ds_form'
  label         [4,516,,] {title:'pobyt - vývojářské údaje', skill:'m'}
  label develop [0,530,380,106] { skill:'m', style:'border:1px solid #bbb;padding:3px' }
  // definice pohledů
  view s: table spolu
  view p: table pobyt  { join:'USING(id_pobyt)' }
  view a: table akce   { join:'ON a.id_duakce=p.id_akce' }
  view o: table osoba  { join:'ON s.id_osoba=o.id_osoba' }
  view t: table tvori  { join_type:'LEFT', join:'ON t.id_osoba=o.id_osoba' }
#   // definice pohledů
#   view p: table pobyt
#   view a: table akce   { join:'ON a.id_duakce=p.id_akce' }
#   view s: table spolu  { join:'USING(id_pobyt)' }
#   view o: table osoba  { join:'ON s.id_osoba=o.id_osoba' }
#   view t: table tvori  { join_type:'LEFT', join:'ON t.id_osoba=o.id_osoba' }
  // seznam účastníků
  browse b [0,0,,] {type:'smart', buf_rows:120, rows:27, qry_rows:1,
    optimize:°{qry:'noseek'}
    // klíče
    show key_spolu  {data:s.id_spolu}
    show key_akce   {data:p.id_akce}
    show key_pobyt  {data:p.id_pobyt}
    show key_osoba  {data:o.id_osoba}
    show key_rodina {data:t.id_rodina}
    show role       {data:t.role}
    // data
    show prijmeni { data:o.prijmeni }
    show jmeno    { data:o.jmeno }
    show sex      { data:o.sex }
    show narozeni { data:o.narozeni }
    show rodne    { data:o.rc_xxxx }
    show cirkev   { data:o.cirkev }
    show ulice    { data:o.ulice }
    show psc      { data:o.psc }
    show obec     { data:o.obec }
    show telefon  { data:o.telefon }
    show email    { data:o.email }
    show pozn     { data:o.note }
    show pozn_akce { data:s.poznamka }
    show s_rodici  { data:s.s_rodici }
    show pulstrava { data:s.pulstrava }
    // zobrazení
    show _nazev    [,,150,] { title:'příjmení jméno', format:'s+q*', expr:"CONCAT(o.prijmeni,' ',o.jmeno)" }
    show skupinka  [,,30,] { title:'sk.', data:s.skupinka, format:'rqs:e'}
    show           [,,170,] { title:'poznámka / k akci', format:'qs',
      expr:"IF(o.note='' AND s.poznamka='','',CONCAT(o.note,' / ',s.poznamka))" }
    // skupinka
    show skup     { expr:"
      IF(s.skupinka=0,'',
         (SELECT GROUP_CONCAT(osoba.id_osoba,'|',id_spolu,'|',prijmeni,'|',jmeno,'|',
            narozeni,'|',narozeni,'|',rc_xxxx,'|',
            IF(note='' AND spolu.poznamka='','',CONCAT(note,' / ',spolu.poznamka))
          ORDER BY IF(pobyt.funkce=99,0,1),prijmeni SEPARATOR '|')
        FROM osoba
        JOIN spolu USING(id_osoba)
        JOIN pobyt USING(id_pobyt)
        WHERE pobyt.id_akce=p.id_akce AND skupinka=s.skupinka
        ))" }
    proc onrowclick() { reload_pecoun }
    // změna
    proc reload_pecoun() {
      [ has_skill('m');
        { b.browse_count;
        develop.set(conc(
           'id_akce=',key_akce.get,', id_pobyt=',key_pobyt.get
          ,'<br>m: id_spolu=', key_spolu.get, ', id_osoba=', key_osoba.get
          , ', id_rodina=', key_rodina.get, '/', role.get
        ))
        | develop.set('') }
      ];
      // ochrana proti neuložené změně, pokud je možné údaje měnit
      has_skill('yaa+;faa+'); not(and(p_oso.same));
      warning('neuložené změny pro pečouna <b>',p_oso.prijmeni.get,'</b> použij [Zpět] nebo [Uložit]');
    | not(b.browse_count);
      p_oso.init; p_oso.d.browse_init
    | switch(zalozka.get,
      'evid', {
        // přechod na jiného pečouna
        copy_by_name(b,p_oso.get); p_oso.o.key(key_osoba.get); p_oso.s.key(key_spolu.get);
        p_oso.d.browse_fill(skup.get);
      },
      'plat',{
        stop
      },
      'akce',{
        stop
      },
      {stop}
    )}
  }
}
# -------------------------------------------------------------------------------------------------- _cmd
form _cmd [,,200,] { css:'ds_form'
  // Nový
  button [6,525,,] {title:'Nový', skill:'yaap+|yaap+;faa+|faa+;caa+|caa+',
    help:'Vytvoření nového pečouna a vložení jako účastníka'
    proc onclick() {   // založení
      p_oso.init(1); p_oso.d.browse_init; p_oso.o.key(0); p_oso.s.key(0);
      b_display(0);
  } }
  // Zpět a Uložit
  label  [478,520,98,30] { css:'ae_parm', skill:'yaap+;faa+;caa+' }
  button [484,525,,] {title:'Zpět',  help:'Neměnit změněná data', skill:'yaap+|yaap+;faa+|faa+;caa+|caa+'
    proc onclick() {
      p_oso.plain;
      b_display(1);
      reload(0) //(up.b.browse_key);
  } }
  button [530,525,,] {title:'Uložit',  help:'Uložit změněná data', skill:'yaap+|yaap+;faa+|faa+;caa+|caa+'
    proc onclick() {
      var ok: number
      p_oso.same
      // oprava
    | p_oso.o.key;
      [ p_oso.o.save ];
      [ p_oso.s.save ];
      p_oso.plain;
      up.b.browse_row; up.b.reload_pecoun
    | // přidání
      p_oso.insert;
      spolu.insert_record(object('id_osoba',p_oso.key,'id_pobyt',pobyt_pecounu));
      p_oso.plain;
      b_display(1);
      reload(0);
    }
  }
  proc pobyt_pecounu() {
    [ the_pobyt.get | the_pobyt.set(pobyt.insert_record(object('id_akce',the_duakce.get))) ];
    return(the_pobyt.get);
  }
  // PŘIDAT z AKCE
  button pridat_a   [69,525,,] {title:'Přidat: z akce', skill:'yaap+|yaap+;faa+|faa+;caa+|caa+',
    help:'Přidání účastníky jiných akcí'
    proc onclick() {
      PridaniAkce.modal(240,50)
  } }
  // PŘIDAT z pečounů
  button [155,525,,] {title:'... jménem', skill:'yaap+|yaap+;faa+|faa+;caa+|caa+',
    help:'Přidání pečouna podle jména - výběr z EVIDENCE'
    proc onclick() {
      PridaniJmenem3.fl.jmena.set_attrib('par.fce','akce_auto_jmena1');
      PridaniJmenem3.modal(240,50)
  } }
  // PŘIDAT z evidence
  button [228,525,,] {title:'... jménem pečouna', skill:'yaap+|yaap+;faa+|faa+;caa+|caa+',
    help:'Přidání pečouna podle jména - výběr z pečounů'
    proc onclick() {
      PridaniJmenem3.fl.jmena.set_attrib('par.fce','akce_auto_jmena3');
      PridaniJmenem3.modal(240,50)
  } }
  // VYŘADIT a VYMAZAT
  button [357,525,,] {title:'Vyřadit', skill:'yaap+|yaap+;faa+|faa+;caa|caa+',
    help:'Vyřazení pečouna z účasti na akci'
    proc onclick() {
      confirm("Opravdu vyřadit ",up.b.prijmeni.get,' (',up.b.jmeno.get,") z této akce?");
      spolu.delete_record(conc('id_spolu=',up.b.key_spolu.get),1);
      reload(0);
  } }
  button [409,525,,] {title:'Vymazat', skill:'yaap+|yaap+;faa+|faa+;caa+|caa+',
    help:'Vymazání informací o pečounovi z evidence'
    proc onclick() {
      confirm("Opravdu vymazat z evidence ",up.b.prijmeni.get,' ',up.b.jmeno.get,"z evidence?");
      alert('není hotovo - tady bude ještě jedno varování s výčtem akcí, kterých se zúčastnil');
      reload(0);
  } }
#   // PŘIDAT DÍTĚ
#   button dite       [225,525,,] {title:'... dítě do skupinky', skill:'yaap+|yaap+;faa+|faa+;caa+|caa+',
#     help:'Přidání dalšího dítěte do skupiny'
#     proc onclick() {
#       Dite.key_dite.set(0); Dite.key_spolu.set(0);
#       Dite.modal(260,130)
#   } }
  // procedury
  proc b_display(on) { up.b.display(on);  }
}
# ================================================================================= Přidání jménem 3
panel PridaniJmenem3 [,,320,280] {type:'popup', title:'Přidání známých pečounů jménem'
  use fl: form _fl [12,10,,]
  proc onfocus() { fl.jmena.init; fl.vse.init; fl.L.init; fl.jmena.focus }
  form _fl [,,300,200] {
    var last: number
    var rod: object
    var popis: text
    button [0,250,50,16] { title:'přidat do pečounů akce', proc onclick() {
      each(L,pecouni_add); reload(0); panel.hide(0)
    } }
    button [144,250,50,16] { title:'jiná jména', proc onclick() {
      jmena.init; vse.init; L.init } }
    button [230,250,50,16] { title:'konec', proc onclick() {
      jmena.init; vse.init; L.init; panel.hide(0) } }
    // výběr z databáze MS
    select jmena [100,0,200,17] { type:'auto', format:'t',
      par:°{fce:'akce_auto_jmena3',patt:'whole',cond:'funkce=99'}, // par.cond je měněno při volání
      proc onchanged() {
        rod.set(ask('akce_auto_jmena3L',this.key));
        each(rod.get,jmenovci); // nesmí volat asynchronní operace
      }
    }
    proc jmenovci(p,i) {
      last.set(L.add);
      popis.set(p.nazev);
      L.part(last.get).part('info').set(popis.get);
      L.part(last.get).part('all').set(p);
    }
    // nastavení výběru
    check vse [4,0,80,16] { title:'všichni', format:'t'
      proc onchange() { each(L,nastav) }
      proc nastav (x,i) { x.chck.set(form.vse.get) }
    }
    list L [0,23,300,200] {rows:22      // anonymní group
      check chck [4,0,50,16] { title:' ', format:'t', value:'1'  /*, proc onchange() { echo('jo/ne') }*/ }
      label info [23,4,300,16] {style:'white-space:nowrap'}
      var all: object
    }
  }
}
# ================================================================================================== Přidání akce
panel PridaniAkce [,,320,280] {type:'popup', title:'Přidání pečounů známých z jiných akcí'
  use fl: form _fl [12,10,,]
  proc onfocus() { fl.akce.init; fl.vse.init; fl.L.init; fl.akce.focus }
  form _fl [,,300,200] {
    var last: number
    var pary: object
    var popis: text
    button [0,250,50,16] { title:'přidat mezi pečouny', proc onclick() {
      each(L,pecouni_add); reload(0);
    } }
    button [130,250,50,16] { title:'jiná akce', proc onclick() {
      akce.init; vse.init; L.init } }
    button [220,250,50,16] { title:'konec', proc onclick() {
      akce.init; vse.init; L.init; panel.hide(0) } }
    // výběr z akcí
    select akce [100,0,200,17] { type:'auto', par:°{fce:'akce_auto_pece'}, format:'t'
      proc onchanged() {
        pary.set(ask('akce_auto_peceL',this.key));
        each(pary.get,ucastnici); // nesmí volat asynchronní operace
      }
    }
    proc ucastnici(p,i) {
      last.set(L.add);
      popis.set(p.nazev);
      L.part(last.get).part('info').set(popis.get);
      L.part(last.get).part('all').set(p);
    }
    // nastavení výběru
    check vse [4,0,80,16] { title:'všichni', format:'t'
      proc onchange() { each(L,nastav) }
      proc nastav (x,i) { x.chck.set(form.vse.get) }
    }
    list L [0,23,300,200] {rows:22      // anonymní group
      check chck [4,0,50,16] { title:' ', format:'t'  /*, proc onchange() { echo('jo/ne') }*/ }
      label info [23,4,300,16] {style:'white-space:nowrap'}
      var all: object
    }
  }
}
# ================================================================================================== BackTrace
# přehled provedených změn
panel BackTrace [,,540,250] { title:'histore oprav', type:'popup', css:'dialog'
  use back: form _back [0,16,,] { tabindex:20 },
  proc back_load(id_pobyt,id_muz,id_zen,id_rodina) {
    BackTrace.popup(0,1);
    back.list.browse_load(
      conc("(kde='pobyt' AND klic=",id_pobyt,
      ") OR (kde='osoba' AND klic IN(",id_muz,",",id_zen,")",
      ") OR (kde='rodina' AND klic=",id_rodina,")"),"kdy DESC");
    back.list.raise('onrowclick')
  }
  # ------------------------------------------------------------- _back
  # id_track,kdy,kdo,kde,klic,zmena:fld,op,val,old
  form _back [,,255,250] {
    browse list [0,0,150,100] { rows:12,
      show kdy [,,90,] { title:'kdy', data:_track.kdy, sql_pipe:'sql_time1' },
      show kdn [,,30,] { title:'kdn', data:_track.kdo },
      show op [,,12,] { title:'?', data:_track.op },
      show fld [,,60,] { title:'položka', data:_track.fld },
      show old [,,200,] { title:'původní hodnota', data:_track.old, format:'t' },
      show val [,,120,] { title:'změněná hodnota', data:_track.val, format:'t' },
      show id_track [,,0,] { data:_track.id_track },
    },
    button zpet [500,515,80,20] { title:'Zpět'
      proc onclick() { panel.close(0) }
    }
  }
}
