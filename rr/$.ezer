menu rr { type:'main', active:rr.richard
  tabs richard {title:"Myšlenky", _sys:'*'
    # =================================================================================== rozesílání
    panel rohr {type:'right', title:'Rozesílání'
      menu myslenka {type:'left'
        menu ukazat {title:'Zobrazení',type:'group'
          item dnesni {title:'dnešní myšlenky', par:°{den:0,poslat:0} }
          item {title:'včerejší myšlenky', par:°{den:-1,poslat:0} }
          item {title:'zítřejší myšlenky', par:°{den:1,poslat:0} }
        }
        menu poslat {title:'Rozeslání',type:'group'
          item dnesni {title:'dnešní myšlenky', par:°{den:0,poslat:1,opakovat:0} }
          item {title:' ... znovu', par:°{den:0,poslat:1,opakovat:1} }
          item {title:'včerejší myšlenky', par:°{den:-1,poslat:1,opakovat:0} }
          item {title:' ... znovu', par:°{den:-1,poslat:1,opakovat:1} }
        }
        proc onclick (i) {
          info.fill(conc(i.owner.title,' - ',i.title),' ');
          info.msg.set('');
          info.fill('',ask('rr_send',i.par));
        }
      }
      use info: form right [12,4,,]
    }
    # ======================================================================================= opravy
    panel plan {type:'right', title:'Opravy'
      use f: form _f [12,4,,] { format:'n' }
      use g: form _g [12,4,,] { format:'n' }
      menu nastav {type:'left', format:'f'
        menu obdobi {title:'Opravy',type:'group'
          item od {title:'Nastavení období'
            proc onclick (i) {
              f.display(1);
              g.display(0);
            }
          }
          item {title:'Oprava překladu'
            proc onclick (i) {
              f.display(0);
              g.display(1);
            }
          }
        }
      }
      proc onfirstfocus() {
        f.b.browse_load;
        g.b.browse_load
      }
      # ---------------------------------------------------------------------------- opravy překladu
      form _g [,,*,100] {
        browse b [0,0,,] { rows:7, qry_rows:1, css_rows:'barva,1:yellow'
          show id_rr { data:rr.id_rr, format:'qs' }
          show [,,40,] { title:'den', data:rr.day, format:'qrs' }
          show [,,70,] { title:'datum', data:rr.datum, format:'qs' }
          show [,,270,] { title:'název', data:rr.subject, format:'qs' }
          show barva { expr:"IF(errata='',0,1)" }
          proc onrowclick () { nacti }
          proc nacti() { g.load(id_rr.get) } }
        button [725,6,438,77] { title:'Dnes' proc onclick() {
          b.browse_seek(conc("datum='",now_sql(),"'"));
          b.nacti
        } }
        field czt [0,181,404,] { data:rr.title_cz }
        field ent [422,181,401,] { data:rr.title_en }
        button [778,6,,] { title:'Uložit' proc onclick() { g.save; g.load } }
        edit cz [0,209,405,548] { type:'html', data:rr.text_cz, par:°{toolbar:'IntranetSlim'} }
        edit en [421,209,405,548] { type:'html', data:rr.text_en, par:°{toolbar:'IntranetSlim'} }
        label [423,19,,] { title:'Důvod pozměnění překladu' }
        edit errata [421,39,402,131] { data:rr.errata }
      }
      # --------------------------------------------------------------------------- nastavení období
      form _f [,,*,500] {
        browse b [0,0,,] { rows:24, qry_rows:1
          show id_rr { data:rr.id_rr, format:'qs' }
          show den [,,30,] { title:'pořadí', data:rr.day_n, format:'qs' }
          show [,,40,] { title:'den', data:rr.day, format:'qs' }
          show [,,20,] { title:'ps', data:rr.state, format:'qs' }
          show datum [,,70,] { title:'datum', data:rr.datum, format:'qs' }
          show [,,140,] { title:'název', data:rr.subject, format:'qs' }
          proc onsubmit() {
            form.den.set(den.get); form.datum.set(replace(sql2date(datum.get),' ','')); }
        }
        label [340,10,,] { title:"Nastav pořadové číslo dne, počáteční datum a počet dnů" }
        field den [340,40,30,] { format:'t' }
        field datum [380,40,80,] { type:'date', format:'t' }
        field pocet [470,40,40,] { format:'t' }
        button [530,38,,] { title:'nastav'
          proc onclick() { var ret:object
            ret.set(ask('rr_nastav',den.get,datum.get,pocet.get));
            msg.set(ret.msg);
            datum.set(replace(sql2date(ret.next),' ',''));
            den.set(sum(den.get,pocet.get));
            f.b.browse_seek(conc('day_n=',sum(den.get,-1)));
          }
        }
        label msg [340,70,300,200]
      }
    }
  }
  # ------------------------------------------------------------------ rr
  table rr {
    number id_rr
    number day_n
    text day
    date datum
    text state /* prepared|sent */
    text subject
    text title_cz
    text text_cz
    text title_en
    text text_en
    text from_en
    text errata
  }
}
// univerzální formulář pro levostranné menu
# ------------------------------------------------------------------ right
form right [,,*,50] {
  label head [0,0,*,50]  { title:'' }
  label msg  [0,35,*,20] { title:'' }
  label note [0,55,*,550] { title:'' }
  proc fill(x,y) {
    [ x; head.set(conc("<div class='karta'>",x,"</div>")) ];
    [ y; note.set(y) ]
  }
}
